////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Заголовок") Тогда
		Заголовок = Параметры.Заголовок;
	КонецЕсли;
	
	Элементы.ГруппаРаздел.Видимость = НЕ Параметры.Свойство("Раздел");
	
	ЗаполнитьСписокРазделов();
		
	Если Параметры.Свойство("Раздел") Тогда
		
		Раздел = Параметры.Раздел;
		
	Иначе
		
		РазделПоУмолчанию = Элементы.Раздел.СписокВыбора[0].Значение;
		ТекущийРаздел = ОбщегоНазначенияВызовСервера.ХранилищеСистемныхНастроекЗагрузить("ПолезнаяИнформация", "ТекущийРаздел", РазделПоУмолчанию);
		Если Элементы.Раздел.СписокВыбора.НайтиПоЗначению(ТекущийРаздел) <> Неопределено Тогда
			Раздел = ТекущийРаздел;
		Иначе
			Раздел = РазделПоУмолчанию;
		КонецЕсли;
		
	КонецЕсли;
	
	Для каждого ЭлементСписка Из Элементы.Раздел.СписокВыбора Цикл
		
		Форма = ЭтаФорма;
		
		ИмяРаздела = ЭлементСписка.Значение;
		ПредставлениеРаздела = ЭлементСписка.Представление;
		
		// Страница раздела.
		СтраницаРаздела = Форма.Элементы.Добавить("ГруппаСтраница" + ИмяРаздела, Тип("ГруппаФормы"), Элементы.ГруппаСтраницы); 
		СтраницаРаздела.Заголовок = ПредставлениеРаздела;
		СтраницаРаздела.Вид = ВидГруппыФормы.Страница;
		СтраницаРаздела.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
		СтраницаРаздела.ОтображатьЗаголовок = Ложь;
		
		// Колонка "Актуальные материалы".
		КолонкаАктуальныеМатериалы = Форма.Элементы.Добавить("ГруппаСтраница" + ИмяРаздела + "АктуальныеМатериалы", Тип("ГруппаФормы"), СтраницаРаздела); 
		КолонкаАктуальныеМатериалы.Заголовок = ПредставлениеРаздела + НСтр("ru=': актуальные материалы';uk=': актуальні матеріали'");
		КолонкаАктуальныеМатериалы.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		КолонкаАктуальныеМатериалы.Отображение = ОтображениеОбычнойГруппы.Нет;
		КолонкаАктуальныеМатериалы.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
		КолонкаАктуальныеМатериалы.ОтображатьЗаголовок = Ложь;
		КолонкаАктуальныеМатериалы.РастягиватьПоГоризонтали = Ложь;
		КолонкаАктуальныеМатериалы.Ширина = 40;

		// Заголовок "Актуальные материалы"
		ЗаголовокАктуальныеМатериалы = Форма.Элементы.Добавить("Заголовок" + ИмяРаздела + "АктуальныеМатериалы", Тип("ДекорацияФормы"), КолонкаАктуальныеМатериалы); 
		ЗаголовокАктуальныеМатериалы.Заголовок = НСтр("ru='Актуальные материалы';uk='Актуальні матеріали'");
		ЗаголовокАктуальныеМатериалы.Вид = ВидДекорацииФормы.Надпись;
		ЗаголовокАктуальныеМатериалы.РастягиватьПоГоризонтали = Истина;
		ЗаголовокАктуальныеМатериалы.ЦветТекста = Новый Цвет(179, 128, 0);
		ЗаголовокАктуальныеМатериалы.Шрифт = Новый Шрифт(, 10, Истина);
		ЗаголовокАктуальныеМатериалы.Ширина = 1;
		
		// Колонка "Быстрое освоение".
		КолонкаБыстроеОсвоение = Форма.Элементы.Добавить("ГруппаСтраница" + ИмяРаздела + "БыстроеОсвоение", Тип("ГруппаФормы"), СтраницаРаздела); 
		КолонкаБыстроеОсвоение.Заголовок = ПредставлениеРаздела + НСтр("ru=': быстрое освоение';uk=': швидке освоєння'");
		КолонкаБыстроеОсвоение.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		КолонкаБыстроеОсвоение.Отображение = ОтображениеОбычнойГруппы.Нет;
		КолонкаБыстроеОсвоение.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
		КолонкаБыстроеОсвоение.ОтображатьЗаголовок = Ложь;
		КолонкаБыстроеОсвоение.РастягиватьПоГоризонтали = Ложь;
		КолонкаБыстроеОсвоение.Ширина = 40;
		
		// Заголовок "Быстрое освоение"
		ЗаголовокБыстроеОсвоение = Форма.Элементы.Добавить("Заголовок" + ИмяРаздела + "БыстроеОсвоение", Тип("ДекорацияФормы"), КолонкаБыстроеОсвоение); 
		ЗаголовокБыстроеОсвоение.Заголовок = НСтр("ru='Быстрое освоение';uk='Швидке освоєння'");
		ЗаголовокБыстроеОсвоение.Вид = ВидДекорацииФормы.Надпись;
		ЗаголовокБыстроеОсвоение.РастягиватьПоГоризонтали = Истина;
		ЗаголовокБыстроеОсвоение.ЦветТекста = Новый Цвет(179, 128, 0);
		ЗаголовокБыстроеОсвоение.Шрифт = Новый Шрифт(, 10, Истина);
		ЗаголовокБыстроеОсвоение.Ширина = 1;
		
	 	ПолезнаяИнформация.ДобавитьГиперссылки(ЭтаФорма, КолонкаАктуальныеМатериалы, ИмяРаздела, "АктуальныеМатериалы");
	 	ПолезнаяИнформация.ДобавитьГиперссылки(ЭтаФорма, КолонкаБыстроеОсвоение, ИмяРаздела, "БыстроеОсвоение");
		
		КолонкаАктуальныеМатериалы.Видимость = КолонкаАктуальныеМатериалы.ПодчиненныеЭлементы.Количество() > 1;
		КолонкаБыстроеОсвоение.Видимость = КолонкаБыстроеОсвоение.ПодчиненныеЭлементы.Количество() > 1;
		
	КонецЦикла;
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы["ГруппаСтраница" + Раздел];
		
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура РазделПриИзменении(Элемент)
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы["ГруппаСтраница" + Раздел];
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Подключаемый_ОткрытьГиперссылкуПолезнойИнформации(Команда)
	
	ПолезнаяИнформацияКлиент.ОткрытьГиперссылку(ЭтаФорма, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОткрытьВсеМатериалыПолезнойИнформации(Команда)
	
	ПолезнаяИнформацияКлиент.ОткрытьВсеМатериалы(ЭтаФорма, Команда);
		
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)

	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияВызовСервера.ХранилищеСистемныхНастроекСохранить("ПолезнаяИнформация", "ТекущийРаздел", Раздел);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ЗаполнитьСписокРазделов()

	Элементы.Раздел.СписокВыбора.Добавить("Главное",           НСтр("ru='Бухгалтерия';uk='Бухгалтерія'"));
	Элементы.Раздел.СписокВыбора.Добавить("БанкИКасса",        НСтр("ru='Банк и касса';uk='Банк і каса'"));
	Элементы.Раздел.СписокВыбора.Добавить("Покупки",           НСтр("ru='Покупки';uk='Купівлі'"));
	Элементы.Раздел.СписокВыбора.Добавить("Продажи",           НСтр("ru='Продажи';uk='Продажі'"));
	Элементы.Раздел.СписокВыбора.Добавить("Склад",             НСтр("ru='Склад';uk='Склад'"));
	Элементы.Раздел.СписокВыбора.Добавить("Производство",      НСтр("ru='Производство';uk='Виробництво'"));
	Элементы.Раздел.СписокВыбора.Добавить("ОСИНМА",            НСтр("ru='ОС и НМА';uk='ОЗ і НМА'"));
	Элементы.Раздел.СписокВыбора.Добавить("ЗарплатаИКадры",    НСтр("ru='Зарплата и кадры';uk='Зарплата і кадри'"));
	Элементы.Раздел.СписокВыбора.Добавить("Операции",          НСтр("ru='Операции';uk='Операції'"));
	Элементы.Раздел.СписокВыбора.Добавить("Отчеты",            НСтр("ru='Отчеты';uk='Звіти'"));
	Элементы.Раздел.СписокВыбора.Добавить("Администрирование", НСтр("ru='Администрирование';uk='Адміністрування'"));

КонецПроцедуры 

