
&НаСервере
Процедура ОбновитьДанныеНаСервере()
	ТаблицаДанных.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	скEDI_Организации.Ссылка КАК Организация,
	               |	СУММА(ВложенныйЗапрос.Проверено) КАК Проверено,
	               |	СУММА(ВложенныйЗапрос.НеобходимоСоздатьДокументДляПовторногоУтверждения) КАК НеобходимоСоздатьДокументДляПовторногоУтверждения,
	               |	СУММА(ВложенныйЗапрос.НеобходимоПолучитьДокументДляПовторногоУтверждения) КАК НеобходимоПолучитьДокументДляПовторногоУтверждения,
	               |	СУММА(ВложенныйЗапрос.СозданДокументДляПовторногоУтверждения) КАК СозданДокументДляПовторногоУтверждения,
	               |	СУММА(ВложенныйЗапрос.ПолученДокументДляПовторногоУтверждения) КАК ПолученДокументДляПовторногоУтверждения
	               |ИЗ
	               |	Справочник.скEDI_Организации КАК скEDI_Организации
	               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |			ВложенныйЗапрос.Организация КАК Организация,
	               |			ВложенныйЗапрос.Ссылка КАК Ссылка,
	               |			ВложенныйЗапрос.Проверено КАК Проверено,
	               |			ВЫБОР
	               |				КОГДА ВложенныйЗапрос.ГдеСоздан = ЗНАЧЕНИЕ(Перечисление.скEDI_ГдеСоздан.ВДаннойИнформационнойБазе)
	               |					ТОГДА ВЫБОР
	               |							КОГДА ВложенныйЗапрос.ТребуетПовторногоУтверждения
	               |									И НЕ ВложенныйЗапрос.ЕстьДругойВариантДокумента
	               |								ТОГДА 1
	               |						КОНЕЦ
	               |			КОНЕЦ КАК НеобходимоСоздатьДокументДляПовторногоУтверждения,
	               |			ВЫБОР
	               |				КОГДА ВложенныйЗапрос.ГдеСоздан = ЗНАЧЕНИЕ(Перечисление.скEDI_ГдеСоздан.ПолученОтКонтрагента)
	               |					ТОГДА ВЫБОР
	               |							КОГДА ВложенныйЗапрос.ТребуетПовторногоУтверждения
	               |									И НЕ ВложенныйЗапрос.ЕстьДругойВариантДокумента
	               |								ТОГДА 1
	               |						КОНЕЦ
	               |			КОНЕЦ КАК НеобходимоПолучитьДокументДляПовторногоУтверждения,
	               |			ВЫБОР
	               |				КОГДА ВложенныйЗапрос.ТребуетПовторногоУтверждения
	               |					ТОГДА ВЫБОР
	               |							КОГДА ВложенныйЗапрос.ОсновнойЭлектронныйДокумент.ГдеСоздан = ЗНАЧЕНИЕ(Перечисление.скEDI_ГдеСоздан.ВДаннойИнформационнойБазе)
	               |								ТОГДА 1
	               |						КОНЕЦ
	               |			КОНЕЦ КАК СозданДокументДляПовторногоУтверждения,
	               |			ВЫБОР
	               |				КОГДА ВложенныйЗапрос.ТребуетПовторногоУтверждения
	               |					ТОГДА ВЫБОР
	               |							КОГДА ВложенныйЗапрос.ОсновнойЭлектронныйДокумент.ГдеСоздан = ЗНАЧЕНИЕ(Перечисление.скEDI_ГдеСоздан.ПолученОтКонтрагента)
	               |								ТОГДА 1
	               |						КОНЕЦ
	               |			КОНЕЦ КАК ПолученДокументДляПовторногоУтверждения
	               |		ИЗ
	               |			(ВЫБРАТЬ
	               |				скEDI_ЭлектронныйДокумент.Организация КАК Организация,
	               |				скEDI_ЭлектронныйДокумент.Ссылка КАК Ссылка,
	               |				скEDI_ЭлектронныйДокумент.ИмяФайла КАК ИмяФайла,
	               |				скEDI_ЭлектронныйДокумент.Состояние КАК Состояние,
	               |				скEDI_ЭлектронныйДокумент.ВидЭлектронногоДокумента.Наименование КАК ВидЭлектронногоДокументаНаименование,
	               |				скEDI_ЭлектронныйДокумент.Дата КАК Дата,
	               |				скEDI_ЭлектронныйДокумент.Номер КАК Номер,
	               |				скEDI_ЭлектронныйДокумент.ГдеСоздан КАК ГдеСоздан,
	               |				скEDI_ЭлектронныйДокумент.ОсновнойЭлектронныйДокумент КАК ОсновнойЭлектронныйДокумент,
	               |				Проверка.ДатаСобытия КАК ДатаСобытияПроверка,
	               |				ВЫБОР
	               |					КОГДА НЕ Проверка.ДатаСобытия ЕСТЬ NULL
	               |						ТОГДА 1
	               |				КОНЕЦ КАК Проверено,
	               |				скEDI_ЭлектронныйДокумент.Состояние В (ЗНАЧЕНИЕ(Перечисление.скEDI_СостоянияЭлектронныхДокументов.ТребуетПовторногоУтверждения)) КАК ТребуетПовторногоУтверждения,
	               |				ВЫБОР
	               |					КОГДА скEDI_ЭлектронныйДокумент.ОсновнойЭлектронныйДокумент = ЗНАЧЕНИЕ(Документ.скEDI_ЭлектронныйДокумент.ПустаясСылка)
	               |						ТОГДА ЛОЖЬ
	               |					ИНАЧЕ ИСТИНА
	               |				КОНЕЦ КАК ЕстьДругойВариантДокумента
	               |			ИЗ
	               |				Документ.скEDI_ЭлектронныйДокумент КАК скEDI_ЭлектронныйДокумент
	               |					ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |						скEDI_СобытияЭлектронныхДокументов.ЭлектронныйДокумент КАК ЭлектронныйДокумент,
	               |						МАКСИМУМ(скEDI_СобытияЭлектронныхДокументов.ДатаСобытия) КАК ДатаСобытия
	               |					ИЗ
	               |						РегистрСведений.скEDI_СобытияЭлектронныхДокументов КАК скEDI_СобытияЭлектронныхДокументов
	               |					ГДЕ
	               |						скEDI_СобытияЭлектронныхДокументов.Событие = ЗНАЧЕНИЕ(Перечисление.скEDI_СобытияЭлектронныхДокументов.ПроверкаДокумента)
	               |					
	               |					СГРУППИРОВАТЬ ПО
	               |						скEDI_СобытияЭлектронныхДокументов.ЭлектронныйДокумент) КАК Проверка
	               |					ПО скEDI_ЭлектронныйДокумент.Ссылка = Проверка.ЭлектронныйДокумент
	               |			ГДЕ
	               |				скEDI_ЭлектронныйДокумент.ВидЭлектронногоДокумента.ТипДокумента В (ЗНАЧЕНИЕ(Перечисление.скEDI_ТипыЭлектронныхДокументов.ПервичныйДокумент), ЗНАЧЕНИЕ(Перечисление.скEDI_ТипыЭлектронныхДокументов.ДокументСВложениями), ЗНАЧЕНИЕ(Перечисление.скEDI_ТипыЭлектронныхДокументов.СоглашениеОАнулированииДокумента))
	               |				И скEDI_ЭлектронныйДокумент.Состояние В (ЗНАЧЕНИЕ(Перечисление.скEDI_СостоянияЭлектронныхДокументов.ПодписанЧастично_НаПодписьБухгалтеру), ЗНАЧЕНИЕ(Перечисление.скEDI_СостоянияЭлектронныхДокументов.ПодписанЧастично_НаПодписьДиректору), ЗНАЧЕНИЕ(Перечисление.скEDI_СостоянияЭлектронныхДокументов.ПодписанЧастично), ЗНАЧЕНИЕ(Перечисление.скEDI_СостоянияЭлектронныхДокументов.Подписан), ЗНАЧЕНИЕ(Перечисление.скEDI_СостоянияЭлектронныхДокументов.Отправлен), ЗНАЧЕНИЕ(Перечисление.скEDI_СостоянияЭлектронныхДокументов.ПолученоСообщениеОДоставке), ЗНАЧЕНИЕ(Перечисление.скEDI_СостоянияЭлектронныхДокументов.ПолученоПодтверждение), ЗНАЧЕНИЕ(Перечисление.скEDI_СостоянияЭлектронныхДокументов.Получен), ЗНАЧЕНИЕ(Перечисление.скEDI_СостоянияЭлектронныхДокументов.ОтправленоПодтверждение), ЗНАЧЕНИЕ(Перечисление.скEDI_СостоянияЭлектронныхДокументов.ТребуетПовторногоУтверждения))) КАК ВложенныйЗапрос) КАК ВложенныйЗапрос
	               |		ПО скEDI_Организации.Ссылка = ВложенныйЗапрос.Организация
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	скEDI_Организации.Ссылка";
	ВыборкаРезультатаЗапроса = Запрос.Выполнить().Выбрать();
	Пока ВыборкаРезультатаЗапроса.Следующий() Цикл
		НоваяСтрокаТаблицыДанных = ТаблицаДанных.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаТаблицыДанных, ВыборкаРезультатаЗапроса);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанные(Команда)
	ОбновитьДанныеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбновитьДанныеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ВыполнитьПроверкуНаСервере()
	скEDI_Сервис.ПроверитьТребуетПовторногоУтверждения_Релиз_1_1_3_16(Истина);
	ОбновитьДанныеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПроверку(Команда)
	ВыполнитьПроверкуНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьЭлектронныєДокументы(Команда)
	ТекущиеДанные = Элементы.ТаблицаДанных.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ОткрытьФорму("Обработка.скEDI.Форма.ФормаРабочегоСтола", , , Ложь);
		Оповестить("скEDI_УстановитьОтбор_ТребуетПовторногоУтверждения_для_Органииизации", ТекущиеДанные.Организация);
	КонецЕсли;
КонецПроцедуры
