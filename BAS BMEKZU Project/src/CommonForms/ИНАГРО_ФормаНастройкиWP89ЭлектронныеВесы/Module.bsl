
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест"
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;

	Параметры.Свойство("Идентификатор", Идентификатор);
	Параметры.Свойство("ДрайверОборудования", ДрайверОборудования);
	
	Заголовок = НСтр("ru='Оборудование:';uk='Устаткування:'") + Символы.НПП  + Строка(Идентификатор);

	СпПорт = Элементы.Порт.СписокВыбора;
	Для Номер = 1 По 30 Цикл
		СпПорт.Добавить(Номер, "COM" + Формат(Номер, "ЧЦ=2; ЧДЦ=0; ЧН=0; ЧГ=0"));
	КонецЦикла;

	СпСкорость = Элементы.Скорость.СписокВыбора;
	СпСкорость.Добавить(600,   "600");
	СпСкорость.Добавить(1200,  "1200");
	СпСкорость.Добавить(2400,  "2400");
	СпСкорость.Добавить(4800,  "4800");
	СпСкорость.Добавить(9600,  "9600");
	СпСкорость.Добавить(14400, "14400");
	СпСкорость.Добавить(19200, "19200");

	СпТипВП = Элементы.ТипВП.СписокВыбора;
	СпТипВП.Добавить(84,  "84");
	СпТипВП.Добавить(86,  "86");
	СпТипВП.Добавить(89,  "89");
	
	времПорт         = Неопределено;
	времСкорость     = Неопределено;
	времТипВП		 = Неопределено;
	времПауза		 = Неопределено;
	времМножитель    = Неопределено;

	Параметры.ПараметрыОборудования.Свойство("Порт"        , времПорт);
	Параметры.ПараметрыОборудования.Свойство("Скорость"    , времСкорость);
	Параметры.ПараметрыОборудования.Свойство("ТипВП"       , времТипВП);
	Параметры.ПараметрыОборудования.Свойство("Пауза"       , времПауза);	
	Параметры.ПараметрыОборудования.Свойство("Множитель"   , времМножитель);	
	
	
	Порт         = ?(времПорт         = Неопределено, 1,    времПорт);
	Скорость     = ?(времСкорость     = Неопределено, 9600, времСкорость);
	ТипВП    	 = ?(времТипВП     	  = Неопределено, 89,   времТипВП);
	Пауза        = ?(времПауза        = Неопределено, 1,    времПауза);
	Множитель    = ?(времМножитель    = Неопределено, 1,    времМножитель);
		
	Элементы.УстановитьДрайвер.Видимость = (ПараметрыСеанса.ИНАГРО_РабочееМестоКлиента = Идентификатор.РабочееМесто);

КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы



#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрытьВыполнить()
	
	НовыеЗначениеПараметров = Новый Структура;
	НовыеЗначениеПараметров.Вставить("Порт"         , Порт);
	НовыеЗначениеПараметров.Вставить("Скорость"     , Скорость);
	НовыеЗначениеПараметров.Вставить("ТипВП"     	, ТипВП);
	НовыеЗначениеПараметров.Вставить("Пауза"        , Пауза);
	НовыеЗначениеПараметров.Вставить("Множитель"    , Множитель);
	
	Результат = Новый Структура;
	Результат.Вставить("Идентификатор", Идентификатор);
	Результат.Вставить("ПараметрыОборудования", НовыеЗначениеПараметров);
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДрайверИзАрхиваПриЗавершении(Результат) Экспорт 
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Установка драйвера завершена.';uk='Установка драйвера завершена.'")); 
	
КонецПроцедуры 

&НаКлиенте
Процедура УстановитьДрайверИзДистрибутиваПриЗавершении(Результат, Параметры) Экспорт 
	
	Если Результат Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Установка драйвера завершена.';uk='Установка драйвера завершена.'")); 
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='При установке драйвера из дистрибутива произошла ошибка.';uk='При встановленні драйвера з дистрибутива сталася помилка.'")); 
	КонецЕсли;

КонецПроцедуры 

&НаКлиенте
Процедура УстановитьДрайвер(Команда)

	ОчиститьСообщения();
	ОповещенияДрайверИзДистрибутиваПриЗавершении = Новый ОписаниеОповещения("УстановитьДрайверИзДистрибутиваПриЗавершении", ЭтотОбъект);
	ОповещенияДрайверИзАрхиваПриЗавершении = Новый ОписаниеОповещения("УстановитьДрайверИзАрхиваПриЗавершении", ЭтотОбъект);
	ИНАГРО_МенеджерОборудованияКлиент.УстановитьДрайвер(ДрайверОборудования, ОповещенияДрайверИзДистрибутиваПриЗавершении, ОповещенияДрайверИзАрхиваПриЗавершении);
	
КонецПроцедуры
	
#КонецОбласти


#Область СлужебныеПроцедурыИФункции

#КонецОбласти