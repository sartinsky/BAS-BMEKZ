
&НаКлиенте
Процедура ИмяФайлаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	лДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	лДиалогВыбораФайла.ПроверятьСуществованиеФайла = Истина;
	лДиалогВыбораФайла.Фильтр = НСтр("ru = 'Файлы ключей jks'; uk = 'Файли ключів jks'") + " (*.jks)|*.jks";
	лДиалогВыбораФайла.МножественныйВыбор = Ложь;
	лДиалогВыбораФайла.Заголовок = НСтр("ru = 'Выберите файл ключа'; uk = 'Виберіть файл ключа'");

	Если лДиалогВыбораФайла.Выбрать() Тогда
		ИмяФайла = лДиалогВыбораФайла.ПолноеИмяФайла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Конвертировать(Команда)
	Если не ЗначениеЗаполнено(ИмяФайла) Тогда
		Сообщить(НСтр("ru = 'Не заполнено поле ""Имя файла""'; uk = 'Не заповнено поле ""Ім''я файла""'"));
		Возврат;
	КонецЕсли;
	
	Если не ЗначениеЗаполнено(КаталогРезультата) Тогда
		Сообщить(НСтр("ru = 'Не заполнено поле ""Каталог результата""'; uk = 'Не заповнено поле ""Каталог результату""'"));
		Возврат;
	КонецЕсли;
	
	ТекФайлОбъект = Новый Файл(ИмяФайла);
	Если ТекФайлОбъект.Существует() Тогда
		Сообщение = "";
		Ключ = Base64Строка(Новый ДвоичныеДанные(ИмяФайла));
		РезультатКонвертации = Неопределено;
		ТекстСообщения = "";
		Если ВыполнитьКонвертациюКлючаНаСервере(Ключ, Пароль, РезультатКонвертации, ТекстСообщения) Тогда
			ЕстьФайлы = Ложь;
			Для Каждого ЭлементРезультатКонвертации из РезультатКонвертации Цикл
				ПутьКФайлу = КаталогРезультата + "\" + ЭлементРезультатКонвертации.ИмяФайла;
			    Файл = Новый Файл(ПутьКФайлу);
				Н = 0;
				ИмяБезРасширения = Файл.ИмяБезРасширения;
				Расширение = Файл.Расширение;
				Пока Файл.Существует() Цикл
					Н = Н + 1;
					ПутьКФайлу = КаталогРезультата + "\" + ИмяБезРасширения + "_" + Формат(Н, "ЧГ=0") + Расширение;
					Файл = Новый Файл(ПутьКФайлу);
				КонецЦикла;
				ДвоичныеДанныеПоBase64 = Base64Значение(ЭлементРезультатКонвертации.СодержимоеФайла);
				ДвоичныеДанныеПоBase64.Записать(ПутьКФайлу);
				ЕстьФайлы = Истина;
			КонецЦикла;
			Если ЕстьФайлы Тогда
				ТекстСообщения = НСтр("ru = 'Ключи сохранены в каталоге'; uk = 'Ключі збережені в каталозі'") + ": " + КаталогРезультата;
				скEDI_ОбщегоНазначенияКлиент.ПоказатьПредупреждение_(ТекстСообщения);
				Закрыть();
			КонецЕсли;
		Иначе
			скEDI_ОбщегоНазначенияКлиент.ПоказатьПредупреждение_(ТекстСообщения);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры


&НаСервереБезКонтекста
Функция ВыполнитьКонвертациюКлючаНаСервере(Ключ, Пароль, РезультатКонвертации, ТекстСообщения)
	РезультатКонвертации = Новый Массив;
	лПараметры = Новый Структура;
	лПараметры.Вставить("Key", Ключ);
	лПараметры.Вставить("Password", Пароль);
	ОтветПоКонвертации = скEDI_КомандыEDIПровайдеру.ПолучитьРезультатКомандыEDIПровайдеру("jks", лПараметры);
	Если ОтветПоКонвертации.Code = 0 Тогда
		Certs = Неопределено;
		ЕДРПОУ = "";
		ДРФО = "";
		ДатаС = '00010101';
		Если ОтветПоКонвертации.Свойство("Certs", Certs) Тогда
			Если ТипЗнч(Certs) = Тип("Массив") Тогда
				Для Каждого ЭлементCerts Из Certs Цикл
					лПараметрыКомандыEDIПровайдеру = Новый Структура;
					лПараметрыКомандыEDIПровайдеру.Вставить("Cert", ЭлементCerts);

					лСтруктураСОтветомEDIПровайдера = скEDI_КомандыEDIПровайдеру.ПолучитьРезультатКомандыEDIПровайдеру("getcertinfo", лПараметрыКомандыEDIПровайдеру);
					Если лСтруктураСОтветомEDIПровайдера.Code = 0 Тогда
						Если не ЗначениеЗаполнено(ДатаС) Тогда
							ДатаС = скEDI_ОбщегоНазначения.СтрокаВДату(лСтруктураСОтветомEDIПровайдера.Info.DateBeg);
						КонецЕсли;
						ЕДРПОУ = лСтруктураСОтветомEDIПровайдера.Info.EDRPOU;
						ДРФО = лСтруктураСОтветомEDIПровайдера.Info.DRFO;
						
						ИмяФайла = ЕДРПОУ + "_" + ДРФО + "_" + Строка(Новый УникальныйИдентификатор) + ".CRT";
						РезультатКонвертации.Добавить(Новый Структура("ИмяФайла, СодержимоеФайла", ИмяФайла, ЭлементCerts));
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		Keys = Неопределено;
		Если ОтветПоКонвертации.Свойство("Keys", Keys) Тогда
			Если ТипЗнч(Keys) = Тип("Массив") Тогда
				Для Каждого ЭлементKeys Из Keys Цикл
					ИмяФайла = ЕДРПОУ + "_" + ДРФО + "_" + Формат(ДатаС, "ДФ=yyyyMMddhhmmss") + ".ZS2";
					РезультатКонвертации.Добавить(Новый Структура("ИмяФайла, СодержимоеФайла", ИмяФайла, ЭлементKeys));
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		Возврат Истина;
	Иначе
		ТекстСообщения = ОтветПоКонвертации.Message;
		Возврат Ложь;
	КонецЕсли;
КонецФункции

&НаКлиенте
Процедура КаталогРезультатаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	лДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	лДиалогВыбораФайла.Каталог = КаталогРезультата;
	лДиалогВыбораФайла.МножественныйВыбор = Ложь;
	лДиалогВыбораФайла.Заголовок = НСтр("ru = 'Выберите каталог для сохранения результата'; uk = 'Виберіть каталог для збереження результату'");

	Если лДиалогВыбораФайла.Выбрать() Тогда
		КаталогРезультата = лДиалогВыбораФайла.Каталог;
	КонецЕсли;
КонецПроцедуры

