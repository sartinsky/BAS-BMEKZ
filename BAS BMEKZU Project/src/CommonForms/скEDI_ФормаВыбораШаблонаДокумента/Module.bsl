
&НаСервере
Процедура ОбновитьСписокШаблоновНаСервере(ТекЕДРПОУВладельца = "", ТекКодШаблона = "", ТекВерсия = "")
	ЭлементыДереваШаблонов = ДеревоШаблонов.ПолучитьЭлементы();
	ЭлементыДереваШаблонов.Очистить();
	
	ТекСтрокаОрганизацияВладелец = Неопределено;
	ТекСтрокаШаблон = Неопределено;
	ТекСтрокаШаблонРодитель = Неопределено;
	ТекСтрокаВерсия = Неопределено;
	ТекСтрокаВерсияРодитель = Неопределено;
	
	лПараметрыКомандыEDIПровайдеру = Новый Структура;
	
	Если ПоЕДРПОУ Тогда
		лПараметрыКомандыEDIПровайдеру.Вставить("TmplOwnerCode", ЕДРПОУВладельца);
	Иначе
		лПараметрыКомандыEDIПровайдеру.Вставить("TmplOwnerCode", "");
	КонецЕсли;
	Если ПолучатьШаблоныСАрхива Тогда
		лПараметрыКомандыEDIПровайдеру.Вставить("IncludeArchived", 1);
	Иначе
		лПараметрыКомандыEDIПровайдеру.Вставить("IncludeArchived", 0);
	КонецЕсли;
	                                         
	лСтруктураСОтветомEDIПровайдера = скEDI_КомандыEDIПровайдеру.ПолучитьРезультатКомандыEDIПровайдеру("templates", лПараметрыКомандыEDIПровайдеру);
	
	Если лСтруктураСОтветомEDIПровайдера.Code <> 0 Тогда
		Сообщить("Ошибка получения списка шаблонов: " + лСтруктураСОтветомEDIПровайдера.Message);
	Иначе
		Для Каждого Организация Из лСтруктураСОтветомEDIПровайдера.Orgs Цикл
			СтрокаОрганизация = ЭлементыДереваШаблонов.Добавить();
			СтрокаОрганизация.Уровень = 1;
			СтрокаОрганизация.ЕДРПОУВладельца = Организация.Code;
			СтрокаОрганизация.Код = Организация.Code;
			СтрокаОрганизация.Наименование = Организация.Name;
			
			Если ТекЕДРПОУВладельца <> "" Тогда
				Если ТекЕДРПОУВладельца = Организация.Code Тогда
					ТекСтрокаОрганизацияВладелец = СтрокаОрганизация;
				КонецЕсли;
			КонецЕсли;
			
			СоответствиеСтрокШаблоны = Новый Соответствие;
			Для Каждого Шаблон Из Организация.Templates Цикл  //Templates
				СтрокаШаблоны = СоответствиеСтрокШаблоны.Получить(Шаблон.Code);
				Если СтрокаШаблоны = Неопределено Тогда
					СтрокаШаблоны = СтрокаОрганизация.ПолучитьЭлементы().Добавить();
					СтрокаШаблоны.Уровень = 2;
					СтрокаШаблоны.КоличествоВерсий = 0;
					СтрокаШаблоны.ЕДРПОУВладельца = Организация.Code;
					СтрокаШаблоны.КодШаблона = Шаблон.Code;
					СтрокаШаблоны.Код = Шаблон.Code;
					СтрокаШаблоны.Наименование = Шаблон.Name;
					СтрокаШаблоны.SDocType = Формат(Шаблон.SDocType, "ЧГ=0");
					СтрокаШаблоны.ТипДокумента = скEDI_ОбщегоНазначения.ПолучитьТипДокументаMEDocПоКоду(СтрокаШаблоны.SDocType);
					СоответствиеСтрокШаблоны.Вставить(Шаблон.Code, СтрокаШаблоны);
					
					Если ТекКодШаблона <> "" Тогда
						Если ТекЕДРПОУВладельца = "" Тогда
							Если ТекКодШаблона = Шаблон.Code Тогда
								ТекСтрокаШаблон = СтрокаШаблоны;
								ТекСтрокаШаблонРодитель = СтрокаОрганизация;
							КонецЕсли;
						Иначе
							Если ТекЕДРПОУВладельца = Организация.Code Тогда
								Если ТекКодШаблона = Шаблон.Code Тогда
									ТекСтрокаШаблон = СтрокаШаблоны;
									ТекСтрокаШаблонРодитель = СтрокаОрганизация;
								КонецЕсли;
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				СтрокаШаблоны.КоличествоВерсий = СтрокаШаблоны.КоличествоВерсий + 1;
				СтрокаВерсия = СтрокаШаблоны.ПолучитьЭлементы().Добавить();
				СтрокаВерсия.Уровень = 3;
				СтрокаВерсия.ЕДРПОУВладельца = Организация.Code;
				СтрокаВерсия.КодШаблона = Шаблон.Code;
				СтрокаВерсия.Код = Шаблон.Version;
				СтрокаВерсия.Наименование = Шаблон.Name;
				СтрокаВерсия.SDocType = Формат(Шаблон.SDocType, "ЧГ=0");
				СтрокаВерсия.ТипДокумента = скEDI_ОбщегоНазначения.ПолучитьТипДокументаMEDocПоКоду(СтрокаВерсия.SDocType);
				СтрокаВерсия.Access = Шаблон.Access;
				СтрокаВерсия.ДоступКШаблону = скEDI_ОбщегоНазначения.ПолучитьТипДоступаКШаблону(СтрокаВерсия.Access);
				СтрокаВерсия.Архив = Шаблон.Archived = 1;
				ДатаВерсии = Дата("00010101");
				Если скEDI_ОбщегоНазначения.ПолучитьДатуФорматаДДММГГГГсРазделителем(Шаблон.ModDate, ДатаВерсии) Тогда
					СтрокаВерсия.ДатаМодификации = ДатаВерсии;
				КонецЕсли;
				
				Если ТекВерсия <> "" Тогда
					Если ТекВерсия = Строка(Шаблон.Version) Тогда
						ТекСтрокаВерсия = СтрокаВерсия;
						ТекСтрокаВерсияРодитель = СтрокаШаблоны;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	ИдентификаторТекСтроки = Неопределено;
	Если ТекЕДРПОУВладельца <> "" Тогда
		Если ТекСтрокаОрганизацияВладелец = Неопределено Тогда
			Сообщить(НСтр("ru = 'Не найдена Организация (владелец шаблона) с кодом: '; uk = 'Не знайдено Організацію (власник шаблону) з кодом: '") + ТекЕДРПОУВладельца);
		Иначе
			ИдентификаторТекСтроки = ТекСтрокаОрганизацияВладелец.ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;
	Если ТекКодШаблона <> "" Тогда
		Если ТекСтрокаШаблон = Неопределено Тогда
			Сообщить(НСтр("ru = 'Не найден шаблон с кодом: '; uk = 'Не знайдено шаблон з кодом: '") + ТекКодШаблона);
		Иначе
			Если ТекСтрокаОрганизацияВладелец = Неопределено Тогда
				ИдентификаторТекСтроки = ТекСтрокаШаблон.ПолучитьИдентификатор();
			Иначе
				Если ТекСтрокаШаблонРодитель = ТекСтрокаОрганизацияВладелец Тогда
					ИдентификаторТекСтроки = ТекСтрокаШаблон.ПолучитьИдентификатор();
				Иначе
					Сообщить(НСтр("ru = 'Найденый шаблон не соответствует владельцу шаблона!'; uk = 'Знайдений шаблон не відповідає власнику шаблону'"));
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Если ТекВерсия <> "" Тогда
		Если ТекСтрокаВерсия = Неопределено Тогда
			Сообщить(НСтр("ru = 'Не найден шаблон с версией: '; uk = 'Не знайдено шаблон з версією: '") + ТекВерсия);
		Иначе
			Если ТекСтрокаШаблон = Неопределено Тогда
				ИдентификаторТекСтроки = ТекСтрокаВерсия.ПолучитьИдентификатор();
			Иначе
				Если ТекСтрокаВерсияРодитель = ТекСтрокаШаблон Тогда
					ИдентификаторТекСтроки = ТекСтрокаВерсия.ПолучитьИдентификатор();
				Иначе
					Сообщить(НСтр("ru = 'Найденая версия не соответствует коду шаблона!'; uk = 'Знайдена версія не відповідає коду шаблону'"));
					ИдентификаторТекСтроки = ТекСтрокаВерсия.ПолучитьИдентификатор();
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ИдентификаторТекСтроки <> Неопределено Тогда
		Элементы.ДеревоШаблонов.ТекущаяСтрока = ИдентификаторТекСтроки;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ТекЕДРПОУВладельца = Неопределено;
	Если НЕ Параметры.Свойство("ТекЕДРПОУВладельца", ТекЕДРПОУВладельца) Тогда
		ТекЕДРПОУВладельца = "";
	КонецЕсли;
	ТекКодШаблона = Неопределено;
	Если НЕ Параметры.Свойство("ТекИмяШаблона", ТекКодШаблона) Тогда
		ТекКодШаблона = "";
	КонецЕсли;
	ТекВерсия = Неопределено;
	Если НЕ Параметры.Свойство("ТекВерсияШаблона", ТекВерсия) Тогда
		ТекВерсия = "";
	КонецЕсли;
	
	ЕДРПОУВладельца = "";
	пЕДРПОУВладельца = Неопределено;
	Если Параметры.Свойство("ЕДРПОУВладельца", пЕДРПОУВладельца) Тогда
		ЕДРПОУВладельца = пЕДРПОУВладельца;
	Иначе
		Организация = Неопределено;
		Если Параметры.Свойство("Организация", Организация) Тогда
			ЕДРПОУВладельца = Организация.Код;
		Иначе
			ВидЭлектронногоДокумента = Неопределено;
			Если Параметры.Свойство("ВидЭлектронногоДокумента", ВидЭлектронногоДокумента) Тогда
				Организация = ВидЭлектронногоДокумента.Владелец;
				ЕДРПОУВладельца = Организация.Код;
			Иначе
				ЕДРПОУВладельца = "";
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	пДляСозданияЭлектронныхДокументов = Неопределено;
	Если Параметры.Свойство("ДляСозданияЭлектронныхДокументов", пДляСозданияЭлектронныхДокументов) Тогда
		ДляСозданияЭлектронныхДокументов = пДляСозданияЭлектронныхДокументов;
	Иначе
		ДляСозданияЭлектронныхДокументов = Ложь;
	КонецЕсли;
	
	Если ДляСозданияЭлектронныхДокументов Тогда
		ПоЕДРПОУ = Истина;
		ПолучатьШаблоныСАрхива = Ложь;
	Иначе
		ПоЕДРПОУ = Ложь;
		ПолучатьШаблоныСАрхива = Истина;
	КонецЕсли;
	
	ОбновитьСписокШаблоновНаСервере(ТекЕДРПОУВладельца, ТекКодШаблона, ТекВерсия);
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	ТекущиеДанныеДереваШаблонов = Элементы.ДеревоШаблонов.ТекущиеДанные;
	Если ТекущиеДанныеДереваШаблонов <> Неопределено Тогда
		Если ТекущиеДанныеДереваШаблонов.Уровень = 1 Тогда
			
		ИначеЕсли ТекущиеДанныеДереваШаблонов.Уровень = 2 Тогда
			Если ТекущиеДанныеДереваШаблонов.КоличествоВерсий = 1 Тогда
				ВыборСтруктура = Новый Структура("ЕДРПОУВладельца, КодШаблона, Версия, Наименование");
				ВыборСтруктура.ЕДРПОУВладельца = ТекущиеДанныеДереваШаблонов.ЕДРПОУВладельца;
				ВыборСтруктура.КодШаблона = ТекущиеДанныеДереваШаблонов.КодШаблона;
				ВыборСтруктура.Версия = "";
				ВыборСтруктура.Наименование = ТекущиеДанныеДереваШаблонов.Наименование;
				Закрыть(ВыборСтруктура);
			ИначеЕсли ТекущиеДанныеДереваШаблонов.КоличествоВерсий > 1 Тогда
				Сообщить(НСтр("ru = 'Выберите шаблон необходимой версии.'; uk = 'Виберіть шаблон потрібної версії.'"));
			КонецЕсли;
		ИначеЕсли ТекущиеДанныеДереваШаблонов.Уровень = 3 Тогда
			ВыборСтруктура = Новый Структура("ЕДРПОУВладельца, КодШаблона, Версия, Наименование");
			ВыборСтруктура.ЕДРПОУВладельца = ТекущиеДанныеДереваШаблонов.ЕДРПОУВладельца;
			ВыборСтруктура.КодШаблона = ТекущиеДанныеДереваШаблонов.КодШаблона;
			ВыборСтруктура.Версия = ТекущиеДанныеДереваШаблонов.Код;
				ВыборСтруктура.Наименование = ТекущиеДанныеДереваШаблонов.Наименование;
			Закрыть(ВыборСтруктура);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоШаблоновПриАктивизацииСтроки(Элемент)
	ТекущиеДанныеДереваШаблонов = Элементы.ДеревоШаблонов.ТекущиеДанные;
	Если ТекущиеДанныеДереваШаблонов <> Неопределено Тогда
		Если ТекущиеДанныеДереваШаблонов.Уровень = 1 Тогда
			Элементы.ФормаВыбрать.Доступность = Ложь;
		ИначеЕсли ТекущиеДанныеДереваШаблонов.Уровень = 2 Тогда
			Если ТекущиеДанныеДереваШаблонов.КоличествоВерсий = 1 Тогда
				Элементы.ФормаВыбрать.Доступность = Истина;
			Иначе
				Элементы.ФормаВыбрать.Доступность = Ложь;
			КонецЕсли;
		ИначеЕсли ТекущиеДанныеДереваШаблонов.Уровень = 3 Тогда
			Элементы.ФормаВыбрать.Доступность = Истина;
		КонецЕсли;
	Иначе
		Элементы.ФормаВыбрать.Доступность = Ложь;
	КонецЕсли;
КонецПроцедуры


