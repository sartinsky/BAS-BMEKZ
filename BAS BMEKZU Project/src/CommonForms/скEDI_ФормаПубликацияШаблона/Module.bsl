
&НаКлиенте
Процедура ИмяФайлаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	лДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	лДиалогВыбораФайла.ПолноеИмяФайла = ИмяФайла;
	лДиалогВыбораФайла.МножественныйВыбор = Ложь;
	лДиалогВыбораФайла.Заголовок = НСтр("ru = 'Выберите файлов шаблона'; uk = 'Виберіть файл шаблону'");

	Если лДиалогВыбораФайла.Выбрать() Тогда
		ИмяФайла = лДиалогВыбораФайла.ПолноеИмяФайла;
		
		ФайлПравильный = Ложь;
		
		ЧтениеXML = Новый ЧтениеXML;
		ЧтениеXML.ОткрытьФайл(ИмяФайла);
		//ЧтениеXML.УстановитьСтроку(КвитанцияПолныйТекст);
		ЧтениеXML.Прочитать();
		мДОМ = Новый ПостроительDOM;
		мДокументДОМ 	  = мДОМ.Прочитать(ЧтениеXML);
		Для Каждого ДочернийУзел1 Из мДокументДОМ.ДочерниеУзлы Цикл
			Если ВРег(ДочернийУзел1.ИмяУзла) = "TEMPLATE" Тогда
				КодШаблона = "";
				ТипДокумента = ПредопределенноеЗначение("Перечисление.скEDI_ТипыДокументовMEDoc.ПустаяСсылка");
				НаименованиеШаблона = "";
				ФайлПравильный = Истина;
				Для Каждого АтрибутУзла Из ДочернийУзел1.Атрибуты Цикл
					Если ВРег(АтрибутУзла.Имя) = "CHARCODE" Тогда
						КодШаблона = АтрибутУзла.Значение;
					ИначеЕсли ВРег(АтрибутУзла.Имя) = "NAME" Тогда
						НаименованиеШаблона = АтрибутУзла.Значение;
					ИначеЕсли ВРег(АтрибутУзла.Имя) = "SDOCTYPE" Тогда
						ТипДокумента = скEDI_ОбщегоНазначения.ПолучитьТипДокументаMEDocПоКоду(АтрибутУзла.Значение);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		Если не ФайлПравильный Тогда
			ТекстСообщения = НСтр("ru = 'Выбраный файл не является шаблоном электронного документа'; uk = 'Вибраний файл не є шаблоном електронного документу'");
			скEDI_ОбщегоНазначенияКлиент.ПоказатьПредупреждение_(ТекстСообщения);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОпубликоватьШаблон(Команда)
	Если не ЗначениеЗаполнено(ОрганизацияEDI) Тогда
		Сообщить(НСтр("ru = 'Не заполнено поле ""Организация""'; uk = 'Не заповнено поле ""Організація""'"));
		Возврат;
	КонецЕсли;
	Если не ЗначениеЗаполнено(ИмяФайла) Тогда
		Сообщить(НСтр("ru = 'Не заполнено поле ""Имя файла""'; uk = 'Не заповнено поле ""Ім''я файла""'"));
		Возврат;
	КонецЕсли;
	Если не ЗначениеЗаполнено(КодШаблона) Тогда
		Сообщить(НСтр("ru = 'Не заполнено поле ""Код шаблона""'; uk = 'Не заповнено поле ""Код шаблону""'"));
		Возврат;
	КонецЕсли;
	Если не ЗначениеЗаполнено(НаименованиеШаблона) Тогда
		Сообщить(НСтр("ru = 'Не заполнено поле ""Наименование шаблона""'; uk = 'Не заповнено поле ""Назва шаблону""'"));
		Возврат;
	КонецЕсли;
	
	Подписанты = Новый Структура;
	МассивПодписей = Новый Массив;
	МассивПодписей.Добавить(Неопределено);
	Подписанты.Вставить("МассивПодписей", МассивПодписей);
	
	МассивПодписей = Новый Массив;
	//МассивПодписей.Добавить(Новый Структура("ОрганизацияEDI, ПодписьШифрования", Организация, Неопределено));
	Подписанты.Вставить("МассивПодписейШифрования", МассивПодписей);
	
	ВыполняемыеОперации = Новый Массив;
	ВыполняемыеОперации.Добавить(НСтр("ru = 'Опубликовать шаблон'; uk = 'Опублікувати шаблон'"));
	
	ВерсияМассиваДанныхПоДокументам = Новый УникальныйИдентификатор;
	лПараметрыФормыНастройкиПодписейЭлектронныхДокументов = Новый Структура("ВерсияМассиваДанныхПоДокументам,ВыполняемыеОперации,Организация,Подписанты", ВерсияМассиваДанныхПоДокументам, ВыполняемыеОперации, ОрганизацияEDI, Подписанты);
	
	Если скEDI_ОбщегоНазначения.ЭтоПлатформа82() Тогда		
		лФормаНастройкиПараметровПодписиДокументов = ПолучитьФорму("ОбщаяФорма.скEDI_ФормаНастройкиПодписейЭлектронныхДокументов", лПараметрыФормыНастройкиПодписейЭлектронныхДокументов, ЭтаФорма);
		ВыполнитьПубликациюШаблона(лФормаНастройкиПараметровПодписиДокументов.ОткрытьМодально());
	Иначе
		скEDI_ОткрытиеФормБезМодальности.ОткрытьФормуНастройкиПараметровПодписиДокументов(лПараметрыФормыНастройкиПодписейЭлектронныхДокументов
																						, ЭтаФорма 
																						, "ВыполнитьПубликациюШаблона");
	КонецЕсли;
КонецПроцедуры


&НаСервереБезКонтекста
Функция ВыполнитьПубликациюШаблонаНаСервере(Организация, СодержимоеФайлаBase64, КодШаблона, НаименованиеШаблона, ТипДокумента, Сертификат, Ключ, Пароль, Сообщение)
	лПараметры = Новый Структура;
	лПараметры.Вставить("Edrpou", Организация.Код);
	лПараметры.Вставить("Name", Организация.Наименование);
	КодТипаДокумента = скEDI_ОбщегоНазначения.ПолучитьКодДокументаПоТипуДокументаMEDoc(ТипДокумента);
	Если ЗначениеЗаполнено(КодТипаДокумента) Тогда
		лПараметры.Вставить("DocType", КодТипаДокумента);
	КонецЕсли;
	лПараметры.Вставить("Charcode", КодШаблона);
	лПараметры.Вставить("DocName", НаименованиеШаблона);
	лПараметры.Вставить("Body", СодержимоеФайлаBase64);
	лПараметры.Вставить("Cert",Сертификат);
	лПараметры.Вставить("Key", Ключ);
	лПараметры.Вставить("Password", Пароль);
	
	РезультатПубликации = скEDI_КомандыEDIПровайдеру.ПолучитьРезультатКомандыEDIПровайдеру("publishtmpl", лПараметры);
	
	Если РезультатПубликации.Code = 0 Тогда
		Сообщение = "";
		Возврат Истина;
	ИначеЕсли РезультатПубликации.Code = 46 Тогда
		Сообщение = НСтр("ru = 'Бланк шаблона не уникален, дублирование запрещено.'; uk = 'Бланк шаблону не унікальний, дублювання заборонено.'");
		Возврат Ложь;
	Иначе
		Сообщение = РезультатПубликации.Message;
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ВыполнитьПубликациюШаблона(ПараметрыОпераций, ДополнительныеПараметрыОпераций = Неопределено) Экспорт
	Если ПараметрыОпераций <> Неопределено Тогда
		Если ПараметрыОпераций.ВерсияМассиваДанныхПоДокументам = ВерсияМассиваДанныхПоДокументам Тогда
			Сертификат = "";
			Ключ = "";
			Пароль = "";
			Флаг = Ложь;
			Для Каждого П Из ПараметрыОпераций.ТаблицаНеобходимыхПодписей Цикл
				Сертификат = П.ТелоСертификата;
				Ключ = П.ТелоСекретногоКлюча;
				Пароль = П.ПарольСекретногоКлюча;
				Флаг = Истина;
				Прервать;
			КонецЦикла;
			Если не Флаг Тогда
				Сообщить(НСтр("ru = 'Не заполнены параметры ЭЦП'; uk = 'Не заповнено параметри ЕЦП'"));
				Возврат;
			КонецЕсли;
			
			
			ТекФайлОбъект = Новый Файл(ИмяФайла);
			Если ТекФайлОбъект.Существует() Тогда
				Сообщение = "";
				СодержимоеФайлаBase64 = Base64Строка(Новый ДвоичныеДанные(ИмяФайла));
				Если ВыполнитьПубликациюШаблонаНаСервере(ОрганизацияEDI, СодержимоеФайлаBase64, КодШаблона, НаименованиеШаблона, ТипДокумента, Сертификат, Ключ, Пароль, Сообщение) Тогда
					ТекстСообщения = НСтр("ru = 'Шаблон опубликован'; uk = 'Шаблон опубліковано'");
					скEDI_ОбщегоНазначенияКлиент.ПоказатьПредупреждение_(ТекстСообщения);
					//Закрыть(Неопределено);
				Иначе
					ТекстСообщения = НСтр("ru = 'При публикации шаблона возникли ошибки:'; uk = 'При публікації шаблону виникли помилки:'");
					ТекстСообщения = ТекстСообщения + Символы.ПС + Сообщение;
					скEDI_ОбщегоНазначенияКлиент.ПоказатьПредупреждение_(ТекстСообщения);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

