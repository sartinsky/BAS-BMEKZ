////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПолеСообщения	= Параметры.Сообщение;
	Заголовок		= Параметры.Заголовок;
	
	Параметры.Свойство("ГиперссылкаТекст",		ГиперссылкаТекст);
	Параметры.Свойство("ГиперссылкаНавигация",	ГиперссылкаНавигация);
	Параметры.Свойство("ГиперссылкаИмяФормы",	ГиперссылкаИмяФормы);
	Параметры.Свойство("ГиперссылкаПараметры",	ГиперссылкаПараметры);
	
	Параметры.Свойство("КлючОбъектаБольшеНеПоказывать",		КлючОбъектаБольшеНеПоказывать);
	Параметры.Свойство("КлючНастроекБольшеНеПоказывать",	КлючНастроекБольшеНеПоказывать);
	
	Если НЕ ПустаяСтрока(КлючОбъектаБольшеНеПоказывать)
		И НЕ ПустаяСтрока(КлючНастроекБольшеНеПоказывать) Тогда
		
		БольшеНеПоказывать = ХранилищеОбщихНастроек.Загрузить(КлючОбъектаБольшеНеПоказывать, КлючНастроекБольшеНеПоказывать);
		
		Если БольшеНеПоказывать Тогда
			Отказ	= Истина;
		КонецЕсли;
		
	Иначе
		Элементы.БольшеНеПоказывать.Видимость	= Ложь;
	КонецЕсли;
	
	УстановитьРазмерыПоляСообщения();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)

	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(КлючОбъектаБольшеНеПоказывать)
		И НЕ ПустаяСтрока(КлючНастроекБольшеНеПоказывать) Тогда
		
		СохранитьЗначениеБольшеНеПоказыватьНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ГиперссылкаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НЕ ПустаяСтрока(ГиперссылкаНавигация) Тогда
		Попытка
			ПерейтиПоНавигационнойСсылке(ГиперссылкаНавигация);
		Исключение
			//обработка исключения не нужна
		КонецПопытки;
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ГиперссылкаИмяФормы) Тогда
		Попытка
			ОткрытьФорму(ГиперссылкаИмяФормы, ГиперссылкаПараметры);
		Исключение
			//обработка исключения не нужна
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура СохранитьЗначениеБольшеНеПоказыватьНаСервере()
	
	ХранилищеОбщихНастроек.Сохранить(КлючОбъектаБольшеНеПоказывать, КлючНастроекБольшеНеПоказывать, БольшеНеПоказывать);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьРазмерыПоляСообщения()
	
	ТекстСообщения	= Новый ТекстовыйДокумент;
	ТекстСообщения.УстановитьТекст(ПолеСообщения);
	
	ВысотаСообщения	= ТекстСообщения.КоличествоСтрок();
	
	ШиринаСообщения	= 0;
	Для Ном = 1 По ВысотаСообщения Цикл
		ШиринаСообщения	= Макс(СтрДлина(ТекстСообщения.ПолучитьСтроку(Ном)), ШиринаСообщения);
	КонецЦикла;
	
	Элементы.ПолеСообщения.Высота	= Макс(Мин(ВысотаСообщения, 27), 12) - 2;
	Элементы.ПолеСообщения.Ширина	= Макс(Мин(ШиринаСообщения, 94), 44) + 6;
	
КонецПроцедуры
