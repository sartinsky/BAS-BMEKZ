
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	лНазваниеОпрерации = Неопределено;
	Если Параметры.Свойство("НазваниеОпрерации", лНазваниеОпрерации) Тогда
		НазваниеОпрерации = лНазваниеОпрерации;
		Если НазваниеОпрерации = "ОтправитьСсылкуНаДокумент" Тогда
			Элементы.Применить.Заголовок = НСтр("ru = 'Отправить ссылку на документ'; uk = 'Відправити посилання на документ'");
		ИначеЕсли НазваниеОпрерации = "ПодписатьОтправитьТолькоСсылкуНаДокумент" Тогда
			Элементы.Применить.Заголовок = НСтр("ru = 'Отправить только ссылку на документ'; uk = 'Відправити тільки посилання на документ'");
		Иначе
			Элементы.Применить.Заголовок = НазваниеОпрерации;
		КонецЕсли;
	КонецЕсли;
	
	лОрганизация = Неопределено;
	Если Параметры.Свойство("Организация", лОрганизация) Тогда
		Организация = лОрганизация;
	КонецЕсли;
	
	лЕДРПОУКонтрагента = Неопределено;
	Если Параметры.Свойство("ЕДРПОУКонтрагента", лЕДРПОУКонтрагента) Тогда
		ЕДРПОУКонтрагента = лЕДРПОУКонтрагента;
	КонецЕсли;
	
	лКонтрагент = Неопределено;
	Если Параметры.Свойство("Контрагент", лКонтрагент) Тогда
		Контрагент = лКонтрагент;
	КонецЕсли;
	
	лЭлектронныйДокумент = Неопределено;
	Если Параметры.Свойство("ЭлектронныйДокумент", лЭлектронныйДокумент) Тогда
		ЭлектронныйДокумент = лЭлектронныйДокумент;
	КонецЕсли;
	
	лИмяФайлаЭлектронногоДокумента = Неопределено;
	Если Параметры.Свойство("ИмяФайлаЭлектронногоДокумента", лИмяФайлаЭлектронногоДокумента) Тогда
		ИмяФайлаЭлектронногоДокумента = лИмяФайлаЭлектронногоДокумента;
	КонецЕсли;
	
	лМассивЭлектронныхАдресов = Неопределено;
	Если Параметры.Свойство("МассивЭлектронныхАдресов", лМассивЭлектронныхАдресов) Тогда
		Если ТипЗнч(лМассивЭлектронныхАдресов) = Тип("ФиксированныйМассив") или ТипЗнч(лМассивЭлектронныхАдресов) = Тип("Массив") Тогда
			Для Каждого ЭлементМассива Из лМассивЭлектронныхАдресов Цикл
				НоваяСтрокаНастройкиСсылкиНаДокумент = НастройкиСсылкиНаДокумент.Добавить();
				НоваяСтрокаНастройкиСсылкиНаДокумент.Отметка = Истина;
				НоваяСтрокаНастройкиСсылкиНаДокумент.ЭлектроннаяПочта = ЭлементМассива.ЭлектроннаяПочта;
				НоваяСтрокаНастройкиСсылкиНаДокумент.Комментарий = ЭлементМассива.Комментарий;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЕДРПОУКонтрагента) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	скEDI_КонтактныеДанныеКонтрагентов.ЕДРПОУКонтрагента КАК ЕДРПОУКонтрагента,
		               |	скEDI_КонтактныеДанныеКонтрагентов.ЭлектроннаяПочта КАК ЭлектроннаяПочта,
		               |	скEDI_КонтактныеДанныеКонтрагентов.ОтправлятьСсылкуНаДокументАвтоматически КАК ОтправлятьСсылкуНаДокументАвтоматически,
		               |	скEDI_КонтактныеДанныеКонтрагентов.Контрагент КАК Контрагент,
		               |	скEDI_КонтактныеДанныеКонтрагентов.Комментарий КАК Комментарий
		               |ИЗ
		               |	РегистрСведений.скEDI_КонтактныеДанныеКонтрагентов КАК скEDI_КонтактныеДанныеКонтрагентов
		               |ГДЕ
		               |	скEDI_КонтактныеДанныеКонтрагентов.ЕДРПОУКонтрагента = &ЕДРПОУКонтрагента";
		Запрос.УстановитьПараметр("ЕДРПОУКонтрагента", ЕДРПОУКонтрагента);
		ВыборкаРезультатаЗапроса = Запрос.Выполнить().Выбрать();
		Пока ВыборкаРезультатаЗапроса.Следующий() Цикл
			НайденаяСтрока = Неопределено;
			Для Каждого СтрокаНастройкиСсылкиНаДокумент из НастройкиСсылкиНаДокумент Цикл
				Если СокрЛП(ВРег(СтрокаНастройкиСсылкиНаДокумент.ЭлектроннаяПочта)) = СокрЛП(ВРег(ВыборкаРезультатаЗапроса.ЭлектроннаяПочта)) Тогда
					НайденаяСтрока = СтрокаНастройкиСсылкиНаДокумент;
					Прервать;;
				КонецЕсли;
			КонецЦикла;
			
			Если НайденаяСтрока = Неопределено Тогда
				НоваяСтрокаНастройкиСсылкиНаДокумент = НастройкиСсылкиНаДокумент.Добавить();
				НоваяСтрокаНастройкиСсылкиНаДокумент.Отметка = ВыборкаРезультатаЗапроса.ОтправлятьСсылкуНаДокументАвтоматически;
				НоваяСтрокаНастройкиСсылкиНаДокумент.ЭлектроннаяПочта = ВыборкаРезультатаЗапроса.ЭлектроннаяПочта;
				НоваяСтрокаНастройкиСсылкиНаДокумент.Комментарий = ВыборкаРезультатаЗапроса.Комментарий;
			Иначе
				Если не НайденаяСтрока.Отметка Тогда
					Если ВыборкаРезультатаЗапроса.ОтправлятьСсылкуНаДокументАвтоматически Тогда
						НайденаяСтрока.Отметка = Истина;
					КонецЕсли;
				КонецЕсли;
				Если не ЗначениеЗаполнено(НайденаяСтрока.Комментарий) Тогда
					НайденаяСтрока.Комментарий = ВыборкаРезультатаЗапроса.Комментарий;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Применить(Команда)
	ВозвращаемыеДанныеСтруктура = Новый Структура;
	МассивСсылокНаДокумент = Новый Массив;
	Для Каждого СтрокаТаблицы Из НастройкиСсылкиНаДокумент Цикл
		Если СтрокаТаблицы.Отметка Тогда
			ТекСтруктураСсылки = Новый Структура;
			ТекСтруктураСсылки.Вставить("ЭлектроннаяПочта", СтрокаТаблицы.ЭлектроннаяПочта);
			ТекСтруктураСсылки.Вставить("Комментарий", СтрокаТаблицы.Комментарий);
			МассивСсылокНаДокумент.Добавить(Новый ФиксированнаяСтруктура(ТекСтруктураСсылки));
		КонецЕсли;
	КонецЦикла;
	ВозвращаемыеДанныеСтруктура.Вставить("МассивСсылокНаДокумент", Новый ФиксированныйМассив(МассивСсылокНаДокумент));
	ВозвращаемыеДанныеСтруктура.Вставить("ИмяФайлаЭлектронногоДокумента", ИмяФайлаЭлектронногоДокумента);
	ВозвращаемыеДанныеСтруктура.Вставить("ЭлектронныйДокумент", ЭлектронныйДокумент);
	ВозвращаемыеДанныеСтруктура.Вставить("НазваниеОпрерации", НазваниеОпрерации);
	
	ВозвращаемыеДанные = Новый ФиксированнаяСтруктура(ВозвращаемыеДанныеСтруктура);
	Закрыть(ВозвращаемыеДанные);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПодготовитьПараметрыДляФомыКонтактныеДанныеКонтрагентов(пЕДРПОУКонтрагента, пКонтрагент, пЭлектроннаяПочта, пКомментарий)
	Результат = Новый Структура;
	
	КлючЗаполнен = Ложь;
	Если ЗначениеЗаполнено(пЭлектроннаяПочта) Тогда
		КонтактныеДанныеКонтрагентовМенеджерЗаписи = РегистрыСведений.скEDI_КонтактныеДанныеКонтрагентов.СоздатьМенеджерЗаписи();
		КонтактныеДанныеКонтрагентовМенеджерЗаписи.ЕДРПОУКонтрагента = пЕДРПОУКонтрагента;
		КонтактныеДанныеКонтрагентовМенеджерЗаписи.ЭлектроннаяПочта = пЭлектроннаяПочта;
		КонтактныеДанныеКонтрагентовМенеджерЗаписи.Прочитать();
		Если КонтактныеДанныеКонтрагентовМенеджерЗаписи.Выбран() Тогда
			Ключ = РегистрыСведений.скEDI_КонтактныеДанныеКонтрагентов.СоздатьКлючЗаписи(Новый Структура("ЕДРПОУКонтрагента, ЭлектроннаяПочта", пЕДРПОУКонтрагента, пЭлектроннаяПочта));
			Результат.Вставить("Ключ", Ключ);
			КлючЗаполнен = Истина;
		КонецЕсли;
	КонецЕсли;
	Если Не КлючЗаполнен Тогда
		ЗначенияЗаполнения = Новый Структура;
		ЗначенияЗаполнения.Вставить("ЕДРПОУКонтрагента", пЕДРПОУКонтрагента);
		ЗначенияЗаполнения.Вставить("ЭлектроннаяПочта", пЭлектроннаяПочта);
		ЗначенияЗаполнения.Вставить("Контрагент", пКонтрагент);
		ЗначенияЗаполнения.Вставить("Комментарий", пКомментарий);
		Результат.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	КонецЕсли;
	МассивТолькоПросмотр = Новый Массив;
	МассивТолькоПросмотр.Добавить("ЕДРПОУКонтрагента");
	МассивТолькоПросмотр.Добавить("Контрагент");
	Результат.Вставить("МассивТолькоПросмотр", МассивТолькоПросмотр);
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура ДобавитьЭлектроннуюПочту(Команда)
	Если ЗначениеЗаполнено(ЕДРПОУКонтрагента) Тогда
		ПараметрыДляФомыКонтактныеДанныеКонтрагентов = ПодготовитьПараметрыДляФомыКонтактныеДанныеКонтрагентов(ЕДРПОУКонтрагента, Контрагент, "", "");
		
		Если скEDI_ОбщегоНазначения.ЭтоПлатформа82() Тогда		
			лФормаНастройкиОтправкиСсылкиНаДокумент = ПолучитьФорму("РегистрСведений.скEDI_КонтактныеДанныеКонтрагентов.ФормаЗаписи", ПараметрыДляФомыКонтактныеДанныеКонтрагентов, ЭтаФорма);
			ОбработатьДобавитьИзменитьЭлектроннуюПочту(лФормаНастройкиОтправкиСсылкиНаДокумент.ОткрытьМодально());
		Иначе
			скEDI_ОткрытиеФормБезМодальности.ОткрытьФормуНастройкиКонтактныеДанныеКонтрагентов(ПараметрыДляФомыКонтактныеДанныеКонтрагентов, ЭтаФорма, "ОбработатьДобавитьИзменитьЭлектроннуюПочту");
		КонецЕсли;
	Иначе
		ТекстСообщения = НСтр("ru = 'Не заполнено поле ""Контрагент""'; uk = 'Не заповнено поле ""Контрагент""'");
		скEDI_ОбщегоНазначенияКлиент.ПоказатьПредупреждение_(ТекстСообщения);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьЭлектроннуюПочту(Команда)
	Если ЗначениеЗаполнено(ЕДРПОУКонтрагента) Тогда
		ТекЭлектроннаяПочта = "";
		ТекКомментарий = "";
		ТекущиеДанные = Элементы.НастройкиСсылкиНаДокумент.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено Тогда
			ТекЭлектроннаяПочта = ТекущиеДанные.ЭлектроннаяПочта;
			ТекКомментарий = ТекущиеДанные.Комментарий;
		КонецЕсли;
		ПараметрыДляФомыКонтактныеДанныеКонтрагентов = ПодготовитьПараметрыДляФомыКонтактныеДанныеКонтрагентов(ЕДРПОУКонтрагента, Контрагент, ТекЭлектроннаяПочта, ТекКомментарий);
		
		Если скEDI_ОбщегоНазначения.ЭтоПлатформа82() Тогда		
			лФормаНастройкиОтправкиСсылкиНаДокумент = ПолучитьФорму("РегистрСведений.скEDI_КонтактныеДанныеКонтрагентов.ФормаЗаписи", ПараметрыДляФомыКонтактныеДанныеКонтрагентов, ЭтаФорма);
			ОбработатьДобавитьИзменитьЭлектроннуюПочту(лФормаНастройкиОтправкиСсылкиНаДокумент.ОткрытьМодально());
		Иначе
			скEDI_ОткрытиеФормБезМодальности.ОткрытьФормуНастройкиКонтактныеДанныеКонтрагентов(ПараметрыДляФомыКонтактныеДанныеКонтрагентов, ЭтаФорма, "ОбработатьДобавитьИзменитьЭлектроннуюПочту");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьДобавитьИзменитьЭлектроннуюПочту(Результат, Параметры = Неопределено) Экспорт
	Если ТипЗнч(Результат) = Тип("ФиксированнаяСтруктура") Тогда
		НайденаяСтрока = Неопределено;
		Для Каждого СтрокаНастройкиСсылкиНаДокумент из НастройкиСсылкиНаДокумент Цикл
			Если СокрЛП(ВРег(СтрокаНастройкиСсылкиНаДокумент.ЭлектроннаяПочта)) = СокрЛП(ВРег(Результат.ЭлектроннаяПочта)) Тогда
				НайденаяСтрока = СтрокаНастройкиСсылкиНаДокумент;
				Прервать;;
			КонецЕсли;
		КонецЦикла;
		
		Если НайденаяСтрока = Неопределено Тогда
			НоваяСтрокаНастройкиСсылкиНаДокумент = НастройкиСсылкиНаДокумент.Добавить();
			НоваяСтрокаНастройкиСсылкиНаДокумент.Отметка = Истина;
			НоваяСтрокаНастройкиСсылкиНаДокумент.ЭлектроннаяПочта = Результат.ЭлектроннаяПочта;
			НоваяСтрокаНастройкиСсылкиНаДокумент.Комментарий = Результат.Комментарий;
		Иначе
			Если не НайденаяСтрока.Отметка Тогда
				НайденаяСтрока.Отметка = Истина;
			КонецЕсли;
			Если не ЗначениеЗаполнено(НайденаяСтрока.Комментарий) Тогда
				НайденаяСтрока.Комментарий = Результат.Комментарий;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УдалитьЭлектроннуюПочтуНаСервере(пЕДРПОУКонтрагента, пЭлектроннаяПочта)
	КонтактныеДанныеКонтрагентовМенеджерЗаписи = РегистрыСведений.скEDI_КонтактныеДанныеКонтрагентов.СоздатьМенеджерЗаписи();
	КонтактныеДанныеКонтрагентовМенеджерЗаписи.ЕДРПОУКонтрагента = пЕДРПОУКонтрагента;
	КонтактныеДанныеКонтрагентовМенеджерЗаписи.ЭлектроннаяПочта = пЭлектроннаяПочта;
	КонтактныеДанныеКонтрагентовМенеджерЗаписи.Удалить();
КонецПроцедуры

&НаКлиенте
Процедура УдалитьЭлектроннуюПочту(Команда)
	Если ЗначениеЗаполнено(ЕДРПОУКонтрагента) Тогда
		ТекущиеДанные = Элементы.НастройкиСсылкиНаДокумент.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено Тогда
			ТекЭлектроннаяПочта = ТекущиеДанные.ЭлектроннаяПочта;
			УдалитьЭлектроннуюПочтуНаСервере(ЕДРПОУКонтрагента, ТекЭлектроннаяПочта);
			НастройкиСсылкиНаДокумент.Удалить(НастройкиСсылкиНаДокумент.Индекс(ТекущиеДанные))
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
