////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ С ДВИЖЕНИЯМИ ДОКУМЕНТОВ

// Процедура удаления существующих движений документа при перепроведении (отмене проведения)
Процедура УдалитьДвиженияРегистратора(ДокументОбъект, Отказ, РучнаяКорректировка = Ложь, ВыборочноОчищатьРегистры = Истина) Экспорт

	Если РучнаяКорректировка Тогда
		ИзменитьАктивностьПоРегистратору(ДокументОбъект, Отказ, Ложь);
		возврат;
	КонецЕсли;

	Если ВыборочноОчищатьРегистры Тогда
		СписокРегистровДляОчисткиДвижений = Новый Массив;
		//СписокРегистровДляОчисткиДвижений.Добавить(Тип("РегистрНакопленияНаборЗаписей.РасходыПриУСН"));
	КонецЕсли;

	//Очистка движений документа
	Для Каждого Движение ИЗ ДокументОбъект.Движения Цикл

		Если ВыборочноОчищатьРегистры И (СписокРегистровДляОчисткиДвижений.Найти(ТипЗнч(Движение))<>неопределено) Тогда
			Продолжить;
		КонецЕсли;
		Движение.Очистить();

	КонецЦикла;

	//Запись пустых наборов движений в ИБ(очистка старых движений)
	Для Каждого Движение ИЗ ДокументОбъект.Движения Цикл

		Если (ВыборочноОчищатьРегистры И (СписокРегистровДляОчисткиДвижений.Найти(ТипЗнч(Движение))<>неопределено))
			ИЛИ НЕ ВыборочноОчищатьРегистры Тогда

			Если Движение.Количество() > 0 Тогда
				ПозицияТочки = Найти(Строка(Движение), ".");
				ТипРегистра = Лев(Строка(Движение), ПозицияТочки - 13);
				ИмяРегистра = СокрП(Сред(Строка(Движение), ПозицияТочки + 1));

				ЕСли ТипРегистра = "РегистрНакопления" Тогда
					МетаданныеНабора = Метаданные.РегистрыНакопления[ИмяРегистра];
					Набор = РегистрыНакопления[ИмяРегистра].СоздатьНаборЗаписей();

				ИначеЕсли ТипРегистра = "РегистрБухгалтерии" Тогда
					МетаданныеНабора = Метаданные.РегистрыБухгалтерии[ИмяРегистра];
					Набор = РегистрыБухгалтерии[ИмяРегистра].СоздатьНаборЗаписей();

				ИначеЕсли ТипРегистра = "РегистрСведений" Тогда
					МетаданныеНабора = Метаданные.РегистрыСведений[ИмяРегистра];
					Набор = РегистрыСведений[ИмяРегистра].СоздатьНаборЗаписей();

				ИначеЕсли ТипРегистра = "РегистрРасчета" Тогда
					МетаданныеНабора = Метаданные.РегистрыРасчета[ИмяРегистра];
					Набор = РегистрыРасчета[ИмяРегистра].СоздатьНаборЗаписей();

				КонецЕсли;

				Если НЕ ПравоДоступа("Изменение", МетаданныеНабора) Тогда
					// отсутствуют права на всю таблицу регистра
					ОбщегоНазначенияБПКлиентСервер.СообщитьОбОшибке("Нарушение прав доступа", Отказ, Строка(Движение));
					Возврат;
				КонецЕсли;

				Набор.Отбор.Регистратор.Установить(ДокументОбъект.Ссылка);

			Иначе
				Набор = Движение;
			КонецЕсли;

			Попытка
				Набор.Записать();
			Исключение
				// возможно «сработал» RLS или механизм даты запрета изменения
				ОбщегоНазначенияБПКлиентСервер.СообщитьОбОшибке(ОписаниеОшибки(), Отказ, Набор);
				ОписаниеОшибки = ИнформацияОбОшибке();
				ЗаписьЖурналаРегистрации(НСтр("ru='Операция не выполнена';uk='Операція не виконана'"), УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки.Описание);
				ВызватьИсключение НСтр("ru='Операция не выполнена';uk='Операція не виконана'");
			КонецПопытки;
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

// Процедура включения активности движений при проведении документа, движения которого
// заданы вручную
Процедура ИзменитьАктивностьПоРегистратору(ДокументОбъект, Отказ,ВключитьАктивность = Истина) Экспорт

	Для Каждого Набор ИЗ ДокументОбъект.Движения Цикл

		Набор.Прочитать();
		Набор.УстановитьАктивность(ВключитьАктивность);

		Попытка
			Набор.Записать();
		Исключение
			// возможно «сработал» RLS или механизм даты запрета изменения
			ОписаниеОшибки = ИнформацияОбОшибке();
			ОбщегоНазначенияБПКлиентСервер.СообщитьОбОшибке(КраткоеПредставлениеОшибки(ОписаниеОшибки), Отказ, Строка(Набор));
			ЗаписьЖурналаРегистрации(НСтр("ru='Операция не выполнена';uk='Операція не виконана'"), 
				УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ОписаниеОшибки));
				
			ВызватьИсключение НСтр("ru='Операция не выполнена';uk='Операція не виконана'");
		КонецПопытки;

	КонецЦикла;

КонецПроцедуры

Функция ПолучитьСтруктуруИзРезультатаЗапроса(РезультатЗапроса) Экспорт

	СтруктураПараметров = Новый Структура;

	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	Для каждого КолонкаРезультата Из РезультатЗапроса.Колонки Цикл
		СтруктураПараметров.Вставить(КолонкаРезультата.Имя, Выборка[КолонкаРезультата.Имя]);
	КонецЦикла;

    Возврат СтруктураПараметров;

КонецФункции

Функция ТекстРазделителяЗапросовПакета() Экспорт

	ТекстРазделителя =
	"
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";

	Возврат ТекстРазделителя;

КонецФункции

Процедура РаспределитьСуммуПоКолонкеТаблицы(РаспределяемаяСумма, Таблица, ИмяКолонки, ИмяКолонкиБазы = "") Экспорт

	Если РаспределяемаяСумма <> 0 Тогда
		Если ПустаяСтрока(ИмяКолонкиБазы) Тогда
			МассивСтарыхСумм = Таблица.ВыгрузитьКолонку(ИмяКолонки);
		Иначе
			МассивСтарыхСумм = Таблица.ВыгрузитьКолонку(ИмяКолонкиБазы);
		КонецЕсли;
		МассивНовыхСумм = ОбщегоНазначенияБПКлиентСервер.РаспределитьПропорционально(РаспределяемаяСумма, МассивСтарыхСумм);
		Если МассивНовыхСумм <> Неопределено Тогда
	 		Таблица.ЗагрузитьКолонку(МассивНовыхСумм, ИмяКолонки);
		КонецЕсли; 
	Иначе
		Таблица.ЗаполнитьЗначения(0, ИмяКолонки);
	КонецЕсли;

КонецПроцедуры

// Возвращает список обязательных колонок (через запятую), отсутствующих в таблице значений
//
//Параметры:
//	Таблица 			- <ТаблицаЗначений> - проверяемая таблица
//	ОбязательныеКолонки - <Строка> - имена колонок, которые обязательно должны присутствовать в таблице
//
//Возвращаемое значение:
//	<Строка> - имена отсутствующих в таблице колонок через запятую
Функция ПроверитьКолонкиТаблицыЗначений(Таблица, ОбязательныеКолонки)	Экспорт

	СтруктураКолонок = Новый Структура(ОбязательныеКолонки);
	КолонкиТаблицы = Таблица.Колонки;
	СтрокаНеНайденных = "";

	Для Каждого ОбязательнаяКолонка Из СтруктураКолонок Цикл

		Если КолонкиТаблицы.Найти(ОбязательнаяКолонка.Ключ) = Неопределено Тогда
			СтрокаНеНайденных = СтрокаНеНайденных + ?(СтрокаНеНайденных = "", "", ", ") + ОбязательнаяКолонка.Ключ;
		КонецЕсли;

	КонецЦикла;

	Возврат СтрокаНеНайденных;

КонецФункции

Функция ПолучитьТаблицуПараметровПроведения(ИсходнаяТаблица, СписокКолонок) Экспорт

	Если ИсходнаяТаблица = Неопределено Тогда
		
		ТаблицаРезультат = Новый ТаблицаЗначений;
		Колонки = Новый Структура(СписокКолонок);
		Для каждого Колонка Из Колонки Цикл
			ТаблицаРезультат.Колонки.Добавить(Колонка.Ключ);
		КонецЦикла;
		Возврат ТаблицаРезультат;

	Иначе

		Возврат ИсходнаяТаблица.Скопировать(, СписокКолонок);

	КонецЕсли;

КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПРОВЕРКИ НАЛИЧИЯ И ЗАПОЛНЕНИЯ РЕВИЗИТОВ

// Позволяет определить есть ли среди реквизитов шапки документа
// реквизит с переданным именем.
//
// Параметры:
//  ИмяРеквизита - строковое имя искомого реквизита,
//  МетаданныеДокумента - объект описания метаданных документа, среди реквизитов которого производится поиск.
//
// Возвращаемое значение:
//  Истина - нашли реквизит с таким именем, Ложь - не нашли.
//
Функция ЕстьРеквизитДокумента(ИмяРеквизита, МетаданныеДокумента) Экспорт

	Возврат ОбщегоНазначенияБП.ЕстьРеквизитДокумента(ИмяРеквизита, МетаданныеДокумента);

КонецФункции // ЕстьРеквизитДокумента()

Функция ЕстьРеквизитТабЧастиДокумента(ИмяРеквизита, МетаданныеДокумента, ИмяТабЧасти) Экспорт

	Возврат ОбщегоНазначенияБП.ЕстьРеквизитТабЧастиДокумента(ИмяРеквизита, МетаданныеДокумента, ИмяТабЧасти);
	
КонецФункции // ЕстьРеквизитТабЧастиДокумента()

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ С ФИЗЛИЦАМИ

// Функция формирует фамилию и инициалы либо по наименованию элемента спр-ка ФизическиеЛица
//  либо по переданным строкам.
//  Если передан Объект, то извлеченная из него строка считается совокупностью
//  Фамилия + Имя + Отчество, разделенными пробелами.
//
// Параметры
//  Объект		- строка или ссылка или объект элемента спр-ка ФизическиеЛица.
//  Фамилия		- фамилия физ. лица.
//  Имя			- имя физ. лица.
//  Отчество	- отчество физ. лица.
//
// Возвращаемое значение
//  Фамилия и Инициалы одной строкой. Побочные эффекты - переданная целая строка
//  Побочные эффекты - переданная целая строка разбивается на подстроки, соответствующие
//  отдельным Фамилии,Имени и Отчеству
//
Функция ФамилияИнициалыФизЛица(Объект = "", Фамилия = " ", Имя = " ", Отчество = " ") Экспорт

	ТипОбъекта = ТипЗнч(Объект);
	Если ТипОбъекта = Тип("Строка") Тогда
		ФИО = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СокрЛП(Объект)," ");

	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ФизическиеЛица") или ТипОбъекта = Тип("СправочникОбъект.ФизическиеЛица") ИЛИ
			  ТипОбъекта = Тип("СправочникСсылка.Сотрудники") или ТипОбъекта = Тип("СправочникОбъект.Сотрудники") Тогда
			  
			  ФизическоеЛицо = УчетЗарплаты.ПолучитьФизическоеЛицо(Объект);
			  ДанныеФизическогоЛица = УчетЗарплаты.ДанныеФизическихЛиц(,ФизическоеЛицо, ТекущаяДата(), Истина);
			  Фамилия = ДанныеФизическогоЛица.Фамилия;
			  Имя	  = ДанныеФизическогоЛица.Имя;
			  Отчество= ДанныеФизическогоЛица.Отчество;
			  Возврат ДанныеФизическогоЛица.Представление;
			  
	Иначе

		// используем возможно переданные отдельные строки
		Возврат ?(НЕ ПустаяСтрока(Фамилия),
				Фамилия + ?(НЕ ПустаяСтрока(Имя)," " + Лев(Имя,1) + "." + ?(НЕ ПустаяСтрока(Отчество),Лев(Отчество,1)+".", ""), ""),
				"")
	КонецЕсли;

	КоличествоПодстрок = ФИО.Количество();
	Фамилия = ?(КоличествоПодстрок > 0,ФИО[0],"");
	Имя		= ?(КоличествоПодстрок > 1,ФИО[1],"");
	Отчество= ?(КоличествоПодстрок > 2,ФИО[2],"");

	Возврат ?(НЕ ПустаяСтрока(Фамилия),
				Фамилия + ?(НЕ ПустаяСтрока(Имя)," " + Лев(Имя,1) + "." + ?(НЕ ПустаяСтрока(Отчество),Лев(Отчество,1)+".", ""), ""),
				"");

КонецФункции

// Процедура возвращает паспортные данные физлица в виде строки
//
// Параметры:
//  ФизЛицо - ссылка на элемент справочника "Физические лица",по которому необходимо
//            получить паспортные данные.
//
// Возвращаемое значение:
//  Строка с данными об удостоверении личности физического лица
//
Функция ПолучитьПаспортныеДанныеСтрокой(Организация, ФизЛицо) Экспорт
    		
	СтруктураПаспортныхДанных = УчетЗарплаты.ДанныеФизическихЛиц(Организация, ФизЛицо, ОбщегоНазначенияБП.ПолучитьРабочуюДату());

	Если ЗначениеЗаполнено(СтруктураПаспортныхДанных.ПредставлениеДокумента) Тогда
		Возврат СтруктураПаспортныхДанных.ПредставлениеДокумента;
	Иначе
		Возврат НСтр("ru='Отсутствуют данные об удостоверении личности.';uk='Відсутні дані про посвідчення особи.'");
	КонецЕсли;

КонецФункции // ПолучитьПаспортныеДанныеСтрокой()

// Функция формирует фамилию, имя и отчество одной строкой
//
// Параметры
//  Фамилия  - фамилия физ. лица.
//  Имя      - имя физ. лица.
//  Отчество - отчество физ. лица.
//  ФИОКратко    - Булево - если Истина (по умолчанию), Представление физ.лица включает фамилию и инициалы, если Ложь - фамилию и полностью имя и отчество
//
// Возвращаемое значение
//  Фамилия, имя, отчество одной строкой.
//
Функция ПолучитьФамилиюИмяОтчество(Фамилия = " ", Имя = " ", Отчество = " ", ФИОКратко = Истина) Экспорт

	Если ФИОКратко Тогда
		Возврат ?(НЕ ПустаяСтрока(Фамилия), Фамилия + ?(НЕ ПустаяСтрока(Имя)," " + Лев(Имя,1) + "." +
				?(НЕ ПустаяСтрока(Отчество) ,
				Лев(Отчество,1)+".", ""), ""), "");
	Иначе
		Возврат ?(НЕ ПустаяСтрока(Фамилия), Фамилия + ?(НЕ ПустаяСтрока(Имя)," " + Имя +
				?(НЕ ПустаяСтрока(Отчество) , " " + Отчество, ""), ""), "");
	КонецЕсли;

КонецФункции // ПолучитьФамилиюИмяОтчество()

// Функция возвращает совокупность данных о физическом лице в виде структуры,
// В совокупность данных входит ФИО, должность в заданной организации,
// паспортные данные и др.
//
// Параметры:
//  Организация  - СправочникСсылка.Организации - организация, по которой
//                 определяется должность и подразделение работника
//  ФизЛицо      - СправочникСсылка.ФизическиеЛица - физическое лицо,
//                 по которому возвращается совокупность данных
//  ДатаСреза    - Дата - дата, на которую считываются данные
//  ФИОКратко    - Булево - если Истина (по умолчанию), Представление физ.лица включает фамилию и инициалы, если Ложь - фамилию и полностью имя и отчество
//
// Возвращаемое значение:
//  Структура    - Структура с совокупностью данных о физическом лице:
//                 - Фамилия
//                 - Имя
//                 - Отчество
//                 - Представление (Фамилия И.О.)
//                 - Подразделение
//                 - ВидДокумента
//                 - Серия
//                 - Номер
//                 - ДатаВыдачи
//                 - КемВыдан
//                 - КодПодразделения
//
Функция ДанныеФизЛица(Организация, ФизЛицо, ДатаСреза, ФИОКратко = Истина) Экспорт
	
	Возврат УчетЗарплаты.ДанныеФизическихЛиц(Организация, ФизЛицо, ДатаСреза, ФИОКратко);

КонецФункции // ДанныеФизЛица

// Функция возвращает совокупность данных о физических лицах в виде таблицы
// значений. В совокупность данных входит ФИО, должность в заданной
// организации, паспортные данные и др.
//
// Параметры:
//  Организация  - СправочникСсылка.Организации - организация, по которой
//                 определяется должность и подразделение работника(ов)
//  ФизЛицо      - СправочникСсылка.ФизическиеЛица или Массив - физическое лицо
//                 или список физ. лиц, по которым возвращается совокупность
//                 данных
//  ДатаСреза    - Дата - дата, на которую считываются данные
//
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица с совокупностью данных о физическом лице.
//                  Колонки: возвращаемой таблицы:
//                  - Фамилия
//                  - Имя
//                  - Отчество
//                  - Представление (Фамилия И.О.)
//                  - Подразделение
//                  - ВидДокумента
//                  - Серия
//                  - Номер
//                  - ДатаВыдачи
//                  - КемВыдан
//                  - КодПодразделения
//
Функция ДанныеФизЛиц(Организация, ФизЛицо, ДатаСреза) Экспорт
	
	Возврат УчетЗарплаты.ДанныеФизическихЛиц(Организация, ФизЛицо, ДатаСреза, Истина, Истина);

КонецФункции // ДанныеФизЛиц


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ С ДОКУМЕНТАМИ

// Функция формирует представление заголовка документа
//
// Возвращаемое значение:
//  Строка - представление номера документа
//
Функция СформироватьЗаголовокДокумента(ДокументОбъект, НазваниеДокумента = "", КодЯзыка = Неопределено) Экспорт
	
	Если КодЯзыка = Неопределено Тогда 
		КодЯзыка = Локализация.ПолучитьЯзыкФормированияПечатныхФорм();
	КонецЕсли;

	Если ДокументОбъект = Неопределено Тогда
		Возврат "";
	Иначе
		Возврат ?(НЕ ЗначениеЗаполнено(НазваниеДокумента), ДокументОбъект.Метаданные().Синоним, НазваниеДокумента) + " № "
			+ ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ДокументОбъект.Номер, Истина, Истина)
			+ НСтр("ru=' от ';uk=' від '",КодЯзыка) + Формат(ДокументОбъект.Дата, "ДФ='дд ММММ гггг';Л="+ Локализация.ОпределитьКодЯзыкаДляФормат(КодЯзыка)) + НСтр("ru=' г.';uk=' р.'",КодЯзыка);
	КонецЕсли;

КонецФункции // СформироватьЗаголовокДокумента()

// Функция формирует представление суммы прописью в указанной валюте
//
// Возвращаемое значение:
//  Строка - сумма прописью
//
Функция СформироватьСуммуПрописью(Сумма, Валюта, КодЯзыка = "ru") Экспорт

	ПараметрыПрописи = Локализация.ПараметрыПрописи(Валюта, КодЯзыка); 
	Если ПараметрыПрописи = "" Тогда
		Возврат ФорматСумм(Сумма);
	Иначе
		Возврат ЧислоПрописью(Сумма,"Л="+Локализация.ОпределитьКодЯзыкаДляФормат(КодЯзыка),ПараметрыПрописи);
	КонецЕсли;

КонецФункции // СформироватьСуммуПрописью()

// Стандартная для данной конфигурации функция форматирования сумм
//
// Параметры:
//  Сумма  - число, которое мы хотим форматировать,
//  Валюта - ссылка на элемент справочника валют, если задан, то к в результирующую строку
//           будет добавлено представление валюты
//  ЧН     - строка, представляющая нулевое значение числа,
//  ЧРГ    - символ-разделитель групп целой части числа.
//
// Возвращаемое значение:
//  Отформатированная должным образом строковое представление суммы.
//
Функция ФорматСумм(Сумма, Валюта = Неопределено, ЧН = "", ЧРГ = "") Экспорт

	ФорматнаяСтрока = "ЧЦ=15;ЧДЦ=2" +
					?(НЕ ЗначениеЗаполнено(ЧН), "", ";" + "ЧН=" + ЧН) +
					?(НЕ ЗначениеЗаполнено(ЧРГ),"", ";" + "ЧРГ=" + ЧРГ);

	РезультирующаяСтрока = СокрЛ(Формат(Сумма, ФорматнаяСтрока));

	Если ЗначениеЗаполнено(Валюта) Тогда
		РезультирующаяСтрока = РезультирующаяСтрока + " " + СокрП(Валюта);
	КонецЕсли;

	Возврат РезультирующаяСтрока;

КонецФункции // ФорматСумм()

// Изменяет текущую страницу формы документа, если при открытии формы
// первая страница содержит пустую табличную часть, но на форме есть еще страницы
// с непустой табличной частью.
// Проверяется также видимость элемента
//
// Параметры:
//   Форма                - форма документа, который открывается;
//   СписокТабличныхПолей - Список значений - табличных полей, где
//                            Значение - имя элемента формы,
//                            Представление - имя табличной части (в метаданных)
//
Функция ПолучитьПервуюНепустуюВидимуюТабличнуюЧасть(Форма, СписокТабличныхПолей) Экспорт

	Для Каждого Элемент из СписокТабличныхПолей Цикл
		Если Форма.Объект[Элемент.Представление].Количество() > 0 Тогда
			Если Форма.Элементы[Элемент.Значение].Видимость Тогда
				// Эта табличная часть - первая видимая, у которой есть данные
				Возврат Элемент.Значение;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	Возврат "";

КонецФункции

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ЗАПОЛНЕНИЯ ПАРАМЕТРОВ СЕАНСА

// Функция применяется при необходимости получить сведения об учетной политике организации.
//
// Параметры: нет
//
// Возвращаемое значение - дерево значений.
//  На первом уровне дерева - список организаций, для которых задана учетная политика
//  На втором уровне дерева - записи учетной политики по организации, каждая запись хранится в дереве как структура
//
Функция ЗаполнениеУчетнойПолитики() Экспорт

	Запрос = Новый Запрос();

	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	*
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитика";

	Результат = Запрос.Выполнить().Выгрузить();
	Результат.Сортировать("Период");

	ДеревоУчетнойПолитики = Новый ДеревоЗначений;
	ДеревоУчетнойПолитики.Колонки.Добавить("Организация");
	ДеревоУчетнойПолитики.Колонки.Добавить("Период");
	ДеревоУчетнойПолитики.Колонки.Добавить("УчетнаяПолитика");

	Для каждого Строка Из Результат Цикл
		УчетнаяПолитикаОрганизации = ДеревоУчетнойПолитики.Строки.Найти(Строка.Организация, "Организация");
		Если УчетнаяПолитикаОрганизации = Неопределено Тогда
			УчетнаяПолитикаОрганизации = ДеревоУчетнойПолитики.Строки.Добавить();
			УчетнаяПолитикаОрганизации.Организация = Строка.Организация;
		КонецЕсли;
		ПоПериоду = УчетнаяПолитикаОрганизации.Строки.Добавить();
		ПоПериоду.Период = Строка.Период;

		СтруктураУчетнойПолитики = Новый Структура;
		Для Каждого Колонка Из Результат.Колонки Цикл
			СтруктураУчетнойПолитики.Вставить(Колонка.Имя, Строка[Колонка.Имя]);
		КонецЦикла;

		ПоПериоду.УчетнаяПолитика = СтруктураУчетнойПолитики;

	КонецЦикла;

	Возврат ДеревоУчетнойПолитики;

КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ С ТАБЛИЦАМИ

// Добавляет в таблицу значений строки из другой таблицы значений и
// в них значения колонок с совпадающими наименованиями.
//
// Параметры:
//  ТаблицаИсточник - таблица значений, откуда берутся значения.
//  ТаблицаПриемник - таблица значений, куда добавляются строки.
//
Процедура ЗагрузитьВТаблицуЗначений(ТаблицаИсточник, ТаблицаПриемник) Экспорт

	Для каждого СтрокаТаблицыИсточника Из ТаблицаИсточник Цикл

		СтрокаТаблицыПриемника = ТаблицаПриемник.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицыПриемника, СтрокаТаблицыИсточника);

	КонецЦикла;

КонецПроцедуры // ЗагрузитьВТаблицуЗначений()

Процедура УпорядочитьТаблицуПоДокументу(ТаблицаЗначений, КолонкаДокумента, КолонкаДаты, Направление = "Возр") Экспорт

	Если ТаблицаЗначений.Колонки.Найти(КолонкаДаты) = Неопределено Тогда
		ТаблицаЗначений.Колонки.Добавить(КолонкаДаты, ОбщегоНазначенияБПКлиентСервер.ПолучитьОписаниеТиповДаты(ЧастиДаты.ДатаВремя));
	КонецЕсли;
	
	Если ТаблицаЗначений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьДатуДокументаКРезультатуЗапроса(ТаблицаЗначений, КолонкаДокумента, КолонкаДаты);
	
	СписокКолонок = КолонкаДаты + " " + Направление + ", " + КолонкаДокумента + " " + Направление;
	ТаблицаЗначений.Сортировать(СписокКолонок, Новый СравнениеЗначений);
	
КонецПроцедуры

Процедура ДобавитьДатуДокументаКРезультатуЗапроса(Результат, КолонкаДокумента, КолонкаСДатой) Экспорт

	КэшПоТипам = Новый Соответствие;
	
	Для каждого СтрокаТаблицы из Результат Цикл
		//Для каждого СтрокаТаблицы из Строка.строки Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы[КолонкаДокумента]) тогда
			Продолжить;
		КонецЕсли;
		
		ТипТекущегоДокумента = ТипЗнч(СтрокаТаблицы[КолонкаДокумента]);
		МассивТипа = КэшПоТипам[ТипТекущегоДокумента];
		Если МассивТипа = Неопределено Тогда
			МассивТипа = Новый Массив;
			КэшПоТипам.Вставить(ТипТекущегоДокумента, МассивТипа);
		КонецЕсли;
		МассивТипа.Добавить(СтрокаТаблицы[КолонкаДокумента]);
		//КонецЦикла;
	КонецЦикла;
	
	Если КэшПоТипам.Количество()=0 тогда
		Возврат;
	КонецЕсли;
	
	Запрос = новый запрос;
	
	Для Каждого КлючИЗначение ИЗ КэшПоТипам Цикл
		ИмяМетаданных = Метаданные.НайтиПоТипу(КлючИЗначение.Ключ).Имя;
		
		Запрос.Текст = Запрос.Текст+
		?(Запрос.Текст="","",
		"
		|Объединить все")+
		"
		|	ВЫБРАТЬ
		|		Док.Ссылка Как Ссылка,
		|		Док.Дата
		|	ИЗ Документ."+ИмяМетаданных+" КАК Док
		|	Где Док.ссылка в  (&ДокументыТипа_"+ИмяМетаданных+")";
		УдалитьПовторяющиесяЭлементыМассива(КлючИЗначение.Значение);
		Запрос.УстановитьПараметр("ДокументыТипа_"+ИмяМетаданных, КлючИЗначение.Значение);
		
	КонецЦикла;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить(КолонкаДокумента, Выборка.Ссылка);
		
		НайденныеСтроки = Результат.НайтиСтроки(ПараметрыОтбора);
		Для Каждого строка ИЗ НайденныеСтроки Цикл
			строка[КолонкаСДатой] = Выборка.Дата;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры // ДобавитьДатуДокументаКРезультатуЗапроса()

Процедура ПронумероватьТаблицу(ТаблицаЗначений, ИмяКолонкиНомера = "НомерСтроки") Экспорт

	Если ТаблицаЗначений.Колонки.Найти(ИмяКолонкиНомера) = Неопределено Тогда
		ТаблицаЗначений.Колонки.Добавить(ИмяКолонкиНомера, Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 0)));
	КонецЕсли;

	Ном = 1;
	Для каждого СтрокаТаблицы Из ТаблицаЗначений Цикл
		СтрокаТаблицы[ИмяКолонкиНомера] = Ном;
		Ном = Ном + 1;
	КонецЦикла;

КонецПроцедуры

Функция ПустаяТаблицаРегистраНакопления(ИмяРегистра) Экспорт

	ПустаяТаблица = РегистрыНакопления[ИмяРегистра].СоздатьНаборЗаписей().ВыгрузитьКолонки();
	ПустаяТаблица.Колонки.Удалить("Регистратор");
	ПустаяТаблица.Колонки.Удалить("МоментВремени");
	ПустаяТаблица.Колонки.Удалить("Активность");
	Если ПустаяТаблица.Колонки.Найти("ВидДвижения") <> Неопределено Тогда
		ПустаяТаблица.Колонки.Удалить("ВидДвижения");
	КонецЕсли;

	Возврат ПустаяТаблица;

КонецФункции

Процедура ДополнитьКолонкиТаблицыЗначений(ТаблицаБазовая,ТаблицаДополнений) Экспорт

	Для каждого Колонка из ТаблицаДополнений.Колонки Цикл

		Если Не(ТаблицаБазовая.Колонки.Найти(Колонка.Имя) = Неопределено) тогда
			//Колонка уже есть
			Продолжить;
		КонецЕсли;

		ТаблицаБазовая.Колонки.Добавить(Колонка.Имя,Колонка.ТипЗначения,Колонка.Заголовок,Колонка.Ширина);

	КонецЦикла;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ УПРАВЛЕНИЯ НОМЕРАМИ ДОКУМЕНТОВ

// Функция возвращает массив вышестоящих групп указанного элемента.
//
// Параметры:
//  Элемент      - Элемент справочника, для которого ищется родитель
//
// Возвращаемое значение
//  Массив вышестоящих групп
//
Функция ПолучитьСписокВышеСтоящихГрупп(ЭлементСправочника) Экспорт

	Результат = Новый Массив;

	Если НЕ ЗначениеЗаполнено(ЭлементСправочника) Тогда
		Возврат Результат;
	КонецЕсли;

	МетаданныеСправочника = ЭлементСправочника.Метаданные();
	Если НЕ МетаданныеСправочника.Иерархический Тогда
		Возврат Результат;
	КонецЕсли;
	ИмяСправочника = МетаданныеСправочника.Имя;
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Справочник1.Родитель КАК Родитель1,
	|	Справочник2.Родитель КАК Родитель2,
	|	Справочник3.Родитель КАК Родитель3,
	|	Справочник4.Родитель КАК Родитель4,
	|	Справочник5.Родитель КАК Родитель5
	|ИЗ
	|	Справочник." + ИмяСправочника + " КАК Справочник1
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник." + ИмяСправочника + " КАК Справочник2
	|		ПО (Справочник2.Ссылка = Справочник1.Родитель)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник." + ИмяСправочника + " КАК Справочник3
	|		ПО (Справочник3.Ссылка = Справочник2.Родитель)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник." + ИмяСправочника + " КАК Справочник4
	|		ПО (Справочник4.Ссылка = Справочник3.Родитель)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник." + ИмяСправочника + " КАК Справочник5
	|		ПО (Справочник5.Ссылка = Справочник4.Родитель)
	|ГДЕ
	|	Справочник1.Ссылка = &Ссылка";

	ТекущийЭлемент = ЭлементСправочника;

	Пока ЗначениеЗаполнено(ТекущийЭлемент) Цикл
		Запрос.УстановитьПараметр("Ссылка", ТекущийЭлемент);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Для Индекс = 1 по 5 Цикл
				ТекущийЭлемент = Выборка["Родитель" + Индекс];
				Если ЗначениеЗаполнено(ТекущийЭлемент) Тогда
					Результат.Добавить(ТекущийЭлемент);
				Иначе
					Прервать;
				КонецЕсли;
			КонецЦикла;
		Иначе
			ТекущийЭлемент = Неопределено;
		КонецЕсли;
	КонецЦикла;

	Возврат Результат;

КонецФункции

// Функция возвращает массив вышестоящих групп указанного элемента.
//
// Параметры:
//  МассивЭлементовСправочника      - Массив элементов справочника, для которого ищются родители
//                                    Все элементы массива должны быть одного вида!
//
// Возвращаемое значение
//  Соответствие массивов вышестоящих групп
//
Функция ПолучитьСписокВышеСтоящихГруппЭлементов(МассивЭлементовСправочника) Экспорт

	Результат = Новый Соответствие;
	
	Если МассивЭлементовСправочника.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	Для Каждого ЭлементСправочника Из МассивЭлементовСправочника Цикл
		Результат.Вставить(ЭлементСправочника, Новый Массив);
	КонецЦикла;
	
	МетаданныеСправочника = МассивЭлементовСправочника[0].Метаданные();
	Если НЕ МетаданныеСправочника.Иерархический Тогда
		Возврат Результат;
	КонецЕсли;
	
	ИмяСправочника = МетаданныеСправочника.Имя;
	
	Запрос = Новый Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Справочник1.Ссылка КАК Элемент,
	|	Справочник1.Родитель КАК Родитель1,
	|	Справочник2.Родитель КАК Родитель2,
	|	Справочник3.Родитель КАК Родитель3,
	|	Справочник4.Родитель КАК Родитель4,
	|	Справочник5.Родитель КАК Родитель5
	|ИЗ
	|	Справочник.Номенклатура КАК Справочник1
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Справочник2
	|		ПО (Справочник2.Ссылка = Справочник1.Родитель)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Справочник3
	|		ПО (Справочник3.Ссылка = Справочник2.Родитель)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Справочник4
	|		ПО (Справочник4.Ссылка = Справочник3.Родитель)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Справочник5
	|		ПО (Справочник5.Ссылка = Справочник4.Родитель)
	|ГДЕ
	|	Справочник1.Ссылка В(&МассивСсылок)";
	
	Запрос.Текст = СтрЗаменить(ТекстЗапроса, "Номенклатура", ИмяСправочника);
	
	//	Таблица соответствия групп и элементов (начальной точки иерархии):
	//		Родитель - группа справочника
	//		Элемент - элемент справочника, для которого строится иерархия
	СоответствиеГруппИЭлементов = Новый ТаблицаЗначений;
	СоответствиеГруппИЭлементов.Колонки.Добавить("Родитель",	Новый ОписаниеТипов("СправочникСсылка." + ИмяСправочника));
	СоответствиеГруппИЭлементов.Колонки.Добавить("Элемент",		Новый ОписаниеТипов("СправочникСсылка." + ИмяСправочника));
	СоответствиеГруппИЭлементов.Индексы.Добавить("Родитель");
	Для Каждого ЭлементСправочника Из МассивЭлементовСправочника Цикл
		НовоеСоответствие = СоответствиеГруппИЭлементов.Добавить();
		НовоеСоответствие.Родитель	= ЭлементСправочника;
		НовоеСоответствие.Элемент	= ЭлементСправочника;
	КонецЦикла;
	
	Отбор	= Новый Структура("Родитель");
	
	ТекущийМассивСсылок = МассивЭлементовСправочника;
	
	Пока ТекущийМассивСсылок.Количество() > 0 Цикл
		
		Запрос.УстановитьПараметр("МассивСсылок", УдалитьПовторяющиесяЭлементыМассива(ТекущийМассивСсылок));
		Выборка = Запрос.Выполнить().Выбрать();
		
		ТекущийМассивСсылок	= Новый Массив;
		
		Пока Выборка.Следующий() Цикл
			
			Отбор.Родитель = Выборка.Элемент;
			
			НайденныеСтроки = СоответствиеГруппИЭлементов.НайтиСтроки(Отбор);
			Для Каждого СоответствиеГруппыИЭлемента Из НайденныеСтроки Цикл
				
				ЭлементСправочника	= СоответствиеГруппыИЭлемента.Элемент;
				
				МассивВышеСтоящихГруп	= Результат.Получить(ЭлементСправочника);
				
				Для Индекс = 1 по 5 Цикл
					
					Родитель = Выборка["Родитель" + Индекс];
					Если ЗначениеЗаполнено(Родитель) Тогда
						
						МассивВышеСтоящихГруп.Добавить(Родитель);
						Если Индекс = 5 Тогда
							ТекущийМассивСсылок.Добавить(Родитель);
							НовоеСоответствие = СоответствиеГруппИЭлементов.Добавить();
							НовоеСоответствие.Родитель	= Родитель;
							НовоеСоответствие.Элемент	= ЭлементСправочника;
						КонецЕсли;
						
					Иначе
						Прервать;
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

// Формирует строку представления документа для сообщений при проведении.
//
// Параметры
//  Документ - ссылка на проводимый документ.
//
// Возвращаемое значение
//  Строка с представлением документа.
//
Функция ПредставлениеДокументаПриПроведении(Документ) Экспорт

	МетаданныеДокумента = Документ.Метаданные();

	ВидОперацииСтр = "";

	Если ОбщегоНазначенияБП.ЕстьРеквизитДокумента("ВидОперации", МетаданныеДокумента) Тогда
		ВидОперацииСтр = " (" + Документ.ВидОперации + ")";
	КонецЕсли;

	Возврат НСтр("ru='Проведение документа: ';uk='Проведення документа: '") + СокрЛП(Документ) + ВидОперацииСтр;

КонецФункции // ПредставлениеДокументаПриПроведении()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ С УПРАВЛЯЕМЫМИ БЛОКИРОВКАМИ

// Устанавливает управляемую блокировку таблицы
//
// Параметры:
//  СтруктураПараметров 		- <Структура>. Структура параметров блокировки. Обязательный параметр.
//								Обязательно должна содержать свойства:
//								"ИмяТаблицы" - <Строка> - имя таблицы, на которую накладывается блокировка.
//									Например: "АвансовыйОтчет"
//								Необязательные свойства:
//								"ТипТаблицы" - <Строка> - тип таблицы, на которую накладывается блокировка.
//									Пространство блокировки состоит из типа таблицы и имени таблицы.
//									Например: "Документ"
//									Значение по умолчанию: "РегистрНакопления"
//								"РежимБлокировки" - <РежимБлокировкиДанных> - режим накладываемой блокировки.
//									Значение по умолчанию: РежимБлокировкиДанных.Исключительный
//								"ИсточникДанных" - источник данных для блокировки.
//									Может передаваться значение любого типа, поддерживаемого свойством ИсточникДанных элемента блокировки,
//									а также типа "Менеджер временных таблиц".
//									Если в структуре нет этого свойства - блокировки через ИспользоватьИзИсточникаДанных() не накладываются.
//								"ИмяВременнойТаблицы" - <Строка> - имя временной таблицы менеджера временных таблиц, которая служит источником данных для блокировки.
//									Обязательно должно указываться, если в качестве источника данных процедуре передан менеджер временных таблиц.
//  КоллекцияЗначенийБлокировки	- <Структура или Соответствие> - описывает значения блокировки, накладываемые с помощью УстановитьЗначение().
//									Ключ - поле блокировки - <Строка или (только для соответствия) ПланыВидовХарактеристикСсылка>,
//										ПланыВидовХарактеристикСсылка используется для блокировки регистра бухгалтерии по виду субконто.
//									Значение - блокируемое значение - <Произвольный тип>.
//									Если передано Неопределено или если коллекция не содержит ни одного элемента -
//									блокировки методом УстановитьЗначение() не накладываются.
//  КоллекцияОписанияИсточника	- <Структура или Соответствие> - описывает значения блокировки, накладываемые с помощью ИспользоватьИзИсточникаДанных().
//									Ключ - поле блокировки - <Строка или (только для соответствия) ПланыВидовХарактеристикСсылка>,
//										ПланыВидовХарактеристикСсылка используется для блокировки регистра бухгалтерии по виду субконто.
//									Значение - поле таблицы источника данных - <Строка>.
//									Если передано Неопределено или если коллекция не содержит ни одного элемента -
//									блокировки методом ИспользоватьИзИсточникаДанных() не накладываются.
//  Отказ 						- <Булево> - при ошибке в процессе установки блокировки в этот параметр процедура возвращает значение Истина
//  Заголовок 					- <Строка> - заголовок сообщения об ошибке при установке блокировки
//
Процедура УстановитьУправляемуюБлокировку(СтруктураПараметров, КоллекцияЗначенийБлокировки = Неопределено, КоллекцияОписанияИсточника = Неопределено, Отказ = Ложь, Заголовок = "") Экспорт

	Если НЕ ТипЗнч(СтруктураПараметров) = Тип("Структура") Тогда
		Возврат;
	КонецЕсли;

	ИспользоватьЗначенияБлокировки = КоллекцияЗначенийБлокировки <> Неопределено
		И (ТипЗнч(КоллекцияЗначенийБлокировки) = Тип("Структура")
			ИЛИ ТипЗнч(КоллекцияЗначенийБлокировки) = Тип("Соответствие"))
		И КоллекцияЗначенийБлокировки.Количество() > 0;

	ИспользоватьИсточникДанных     = КоллекцияОписанияИсточника <> Неопределено
		И (ТипЗнч(КоллекцияОписанияИсточника) = Тип("Структура")
			ИЛИ ТипЗнч(КоллекцияОписанияИсточника) = Тип("Соответствие"))
		И КоллекцияОписанияИсточника.Количество() > 0
		И СтруктураПараметров.Свойство("ИсточникДанных");

	Если НЕ ИспользоватьЗначенияБлокировки И НЕ ИспользоватьИсточникДанных Тогда
		Возврат;
	КонецЕсли;

	Блокировка = Новый БлокировкаДанных;

	ТипТаблицы = ?(СтруктураПараметров.Свойство("ТипТаблицы"), СтруктураПараметров.ТипТаблицы, "РегистрНакопления");
	ИмяТаблицы = СтруктураПараметров.ИмяТаблицы;
	ПространствоБлокировки = ТипТаблицы  + "." + ИмяТаблицы;
	ЭлементБлокировки = Блокировка.Добавить(ПространствоБлокировки);

	РежимБлокировки = ?(СтруктураПараметров.Свойство("РежимБлокировки"), СтруктураПараметров.РежимБлокировки, РежимБлокировкиДанных.Исключительный);
	ЭлементБлокировки.Режим = РежимБлокировки;

	Если ИспользоватьЗначенияБлокировки Тогда

		Для каждого ЭлементКоллекции Из КоллекцияЗначенийБлокировки Цикл

			ЭлементБлокировки.УстановитьЗначение(ЭлементКоллекции.Ключ, ЭлементКоллекции.Значение);

		КонецЦикла;

	КонецЕсли;

	Если ИспользоватьИсточникДанных Тогда

		ИсточникДанных = СтруктураПараметров.ИсточникДанных;

		Если ТипЗнч(ИсточникДанных) = Тип("МенеджерВременныхТаблиц") Тогда

			Запрос = Новый Запрос;
			Запрос.МенеджерВременныхТаблиц = ИсточникДанных;
			ТекстЗапроса = "";
			Для каждого ЭлементКоллекции Из КоллекцияОписанияИсточника Цикл
				ТекстЗапроса = ТекстЗапроса + ",
				|	Таб." + ЭлементКоллекции.Значение;
			КонецЦикла;
			ТекстЗапроса = Сред(ТекстЗапроса, 2);
			ТекстЗапроса =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ"
			+ ТекстЗапроса + "
			|ИЗ
			|	" + СтруктураПараметров.ИмяВременнойТаблицы + " КАК Таб";
			Запрос.Текст = ТекстЗапроса;
			Результат    = Запрос.Выполнить();

			ЭлементБлокировки.ИсточникДанных = Результат;

		Иначе

			ЭлементБлокировки.ИсточникДанных = ИсточникДанных;

		КонецЕсли;

		Для каждого ЭлементКоллекции Из КоллекцияОписанияИсточника Цикл

			ЭлементБлокировки.ИспользоватьИзИсточникаДанных(ЭлементКоллекции.Ключ, ЭлементКоллекции.Значение);

		КонецЦикла;

	КонецЕсли;

	Попытка

		Блокировка.Заблокировать();

	Исключение

		ОбщегоНазначенияБПКлиентСервер.СообщитьОбОшибке(ОписаниеОшибки(), Отказ, Заголовок);
		ОписаниеОшибки = ИнформацияОбОшибке();
		ЗаписьЖурналаРегистрации(НСтр("ru='Операция не выполнена';uk='Операція не виконана'"), УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки.Описание);
		ВызватьИсключение НСтр("ru='Операция не выполнена';uk='Операція не виконана'");

	КонецПопытки;

КонецПроцедуры

//Удаляет повторяющиеся элементы массива.
Функция УдалитьПовторяющиесяЭлементыМассива(Массив, НеИспользоватьНеопределено = Ложь) Экспорт

	ОписаниеТиповСправочники  = Справочники.ТипВсеСсылки();
	ОписаниеТиповДокументы    = Документы.ТипВсеСсылки();
	ОписаниеТиповПВХ          = ПланыВидовХарактеристик.ТипВсеСсылки();
	ОписаниеТиповПланыСчетов  = ПланыСчетов.ТипВсеСсылки();
	ОписаниеТиповПланыРасчета = ПланыВидовРасчета.ТипВсеСсылки();

	Если ТипЗнч(Массив) = Тип("Массив") Тогда

		УжеВМассиве = Новый Соответствие;
		БылоНеопределено = Ложь;

		КолвоЭлементовВМассиве = Массив.Количество();

		Для ОбратныйИндекс = 1 По КолвоЭлементовВМассиве Цикл
			ЭлементМассива = Массив[КолвоЭлементовВМассиве - ОбратныйИндекс];
			ТипЭлемента = ТипЗнч(ЭлементМассива);
			Если ЭлементМассива = Неопределено Тогда
				Если БылоНеопределено или НеИспользоватьНеопределено Тогда
					Массив.Удалить(КолвоЭлементовВМассиве - ОбратныйИндекс);
				Иначе
					БылоНеопределено = Истина;
				КонецЕсли;
				Продолжить;
			ИначеЕсли ОписаниеТиповСправочники.СодержитТип(ТипЭлемента)
				ИЛИ ОписаниеТиповДокументы.СодержитТип(ТипЭлемента)
				ИЛИ ОписаниеТиповПВХ.СодержитТип(ТипЭлемента)
				ИЛИ ОписаниеТиповПланыСчетов.СодержитТип(ТипЭлемента)
				ИЛИ ОписаниеТиповПланыРасчета.СодержитТип(ТипЭлемента) Тогда

				ИДЭлемента = Строка(ЭлементМассива.УникальныйИдентификатор());

			Иначе

				ИДЭлемента = ЭлементМассива;

			КонецЕсли;

			Если УжеВМассиве[ИДЭлемента] = Истина Тогда
				Массив.Удалить(КолвоЭлементовВМассиве - ОбратныйИндекс);
			Иначе
				УжеВМассиве[ИДЭлемента] = Истина;
			КонецЕсли;
		КонецЦикла;

	КонецЕсли;

	Возврат Массив;

КонецФункции

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ НАСТРОЙКИ ПАРАМЕТРОВ УЧЕТА

Функция ОпределитьПараметрыУчета() Экспорт

	ПараметрыУчета = ОбщегоНазначенияБПКлиентСервер.СтруктураПараметровУчета();

	// Запасы
	БУ = ПланыСчетов.Хозрасчетный.ТоварыНаСкладе.ПолучитьОбъект();

	ПараметрыУчета.ВестиПартионныйУчет = БУ.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии, "ВидСубконто") <> Неопределено;
	ВестиСкладскойУчет  = БУ.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады, "ВидСубконто") <> Неопределено;
	Если ВестиСкладскойУчет Тогда
		ВестиСуммовойУчетПоСкладам = БУ.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады, "ВидСубконто").Суммовой;
	Иначе
		ВестиСуммовойУчетПоСкладам = Ложь;
	КонецЕсли;

	ПараметрыУчета.СкладскойУчет = ?(ВестиСкладскойУчет, ?(ВестиСуммовойУчетПоСкладам, 2, 1), 0);

	//Розница
	БУ = ПланыСчетов.Хозрасчетный.ТоварыВРозничнойТорговлеВПродажныхЦенахНТТ.ПолучитьОбъект();
	ПараметрыУчета.ИспользоватьОборотнуюНоменклатуру = БУ.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура, "ВидСубконто") <> Неопределено;
	ПараметрыУчета.РазделятьПоСтавкамНДС = БУ.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтавкиНДС, "ВидСубконто") <> Неопределено;

	//Расчеты
	БУ = ПланыСчетов.Хозрасчетный.РасчетыСПоставщикамиИПодрядчиками;
	ПараметрыУчета.ВестиРасчетыПоДокументам = ?(БУ.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами, "ВидСубконто") = Неопределено, Ложь, Истина);
	
	// Кассы обособленных подразделений
	ПараметрыУчета.КассыОбособленныхПодразделений = БухгалтерскийУчетПереопределяемый.ВедетсяУчетКассОбособленныхПодразделений();
	
	// ИспользоватьКлассыСчетовВКачествеГрупп
	ПараметрыУчета.ИспользоватьКлассыСчетовВКачествеГрупп = БухгалтерскийУчетПереопределяемый.ПолучитьИспользоватьКлассыСчетовВКачествеГрупп();
	
	//Работники
	ПараметрыУчета.ВестиУчетПоРаботникам = ?(БухгалтерскийУчетПереопределяемый.ВедетсяУчетРасчетовПоЗарплатеСводно(),0,1);
	ПараметрыУчета.УчетЗарплатыИКадровВоВнешнейПрограмме = БухгалтерскийУчетПереопределяемый.УчетЗарплатыИКадровВоВнешнейПрограмме();
	ПараметрыУчета.КадровыйУчет = БухгалтерскийУчетПереопределяемый.ВедетсяКадровыйУчет();

	Возврат ПараметрыУчета;

КонецФункции

Функция ПолучитьСоответствиеСубконтоПараметрамУчета(ТолькоМПЗ = Ложь) Экспорт

	// Структура параметров

	Результат = Новый ТаблицаЗначений; // Структуры СтруктураПараметров
	Результат.Колонки.Добавить("Счета"); // Счета для обработки, таблица значений со структурой КолонкиСчетов
	Результат.Колонки.Добавить("ИсключенияИерархии", Новый ОписаниеТипов("Массив")); // Массив счетов
		// которые не должны обрабатываться при обработке подчиненных счетов
	Результат.Колонки.Добавить("Субконто"); // Параметры субконто, таблица значений со структурой КолонкиСубконто
	Результат.Колонки.Добавить("Параметры", Новый ОписаниеТипов("ТаблицаЗначений")); // Список параметров и
		// значения исключений, таблица значений со структурой КолонкиПараметров

	КолонкиСчетов = Новый ТаблицаЗначений;
	КолонкиСчетов.Колонки.Добавить("Счет", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	КолонкиСчетов.Колонки.Добавить("СПодчиненными", Новый ОписаниеТипов("Булево")); // Обрабатывать все субсчета

	// Имя параметра, константа типа Булево или строка "ПоСчету" (только для признаков учета Количественный и Валютный)
	// Неопределено - не менять
	ТипПараметра = Новый ОписаниеТипов("Неопределено, Строка, Булево", , Новый КвалификаторыСтроки(0, ДопустимаяДлина.Переменная));

	КолонкиСубконто = Новый ТаблицаЗначений;
	КолонкиСубконто.Колонки.Добавить("Вид", Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.ВидыСубконтоХозрасчетные"));
	КолонкиСубконто.Колонки.Добавить("Параметр", ТипПараметра); // Необходимость включения субконто
	КолонкиСубконто.Колонки.Добавить("Количественный", ТипПараметра);
	КолонкиСубконто.Колонки.Добавить("Поштучный", ТипПараметра); //ИНАГРО ++
	КолонкиСубконто.Колонки.Добавить("Суммовой", ТипПараметра);
	КолонкиСубконто.Колонки.Добавить("ТолькоОбороты", ТипПараметра);
	КолонкиСубконто.Колонки.Добавить("Валютный", ТипПараметра);

	КолонкиПараметров = Новый ТаблицаЗначений; // Описания параметров учета
	// Имя параметра учета
	КолонкиПараметров.Колонки.Добавить("Имя", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(0, ДопустимаяДлина.Переменная)));
	КолонкиПараметров.Колонки.Добавить("Исключения", Новый ОписаниеТипов("ТаблицаЗначений")); // Счета, для которых будут
		//использоваться константные значения вместо значений параметров

	КолонкиИсключений = Новый ТаблицаЗначений;
	КолонкиИсключений.Колонки.Добавить("Счет", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	КолонкиИсключений.Колонки.Добавить("СПодчиненными", Новый ОписаниеТипов("Булево"));
	КолонкиИсключений.Колонки.Добавить("Значение", Новый ОписаниеТипов("Неопределено, Булево"));

	////////////////////////////////////////////////////////////////
	// Учет МПЗ
	СтрокаРезультата = Результат.Добавить();

	// Счета
	Счета = КолонкиСчетов.СкопироватьКолонки();
	СтрокаРезультата.Счета = Счета;
	
	СтрокаСчета = Счета.Добавить();
	СтрокаСчета.Счет = ПланыСчетов.Хозрасчетный.ПриобретениеОсновныхСредств;
	СтрокаСчета.СПодчиненными = Истина;
	
	СтрокаСчета = Счета.Добавить();
	СтрокаСчета.Счет = ПланыСчетов.Хозрасчетный.ПриобретениеДругихНеоборотныхМатериальныхАктивов;
	СтрокаСчета.СПодчиненными = Истина;

	СтрокаСчета = Счета.Добавить();
	СтрокаСчета.Счет = ПланыСчетов.Хозрасчетный.ПроизводственныеЗапасы;
	СтрокаСчета.СПодчиненными = Истина;
	
	СтрокаСчета = Счета.Добавить();
	СтрокаСчета.Счет = ПланыСчетов.Хозрасчетный.Полуфабрикаты;
	СтрокаСчета.СПодчиненными = Истина;

	СтрокаСчета = Счета.Добавить();
	СтрокаСчета.Счет = ПланыСчетов.Хозрасчетный.Товары;
	СтрокаСчета.СПодчиненными = Истина;

	СтрокаСчета = Счета.Добавить();
	СтрокаСчета.Счет = ПланыСчетов.Хозрасчетный.ГотоваяПродукция;
	СтрокаСчета.СПодчиненными = Истина;

	СтрокаСчета = Счета.Добавить();
	СтрокаСчета.Счет = ПланыСчетов.Хозрасчетный.ПродукцияСельскохозяйственногоПроизводства;
	СтрокаСчета.СПодчиненными = Истина;

	СтрокаСчета = Счета.Добавить();
	СтрокаСчета.Счет = ПланыСчетов.Хозрасчетный.МалоценныеИБыстроизнашивающиесяПредметыНаСкладе;
	СтрокаСчета.СПодчиненными = Истина;


	СтрокаСчета = Счета.Добавить();
	СтрокаСчета.Счет = ПланыСчетов.Хозрасчетный.МатериалыПринятыеВПереработку;
	СтрокаСчета.СПодчиненными = Истина;
	
	СтрокаСчета = Счета.Добавить();
	СтрокаСчета.Счет = ПланыСчетов.Хозрасчетный.ТоварыПринятыеНаКомиссию;
	СтрокаСчета.СПодчиненными = Истина;
	
	СтрокаСчета = Счета.Добавить();
	СтрокаСчета.Счет = ПланыСчетов.Хозрасчетный.УценкиЗапасов;
	СтрокаСчета.СПодчиненными = Ложь;
	
	СтрокаСчета = Счета.Добавить();
	СтрокаСчета.Счет = ПланыСчетов.Хозрасчетный.ТоварыВРозничнойТорговлеВПродажныхЦенахАТТ;
	СтрокаСчета.СПодчиненными = Ложь;

	СтрокаСчета = Счета.Добавить();
	СтрокаСчета.Счет = ПланыСчетов.Хозрасчетный.ТорговаяНаценкаАТТ;
	СтрокаСчета.СПодчиненными = Ложь;

	// Исключения иерархии
	ИсключенияИерархии = Новый Массив;
	СтрокаРезультата.ИсключенияИерархии = ИсключенияИерархии;

	ИсключенияИерархии.Добавить(ПланыСчетов.Хозрасчетный.ТоварыВТорговле);
	ИсключенияИерархии.Добавить(ПланыСчетов.Хозрасчетный.ТоварыВРозничнойТорговлеВПродажныхЦенахНТТ);
	ИсключенияИерархии.Добавить(ПланыСчетов.Хозрасчетный.ТорговаяНаценка);
	ИсключенияИерархии.Добавить(ПланыСчетов.Хозрасчетный.ТорговаяНаценкаНТТ);
	ИсключенияИерархии.Добавить(ПланыСчетов.Хозрасчетный.ТранспортноЗаготовительныеРасходыМатериалы);
	ИсключенияИерархии.Добавить(ПланыСчетов.Хозрасчетный.ТранспортноЗаготовительныеРасходы);
	ИсключенияИерархии.Добавить(ПланыСчетов.Хозрасчетный.ТранспортноЗаготовительныеРасходыТовары);
	ИсключенияИерархии.Добавить(ПланыСчетов.Хозрасчетный.ТранспортноЗаготовительныеРасходыТоварыСуммовойУчет);
	ИсключенияИерархии.Добавить(ПланыСчетов.Хозрасчетный.НеоборотныеАктивыИГруппыВыбытияУдерживаемыеДляПродажи);
	
	// Субконто
	Субконто = КолонкиСубконто.СкопироватьКолонки();
	СтрокаРезультата.Субконто = Субконто;

	СтрокаСубконто = Субконто.Добавить();
	СтрокаСубконто.Вид = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура;
	СтрокаСубконто.Параметр = Истина;
	СтрокаСубконто.Количественный = Истина;
	СтрокаСубконто.Поштучный = Ложь;  //ИНАГРО ++

	СтрокаСубконто.Суммовой = Истина;
	СтрокаСубконто.ТолькоОбороты = Ложь;
	СтрокаСубконто.Валютный = Неопределено;

	СтрокаСубконто = Субконто.Добавить();
	СтрокаСубконто.Вид = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтавкиНДС;
	СтрокаСубконто.Параметр = Ложь;
	СтрокаСубконто.Количественный = Истина;
	СтрокаСубконто.Поштучный = Ложь;    //ИНАГРО ++
	СтрокаСубконто.Суммовой = Истина;
	СтрокаСубконто.ТолькоОбороты = Ложь;
	СтрокаСубконто.Валютный = Неопределено;

	СтрокаСубконто = Субконто.Добавить();
	СтрокаСубконто.Вид = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии;
	СтрокаСубконто.Параметр = "ВестиПартионныйУчет";
	СтрокаСубконто.Количественный = Истина;
	СтрокаСубконто.Поштучный = Ложь;       //ИНАГРО ++
	СтрокаСубконто.Суммовой = Истина;
	СтрокаСубконто.ТолькоОбороты = Ложь;
	СтрокаСубконто.Валютный = Неопределено;

	СтрокаСубконто = Субконто.Добавить();
	СтрокаСубконто.Вид = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады;
	СтрокаСубконто.Параметр = "ВестиСкладскойУчет";
	СтрокаСубконто.Количественный = Истина;
	СтрокаСубконто.Поштучный = Ложь;      //ИНАГРО ++
	СтрокаСубконто.Суммовой = "ВестиСуммовойУчетПоСкладам";
	СтрокаСубконто.ТолькоОбороты = Ложь;
	СтрокаСубконто.Валютный = Неопределено;

	// Параметры
	Параметры = КолонкиПараметров.СкопироватьКолонки();
	СтрокаРезультата.Параметры = Параметры;

	СтрокаПараметра = Параметры.Добавить();
	СтрокаПараметра.Имя = "ВестиПартионныйУчет";
	СтрокаПараметра.Исключения = КолонкиИсключений.Скопировать();

		СтрокаИсключения = СтрокаПараметра.Исключения.Добавить();
		СтрокаИсключения.Счет = ПланыСчетов.Хозрасчетный.ТоварыПринятыеНаКомиссию;
		СтрокаИсключения.СПодчиненными = Истина;
		СтрокаИсключения.Значение = Истина;

		СтрокаИсключения = СтрокаПараметра.Исключения.Добавить();
		СтрокаИсключения.Счет = ПланыСчетов.Хозрасчетный.МатериалыПринятыеВПереработку;
		СтрокаИсключения.СПодчиненными = Ложь;
		СтрокаИсключения.Значение = Неопределено;

	СтрокаПараметра = Параметры.Добавить();
	СтрокаПараметра.Имя = "ВестиСкладскойУчет";
	СтрокаПараметра.Исключения = КолонкиИсключений.Скопировать();

		СтрокаИсключения = СтрокаПараметра.Исключения.Добавить();
		СтрокаИсключения.Счет = ПланыСчетов.Хозрасчетный.МатериалыПереданныеВПереработку;
		СтрокаИсключения.СПодчиненными = Истина;
		СтрокаИсключения.Значение = Неопределено;

		СтрокаИсключения = СтрокаПараметра.Исключения.Добавить();
		СтрокаИсключения.Счет = ПланыСчетов.Хозрасчетный.ТоварыНаКомиссии;
		СтрокаИсключения.СПодчиненными = Ложь;
		СтрокаИсключения.Значение = Неопределено;

		СтрокаИсключения = СтрокаПараметра.Исключения.Добавить();
		СтрокаИсключения.Счет = ПланыСчетов.Хозрасчетный.ТоварыВРозничнойТорговлеВПродажныхЦенахАТТ;
		СтрокаИсключения.СПодчиненными = Ложь;
		СтрокаИсключения.Значение = Истина;

		СтрокаИсключения = СтрокаПараметра.Исключения.Добавить();
		СтрокаИсключения.Счет = ПланыСчетов.Хозрасчетный.ТорговаяНаценкаАТТ;
		СтрокаИсключения.СПодчиненными = Ложь;
		СтрокаИсключения.Значение = Истина;

	СтрокаПараметра = Параметры.Добавить();
	СтрокаПараметра.Имя = "ВестиСуммовойУчетПоСкладам";
	СтрокаПараметра.Исключения = КолонкиИсключений.Скопировать();

		СтрокаИсключения = СтрокаПараметра.Исключения.Добавить();
		СтрокаИсключения.Счет = ПланыСчетов.Хозрасчетный.ТоварыВРозничнойТорговлеВПродажныхЦенахАТТ;
		СтрокаИсключения.СПодчиненными = Ложь;
		СтрокаИсключения.Значение = Истина;

		СтрокаИсключения = СтрокаПараметра.Исключения.Добавить();
		СтрокаИсключения.Счет = ПланыСчетов.Хозрасчетный.ТорговаяНаценкаАТТ;
		СтрокаИсключения.СПодчиненными = Ложь;
		СтрокаИсключения.Значение = Истина;

	Если ТолькоМПЗ Тогда
		Возврат Результат;
	КонецЕсли;
		
	////////////////////////////////////////////////////////////////
	// Розничная торговля
	СтрокаРезультата = Результат.Добавить();

	// Счета
	Счета = КолонкиСчетов.СкопироватьКолонки();
	СтрокаРезультата.Счета = Счета;

	СтрокаСчета = Счета.Добавить();
	СтрокаСчета.Счет = ПланыСчетов.Хозрасчетный.ТоварыВРозничнойТорговлеВПродажныхЦенахНТТ;
	СтрокаСчета.СПодчиненными = Ложь;

	СтрокаСчета = Счета.Добавить();
	СтрокаСчета.Счет = ПланыСчетов.Хозрасчетный.ТорговаяНаценкаНТТ;
	СтрокаСчета.СПодчиненными = Ложь;

	// Субконто
	Субконто = КолонкиСубконто.СкопироватьКолонки();
	СтрокаРезультата.Субконто = Субконто;

	СтрокаСубконто = Субконто.Добавить();
	СтрокаСубконто.Вид = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура;
	СтрокаСубконто.Параметр = "ИспользоватьОборотнуюНоменклатуру";
	СтрокаСубконто.Количественный = Ложь;
	СтрокаСубконто.Поштучный = Ложь;    //ИНАГРО ++
	СтрокаСубконто.Суммовой = Истина;
	СтрокаСубконто.ТолькоОбороты = Истина;
	СтрокаСубконто.Валютный = Неопределено;

	СтрокаСубконто = Субконто.Добавить();
	СтрокаСубконто.Вид = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтавкиНДС;
	СтрокаСубконто.Параметр = "РазделятьПоСтавкамНДС";
	СтрокаСубконто.Количественный = Ложь;
	СтрокаСубконто.Поштучный = Ложь;      //ИНАГРО ++
	СтрокаСубконто.Суммовой = Истина;
	СтрокаСубконто.ТолькоОбороты = Ложь;
	СтрокаСубконто.Валютный = Неопределено;

	СтрокаСубконто = Субконто.Добавить();
	СтрокаСубконто.Вид = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии;
	СтрокаСубконто.Параметр = Ложь;
	СтрокаСубконто.Количественный = Истина;
	СтрокаСубконто.Поштучный = Ложь;        //ИНАГРО ++
	СтрокаСубконто.Суммовой = Истина;
	СтрокаСубконто.ТолькоОбороты = Ложь;
	СтрокаСубконто.Валютный = Неопределено;

	СтрокаСубконто = Субконто.Добавить();
	СтрокаСубконто.Вид = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады;
	СтрокаСубконто.Параметр = Истина;
	СтрокаСубконто.Количественный = Ложь;
	СтрокаСубконто.Поштучный = Ложь;        //ИНАГРО ++
	СтрокаСубконто.Суммовой = Истина;
	СтрокаСубконто.ТолькоОбороты = Ложь;
	СтрокаСубконто.Валютный = Неопределено;

	// Параметры
	Параметры = КолонкиПараметров.СкопироватьКолонки();
	СтрокаРезультата.Параметры = Параметры;

	СтрокаПараметра = Параметры.Добавить();
	СтрокаПараметра.Имя = "ИспользоватьОборотнуюНоменклатуру";
	СтрокаПараметра.Исключения = КолонкиИсключений.Скопировать();

		СтрокаИсключения = СтрокаПараметра.Исключения.Добавить();
		СтрокаИсключения.Счет = ПланыСчетов.Хозрасчетный.ТорговаяНаценкаНТТ;
		СтрокаИсключения.СПодчиненными = Ложь;
		СтрокаИсключения.Значение = Ложь;

	СтрокаПараметра = Параметры.Добавить();
	СтрокаПараметра.Имя = "РазделятьПоСтавкамНДС";
	СтрокаПараметра.Исключения = КолонкиИсключений.Скопировать();

	////////////////////////////////////////////////////////////////
	// Денежные средства
	СтрокаРезультата = Результат.Добавить();

	// Счета
	Счета = КолонкиСчетов.СкопироватьКолонки();
	СтрокаРезультата.Счета = Счета;

	СтрокаСчета = Счета.Добавить();
	СтрокаСчета.Счет = ПланыСчетов.Хозрасчетный.Касса;
	СтрокаСчета.СПодчиненными = Истина;
	// Субконто
	Субконто = КолонкиСубконто.СкопироватьКолонки();
	СтрокаРезультата.Субконто = Субконто;

	СтрокаСубконто = Субконто.Добавить();
	СтрокаСубконто.Вид = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОбособленныеПодразделенияБезОбразованияЮрЛица;
	СтрокаСубконто.Параметр = "КассыОбособленныхПодразделений";
	СтрокаСубконто.Количественный = Ложь;
	СтрокаСубконто.Поштучный = Ложь;     //ИНАГРО ++
	СтрокаСубконто.Суммовой = Истина;
	СтрокаСубконто.ТолькоОбороты = Ложь;
	СтрокаСубконто.Валютный = Истина;

	// Параметры
	Параметры = КолонкиПараметров.СкопироватьКолонки();
	СтрокаРезультата.Параметры = Параметры;

	СтрокаПараметра = Параметры.Добавить();
	СтрокаПараметра.Имя = "КассыОбособленныхПодразделений";
	СтрокаПараметра.Исключения = КолонкиИсключений.Скопировать();

	////////////////////////////////////////////////////////////////
	// Расчеты по документам
	СтрокаРезультата = Результат.Добавить();

	// Счета
	Счета = КолонкиСчетов.СкопироватьКолонки();
	СтрокаРезультата.Счета = Счета;

	МассивСчета = Новый Массив();
	МассивСчета.Добавить("РасчетыСПоставщикамиИПодрядчиками");
	МассивСчета.Добавить("РасчетыСПокупателямиИЗаказчиками");
	МассивСчета.Добавить("РасчетыПоВыданнымАвансам");
	МассивСчета.Добавить("РасчетыСДругимиДебиторами");
	МассивСчета.Добавить("РасчетыПоАвансамПолученным");
	МассивСчета.Добавить("РасчетыСДругимиКредиторами");
	МассивСчета.Добавить("РасчетыПоНеоборотнымАктивамИГруппамВыбытияУдерживаемымиДляПродажи");
	
	Для каждого Счет Из МассивСчета Цикл
		СтрокаСчета = Счета.Добавить();
		СтрокаСчета.Счет = ПланыСчетов.Хозрасчетный[Счет];
		СтрокаСчета.СПодчиненными = Истина;
	КонецЦикла;
	
	// Субконто
	Субконто = КолонкиСубконто.СкопироватьКолонки();
	СтрокаРезультата.Субконто = Субконто;

	СтрокаСубконто = Субконто.Добавить();
	СтрокаСубконто.Вид = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами;
	СтрокаСубконто.Параметр = "ВестиРасчетыПоДокументам";
	СтрокаСубконто.Количественный = Ложь;
	СтрокаСубконто.Поштучный = Ложь;   //ИНАГРО ++
	СтрокаСубконто.Суммовой = Истина;
	СтрокаСубконто.ТолькоОбороты = Ложь;
	СтрокаСубконто.Валютный = Истина;

	// Параметры
	Параметры = КолонкиПараметров.СкопироватьКолонки();
	СтрокаРезультата.Параметры = Параметры;

	СтрокаПараметра = Параметры.Добавить();
	СтрокаПараметра.Имя = "ВестиРасчетыПоДокументам";
	СтрокаПараметра.Исключения = КолонкиИсключений.Скопировать();
	
	СтрокаИсключения = СтрокаПараметра.Исключения.Добавить();
	СтрокаИсключения.Счет = ПланыСчетов.Хозрасчетный.РасчетыПоИсполнительнымДокументам; //6853
	СтрокаИсключения.СПодчиненными = Ложь;
	СтрокаИсключения.Значение = Ложь;
	
	СтрокаИсключения = СтрокаПараметра.Исключения.Добавить();
	СтрокаИсключения.Счет = ПланыСчетов.Хозрасчетный.РасчетыСРабочимиИСлужащимиПоДругимОперациям; //3773
	СтрокаИсключения.СПодчиненными = Ложь;
	СтрокаИсключения.Значение = Ложь;
	
	СтрокаИсключения = СтрокаПараметра.Исключения.Добавить();
	СтрокаИсключения.Счет = ПланыСчетов.Хозрасчетный.РасчетыСРозничнымиПокупателями; //36Р
	СтрокаИсключения.СПодчиненными = Ложь;
	СтрокаИсключения.Значение = Ложь;
	
	СтрокаИсключения = СтрокаПараметра.Исключения.Добавить();
	СтрокаИсключения.Счет = ПланыСчетов.Хозрасчетный.РасчетыПоГарантийномуОбеспечению;  //364
	СтрокаИсключения.СПодчиненными = Ложь;
	СтрокаИсключения.Значение = Ложь;
	
	СтрокаИсключения = СтрокаПараметра.Исключения.Добавить();
	СтрокаИсключения.Счет = ПланыСчетов.Хозрасчетный.РасчетыАрендыЗемлиИИмущества; //6856 ИНАГРО
	СтрокаИсключения.СПодчиненными = Ложь;
	СтрокаИсключения.Значение = Ложь;

	////////////////////////////////////////////////////////////////
	// Зарплата
	
	СтрокаРезультата = Результат.Добавить();

	// Счета
	Счета = КолонкиСчетов.СкопироватьКолонки();
	СтрокаРезультата.Счета = Счета;

	СтрокаСчета = Счета.Добавить();
	СтрокаСчета.Счет = ПланыСчетов.Хозрасчетный.РасчетыПоОплатеТруда;
	СтрокаСчета.СПодчиненными = Истина;
	
	// Субконто
	Субконто = КолонкиСубконто.СкопироватьКолонки();
	СтрокаРезультата.Субконто = Субконто;

	СтрокаСубконто = Субконто.Добавить();
	СтрокаСубконто.Вид = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РаботникиОрганизаций;
	СтрокаСубконто.Параметр = "ВестиУчетПоРаботникам";
	СтрокаСубконто.Количественный = Ложь;
	СтрокаСубконто.Поштучный = Ложь;     //ИНАГРО ++
	СтрокаСубконто.Суммовой = Истина;
	СтрокаСубконто.ТолькоОбороты = Ложь;
	СтрокаСубконто.Валютный = Ложь;

	// Параметры
	Параметры = КолонкиПараметров.СкопироватьКолонки();
	СтрокаРезультата.Параметры = Параметры;

	СтрокаПараметра = Параметры.Добавить();
	СтрокаПараметра.Имя = "ВестиУчетПоРаботникам";
	СтрокаПараметра.Исключения = КолонкиИсключений.Скопировать();

	Возврат Результат;

КонецФункции

Функция ПолучитьЗначенияПараметровУчетаДляСчета(ПараметрыУчета, ПараметрыГруппыСчетов, Счет)

	Результат = Новый Структура;

	Для каждого СтрокаПараметра Из ПараметрыГруппыСчетов Цикл
		ЗначениеУстановлено = Ложь;
		Для каждого СтрокаИсключения Из СтрокаПараметра.Исключения Цикл
			Если СтрокаИсключения.Счет = Счет Тогда
				ЗначениеПараметра = СтрокаИсключения.Значение;
				ЗначениеУстановлено = Истина;
				Прервать;
			Иначе
				Если СтрокаИсключения.СПодчиненными
					И БухгалтерскийУчетВызовСервераПовтИсп.СчетВИерархии(Счет, СтрокаИсключения.Счет) Тогда

					ЗначениеПараметра = СтрокаИсключения.Значение;
					ЗначениеУстановлено = Истина;
					Прервать;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;

		Если НЕ ЗначениеУстановлено Тогда
			ЗначениеПараметра = ПараметрыУчета[СтрокаПараметра.Имя];
		КонецЕсли;
		Результат.Вставить(СтрокаПараметра.Имя, ЗначениеПараметра);
	КонецЦикла;

	Возврат Результат;

КонецФункции

Функция ПолучитьЗначениеПараметраУчетаСубконто(Параметр, ЗначенияПараметровУчетаДляСчета, СтрокаСчета)

	Если Параметр = Неопределено ИЛИ ТипЗнч(Параметр) = Тип("Булево") Тогда
		Возврат Параметр;
	Иначе
		Если ЗначенияПараметровУчетаДляСчета.Свойство(Параметр) Тогда
			Возврат ЗначенияПараметровУчетаДляСчета[Параметр];
		Иначе
			Возврат СтрокаСчета[Параметр];
		КонецЕсли;
	КонецЕсли;

КонецФункции

Функция ПолучитьТекстСообщенияИзмененияСубконто(ОписаниеИзменения)

	ШаблонСообщения = НСтр("ru='Счет %1, субконто ""%2""%3';uk='Рахунок %1, субконто ""%2""%3'");
	Сообщение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения,
		ОписаниеИзменения.Счет, ОписаниеИзменения.Субконто, ОписаниеИзменения.Изменение);

	Возврат Сообщение;

КонецФункции

Функция ПолучитьСтруктуруПризнаковУчетаСубконто()

	ПризнакиУчета = Новый Структура;
	ПризнакиУчета.Вставить("Количественный", НСтр("ru='Количественный';uk='Кількісний'"));
	ПризнакиУчета.Вставить("Поштучный", НСтр("ru='Поголовный';uk='Поголівний'")); // ИНАГРО
	ПризнакиУчета.Вставить("Суммовой", НСтр("ru='Суммовой';uk='Сумовий'"));
	ПризнакиУчета.Вставить("ТолькоОбороты", НСтр("ru='Только обороты';uk='Тільки обороти'"));
	ПризнакиУчета.Вставить("Валютный", НСтр("ru='Валютный';uk='Валютний'"));

	Возврат ПризнакиУчета;

КонецФункции

Функция ПолучитьДействияИзмененияСубконто(ПараметрыУчетаФормы, ТолькоМПЗ = Ложь) Экспорт

	ПараметрыУчета = Новый Структура;
	Для каждого КлючИЗначение Из ПараметрыУчетаФормы Цикл
		ПараметрыУчета.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла;
	// Учет МПЗ
	Если НЕ ПараметрыУчета.Свойство("ВестиСкладскойУчет") Тогда
		ПараметрыУчета.Вставить("ВестиСкладскойУчет", ПараметрыУчета.СкладскойУчет > 0);
	КонецЕсли;
	Если НЕ ПараметрыУчета.Свойство("ВестиСуммовойУчетПоСкладам") Тогда
		ПараметрыУчета.Вставить("ВестиСуммовойУчетПоСкладам", ПараметрыУчета.СкладскойУчет > 1);
	КонецЕсли;

	ПараметрыСубконто = ПолучитьСоответствиеСубконтоПараметрамУчета(ТолькоМПЗ);

	ПризнакиУчета = ПолучитьСтруктуруПризнаковУчетаСубконто();

	ТипДействия = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(1, 0, ДопустимыйЗнак.Любой));
	// -1 удалить; 0 - не менять; 1 - установить

	ТаблицаДействий = Новый ТаблицаЗначений;
	ТаблицаДействий.Колонки.Добавить("Счет", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ТаблицаДействий.Колонки.Добавить("Субконто", Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.ВидыСубконтоХозрасчетные"));
	ТаблицаДействий.Колонки.Добавить("Действие", ТипДействия);
	Для каждого ПризнакУчета Из ПризнакиУчета Цикл
		ТаблицаДействий.Колонки.Добавить(ПризнакУчета.Ключ, ТипДействия);
	КонецЦикла;

	Для каждого ОписаниеГруппыСчетов Из ПараметрыСубконто Цикл

		СчетаВСписке = Новый Массив;
		СчетаВИерархии = Новый Массив;
		СчетаНеВСписке = ОписаниеГруппыСчетов.ИсключенияИерархии;

		Для каждого ОписаниеСчета Из ОписаниеГруппыСчетов.Счета Цикл
			Если ОписаниеСчета.СПодчиненными Тогда
				СчетаВИерархии.Добавить(ОписаниеСчета.Счет);
			Иначе
				СчетаВСписке.Добавить(ОписаниеСчета.Счет);
			КонецЕсли;
		КонецЦикла;

		// Получим список счетов для обработки
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("СчетаВСписке", СчетаВСписке);
		Запрос.УстановитьПараметр("СчетаВИерархии", СчетаВИерархии);
		Запрос.УстановитьПараметр("СчетаНеВСписке", СчетаНеВСписке);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Хозрасчетный.Ссылка КАК Счет,
		|	Хозрасчетный.Порядок КАК Порядок,
		|	Хозрасчетный.Код,
		|	Хозрасчетный.Валютный,
		|	Хозрасчетный.Количественный,
		|	Хозрасчетный.Поштучный,
		|	Хозрасчетный.ВидыСубконто.(
		|		НомерСтроки КАК НомерСтроки,
		|		ВидСубконто,
		|		ТолькоОбороты,
		|		Суммовой,
		|		Валютный,
		|		Количественный,
		|		Поштучный  // ИНАГРО
		|	)
		|ИЗ
		|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
		|ГДЕ
		|	(Хозрасчетный.Ссылка В (&СчетаВСписке)
		|			ИЛИ Хозрасчетный.Ссылка В ИЕРАРХИИ (&СчетаВИерархии)
		|				И (НЕ Хозрасчетный.Ссылка В (&СчетаНеВСписке)))
		|
		|УПОРЯДОЧИТЬ ПО
		|	Порядок,
		|	Счет,
		|	НомерСтроки";

		ВыборкаСчетов = Запрос.Выполнить().Выбрать();
		Пока ВыборкаСчетов.Следующий() Цикл

			ПараметрыСчета = ПолучитьЗначенияПараметровУчетаДляСчета(ПараметрыУчета,
				ОписаниеГруппыСчетов.Параметры, ВыборкаСчетов.Счет);

			ВидыСубконто = ВыборкаСчетов.ВидыСубконто.Выгрузить();

			Для каждого ОписаниеСубконто Из ОписаниеГруппыСчетов.Субконто Цикл
				СтрокаДействия = Неопределено;

				ИспользованиеСубконто = ПолучитьЗначениеПараметраУчетаСубконто(ОписаниеСубконто.Параметр,
					ПараметрыСчета, ВыборкаСчетов);

				Если ИспользованиеСубконто = Неопределено Тогда
					Продолжить;
				КонецЕсли;

				СтрокаСубконто = ВидыСубконто.Найти(ОписаниеСубконто.Вид, "ВидСубконто");
				Если ИспользованиеСубконто Тогда
					Если СтрокаСубконто = Неопределено Тогда
						СтрокаДействия = ТаблицаДействий.Добавить();
						СтрокаДействия.Счет = ВыборкаСчетов.Счет;
						СтрокаДействия.Субконто = ОписаниеСубконто.Вид;
						СтрокаДействия.Действие = 1;						
					КонецЕсли;

					// проверим признаки учета
					Для каждого ПризнакУчета Из ПризнакиУчета Цикл
						ЗначениеПризнака = ПолучитьЗначениеПараметраУчетаСубконто(ОписаниеСубконто[ПризнакУчета.Ключ],
							ПараметрыСчета, ВыборкаСчетов);
						Если ЗначениеПризнака = Неопределено Тогда
							Продолжить;
						КонецЕсли;

						Если СтрокаСубконто = Неопределено
							ИЛИ СтрокаСубконто[ПризнакУчета.Ключ] <> ЗначениеПризнака Тогда

							Если СтрокаДействия = Неопределено Тогда
								СтрокаДействия = ТаблицаДействий.Добавить();
								СтрокаДействия.Счет = ВыборкаСчетов.Счет;
								СтрокаДействия.Субконто = ОписаниеСубконто.Вид;
							КонецЕсли;
							Если ЗначениеПризнака Тогда
								СтрокаДействия[ПризнакУчета.Ключ] = 1;
							Иначе
								СтрокаДействия[ПризнакУчета.Ключ] = -1;
							КонецЕсли;
						КонецЕсли;
					КонецЦикла;
				Иначе
					Если СтрокаСубконто = Неопределено Тогда
						Продолжить;
					Иначе
						СтрокаДействия = ТаблицаДействий.Добавить();
						СтрокаДействия.Счет = ВыборкаСчетов.Счет;
						СтрокаДействия.Субконто = ОписаниеСубконто.Вид;
						СтрокаДействия.Действие = -1;
					КонецЕсли;
				КонецЕсли;

			КонецЦикла;

		КонецЦикла;
	КонецЦикла;

	Возврат ТаблицаДействий;

КонецФункции

Процедура ПрименитьПараметрыУчета(ПараметрыУчета, ИзмененыПараметрыСубконто = Ложь, Отказ = Ложь, ТолькоМПЗ = Ложь) Экспорт

	НачатьТранзакцию();

	ДействияИзмененияСубконто = ПолучитьДействияИзмененияСубконто(ПараметрыУчета);
	ДействияИзмененияСубконто.Добавить();

	ИзмененияСубконто = Новый Массив;

	ПризнакиУчета = ПолучитьСтруктуруПризнаковУчетаСубконто();

	Объект = Неопределено;

	Для каждого СтрокаДействия Из ДействияИзмененияСубконто Цикл

		Если Объект = Неопределено ИЛИ Объект.Ссылка <> СтрокаДействия.Счет Тогда

			Если Объект <> Неопределено
				И Объект.Модифицированность() Тогда // Зафиксируем изменения

				Попытка
					Объект.Записать();
				Исключение

					// выведем все изменения по счету
					ИзмененияПоСчету = "";
					Для каждого ОписаниеИзменения Из ИзмененияСубконто Цикл
						Если ОписаниеИзменения.Счет <> Объект.Ссылка Тогда
							Продолжить;
						КонецЕсли;

						ОписаниеИзменения.Счет = Объект.Код;

						ИзмененияПоСчету = ИзмененияПоСчету + Символы.ПС
							+ ПолучитьТекстСообщенияИзмененияСубконто(ОписаниеИзменения);
					КонецЦикла;

					ШаблонСообщения = НСтр("ru='При попытке изменения субконто %1
|произошла ошибка:
|%2';uk='При спробі зміни субконто %1
|відбулася помилка:
|%2'");

					ОписаниеОшибки = ИнформацияОбОшибке();
					Сообщение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения,
						ИзмененияПоСчету, КраткоеПредставлениеОшибки(ОписаниеОшибки));
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Сообщение);

					Отказ = Истина;

					ЗаписьЖурналаРегистрации(НСтр("ru='Операция не выполнена';uk='Операція не виконана'"), УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки.Описание);
					ОтменитьТранзакцию();
					Возврат;

				КонецПопытки;
			КонецЕсли;

			Если ЗначениеЗаполнено(СтрокаДействия.Счет) Тогда
				Объект = СтрокаДействия.Счет.ПолучитьОбъект();
			Иначе
				Прервать;
			КонецЕсли;

		КонецЕсли;

		СтрокаСубконто = Объект.ВидыСубконто.Найти(СтрокаДействия.Субконто, "ВидСубконто");

		Если СтрокаДействия.Действие = -1 Тогда
			ИзмененияСубконто.Добавить(Новый Структура("Счет, Субконто, Изменение",
				СтрокаДействия.Счет, СтрокаДействия.Субконто, НСтр("ru=' удалено';uk=' вилучено'")));
			Объект.ВидыСубконто.Удалить(СтрокаСубконто);
		Иначе
			Если СтрокаДействия.Действие = 1 Тогда
				
				Если Объект.Ссылка = ПланыСчетов.Хозрасчетный.РасчетыПоОплатеТруда Тогда
					СтрокаСубконто = Объект.ВидыСубконто.Вставить(0);
				Иначе				
					СтрокаСубконто = Объект.ВидыСубконто.Добавить();
				КонецЕсли;
				
				СтрокаСубконто.ВидСубконто = СтрокаДействия.Субконто;
				ИзмененияСубконто.Добавить(Новый Структура("Счет, Субконто, Изменение",
					СтрокаДействия.Счет, СтрокаДействия.Субконто, НСтр("ru=' установлено';uk=' встановлено'")));
			КонецЕсли;

			// проверим признаки учета
			Для каждого ПризнакУчета Из ПризнакиУчета Цикл
				ДействиеСПризнаком = СтрокаДействия[ПризнакУчета.Ключ];
				Если ДействиеСПризнаком = 0 Тогда
					Продолжить;
				КонецЕсли;

				Если ДействиеСПризнаком = 1 Тогда
					ЗначениеПризнака = Истина;
				Иначе
					ЗначениеПризнака = Ложь;
				КонецЕсли;

				СтрокаСубконто[ПризнакУчета.Ключ] = ЗначениеПризнака;

				Если СтрокаДействия.Действие <> 1 Тогда
					Если ЗначениеПризнака Тогда
						ШаблонТекста = НСтр("ru=', установлен признак ""%1""';uk=', встановлена ознака ""%1""'");
					Иначе
						ШаблонТекста = НСтр("ru=', снят признак ""%1""';uk=', знята ознака ""%1""'");
					КонецЕсли;
					ТекстИзменения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста,
						ПризнакУчета.Значение);
					ИзмененияСубконто.Добавить(Новый Структура("Счет, Субконто, Изменение",
						СтрокаДействия.Счет, СтрокаДействия.Субконто, ТекстИзменения));
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;

	КонецЦикла;

	Если ИзмененияСубконто.Количество() > 0 Тогда
		ИзмененыПараметрыСубконто = Истина;

		Для каждого ОписаниеИзменения Из ИзмененияСубконто Цикл
			Сообщение = ПолучитьТекстСообщенияИзмененияСубконто(ОписаниеИзменения);
			ЗаписьЖурналаРегистрации(НСтр("ru='Изменения параметров субконто';uk='Зміни параметрів субконто'"),
				УровеньЖурналаРегистрации.Информация, ОписаниеИзменения.Счет.Метаданные(),
				ОписаниеИзменения.Счет, Сообщение, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		КонецЦикла;

	КонецЕсли;

	БухгалтерскийУчетПереопределяемый.УстановитьПараметрыКлассовСчетов(ПараметрыУчета.ИспользоватьКлассыСчетовВКачествеГрупп);	
	
	БухгалтерскийУчетПереопределяемый.УстановитьПараметрыУчетаРасчетовПоЗарплате(?(ПараметрыУчета.ВестиУчетПоРаботникам = 1, Ложь, Истина));
	БухгалтерскийУчетПереопределяемый.УстановитьУчетЗарплатыИКадровВоВнешнейПрограмме(?(ПараметрыУчета.УчетЗарплатыИКадровВоВнешнейПрограмме = 1, Истина, Ложь));
	БухгалтерскийУчетПереопределяемый.УстановитьНастройкиКадровогоУчета(ПараметрыУчета.КадровыйУчет, ?(ПараметрыУчета.УчетЗарплатыИКадровВоВнешнейПрограмме = 0, Истина, Ложь));

	ЗафиксироватьТранзакцию();

КонецПроцедуры

Функция НайтиУчетнуюПолитику(Организация) Экспорт
	
	Перем Ключ;
	
	Выборка = РегистрыСведений.УчетнаяПолитикаОрганизаций.Выбрать(,, Новый Структура("Организация", Организация), "Убыв");
	Если Выборка.Следующий() Тогда
		ДанныеКлюча	= Новый Структура("Период, Организация", Выборка.Период, Выборка.Организация);
		Ключ	= РегистрыСведений.УчетнаяПолитикаОрганизаций.СоздатьКлючЗаписи(ДанныеКлюча);
	Иначе
		Ключ	= РегистрыСведений.УчетнаяПолитикаОрганизаций.ПустойКлюч();
	КонецЕсли;
	
	Возврат Ключ;

КонецФункции

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПОЛУЧЕНИЯ И УСТАНОВКИ НАСТРОЕК ПОЛЬЗОВАТЕЛЕЙ

// Процедура записывает значение по умолчанию для передаваемого пользователя и настройки.
//
// Параметры:
//  Настройка    - Строка - вид настройки
//  Значение     - значение настройки
//  Пользователь - СправочникСсылка.Пользователи - текущий пользователь программы, для которого устанавливается настройка
//
// Возвращаемое значение:
//  Нет
//
Процедура УстановитьЗначениеПоУмолчанию(Настройка, Значение, Пользователь = Неопределено) Экспорт

	Если ВРег(Настройка) = ВРег("ОсновнаяОрганизация")
		ИЛИ ВРег(Настройка) = ВРег("ОсновноеПодразделениеОрганизации")
		ИЛИ ВРег(Настройка) = ВРег("ОсновноеОбособленноеПодразделениеОрганизации")
		ИЛИ ВРег(Настройка) = ВРег("ОсновнойСклад")
		ИЛИ ВРег(Настройка) = ВРег("ОсновноеМестоСоставленияДокумента")
		ИЛИ ВРег(Настройка) = ВРег("ОсновнойПредставительОрганизации")
		ИЛИ ВРег(Настройка) = ВРег("КтоВыписалНалоговуюНакладную")
		ИЛИ ВРег(Настройка) = ВРег("НоменклатураДляЗаполненияНалоговыхНакладных")
		ИЛИ ВРег(Настройка) = ВРег("РабочаяДата") Тогда
		
		ТекущаяНастройка = ПолучитьЗначениеПоУмолчанию(Настройка, Пользователь);
		
		Если ВРег(Настройка) = ВРег("ОсновнаяОрганизация")
			И НЕ ТекущаяНастройка = Настройка Тогда
			
			// при изменении организации очищаем настройки связанных объектов
			ХранилищеОбщихНастроек.Сохранить(ВРег("ОсновноеПодразделениеОрганизации"),,
				БухгалтерскийУчетПереопределяемый.ПустоеПодразделение(),, Пользователь);
		    
			ХранилищеОбщихНастроек.Сохранить(ВРег("ОсновноеОбособленноеПодразделениеОрганизации"),,
				БухгалтерскийУчетПереопределяемый.ПустоеПодразделение(),, Пользователь);
			
		КонецЕсли;
		
		ХранилищеОбщихНастроек.Сохранить(ВРег(Настройка),, Значение,, Пользователь);

	КонецЕсли;
	
КонецПроцедуры // УстановитьЗначениеПоУмолчанию()

Функция ПолучитьЗначениеПоУмолчанию(Настройка, Пользователь = Неопределено) Экспорт
	
	Возврат БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию(Настройка, Пользователь);
	
КонецФункции

// Определяет подразделение по данным о складе и организации
//
// Параметры:
//  Склад  -  СправочникСсылка.Склад - место хранения МПЗ
//  Организация  - СправочникСсылка.Организации
//
// Возвращаемое значение:
//   Подразделение  -  СправочникСсылка.Подразделение - подразделение, соответствующее
//   указанному месту хранания МПЗ
//
Функция ПолучитьПодразделение(Организация, Склад) Экспорт

	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("Ссылка", Склад);
	Запрос.Текст = 
	
	 "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	 |	Склады.ПодразделениеОрганизации,
	 |	Склады.ПодразделениеОрганизации.Владелец КАК ПодразделениеОрганизацииВладелец
	 |ИЗ
	 |	Справочник.Склады КАК Склады
	 |ГДЕ
	 |	Склады.Ссылка = &Ссылка";
	 
	 Результат = Запрос.Выполнить().Выбрать();
	 Результат.Следующий();
	 
	 ПодразделениеОрганизации = Результат.ПодразделениеОрганизации;
	 ПодразделениеОрганизацииВладелец = Результат.ПодразделениеОрганизацииВладелец;
	
	Если ПодразделениеОрганизацииВладелец = Организация Тогда
		Возврат ПодразделениеОрганизации;
	Иначе
		Возврат БухгалтерскийУчетПереопределяемый.ПустоеПодразделение();
	КонецЕсли;

КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ С ФУНКЦИОНАЛЬНЫМИ ОПЦИЯМИ

Функция ПолучитьФункциональнуюОпциюОбъекта(Имя, Объект) Экспорт

	ПараметрыФО = Новый Структура("Организация, Период",
		Объект.Организация, НачалоМесяца(Объект.Дата));

	Возврат ПолучитьФункциональнуюОпцию(Имя, ПараметрыФО);

КонецФункции

Функция ИспользуетсяНеСтандартныйИнтерфейс() Экспорт
		
	Возврат Ложь;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ С ДИНАМИЧЕСКИМИ СПИСКАМИ

// Возвращает отборы динамического списка как значения заполнения при программном вводе новой строки в список
//
// Параметры:
//  КомпоновщикНастроек  - КомпоновщикНастроекДинамическогоСписка - компоновщик настроек списка
//
// Возвращаемое значение:
//   Структура   - значения отборов для заполнения нового элемента списка
//
Функция ЗначенияЗаполненияДинамическогоСписка(Знач КомпоновщикНастроек) Экспорт
	
	ЗначенияЗаполнения = Новый Структура;
	
	НастройкиСписка = КомпоновщикНастроек.ПолучитьНастройки();
	ДобавитьЗначенияЗаполнения(НастройкиСписка.Отбор.Элементы, ЗначенияЗаполнения);
	
	Возврат ЗначенияЗаполнения;

КонецФункции 

Процедура ДобавитьЗначенияЗаполнения(КоллекцияОтборов, ЗначенияЗаполнения)

	Для каждого ЭлементОтбора Из КоллекцияОтборов Цикл
	
		Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных") 
			И ЭлементОтбора.Использование 
			И ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно Тогда
			НаименованиеОтбора = Строка(ЭлементОтбора.ЛевоеЗначение);
			Если Найти(НаименованиеОтбора, ".") = 0 Тогда
				ЗначенияЗаполнения.Вставить(НаименованиеОтбора, ЭлементОтбора.ПравоеЗначение);
			КонецЕсли; 
		ИначеЕсли ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") 
			И ЭлементОтбора.Использование Тогда
		    ДобавитьЗначенияЗаполнения(ЭлементОтбора, ЗначенияЗаполнения);
		КонецЕсли;
	
	КонецЦикла;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////

Процедура ЗаменитьСвязьПараметровВыбора(Элемент, Имя, НоваяСвязьПараметровВыбора) Экспорт


	МассивСвязей = Новый Массив;
	Для Каждого СвязьПараметровВыбора Из Элемент.СвязиПараметровВыбора Цикл
		Если ВРег(СвязьПараметровВыбора.Имя) <> ВРег(Имя) Тогда
			МассивСвязей.Добавить(СвязьПараметровВыбора);
		КонецЕсли;
	КонецЦикла;

	Элемент.СвязиПараметровВыбора = Новый ФиксированныйМассив(Новый Массив);
	МассивСвязей.Добавить(НоваяСвязьПараметровВыбора);
	Элемент.СвязиПараметровВыбора = Новый ФиксированныйМассив(МассивСвязей);

КонецПроцедуры

// Процедура вызывается из форм платежных документов при смене кассы или
// банковского счета. Меняет валюту, курс документа и пересчитывает сумму.
//
// Параметры:
//  ВалютаДокумента  - ссылка на справочник, валюта документа до изменения;
//  КурсДокумента    - число, курс документа до изменения;
//  ДатаДокумента    - дата, дата на которую будем получать новый курс;
//  СуммаДокумента   - число, сумма документа;
//  ВалютаДенежныхСредств - ссылка на справочник, валюта выбранного счета или кассы
//                     (новая валюта документа);
//
Процедура ПриИзмененииЗначенияКассыБанковскогоСчета(ВалютаДокумента, КурсДокумента, КратностьДокумента, ДатаДокумента,
	                                        СуммаДокумента, ВалютаДенежныхСредств) Экспорт

	СтараяВалюта    = ВалютаДокумента;
	СтарыйКурс      = КурсДокумента;
	СтараяКратность = КратностьДокумента;
	ВалютаДокумента = ВалютаДенежныхСредств;

	СтруктураКурсаВалютаДокумента = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаДокумента, ДатаДокумента);
	КурсДокумента        = СтруктураКурсаВалютаДокумента.Курс;
	КратностьДокумента   = СтруктураКурсаВалютаДокумента.Кратность;

	Если ВалютаДокумента <>  СтараяВалюта
	   И СуммаДокумента > 0 Тогда

		СуммаДокумента = РаботаСКурсамиВалютКлиентСервер.ПересчитатьИзВалютыВВалюту(СуммаДокумента, СтараяВалюта, ВалютаДокумента, СтарыйКурс,
									    КурсДокумента,СтараяКратность,КратностьДокумента);

	КонецЕсли;

КонецПроцедуры

Функция ПолучитьПодразделениеПриИзмененииСчета(Подразделение, Организация, ПодразделениеПоУмолчанию = Неопределено) Экспорт

	Если Не (ЗначениеЗаполнено(Подразделение) 
			И БухгалтерскийУчетПереопределяемый.ПодразделениеПринадлежитОрганизации(Подразделение, Организация)) Тогда
		Если ПодразделениеПоУмолчанию = Неопределено Тогда
			ПодразделениеПоУмолчанию = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновноеПодразделениеОрганизации");
		КонецЕсли;

		Если ЗначениеЗаполнено(ПодразделениеПоУмолчанию) Тогда
			Если БухгалтерскийУчетПереопределяемый.ПодразделениеПринадлежитОрганизации(ПодразделениеПоУмолчанию, Организация) Тогда
				Подразделение = ПодразделениеПоУмолчанию;
			Иначе
				Подразделение = Неопределено;
			КонецЕсли;
		Иначе
			Подразделение = Неопределено;
		КонецЕсли;
	КонецЕсли;

	Возврат Подразделение;

КонецФункции

// Выполняет установку отбора по указанной организации в динамических списках.
// Вызывать необходимо из обработчика формы ПриСозданииНаСервере.
// Если в форму при открытии был передан отбор по организации, то функция не будет выполнена.
//
// Параметры
//  Форма          - УправляемаяФорма  - форма, в которой необходимо установить отбор
//  ИмяСписка      - Строка - имя реквизита формы типа ДинамическийСписок.
//  ИмяРеквизита   - Строка - имя поля-организации в динамическом списке.
//  ЗначениеОтбора - СправочникСсылка.Организации, СписокЗначений, Массив - значение отбора.
//                   Если значение не задано, то будет подставлена основная организация из
//                   настроек пользователя.
//
// Возвращаемое значение:
//   СправочникСсылка.Организации - Если отбор установлен, то вернет значение отбора.
//
Функция УстановитьОтборПоОсновнойОрганизации(Форма, ИмяСписка = "Список", ИмяРеквизита = "Организация", ЗначениеОтбора = Неопределено) Экспорт

	Если Справочники.Организации.ИспользуетсяНесколькоОрганизаций() Тогда
		
		Если Форма.Параметры.Свойство("Отбор") И Форма.Параметры.Отбор.Свойство(ИмяРеквизита) Тогда
			// Если значение отбора передается в параметрах формы - берем его оттуда, параметр при этом удаляем
			ОсновнаяОрганизация = Форма.Параметры.Отбор[ИмяРеквизита];
			Форма.Параметры.Отбор.Удалить(ИмяРеквизита);
		ИначеЕсли ТипЗнч(ЗначениеОтбора) = Тип("СправочникСсылка.Организации") 
			ИЛИ ТипЗнч(ЗначениеОтбора) = Тип("СписокЗначений") 
			ИЛИ ТипЗнч(ЗначениеОтбора) = Тип("Массив") Тогда
			ОсновнаяОрганизация = ЗначениеОтбора;
		Иначе
			ОсновнаяОрганизация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
		КонецЕсли;
		
		Если ТипЗнч(ОсновнаяОрганизация) = Тип("СправочникСсылка.Организации") Тогда
			ВидСравненияОтбора = ВидСравненияКомпоновкиДанных.Равно;
		Иначе
			ВидСравненияОтбора = ВидСравненияКомпоновкиДанных.ВСписке;
		КонецЕсли;
		
		ИспользованиеОтбора = ЗначениеЗаполнено(ОсновнаяОрганизация);
		
		РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ;
		
	Иначе
		
		ОсновнаяОрганизация = Справочники.Организации.ПустаяСсылка();
		ВидСравненияОтбора  = ВидСравненияКомпоновкиДанных.Равно;
		ИспользованиеОтбора = Ложь;
		РежимОтображения    = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Форма[ИмяСписка], ИмяРеквизита, ОсновнаяОрганизация, ВидСравненияОтбора, , ИспользованиеОтбора, РежимОтображения);
	
	Возврат ОсновнаяОрганизация;

КонецФункции


// Возвращает структуру данных со сводным описанием контрагента
//
// Параметры: 
//  СписокСведений - список значений со значенийми параметров организации
//   СписокСведений формируется функцией СведенияОЮрФизЛице
//  Список         - список запрашиваемых параметров организаиии
//  СПрефиксом     - Признак выводить или нет префикс параметра организации
//
// Возвращаемое значение:
//  Строка - описатель организации / контрагента / физ.лица.
//
Функция ОписаниеОрганизации(СписокСведений, Список = "", СПрефиксом = Истина, КодЯзыка = "ru") Экспорт

	Если ПустаяСтрока(Список) Тогда
		Список = "ПолноеНаименование,КодПоЕДРПОУ,КодПоДРФО,ИНН,НомерСвидетельства,ЮридическийАдрес,Телефоны,НомерСчета,Банк,МФО";
	КонецЕсли; 

	Результат = "";
	
	Список = СтрЗаменить(Список, "НомерСчета,Банк,МФО", "ПредставлениеБанковскогоСчета");
	//СписокСведений.Вставить("ПредставлениеБанковскогоСчета", ПолучитьОписаниеБанковскогоСчета(СписокСведений, КодЯзыка));
	СоответствиеПараметров = Новый Соответствие();
	СоответствиеПараметров.Вставить("ПолноеНаименование",		НСтр("ru='';uk=''",КодЯзыка));
	СоответствиеПараметров.Вставить("КодПоЕДРПОУ", 				НСтр("ru='код по ЕДРПОУ ';uk='код за ЄДРПОУ '",КодЯзыка));
	СоответствиеПараметров.Вставить("КодПоДРФО", 				НСтр("ru='код по ДРФО ';uk='код за ДРФО '",КодЯзыка));
	СоответствиеПараметров.Вставить("ИНН",						НСтр("ru='ИНН ';uk='ІПН '",КодЯзыка));
	СоответствиеПараметров.Вставить("НомерСвидетельства", 		НСтр("ru='№ свид. ';uk='№ свід. '",КодЯзыка));
	СоответствиеПараметров.Вставить("ЮридическийАдрес",			НСтр("ru='';uk=''",КодЯзыка));
	СоответствиеПараметров.Вставить("ФактическийАдрес",			НСтр("ru='';uk=''",КодЯзыка));
	СоответствиеПараметров.Вставить("Телефоны",					НСтр("ru='тел.: ';uk='тел.: '",КодЯзыка));
	СоответствиеПараметров.Вставить("Факс",						НСтр("ru='факс: ';uk='факс: '",КодЯзыка));
	СоответствиеПараметров.Вставить("Email",					НСтр("ru='';uk=''",КодЯзыка));
	СоответствиеПараметров.Вставить("НомерСчета",				НСтр("ru='т/с ';uk='п/р '",КодЯзыка));
	СоответствиеПараметров.Вставить("Банк",               		НСтр("ru='в банке ';uk='у банку '",КодЯзыка));
	СоответствиеПараметров.Вставить("МФО",                		НСтр("ru='МФО ';uk='МФО '",КодЯзыка));
	СоответствиеПараметров.Вставить("ПлательщикНалогаНаПрибыль",НСтр("ru='';uk=''",КодЯзыка));
	СоответствиеПараметров.Вставить("ПредставлениеБанковскогоСчета", "");
	СоответствиеПараметров.Вставить("МФО_Платеж",               НСтр("ru='МФО ';uk='МФО '",КодЯзыка));

	ЧислоПараметров = СтрЧислоВхождений(Список, ",");
	Для Счетчик = 1 по ЧислоПараметров Цикл

		ПозЗапятой = Найти(Список, ",");

		Если ПозЗапятой > 0  Тогда
			ИмяПараметра = Лев(Список, ПозЗапятой - 1);
			Список = Сред(Список, ПозЗапятой + 1, СтрДлина(Список));
			
			Если ИмяПараметра = "/" Тогда
				НоваяСтрока = Истина;
				Продолжить;
			КонецЕсли; 
			
			Попытка
				СтрокаДополнения = "";
				СписокСведений.Свойство(ИмяПараметра, СтрокаДополнения);

				Если ПустаяСтрока(СтрокаДополнения) Тогда
					Продолжить;
				КонецЕсли;

				Префикс = СоответствиеПараметров[ИмяПараметра];
				
				Если ПустаяСтрока(Результат)Тогда 
					// это первый параметр, выведем префикс с большой буквы
					Префикс = ВРег(Лев(Префикс,1)) + Сред(Префикс,2);
				ИначеЕсли НоваяСтрока Тогда
					Результат = Результат + "," +Символы.ПС;
				Иначе
					Результат = Результат + ", " 
				КонецЕсли; 
				НоваяСтрока = Ложь;

				Результат = Результат + ?(СПрефиксом = Истина, Префикс, "") + СокрЛП(СтрокаДополнения);
			Исключение
				Сообщить(НСтр("ru='Не удалось определить значение параметра организации: ';uk='Не вдалося визначити значення параметра організації: '") + ИмяПараметра, СтатусСообщения.Внимание);
			КонецПопытки;

		КонецЕсли; 

	КонецЦикла;

	Возврат Результат;

КонецФункции // ОписаниеОрганизации()

// Фомрирует массив параметров, для печати дополнительной информации
//
// Параметры: 
//  ВыборкаШапка   - результат запроса по шапке
//  Список         - список запрашиваемых параметров организаиии
//
// Возвращаемое значение:
//  Массив - со струтктурой (парами НазваниеПараметра:ЗначениеПараметра).
//
Функция ДополнительнаяИнформация(ВыборкаШапка, Список = "", КодЯзыка = "ru") Экспорт

	МассивСтруктур = Новый Массив();

	Если ПустаяСтрока(Список) Тогда
		Возврат МассивСтруктур;
	КонецЕсли; 

	СоответствиеПараметров = Новый Соответствие();
	СоответствиеПараметров.Вставить("ДоговорНаименованиеДляПечати", НСтр("ru='Договор:';uk='Договір:'",КодЯзыка));
	СоответствиеПараметров.Вставить("Содержание",					НСтр("ru='Содержание:';uk='Зміст:'",КодЯзыка));
	СоответствиеПараметров.Вставить("Склад",						НСтр("ru='Склад:';uk='Склад:'",КодЯзыка));
	СоответствиеПараметров.Вставить("Сделка",						НСтр("ru='Сделка:';uk='Угода:'",КодЯзыка));
	СоответствиеПараметров.Вставить("ДокументПередачи",				НСтр("ru='Документ передачи:';uk='Документ передачі:'",КодЯзыка));
	СоответствиеПараметров.Вставить("Инвентаризация",				НСтр("ru='Инвентаризация:';uk='Інвентаризація:'",КодЯзыка));
	СоответствиеПараметров.Вставить("КассаККМ",						НСтр("ru='ЭККА:';uk='ЕККА:'",КодЯзыка));
	СоответствиеПараметров.Вставить("Валюта",						НСтр("ru='Валюта:';uk='Валюта:'",КодЯзыка));
	СоответствиеПараметров.Вставить("Подразделение",				НСтр("ru='Подразделение:';uk='Підрозділ:'",КодЯзыка));
	СоответствиеПараметров.Вставить("АдресДоставки",				НСтр("ru='Адрес доставки:';uk='Адреса доставки:'",КодЯзыка));
	// ИНАГРО ++
	СоответствиеПараметров.Вставить("ФизЛицо",						НСтр("ru='ФизЛицо:';uk='ФізОсоба:'",КодЯзыка));
	// ИНАГРО --

	ЧислоПараметров = СтрЧислоВхождений(Список, ",");
	Для Счетчик = 1 по ЧислоПараметров Цикл

		ПозЗапятой = Найти(Список, ",");

		Если ПозЗапятой > 0  Тогда
			ИмяПараметра = СокрЛП(Лев(Список, ПозЗапятой - 1));
			Список = Сред(Список, ПозЗапятой + 1, СтрДлина(Список));

			Попытка
				ЗначениеПараметра = ВыборкаШапка[ИмяПараметра];
				Если НЕ ЗначениеЗаполнено(ЗначениеПараметра) Тогда
					Если ИмяПараметра = "ДоговорНаименованиеДляПечати" Тогда
						
						Попытка
							ДоговорКонтрагента = ВыборкаШапка["ДоговорКонтрагента"];
						Исключение 
							Продолжить;	
						КонецПопытки;
						
						ДоговорДата  = '00010101';
						ДоговорНомер = "";
						Попытка
							ДоговорДата = ВыборкаШапка["ДоговорДата"];
						Исключение КонецПопытки;
						Попытка
							ДоговорНомер = ВыборкаШапка["ДоговорНомер"];
						Исключение КонецПопытки;
						
						Если ЗначениеЗаполнено(ДоговорНомер) Тогда
							//заменим название договора на его настоящий номер
							ЗначениеПараметра = "№ " + СокрП(ДоговорНомер);
						Иначе
							// названием остается наименование справочника
							ЗначениеПараметра = ДоговорКонтрагента.Наименование;
						КонецЕсли;
						
						Если ЗначениеЗаполнено(ДоговорДата) Тогда
							// добавим дату договора
							ЗначениеПараметра = ЗначениеПараметра + НСтр("ru=' от ';uk=' від '",КодЯзыка) + Формат(ДоговорДата,"ДФ=dd.MM.yyyy")
						КонецЕсли
					Иначе
						Продолжить
					КонецЕсли
				КонецЕсли;
				
				// корректируем значение параметра в некоторых случаях
				Если ИмяПараметра = "ДокументПередачи"
					ИЛИ ИмяПараметра = "Инвентаризация"
					ИЛИ ИмяПараметра = "Заказ" Тогда
					
					ЗначениеПараметра = Локализация.ПолучитьЛокализованноеПредставление(ЗначениеПараметра, КодЯзыка);
					
				ИначеЕсли    ИмяПараметра = "Сделка" Тогда
				
					Сделка = ЗначениеПараметра;
					МетаданныеДокумента = Сделка.Метаданные();
					
					ЗначениеПараметра = Локализация.ПолучитьЛокализованныйСинонимОбъекта(Сделка, КодЯзыка);
					НомерДокумента = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(Сделка.Номер);
					Если ОбщегоНазначенияБП.ЕстьРеквизитДокумента("НомерВходящегоДокумента", МетаданныеДокумента) И ЗначениеЗаполнено(Сделка.НомерВходящегоДокумента) Тогда
						НомерДокумента = Сделка.НомерВходящегоДокумента;
					КонецЕсли;
					ДатаДокумента = Формат(Сделка.Дата, "ДФ='дд ММММ гггг';Л="+ Локализация.ОпределитьКодЯзыкаДляФормат(КодЯзыка));
					Если ОбщегоНазначенияБП.ЕстьРеквизитДокумента("ДатаВходящегоДокумента", МетаданныеДокумента) И ЗначениеЗаполнено(Сделка.ДатаВходящегоДокумента) Тогда
						ДатаДокумента = Формат(Сделка.ДатаВходящегоДокумента, "ДФ='дд ММММ гггг';Л="+ Локализация.ОпределитьКодЯзыкаДляФормат(КодЯзыка));
					КонецЕсли;
					ЗначениеПараметра = ЗначениеПараметра + " № " + НомерДокумента + НСтр("ru=' от ';uk=' від '",КодЯзыка) + ДатаДокумента;
				КонецЕсли; 
				
				
				НазваниеПараметра = Строка(СоответствиеПараметров[ИмяПараметра]);
				Если НЕ ЗначениеЗаполнено(НазваниеПараметра) Тогда
					НазваниеПараметра = ИмяПараметра + ":";
				КонецЕсли;
				
				// корректируем название параметра в некоторых случаях	
				Если ИмяПараметра = "Сделка" Тогда
					
					НазваниеПараметра = НСтр("ru='Расч. док.:';uk='Розр. док.:'",КодЯзыка);
					
				КонецЕсли; 

				МассивСтруктур.Добавить(Новый Структура("НазваниеПараметра,ЗначениеПараметра", 
				                                  НазваниеПараметра,
												  ЗначениеПараметра));
			Исключение
			КонецПопытки;
		КонецЕсли; 
	КонецЦикла;

	Возврат МассивСтруктур;

КонецФункции // ДополнительнаяИнформация()

Функция ПолучитьТелефонДляНалоговойНакладной(Телефоны) Экспорт
	
	Результат = СокрЛП(Телефоны);
	// может быть несколько телефонов, через запятую, возьмем до первой запятой
	Поз = Найти(Результат, ",");
	Если Поз > 0 Тогда
		Результат = Лев(Результат, Поз - 1);
	КонецЕсли; 
	
	Поз = Найти(Результат, ";");
	Если Поз > 0 Тогда
		Результат = Лев(Результат, Поз - 1);
	КонецЕсли; 
	
	РезультатТолькоЦифры = "";
	ДлинаСтрокиТелефонов = СтрДлина(Результат);
	
	// удалим все не цифры
	Для НомерСимвола = 1 По ДлинаСтрокиТелефонов Цикл
		ТекСимвол = Сред(Результат, НомерСимвола, 1);
		Если Найти("0123456789", ТекСимвол) = 0 Тогда
			Продолжить;
		Иначе
			РезультатТолькоЦифры = РезультатТолькоЦифры + ТекСимвол;
		КонецЕсли;
	КонецЦикла; 	
	//для выравнивания по правому краю дополним слева пробелами
	РезультатТолькоЦифры = "          " + РезультатТолькоЦифры;
	
	// возьмем 10 правых 
	РезультатТолькоЦифры = Прав(РезультатТолькоЦифры, 10);
	
	Возврат РезультатТолькоЦифры;

КонецФункции // ПолучитьТелефонДляНалоговойНакладной()

// Стандартная функция форматирования прописи количества
//
// Параметры:
//  Количество - число, которое мы хотим форматировать
//
// Возвращаемое значение:
//  Отформатированная должным образом строковое представление количества.
//
Функция КоличествоПрописью(Количество, КодЯзыка = "ru") Экспорт

	ЦелаяЧасть   = Цел(Количество);
	ДробнаяЧасть = Окр(Количество - ЦелаяЧасть, 3);

	Если ДробнаяЧасть = Окр(ДробнаяЧасть,0) Тогда
		ПараметрыПрописи = ", , , , , , , , 0";

	ИначеЕсли ДробнаяЧасть = Окр(ДробнаяЧасть, 1) Тогда
		ПараметрыПрописи = НСтр("ru='целая, целых, целых, ж, десятая, десятых, десятых, м, 1';uk='ціла, цілих, цілих, ж, десята, десятих, десятих, м, 1'",КодЯзыка);

	ИначеЕсли ДробнаяЧасть = Окр(ДробнаяЧасть, 2) Тогда
		ПараметрыПрописи = НСтр("ru='целая, целых, целых, ж, сотая, сотых, сотых, м, 2';uk='ціла, цілих, цілих, ж, сота, сотих, сотих, м, 2'",КодЯзыка);

	Иначе
		ПараметрыПрописи = НСтр("ru='целая, целых, целых, ж, тысячная, тысячных, тысячных, м, 3';uk='ціла, цілих, цілих, ж, тисячна, тисячних, тисячних, м, 3'",КодЯзыка);

	КонецЕсли;

	Возврат ЧислоПрописью(Количество,"Л="+Локализация.ОпределитьКодЯзыкаДляФормат(КодЯзыка), ПараметрыПрописи);

КонецФункции // КоличествоПрописью()

// Проверяет, умещаются ли переданные табличные документы на страницу при печати.
//
// Параметры
//  ТабДокумент       – Табличный документ
//  ВыводимыеОбласти  – Массив из проверяемых таблиц или табличный документ
//  РезультатПриОшибке - Какой возвращать результат при возникновении ошибки
//
// Возвращаемое значение:
//   Булево   – умещаются или нет переданные документы
//
Функция ПроверитьВыводТабличногоДокумента(ТабДокумент, ВыводимыеОбласти, РезультатПриОшибке = Истина) Экспорт

	Попытка
		Возврат ТабДокумент.ПроверитьВывод(ВыводимыеОбласти);
	Исключение
		ОписаниеОшибки = ИнформацияОбОшибке();
		ЗаписьЖурналаРегистрации(
			НСтр("ru='Невозможно получить информацию о текущем принтере (возможно, в системе не установлено ни одного принтера)';uk='Неможливо отримати інформацію про поточний принтер (можливо, в системі не встановлено жодного принтера)'"),
			УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки.Описание);
		Возврат РезультатПриОшибке;
	КонецПопытки;

КонецФункции // ПроверитьВыводТабличногоДокумента()

// Функция возвращает часть запроса для корректного заполнения содержания услуг
// при формировании печатных форм.
//
// Параметр:
//  ТабличнаяЧасть - имя табличной части, из которой выбирается содержание
//                   услуг.
//
// Возвращаемое значение:
//  Строка - текст части запроса.
//
Функция ПолучитьЧастьЗапросаДляВыбораСодержанияУслуг(Знач ТабличнаяЧасть) Экспорт

	ТабличнаяЧасть = ТабличнаяЧасть + ?(ПустаяСтрока(ТабличнаяЧасть), "", ".");

	ЧастьЗапроса =
	"	ВЫБОР
	|		КОГДА НЕ (" + ТабличнаяЧасть + "Содержание ПОДОБНО """") ТОГДА
	|			" + ТабличнаяЧасть + "Содержание
	|		КОГДА НЕ (" + ТабличнаяЧасть + "Номенклатура.НаименованиеПолное ПОДОБНО """") ТОГДА
	|			" + ТабличнаяЧасть + "Номенклатура.НаименованиеПолное
	|		ИНАЧЕ
	|			" + ТабличнаяЧасть + "Номенклатура.Наименование
	|	КОНЕЦ";

	Возврат ЧастьЗапроса;

КонецФункции // ПолучитьЧастьЗапросаДляВыбораСодержанияУслуг()

Функция ПолучитьМассивПустыхЗначенийПоОписаниюТипов(ОписаниеТипов) Экспорт

	МассивПустыхЗначений = Новый Массив;
	МассивПустыхЗначений.Добавить(Неопределено);

	Для каждого Тип Из ОписаниеТипов.Типы() Цикл

		МассивПустыхЗначений.Добавить(Новый(Тип));

	КонецЦикла;

	Возврат МассивПустыхЗначений;

КонецФункции

Функция ПолучитьКопиюКоллекции(Знач Коллекция) Экспорт

	Возврат СериализаторXDTO.ПрочитатьXDTO(СериализаторXDTO.ЗаписатьXDTO(Коллекция));

КонецФункции

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ РАБОТЫ С ФОРМАМИ

Процедура УстановитьУсловноеОформлениеОсновногоЭлемента(Список, ПараметрыОтбора, 
			ИмяНастройки = "ОсновнойЭлемент", ПредставлениеНастройки = "Выделение основного элемента") Экспорт
	
	Если ТипЗнч(ПараметрыОтбора)<>Тип("Структура") Тогда
		Возврат;
	ИначеЕсли НЕ (ПараметрыОтбора.Свойство("ЛевоеЗначение") 
				И ПараметрыОтбора.Свойство("ВидСравнения") 
				И ПараметрыОтбора.Свойство("ПравоеЗначение")) Тогда
		Возврат;
	КонецЕсли;
	
	СписокУдаляемыхЭлементов = Новый СписокЗначений;
	Для каждого ЭлементУсловногоОформления Из Список.УсловноеОформление.Элементы Цикл
		Если ЭлементУсловногоОформления.ИдентификаторПользовательскойНастройки = ИмяНастройки Тогда
			СписокУдаляемыхЭлементов.Добавить(ЭлементУсловногоОформления);
		КонецЕсли;
	КонецЦикла;
	Для каждого Элемент Из СписокУдаляемыхЭлементов Цикл
		Список.УсловноеОформление.Элементы.Удалить(Элемент.Значение);
	КонецЦикла;
	
	ЭлементУсловногоОформления = Список.УсловноеОформление.Элементы.Добавить();
	
	ЭлементОтбора = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	
	ЭлементОтбора.ЛевоеЗначение  = ПараметрыОтбора.ЛевоеЗначение;
	ЭлементОтбора.ВидСравнения   = ПараметрыОтбора.ВидСравнения;
	ЭлементОтбора.ПравоеЗначение = ПараметрыОтбора.ПравоеЗначение;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Шрифт", Новый Шрифт(, , Истина));
	ЭлементУсловногоОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	ЭлементУсловногоОформления.ИдентификаторПользовательскойНастройки = ИмяНастройки;
	ЭлементУсловногоОформления.Представление = ПредставлениеНастройки;
	
КонецПроцедуры

Функция ПолучитьПараметрыУсловногоОформленияОсновногоЭлемента(ИмяПоляОтбораЛевое, ИмяПоляОтбораПравое, ВидСравнения = "Равно") Экспорт
	
	Возврат(Новый Структура("ЛевоеЗначение, ВидСравнения, ПравоеЗначение",
		Новый ПолеКомпоновкиДанных(ИмяПоляОтбораЛевое), ВидСравненияКомпоновкиДанных[ВидСравнения],
		Новый ПолеКомпоновкиДанных(ИмяПоляОтбораПравое)));
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ЗАПРОСЫ, ПОЛЯ И РЕКВИЗИТЫ

// Получение незаполненного значения для типа определенного реквизита.
// Результат - массив значений (актуально для составного типа)
//
Функция ПолучитьНезаполненноеЗначениеРеквизита(ТипДанныхРеквизита) Экспорт
	
	ПустыеЗначенияТипа = Новый Массив;

	Для Каждого Тип Из ТипДанныхРеквизита.Типы() Цикл
		МассивТипов = Новый Массив;
		МассивТипов.Добавить(Тип);
		Описание = Новый ОписаниеТипов(МассивТипов);
		ПустыеЗначенияТипа.Добавить(Описание.ПривестиЗначение());
	КонецЦикла;

	Возврат Новый ФиксированныйМассив(ПустыеЗначенияТипа);
	
КонецФункции                                  

// Возвращает массив ссылок на все договоры данной организации
//
Функция ПолучитьВсеДоговорыОрганизации(Организация) Экспорт
	
	Результат = Новый Массив();
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК ДоговорСсылка
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.Организация = &Организация";
	Запрос.УстановитьПараметр("Организация", Организация);
	Выборка = Запрос.Выполнить().Выбрать();
	                                                        
	Пока Выборка.Следующий() Цикл
		Результат.Добавить(Выборка.ДоговорСсылка);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// РАБОТА С КЛАССИФИКАТОРАМИ

Функция ПолучитьКлассификатор(ИмяМакета) Экспорт

	Классификатор = Новый Соответствие();
	
	Макет = Справочники.Организации.ПолучитьМакет(ИмяМакета);
	
	ТекущаяОбласть = Макет.Область("Классификатор");

	Если ТекущаяОбласть <> Неопределено Тогда	
	
		Для НомерСтр = ТекущаяОбласть.Верх По ТекущаяОбласть.Низ Цикл
			
			// Перебор строк макета.
			КодПоказателя = СокрП(Макет.Область(НомерСтр, 1).Текст);
			Название      = СокрП(Макет.Область(НомерСтр, 2).Текст);
			
			Если КодПоказателя = "###" Тогда
				Прервать;
			ИначеЕсли ПустаяСтрока(КодПоказателя) Тогда
				Продолжить;
			Иначе
				Классификатор.Вставить(КодПоказателя, Название);
			КонецЕсли;

		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Новый ФиксированноеСоответствие(Классификатор);

КонецФункции


////////////////////////////////////////////////////////////////////////////////
// РАБОТА СО СПРАВОЧНИКАМИ

Функция ПолучитьКоличествоЭлементовСправочника(ИмяСправочника) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО(Организации.Ссылка) КАК КоличествоЭлементов
	|ИЗ
	|	Справочник.Организации КАК Организации";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "Организации", ИмяСправочника);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.КоличествоЭлементов;
	КонецЕсли;
	
	Возврат 0;

КонецФункции

Функция ПроверитьНаличиеДоступныхОрганизаций() Экспорт

	Если НЕ ОбщегоНазначенияПовтИсп.ДоступноИспользованиеРазделенныхДанных() Тогда
	 	Возврат Истина;
	КонецЕсли;
	
	Если Пользователи.ЭтоПолноправныйПользователь() Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат ПолучитьКоличествоЭлементовСправочника("Организации") > 0;

КонецФункции

Функция СформироватьМассивПрефиксовДляРИБИОрганизации(ПрефиксыОрганизаций) 
	
	МассивПрефиксов = ПолучитьМассивПрефиксовРИБ();
	
	Для Каждого ПрефиксОрганизации Из ПрефиксыОрганизаций Цикл
		Если ПрефиксОрганизации <> "" Тогда
			МассивПрефиксов.Добавить(СокрЛП(ПрефиксОрганизации));
		КонецЕсли;
	КонецЦикла;
	
	Если МассивПрефиксов.Количество() = 0 Тогда
		МассивПрефиксов.Добавить("");
	КонецЕсли;
	
	Возврат МассивПрефиксов;
	
КонецФункции

Функция ПолучитьМассивПрефиксовРИБ()
	
	МассивПрефиксов = Новый Массив();
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ Различные
	               |	ПрефиксыИнформационныхБаз.Префикс КАК Префикс
	               |ИЗ
	               |	РегистрСведений.ПрефиксыИнформационныхБазБ12 КАК ПрефиксыИнформационныхБаз";
	
	ВыборкаУзлов = Запрос.Выполнить().Выбрать();
	Пока ВыборкаУзлов.Следующий() Цикл
		
		Если Не ПустаяСтрока(ВыборкаУзлов.Префикс) Тогда
			
			МассивПрефиксов.Добавить(СокрЛП(ВыборкаУзлов.Префикс));

		КонецЕсли;
		
	КонецЦикла;	
	
	Возврат МассивПрефиксов;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// 

Функция ОчиститьДанныеВБазе() Экспорт
	
	Если НЕ Пользователи.ЭтоПолноправныйПользователь(, Истина) Тогда
		ВызватьИсключение(НСтр("ru='Не достаточно прав для выполнения операции';uk='Не достатньо прав для виконання операції'"));
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Попытка
		ОбщегоНазначения.ЗаблокироватьИБ();
	Исключение
		Сообщение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Не удалось установить монопольный режим (%1)';uk='Не вдалося встановити монопольний режим (%1)'"),
			КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат Ложь;
	КонецПопытки;
	
	НачатьТранзакцию();
	Попытка
		
		МДОбщегоРеквизита = Метаданные.ОбщиеРеквизиты.ОбластьДанныхОсновныеДанные;
		
		// Переберем все метаданные
		
		// Константы
		Для каждого МетаданныеКонстанты Из Метаданные.Константы Цикл
			Если НЕ ОбщегоНазначения.ЭтоРазделенныйОбъектМетаданных(МетаданныеКонстанты,
				ОбщегоНазначенияПовтИсп.РазделительОсновныхДанных()) Тогда
				Продолжить;
			КонецЕсли;
			
			МенеджерЗначения = Константы[МетаданныеКонстанты.Имя].СоздатьМенеджерЗначения();
			МенеджерЗначения.ОбменДанными.Загрузка = Истина;
			МенеджерЗначения.Значение = МетаданныеКонстанты.Тип.ПривестиЗначение();
			МенеджерЗначения.Записать();
		КонецЦикла;
		
		// Ссылочные типы
		
		ВидыОбъектов = Новый Массив;
		ВидыОбъектов.Добавить("Справочники");
		ВидыОбъектов.Добавить("Документы");
		ВидыОбъектов.Добавить("ПланыВидовХарактеристик");
		ВидыОбъектов.Добавить("ПланыСчетов");
		ВидыОбъектов.Добавить("ПланыВидовРасчета");
		ВидыОбъектов.Добавить("БизнесПроцессы");
		ВидыОбъектов.Добавить("Задачи");
		
		Для каждого ВидОбъекта Из ВидыОбъектов Цикл
			МетаданныеКоллекция = Метаданные[ВидОбъекта];
			Для каждого МДОбъекта Из МетаданныеКоллекция Цикл
				Если НЕ ОбщегоНазначения.ЭтоРазделенныйОбъектМетаданных(МДОбъекта,
					ОбщегоНазначенияПовтИсп.РазделительОсновныхДанных()) Тогда
					Продолжить;
				КонецЕсли;
				
				Запрос = Новый Запрос;
				Запрос.Текст =
				"ВЫБРАТЬ
				|	_XMLВыгрузка_Таблица.Ссылка КАК Ссылка
				|ИЗ
				|	" + МДОбъекта.ПолноеИмя() + " КАК _XMLВыгрузка_Таблица";
				Если ВидОбъекта = "Справочники"
					ИЛИ ВидОбъекта = "ПланыВидовХарактеристик"
					ИЛИ ВидОбъекта = "ПланыСчетов"
					ИЛИ ВидОбъекта = "ПланыВидовРасчета" Тогда
					
					Запрос.Текст = Запрос.Текст + "
					|ГДЕ
					|	_XMLВыгрузка_Таблица.Предопределенный = ЛОЖЬ";
				КонецЕсли;
				
				РезультатЗапроса = Запрос.Выполнить();
				Выборка = РезультатЗапроса.Выбрать();
				Пока Выборка.Следующий() Цикл
					Удаление = Новый УдалениеОбъекта(Выборка.Ссылка);
					Удаление.ОбменДанными.Загрузка = Истина;
					Удаление.Записать();
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
		
		// Регистры кроме независимых регистров сведений и последовательности
		ВидыТаблиц = Новый Массив;
		ВидыТаблиц.Добавить("РегистрыНакопления");
		ВидыТаблиц.Добавить("РегистрыРасчета");
		ВидыТаблиц.Добавить("РегистрыБухгалтерии");
		ВидыТаблиц.Добавить("РегистрыСведений");
		ВидыТаблиц.Добавить("Последовательности");
		Для каждого ВидТаблицы Из ВидыТаблиц Цикл
			МетаданныеКоллекция = Метаданные[ВидТаблицы];
			МенеджерВида = Вычислить(ВидТаблицы);
			Для каждого МДРегистра Из МетаданныеКоллекция Цикл
				
				Если НЕ ОбщегоНазначения.ЭтоРазделенныйОбъектМетаданных(МДРегистра,
					ОбщегоНазначенияПовтИсп.РазделительОсновныхДанных()) Тогда
					Продолжить;
				КонецЕсли;
				
				Если ВидТаблицы = "РегистрыСведений"
					И МДРегистра.РежимЗаписи = Метаданные.СвойстваОбъектов.РежимЗаписиРегистра.Независимый Тогда
					
					Продолжить;
				КонецЕсли;
				
				МенеджерТипа = МенеджерВида[МДРегистра.Имя];
				
				Запрос = Новый Запрос;
				Запрос.Текст =
				"ВЫБРАТЬ РАЗЛИЧНЫЕ
				|	_XMLВыгрузка_Таблица.Регистратор КАК Регистратор
				|ИЗ
				|	" + МДРегистра.ПолноеИмя() + " КАК _XMLВыгрузка_Таблица";
				РезультатЗапроса = Запрос.Выполнить();
				Выборка = РезультатЗапроса.Выбрать();
				Пока Выборка.Следующий() Цикл
					НаборЗаписей = МенеджерТипа.СоздатьНаборЗаписей();
					НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
					НаборЗаписей.ОбменДанными.Загрузка = Истина;
					НаборЗаписей.Записать();
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
		
		// Независимые регистры сведений
		Для каждого МДРегистра Из Метаданные.РегистрыСведений Цикл
			
			Если НЕ ОбщегоНазначения.ЭтоРазделенныйОбъектМетаданных(МДРегистра,
				ОбщегоНазначенияПовтИсп.РазделительОсновныхДанных()) Тогда
				Продолжить;
			КонецЕсли;
			
			Если МДРегистра.РежимЗаписи = Метаданные.СвойстваОбъектов.РежимЗаписиРегистра.ПодчинениеРегистратору Тогда
				
				Продолжить;
			КонецЕсли;
			
			МенеджерТипа = РегистрыСведений[МДРегистра.Имя];
			
			НаборЗаписей = МенеджерТипа.СоздатьНаборЗаписей();
			НаборЗаписей.ОбменДанными.Загрузка = Истина;
			НаборЗаписей.Записать();
		КонецЦикла;
		
		// Планы обмена
		
		Для каждого МДПланаОбмена Из Метаданные.ПланыОбмена Цикл
			
			Если НЕ ОбщегоНазначения.ЭтоРазделенныйОбъектМетаданных(МДПланаОбмена,
				ОбщегоНазначенияПовтИсп.РазделительОсновныхДанных()) Тогда
				Продолжить;
			КонецЕсли;
			
			МенеджерТипа = ПланыОбмена[МДПланаОбмена.Имя];
			
			Запрос = Новый Запрос;
			Запрос.Текст =
			"ВЫБРАТЬ
			|	_XMLВыгрузка_Таблица.Ссылка КАК Ссылка
			|ИЗ
			|	" + МДПланаОбмена.ПолноеИмя() + " КАК _XMLВыгрузка_Таблица
			|ГДЕ
			|	_XMLВыгрузка_Таблица.Ссылка <> &ЭтотУзел";
			Запрос.УстановитьПараметр("ЭтотУзел", МенеджерТипа.ЭтотУзел());
			РезультатЗапроса = Запрос.Выполнить();
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				Удаление = Новый УдалениеОбъекта(Выборка.Ссылка);
				Удаление.ОбменДанными.Загрузка = Истина;
				Удаление.Записать();
			КонецЦикла;
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
		ОбщегоНазначения.РазблокироватьИБ();
		
	Исключение
		ОтменитьТранзакцию();
		ЗаписьЖурналаРегистрации(НСтр("ru='Удаление данных';uk='Вилучення даних'"), 
			УровеньЖурналаРегистрации.Ошибка, , , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат Ложь;
	КонецПопытки;
	Возврат Истина;
	
КонецФункции


////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС ПОЛЯ ВЫБОРА ОРГАНИЗАЦИИ С ОБОСОБЛЕННЫМИ ПОДРАЗДЕЛЕНИЯМИ
//

Процедура ЗаполнитьСписокОрганизаций(ЭлементПолеОрганизация, СоответствиеОрганизаций) Экспорт
	
	СоответствиеОрганизаций = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НаборОрганизаций.Организация КАК Организация,
	|	НаборОрганизаций.ОрганизацияПредставление КАК ОрганизацияПредставление,
	|	НаборОрганизаций.ВключатьОбособленныеПодразделения
	|ИЗ
	|	(ВЫБРАТЬ
	|		Организации.Ссылка КАК Организация,
	|		Организации.Наименование КАК ОрганизацияПредставление,
	|		ЛОЖЬ КАК ВключатьОбособленныеПодразделения
	|	ИЗ
	|		Справочник.Организации КАК Организации
	|";
	
	Если БухгалтерскиеОтчетыВызовСервераПовтИсп.ДоступностьУчетаПоПодразделениям() Тогда
		Запрос.Текст = Запрос.Текст +
		"
		|	ОБЪЕДИНИТЬ ВСЕ
		|" + "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		Организации.ГоловнаяОрганизация,
		|		Организации.ГоловнаяОрганизация.Наименование + "" с обособленными подразделениями"",
		|		ИСТИНА
		|	ИЗ
		|		Справочник.Организации КАК Организации
		|	ГДЕ
		|		Организации.ОбособленноеПодразделение";
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст +
		") КАК НаборОрганизаций
		|УПОРЯДОЧИТЬ ПО
		|	ОрганизацияПредставление";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ЭлементПолеОрганизация.СписокВыбора.Очистить();
	МаксКоличествоСимволов = 40;
	Пока Выборка.Следующий() Цикл
		Ключ     = СтрЗаменить(Строка(Выборка.ВключатьОбособленныеПодразделения) + Выборка.Организация.УникальныйИдентификатор(), "-", "");
		Значение = Новый Структура("Организация,ВключатьОбособленныеПодразделения", Выборка.Организация, Выборка.ВключатьОбособленныеПодразделения);
		СоответствиеОрганизаций.Вставить(Ключ, Значение);
		ЭлементПолеОрганизация.СписокВыбора.Добавить(Ключ, Выборка.ОрганизацияПредставление);
		
		МаксКоличествоСимволов = Макс(МаксКоличествоСимволов, СтрДлина(Выборка.ОрганизацияПредставление));
	КонецЦикла;
	
	ЭлементПолеОрганизация.ШиринаСпискаВыбора = Окр(?(МаксКоличествоСимволов > 200, 200, МаксКоличествоСимволов) * 1.3);
	ЭлементПолеОрганизация.ВысотаСпискаВыбора = ?(ЭлементПолеОрганизация.СписокВыбора.Количество() > 15, 15, ЭлементПолеОрганизация.СписокВыбора.Количество());

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБЩИЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ РАБОТЫ С ДАННЫМИ В БАЗЕ

// Возвращает структуру, содержащую значения реквизитов прочитанные из информационной базы
// по ссылке на объект.
// 
//  Если доступа к одному из реквизитов нет, возникнет исключение прав доступа.
//  Если необходимо зачитать реквизит независимо от прав текущего пользователя,
//  то следует использовать предварительный переход в привилегированный режим.
// 
// Параметры:
//  Ссылка    - Ссылка на объект - элемент справочника, документ, ...
//
//  Реквизиты - Строка - имена реквизитов, перечисленные через запятую, в формате
//              требований к свойствам структуры.
//              Например, "Код, Наименование, Родитель".
//            - Структура, ФиксированнаяСтруктура - в качестве ключа передается
//              псевдоним поля для возвращаемой структуры с результатом, а в качестве
//              значения (опционально) фактическое имя поля в таблице.
//              Если значение не определено, то имя поля берется из ключа.
//            - Массив, ФиксированныйМассив - имена реквизитов в формате требований
//              к свойствам структуры.
//
// Возвращаемое значение:
//  Структура - содержит имена (ключи) и значения затребованных реквизитов.
// 
Функция ЗначенияРеквизитовОбъекта(Ссылка, Реквизиты) Экспорт
	
	Если ТипЗнч(Реквизиты) = Тип("Строка") Тогда
		СтруктураРеквизитов = Новый Структура(Реквизиты);
		
	ИначеЕсли ТипЗнч(Реквизиты) = Тип("Структура")
	      ИЛИ ТипЗнч(Реквизиты) = Тип("ФиксированнаяСтруктура") Тогда
		
		СтруктураРеквизитов = Реквизиты;
		
	ИначеЕсли ТипЗнч(Реквизиты) = Тип("Массив")
	      ИЛИ ТипЗнч(Реквизиты) = Тип("ФиксированныйМассив") Тогда
		
		СтруктураРеквизитов = Новый Структура;
		Для каждого Реквизит Из Реквизиты Цикл
			СтруктураРеквизитов.Вставить(Реквизит);
		КонецЦикла;
	Иначе
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Неверный тип второго параметра Реквизиты: %1';uk='Невірний тип другого параметра Реквізити: %1'"),
			Строка(ТипЗнч(Реквизиты)));
	КонецЕсли;
	
	ТекстПолей = "";
	Для Каждого КлючИЗначение Из СтруктураРеквизитов Цикл
		
		ИмяПоля   = ?(ЗначениеЗаполнено(КлючИЗначение.Значение),
		              СокрЛП(КлючИЗначение.Значение),
		              СокрЛП(КлючИЗначение.Ключ));
		
		Псевдоним = СокрЛП(КлючИЗначение.Ключ);
		
		ТекстПолей  = ТекстПолей + ?(ПустаяСтрока(ТекстПолей), "", ",") + "
		|	" + ИмяПоля + " КАК " + Псевдоним;
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|" + ТекстПолей + "
	|ИЗ
	|	" + Ссылка.Метаданные().ПолноеИмя() + " КАК ПсевдонимЗаданнойТаблицы
	|ГДЕ
	|	ПсевдонимЗаданнойТаблицы.Ссылка = &Ссылка
	|";
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Результат = Новый Структура;
	Для Каждого КлючИЗначение Из СтруктураРеквизитов Цикл
		Результат.Вставить(КлючИЗначение.Ключ);
	КонецЦикла;
	ЗаполнитьЗначенияСвойств(Результат, Выборка);
	
	Возврат Результат;
	
КонецФункции

// Возвращает значение реквизита, прочитанного из информационной базы по ссылке на объект.
// 
//  Если доступа к реквизиту нет, возникнет исключение прав доступа.
//  Если необходимо зачитать реквизит независимо от прав текущего пользователя,
//  то следует использовать предварительный переход в привилегированный режим.
// 
// Параметры:
//  Ссылка       - ссылка на объект, - элемент справочника, документ, ...
//  ИмяРеквизита - Строка, например, "Код".
// 
// Возвращаемое значение:
//  Произвольный    - зависит от типа значения прочитанного реквизита.
// 
Функция ЗначениеРеквизитаОбъекта(Ссылка, ИмяРеквизита) Экспорт
	
	Результат = ЗначенияРеквизитовОбъекта(Ссылка, ИмяРеквизита);
	Возврат Результат[ИмяРеквизита];
	
КонецФункции 

// Процедура активизирует элемент формы.
// Если это - табличная часть, то тогда анализируется,
// может табличная часть на закладке и если так,
// то закладка становится текущей, но табличная часть не активизируется
//
// Параметры:
//  Форма            - Управляемая форма
//  ИмяЭлементаФормы - Строка - имя элемента, который необходимо активизировать
//
Процедура АктивизироватьЭлементФормы(Форма, ИмяЭлементаФормы) Экспорт

	Если НЕ ПустаяСтрока(ИмяЭлементаФормы) Тогда
		НайденныйЭлементФормы = Форма.Элементы.Найти(ИмяЭлементаФормы);
		Если НайденныйЭлементФормы <> Неопределено Тогда
			Если ТипЗнч(НайденныйЭлементФормы) = Тип("ТаблицаФормы") Тогда
				// Для таблицы определить - если она находится на закладке, то не активизировать элемент,
				// а сделать активной страницу, на которой находится эта табличная часть
				Страница = НайденныйЭлементФормы.Родитель;
				Пока Страница <> Неопределено И ТипЗнч(Страница) = Тип("ГруппаФормы") И Страница.Вид = ВидГруппыФормы.ОбычнаяГруппа Цикл
					// Таблица может быть внутри группы, а группа на странице
					Страница = Страница.Родитель;
				КонецЦикла;
				Если (Страница <> Неопределено) И ТипЗнч(Страница) = Тип("ГруппаФормы") И (Страница.Вид = ВидГруппыФормы.Страница) Тогда
					// Определим владельца этой страницы и активизируем эту страницу
					ПанельСтраниц = Страница.Родитель;
					Если (ПанельСтраниц <> Неопределено) И (ПанельСтраниц.Вид = ВидГруппыФормы.Страницы) Тогда
						ПанельСтраниц.ТекущаяСтраница = Страница;
					Иначе
						Форма.ТекущийЭлемент = НайденныйЭлементФормы;
					КонецЕсли;
				Иначе
					Форма.ТекущийЭлемент = НайденныйЭлементФормы;
				КонецЕсли;
			Иначе
				Форма.ТекущийЭлемент = НайденныйЭлементФормы;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Возвращает имя внешней обработки путеводителя по демо-базе иил пустое значение, если путеводитель запускать не требуется
// 
Функция ПолучитьИмяОбработкиПутеводительПоДемоБазе() Экспорт
	
	ПоказыватьПриСтарте = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ПутеводительПоДемоБазе", "Показывать", Истина);
	
	Если НЕ ПоказыватьПриСтарте Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ОбщегоНазначенияПовтИсп.РазделениеВключено() Тогда
		Возврат Неопределено;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВнешниеОбработки.Ссылка
	|ИЗ
	|	Справочник.ДополнительныеОтчетыИОбработки КАК ВнешниеОбработки
	|ГДЕ
	|	ВнешниеОбработки.ИмяОбъекта = &ИмяОбъекта";
	
	Запрос.УстановитьПараметр("ИмяОбъекта", "ПутеводительПоДемонстрационнойБазе");
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат ДополнительныеОтчетыИОбработки.ПодключитьВнешнююОбработку(Выборка.Ссылка);
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Загрузка из 7.7

Функция ТребуетсяПредложитьЗагрузкуИз1СПредприятия77() Экспорт
	
	Если НЕ РольДоступна("ПолныеПрава") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Организации.Ссылка
	|ИЗ
	|	Справочник.Организации КАК Организации";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Пустой();
	
КонецФункции


// Данная функция получает сведения о составе комиссии,
// 	занимаемых должностях членами комиссии для вывода
// 	в печатные формы документов.
//
// Параметры:
//  ДокументОбъект – Тип: ДокументОбъект – Распечатываемый документ
//                 
//
// Возвращаемое значение:
//   Тип:   Выборка из запроса – Сведения о составе комиссии, должностях и ФИО членов комиссии 
//
Функция ПолучитьСведенияОКомиссии(ДокументОбъект) Экспорт
	
	
	СоставКомиссии = Новый Структура;
	
	СоставКомиссии.Вставить("ПредседательКомиссииФИО" , "");
	СоставКомиссии.Вставить("ПредседательКомиссииДолжность", "");
	
	СоставКомиссии.Вставить("ПервыйЧленКомиссииФИО", "");
	СоставКомиссии.Вставить("ПервыйЧленКомиссииДолжность", "");
	
	СоставКомиссии.Вставить("ВторойЧленКомиссииФИО", "");
	СоставКомиссии.Вставить("ВторойЧленКомиссииДолжность", "");
	
	СоставКомиссии.Вставить("ТретийЧленКомиссииФИО", "");
	СоставКомиссии.Вставить("ТретийЧленКомиссииДолжность", "");
	
	Если ЗначениеЗаполнено(ДокументОбъект.ПредседательКомиссии) Тогда
		
		ДанныеФизЛицаП = ИНАГРО_ЗарплатаКадрыРасширенный.ДанныеФизЛица(ДокументОбъект.Организация,ДокументОбъект.ПредседательКомиссии,ДокументОбъект.Дата);
		ДанныеПредседательКомиссии = ДанныеФизЛица(
										ДокументОбъект.Организация, 
										ДокументОбъект.ПредседательКомиссии, 
										ДокументОбъект.Дата, 
										Истина // ФИОКратко 
									 );
									 
		СоставКомиссии.Вставить("ПредседательКомиссииФИО" ,      ДанныеПредседательКомиссии.Представление);
		СоставКомиссии.Вставить("ПредседательКомиссииДолжность", ДанныеФизЛицаП.Должность);
									 
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(ДокументОбъект.ПервыйЧленКомиссии) Тогда
		
		ДанныеФизЛица1 = ИНАГРО_ЗарплатаКадрыРасширенный.ДанныеФизЛица(ДокументОбъект.Организация,ДокументОбъект.ПервыйЧленКомиссии,ДокументОбъект.Дата);
		ДанныеПервыйЧленКомиссии =   ДанныеФизЛица(
										ДокументОбъект.Организация, 
										ДокументОбъект.ПервыйЧленКомиссии, 
										ДокументОбъект.Дата, 
										Истина // ФИОКратко 
									 );
									 
		СоставКомиссии.Вставить("ПервыйЧленКомиссииФИО" ,      ДанныеПервыйЧленКомиссии.Представление);
		СоставКомиссии.Вставить("ПервыйЧленКомиссииДолжность", ДанныеФизЛица1.Должность);
									 
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(ДокументОбъект.ВторойЧленКомиссии) Тогда
		
		ДанныеФизЛица2 = ИНАГРО_ЗарплатаКадрыРасширенный.ДанныеФизЛица(ДокументОбъект.Организация,ДокументОбъект.ВторойЧленКомиссии,ДокументОбъект.Дата);
		ДанныеВторойЧленКомиссии =   ДанныеФизЛица(
										ДокументОбъект.Организация, 
										ДокументОбъект.ВторойЧленКомиссии, 
										ДокументОбъект.Дата, 
										Истина // ФИОКратко 
									 );
									 
		СоставКомиссии.Вставить("ВторойЧленКомиссииФИО" ,      ДанныеВторойЧленКомиссии.Представление);
		СоставКомиссии.Вставить("ВторойЧленКомиссииДолжность", ДанныеФизЛица2.Должность);
									 
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(ДокументОбъект.ТретийЧленКомиссии) Тогда
		
		ДанныеФизЛица3 = ИНАГРО_ЗарплатаКадрыРасширенный.ДанныеФизЛица(ДокументОбъект.Организация,ДокументОбъект.ТретийЧленКомиссии,ДокументОбъект.Дата);
		ДанныеТретийЧленКомиссии =   ДанныеФизЛица(
										ДокументОбъект.Организация, 
										ДокументОбъект.ТретийЧленКомиссии, 
										ДокументОбъект.Дата, 
										Истина // ФИОКратко 
									 );
									 
		СоставКомиссии.Вставить("ТретийЧленКомиссииФИО" ,      ДанныеТретийЧленКомиссии.Представление);
		СоставКомиссии.Вставить("ТретийЧленКомиссииДолжность", ДанныеФизЛица3.Должность);
									 
	КонецЕсли; 
	
	Возврат СоставКомиссии;
	

КонецФункции // ПолучитьСведенияОКомиссии()

Функция УстановитьОсновнуюОрганизациюТекущемуПользователю() Экспорт
	
	// Если пользователю доступна только одна организация, она устанавливается в качестве основной
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Организации.Наименование,
	|	Организации.Ссылка
	|ИЗ
	|	Справочник.Организации КАК Организации";
	Выборка = Запрос.Выполнить().Выбрать();
	
	КоличествоЗаписейВВыборке = Выборка.Количество();
	
	Если КоличествоЗаписейВВыборке = 1 Тогда
		
		Выборка.Следующий();
		
		УстановитьЗначениеПоУмолчанию("ОсновнаяОрганизация", Выборка.Ссылка);
			
	ИначеЕсли КоличествоЗаписейВВыборке = 0 Тогда
		
		Выборка.Следующий();
		
		УстановитьЗначениеПоУмолчанию("ОсновнаяОрганизация", Неопределено);
			
	КонецЕсли;			
	
	ОсновнаяОрганизация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	
	Возврат ОсновнаяОрганизация;
	
КонецФункции

Процедура УстановитьОсновнойИнтерфейс() Экспорт
	
	
КонецПроцедуры
 
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ЗАМЕРА ПРОИЗВОДИТЕЛЬНОСТИ

// Фиксирует время начала ключевой операции
//
// Параметры:
//  ДополнительныеСвойства - Структура, дополнительные свойства объекта документа
//  КлючеваяОперация - СправочникСсылка.КлючевыеОперации, ключевая операция, которая выполняется в данный момент
//
Процедура ЗафиксироватьВремяНачалаКлючевойОперации(ДополнительныеСвойства, КлючеваяОперация) Экспорт
	
	ВремяНачала = ОценкаПроизводительностиКлиентСервер.НачатьЗамерВремени(КлючеваяОперация);
	ДополнительныеСвойства.Вставить("КлючеваяОперация", КлючеваяОперация);
	ДополнительныеСвойства.Вставить("ВремяНачала", ВремяНачала);
	
КонецПроцедуры

// Фиксирует время окончания ключевой операции
//
// Параметры:
//  ДополнительныеСвойства - Структура, дополнительные свойства объекта документа
//
Процедура ЗафиксироватьВремяОкончанияКлючевойОперации(ДополнительныеСвойства) Экспорт
	
	КлючеваяОперация = Неопределено;
	ВремяНачала = Неопределено;
	Если ДополнительныеСвойства.Свойство("КлючеваяОперация", КлючеваяОперация) И
		ДополнительныеСвойства.Свойство("ВремяНачала", ВремяНачала) Тогда
		
		ОценкаПроизводительностиКлиентСервер.ЗакончитьЗамерВремени(КлючеваяОперация, ВремяНачала);
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// РАБОТА С НУМЕРАЦИЕЙ

Функция ПолучитьНомерБ12(НомерОбъекта) Экспорт
	
	
	МассивПрефиксовДляОбхода = ОбщегоНазначенияБПВызовСервераПовтИсп.ПрефиксыОрганизацийБ12();
	
	Номер = НомерОбъекта;
	НомерИзменен = Ложь;
	
	Для Каждого ТекущийПрефикс ИЗ МассивПрефиксовДляОбхода Цикл
		
		// удаление префикса из номера документа
		Если Найти(Номер, ТекущийПрефикс)=1 Тогда 
			Номер = Сред(Номер, СтрДлина(ТекущийПрефикс)+1);
			НомерИзменен = Истина;
		КонецЕсли;
		
		// так же, может остаться "минус" впереди
		Если Лев(Номер, 1) = "-" Тогда
			Номер = Сред(Номер, 2);
			НомерИзменен = Истина;
		КонецЕсли;
		
		// удаление ведущих нулей
		Пока Лев(Номер, 1)="0" Цикл
			Номер = Сред(Номер, 2);
			НомерИзменен = Истина;
		КонецЦикла;
	КонецЦикла;
	
	Если Лев(Номер, 1) = "Д" Тогда
		Номер = Сред(Номер, 4);
	КонецЕсли;
	
	Возврат Номер;
	
КонецФункции

// Функция возвращает массив с префиксами организаций в формате Бух 1.2
// Для кэширования необходимо вызывать эту функцию из общего модуля 
// ОбщегоНазначенияБПВызовСервераПовтИсп.
//
Функция ПрефиксыОрганизацийБ12() Экспорт

	//Получим значения всех возможных префиксов организаций
	УстановитьПривилегированныйРежим(Истина);

	Запрос = Новый Запрос();
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ                                 // ИНАГРО
	|	Организации.ПрефиксБ12 КАК Префикс
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.ПрефиксБ12 <> """"";
	Запрос.Текст = ТекстЗапроса;
	ПрефиксыОрганизаций = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Префикс");
	
	МассивПрефиксов = СформироватьМассивПрефиксовДляРИБИОрганизации(ПрефиксыОрганизаций);

    Возврат МассивПрефиксов;
	
КонецФункции

Функция ИНАГРО_ПолучитьСоответствиеСубконтоПараметрамУчета() Экспорт

	// Структура параметров

	Результат = Новый ТаблицаЗначений; // Структуры СтруктураПараметров
	Результат.Колонки.Добавить("Счета"); // Счета для обработки, таблица значений со структурой КолонкиСчетов
	Результат.Колонки.Добавить("ИсключенияИерархии", Новый ОписаниеТипов("Массив")); // Массив счетов
		// которые не должны обрабатываться при обработке подчиненных счетов
	Результат.Колонки.Добавить("Субконто"); // Параметры субконто, таблица значений со структурой КолонкиСубконто
	Результат.Колонки.Добавить("Параметры", Новый ОписаниеТипов("ТаблицаЗначений")); // Список параметров и
		// значения исключений, таблица значений со структурой КолонкиПараметров

	КолонкиСчетов = Новый ТаблицаЗначений;
	КолонкиСчетов.Колонки.Добавить("Счет", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	КолонкиСчетов.Колонки.Добавить("СПодчиненными", Новый ОписаниеТипов("Булево")); // Обрабатывать все субсчета

	// Имя параметра, константа типа Булево или строка "ПоСчету" (только для признаков учета Количественный и Валютный)
	// Неопределено - не менять
	ТипПараметра = Новый ОписаниеТипов("Неопределено, Строка, Булево", , Новый КвалификаторыСтроки(0, ДопустимаяДлина.Переменная));

	КолонкиСубконто = Новый ТаблицаЗначений;
	КолонкиСубконто.Колонки.Добавить("Вид", Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.ВидыСубконтоХозрасчетные"));
	КолонкиСубконто.Колонки.Добавить("Параметр", ТипПараметра); // Необходимость включения субконто
	КолонкиСубконто.Колонки.Добавить("Количественный", ТипПараметра);
	КолонкиСубконто.Колонки.Добавить("Поштучный", ТипПараметра);     //ИНАГРО ++
	КолонкиСубконто.Колонки.Добавить("Суммовой", ТипПараметра);
	КолонкиСубконто.Колонки.Добавить("ТолькоОбороты", ТипПараметра);
	КолонкиСубконто.Колонки.Добавить("Валютный", ТипПараметра);

	КолонкиПараметров = Новый ТаблицаЗначений; // Описания параметров учета
	// Имя параметра учета
	КолонкиПараметров.Колонки.Добавить("Имя", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(0, ДопустимаяДлина.Переменная)));
	КолонкиПараметров.Колонки.Добавить("Исключения", Новый ОписаниеТипов("ТаблицаЗначений")); // Счета, для которых будут
		//использоваться константные значения вместо значений параметров

	КолонкиИсключений = Новый ТаблицаЗначений;
	КолонкиИсключений.Колонки.Добавить("Счет", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	КолонкиИсключений.Колонки.Добавить("СПодчиненными", Новый ОписаниеТипов("Булево"));
	КолонкиИсключений.Колонки.Добавить("Значение", Новый ОписаниеТипов("Неопределено, Булево"));

	
	///////////////////////////////////////////////////////////////////
	// Учет БА
	СтрокаРезультата = Результат.Добавить();

	// Счета
	Счета = КолонкиСчетов.СкопироватьКолонки();
	СтрокаРезультата.Счета = Счета;
	
	СтрокаСчета = Счета.Добавить();
	СтрокаСчета.Счет = ПланыСчетов.Хозрасчетный.ДолгосрочныеБиологическиеАктивы;
	СтрокаСчета.СПодчиненными = Истина;
	
	СтрокаСчета = Счета.Добавить();
	СтрокаСчета.Счет = ПланыСчетов.Хозрасчетный.ТекущиеБиологическиеАктивы;
	СтрокаСчета.СПодчиненными = Истина;
	
	СтрокаСчета = Счета.Добавить();
	СтрокаСчета.Счет = ПланыСчетов.Хозрасчетный.ПриобретениеВыращиваниеДолгосрочныхБиологическихАктивовНеАмортизируются;
	СтрокаСчета.СПодчиненными = Ложь;

	// Исключения иерархии
	ИсключенияИерархии = Новый Массив;
	СтрокаРезультата.ИсключенияИерархии = ИсключенияИерархии;

	ИсключенияИерархии.Добавить(ПланыСчетов.Хозрасчетный.ДолгосрочныеБиологическиеАктивыРастениеводстваПоПервоначальнойСтоимостиАмортизируются);
	ИсключенияИерархии.Добавить(ПланыСчетов.Хозрасчетный.ДолгосрочныеБиологическиеАктивыЖивотноводстваПоПервоначальнойСтоимостиАмортизируются);
	ИсключенияИерархии.Добавить(ПланыСчетов.Хозрасчетный.НезрелыеДолгосрочныеБиологическиеАктивыПоПервоначальнойСтоимостиАмортизируются);
	
	// Субконто
	Субконто = КолонкиСубконто.СкопироватьКолонки();
	СтрокаРезультата.Субконто = Субконто;

	СтрокаСубконто = Субконто.Добавить();
	СтрокаСубконто.Вид = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.БиологическиеАктивы;
	СтрокаСубконто.Параметр = Истина;
	СтрокаСубконто.Количественный = Истина;
	СтрокаСубконто.Поштучный = Истина; //ИНАГРО ++
	СтрокаСубконто.Суммовой = Истина;
	СтрокаСубконто.ТолькоОбороты = Ложь;
	СтрокаСубконто.Валютный = Неопределено;

	СтрокаСубконто = Субконто.Добавить();
	СтрокаСубконто.Вид = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтавкиНДС;
	СтрокаСубконто.Параметр = Ложь;
	СтрокаСубконто.Количественный = Истина;
	СтрокаСубконто.Поштучный = Истина; //ИНАГРО ++
	СтрокаСубконто.Суммовой = Истина;
	СтрокаСубконто.ТолькоОбороты = Ложь;
	СтрокаСубконто.Валютный = Неопределено;

	СтрокаСубконто = Субконто.Добавить();
	СтрокаСубконто.Вид = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии;
	СтрокаСубконто.Параметр = "ВестиПартионныйУчет";
	СтрокаСубконто.Количественный = Истина;
	СтрокаСубконто.Поштучный = Истина; //ИНАГРО ++
	СтрокаСубконто.Суммовой = Истина;
	СтрокаСубконто.ТолькоОбороты = Ложь;
	СтрокаСубконто.Валютный = Неопределено;

	СтрокаСубконто = Субконто.Добавить();
	СтрокаСубконто.Вид = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады;
	СтрокаСубконто.Параметр = Истина;
	СтрокаСубконто.Количественный = Истина;
	СтрокаСубконто.Поштучный = Истина; //ИНАГРО ++
	СтрокаСубконто.Суммовой = Истина;
	СтрокаСубконто.ТолькоОбороты = Ложь;
	СтрокаСубконто.Валютный = Неопределено;

	// Параметры
	Параметры = КолонкиПараметров.СкопироватьКолонки();
	СтрокаРезультата.Параметры = Параметры;

	СтрокаПараметра = Параметры.Добавить();
	СтрокаПараметра.Имя = "ВестиПартионныйУчет";
	СтрокаПараметра.Исключения = КолонкиИсключений.Скопировать();
		
	Возврат Результат;

КонецФункции

Процедура ИНАГРО_ПрименитьПараметрыУчета(ПараметрыУчета, ИзмененыПараметрыСубконто = Ложь, Отказ = Ложь, ТолькоМПЗ = Ложь) Экспорт

	НачатьТранзакцию();

	ДействияИзмененияСубконто = ИНАГРО_ПолучитьДействияИзмененияСубконто(ПараметрыУчета);
	ДействияИзмененияСубконто.Добавить();

	ИзмененияСубконто = Новый Массив;

	ПризнакиУчета = ПолучитьСтруктуруПризнаковУчетаСубконто();

	Объект = Неопределено;

	Для каждого СтрокаДействия Из ДействияИзмененияСубконто Цикл

		Если Объект = Неопределено ИЛИ Объект.Ссылка <> СтрокаДействия.Счет Тогда

			Если Объект <> Неопределено
				И Объект.Модифицированность() Тогда // Зафиксируем изменения

				Попытка
					Объект.Записать();
				Исключение

					// выведем все изменения по счету
					ИзмененияПоСчету = "";
					Для каждого ОписаниеИзменения Из ИзмененияСубконто Цикл
						Если ОписаниеИзменения.Счет <> Объект.Ссылка Тогда
							Продолжить;
						КонецЕсли;

						ОписаниеИзменения.Счет = Объект.Код;

						ИзмененияПоСчету = ИзмененияПоСчету + Символы.ПС
							+ ПолучитьТекстСообщенияИзмененияСубконто(ОписаниеИзменения);
					КонецЦикла;

					ШаблонСообщения = НСтр("ru='При попытке изменения субконто %1"
"произошла ошибка:"
"%2';uk='При спробі зміни субконто %1"
"відбулася помилка:"
"%2'");

					ОписаниеОшибки = ИнформацияОбОшибке();
					Сообщение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения,
						ИзмененияПоСчету, КраткоеПредставлениеОшибки(ОписаниеОшибки));
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Сообщение);

					Отказ = Истина;

					ЗаписьЖурналаРегистрации(НСТр("ru='Операция не выполнена';uk='Операція не виконана'"), УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки.Описание);
					ОтменитьТранзакцию();
					Возврат;

				КонецПопытки;
			КонецЕсли;

			Если ЗначениеЗаполнено(СтрокаДействия.Счет) Тогда
				Объект = СтрокаДействия.Счет.ПолучитьОбъект();
			Иначе
				Прервать;
			КонецЕсли;

		КонецЕсли;

		СтрокаСубконто = Объект.ВидыСубконто.Найти(СтрокаДействия.Субконто, "ВидСубконто");

		Если СтрокаДействия.Действие = -1 Тогда
			ИзмененияСубконто.Добавить(Новый Структура("Счет, Субконто, Изменение",
				СтрокаДействия.Счет, СтрокаДействия.Субконто, НСтр("ru=' удалено';uk=' вилучено'")));
			Объект.ВидыСубконто.Удалить(СтрокаСубконто);
		Иначе
			Если СтрокаДействия.Действие = 1 Тогда
				
				Если Объект.Ссылка = ПланыСчетов.Хозрасчетный.РасчетыПоОплатеТруда Тогда
					СтрокаСубконто = Объект.ВидыСубконто.Вставить(0);
				Иначе				
					СтрокаСубконто = Объект.ВидыСубконто.Добавить();
				КонецЕсли;
				
				СтрокаСубконто.ВидСубконто = СтрокаДействия.Субконто;
				ИзмененияСубконто.Добавить(Новый Структура("Счет, Субконто, Изменение",
					СтрокаДействия.Счет, СтрокаДействия.Субконто, НСтр("ru=' установлено';uk=' встановлено'")));
			КонецЕсли;

			// проверим признаки учета
			Для каждого ПризнакУчета Из ПризнакиУчета Цикл
				ДействиеСПризнаком = СтрокаДействия[ПризнакУчета.Ключ];
				Если ДействиеСПризнаком = 0 Тогда
					Продолжить;
				КонецЕсли;

				Если ДействиеСПризнаком = 1 Тогда
					ЗначениеПризнака = Истина;
				Иначе
					ЗначениеПризнака = Ложь;
				КонецЕсли;

				СтрокаСубконто[ПризнакУчета.Ключ] = ЗначениеПризнака;

				Если СтрокаДействия.Действие <> 1 Тогда
					Если ЗначениеПризнака Тогда
						ШаблонТекста = НСтр("ru=', установлен признак ""%1""';uk=', встановлена ознака ""%1""'");
					Иначе
						ШаблонТекста = НСтр("ru=', снят признак ""%1""';uk=', знята ознака ""%1""'");
					КонецЕсли;
					ТекстИзменения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста,
						ПризнакУчета.Значение);
					ИзмененияСубконто.Добавить(Новый Структура("Счет, Субконто, Изменение",
						СтрокаДействия.Счет, СтрокаДействия.Субконто, ТекстИзменения));
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;

	КонецЦикла;

	Если ИзмененияСубконто.Количество() > 0 Тогда
		ИзмененыПараметрыСубконто = Истина;

		Для каждого ОписаниеИзменения Из ИзмененияСубконто Цикл
			Сообщение = ПолучитьТекстСообщенияИзмененияСубконто(ОписаниеИзменения);
			ЗаписьЖурналаРегистрации(НСтр("ru='Изменения параметров субконто';uk='Зміни параметрів субконто'"),
				УровеньЖурналаРегистрации.Информация, ОписаниеИзменения.Счет.Метаданные(),
				ОписаниеИзменения.Счет, Сообщение, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		КонецЦикла;

	КонецЕсли;

	//БухгалтерскийУчетПереопределяемый.УстановитьПараметрыКлассовСчетов(ПараметрыУчета.ИспользоватьКлассыСчетовВКачествеГрупп);	
	//
	//БухгалтерскийУчетПереопределяемый.УстановитьПараметрыУчетаРасчетовПоЗарплате(?(ПараметрыУчета.ВестиУчетПоРаботникам = 1, Ложь, Истина));
	//БухгалтерскийУчетПереопределяемый.УстановитьУчетЗарплатыИКадровВоВнешнейПрограмме(?(ПараметрыУчета.УчетЗарплатыИКадровВоВнешнейПрограмме = 1, Истина, Ложь));
	//БухгалтерскийУчетПереопределяемый.УстановитьНастройкиКадровогоУчета(ПараметрыУчета.КадровыйУчет, ?(ПараметрыУчета.УчетЗарплатыИКадровВоВнешнейПрограмме = 0, Истина, Ложь));

	ЗафиксироватьТранзакцию();

КонецПроцедуры

Функция ИНАГРО_ПолучитьДействияИзмененияСубконто(ПараметрыУчетаФормы, ТолькоМПЗ = Ложь) Экспорт

	ПараметрыУчета = Новый Структура;
	Для каждого КлючИЗначение Из ПараметрыУчетаФормы Цикл
		ПараметрыУчета.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла;

	ПараметрыСубконто = ИНАГРО_ПолучитьСоответствиеСубконтоПараметрамУчета();

	ПризнакиУчета = ПолучитьСтруктуруПризнаковУчетаСубконто();

	ТипДействия = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(1, 0, ДопустимыйЗнак.Любой));
	// -1 удалить; 0 - не менять; 1 - установить

	ТаблицаДействий = Новый ТаблицаЗначений;
	ТаблицаДействий.Колонки.Добавить("Счет", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ТаблицаДействий.Колонки.Добавить("Субконто", Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.ВидыСубконтоХозрасчетные"));
	ТаблицаДействий.Колонки.Добавить("Действие", ТипДействия);
	Для каждого ПризнакУчета Из ПризнакиУчета Цикл
		ТаблицаДействий.Колонки.Добавить(ПризнакУчета.Ключ, ТипДействия);
	КонецЦикла;

	Для каждого ОписаниеГруппыСчетов Из ПараметрыСубконто Цикл

		СчетаВСписке = Новый Массив;
		СчетаВИерархии = Новый Массив;
		СчетаНеВСписке = ОписаниеГруппыСчетов.ИсключенияИерархии;

		Для каждого ОписаниеСчета Из ОписаниеГруппыСчетов.Счета Цикл
			Если ОписаниеСчета.СПодчиненными Тогда
				СчетаВИерархии.Добавить(ОписаниеСчета.Счет);
			Иначе
				СчетаВСписке.Добавить(ОписаниеСчета.Счет);
			КонецЕсли;
		КонецЦикла;

		// Получим список счетов для обработки
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("СчетаВСписке", СчетаВСписке);
		Запрос.УстановитьПараметр("СчетаВИерархии", СчетаВИерархии);
		Запрос.УстановитьПараметр("СчетаНеВСписке", СчетаНеВСписке);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Хозрасчетный.Ссылка КАК Счет,
		|	Хозрасчетный.Порядок КАК Порядок,
		|	Хозрасчетный.Код,
		|	Хозрасчетный.Валютный,
		|	Хозрасчетный.Количественный,
		|	Хозрасчетный.Поштучный,
		|	Хозрасчетный.ВидыСубконто.(
		|		НомерСтроки КАК НомерСтроки,
		|		ВидСубконто,
		|		ТолькоОбороты,
		|		Суммовой,
		|		Валютный,
		|		Количественный,
		|		Поштучный  // ИНАГРО
		|	)
		|ИЗ
		|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
		|ГДЕ
		|	(Хозрасчетный.Ссылка В (&СчетаВСписке)
		|			ИЛИ Хозрасчетный.Ссылка В ИЕРАРХИИ (&СчетаВИерархии)
		|				И (НЕ Хозрасчетный.Ссылка В (&СчетаНеВСписке)))
		|
		|УПОРЯДОЧИТЬ ПО
		|	Порядок,
		|	Счет,
		|	НомерСтроки";

		ВыборкаСчетов = Запрос.Выполнить().Выбрать();
		Пока ВыборкаСчетов.Следующий() Цикл

			ПараметрыСчета = ПолучитьЗначенияПараметровУчетаДляСчета(ПараметрыУчета,
				ОписаниеГруппыСчетов.Параметры, ВыборкаСчетов.Счет);

			ВидыСубконто = ВыборкаСчетов.ВидыСубконто.Выгрузить();

			Для каждого ОписаниеСубконто Из ОписаниеГруппыСчетов.Субконто Цикл
				СтрокаДействия = Неопределено;

				ИспользованиеСубконто = ПолучитьЗначениеПараметраУчетаСубконто(ОписаниеСубконто.Параметр,
					ПараметрыСчета, ВыборкаСчетов);

				Если ИспользованиеСубконто = Неопределено Тогда
					Продолжить;
				КонецЕсли;

				СтрокаСубконто = ВидыСубконто.Найти(ОписаниеСубконто.Вид, "ВидСубконто");
				Если ИспользованиеСубконто Тогда
					Если СтрокаСубконто = Неопределено Тогда
						СтрокаДействия = ТаблицаДействий.Добавить();
						СтрокаДействия.Счет = ВыборкаСчетов.Счет;
						СтрокаДействия.Субконто = ОписаниеСубконто.Вид;
						СтрокаДействия.Действие = 1;						
					КонецЕсли;

					// проверим признаки учета
					Для каждого ПризнакУчета Из ПризнакиУчета Цикл
						ЗначениеПризнака = ПолучитьЗначениеПараметраУчетаСубконто(ОписаниеСубконто[ПризнакУчета.Ключ],
							ПараметрыСчета, ВыборкаСчетов);
						Если ЗначениеПризнака = Неопределено Тогда
							Продолжить;
						КонецЕсли;

						Если СтрокаСубконто = Неопределено
							ИЛИ СтрокаСубконто[ПризнакУчета.Ключ] <> ЗначениеПризнака Тогда

							Если СтрокаДействия = Неопределено Тогда
								СтрокаДействия = ТаблицаДействий.Добавить();
								СтрокаДействия.Счет = ВыборкаСчетов.Счет;
								СтрокаДействия.Субконто = ОписаниеСубконто.Вид;
							КонецЕсли;
							Если ЗначениеПризнака Тогда
								СтрокаДействия[ПризнакУчета.Ключ] = 1;
							Иначе
								СтрокаДействия[ПризнакУчета.Ключ] = -1;
							КонецЕсли;
						КонецЕсли;
					КонецЦикла;
				Иначе
					Если СтрокаСубконто = Неопределено Тогда
						Продолжить;
					Иначе
						СтрокаДействия = ТаблицаДействий.Добавить();
						СтрокаДействия.Счет = ВыборкаСчетов.Счет;
						СтрокаДействия.Субконто = ОписаниеСубконто.Вид;
						СтрокаДействия.Действие = -1;
					КонецЕсли;
				КонецЕсли;

			КонецЦикла;

		КонецЦикла;
	КонецЦикла;

	Возврат ТаблицаДействий;

КонецФункции

