////////////////////////////////////////////////////////////////////////////////
// КадровыйУчетКлиент: методы кадрового учета, работающие на стороне клиента
//  
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Открывает формы выбора физических лиц, работающих в организации
//
// Параметры:
//		ЭлементФормы
//		Организация
//		МножественныйВыбор
//		ФиксированныеНастройки - НастройкаКомпоновкиДанных (параметр, расширения динамического списка)
//
Процедура ВыбратьФизическихЛицОрганизации(ЭлементФормы, Организация, МножественныйВыбор = Истина, ФиксированныеНастройки = Неопределено) Экспорт
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Организация", Организация);
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("МножественныйВыбор", МножественныйВыбор);
	ПараметрыОткрытияФормы.Вставить("ЗакрыватьПриВыборе", НЕ МножественныйВыбор);
	ПараметрыОткрытияФормы.Вставить("Отбор", СтруктураОтбора);
	
	Если ФиксированныеНастройки <> Неопределено Тогда
		ПараметрыОткрытияФормы.Вставить("ФиксированныеНастройки", ФиксированныеНастройки);
	КонецЕсли; 
	// ИНАГРО ++
	//ОткрытьФорму("Справочник.ФизическиеЛица.Форма.ФормаВыбораСотрудников", ПараметрыОткрытияФормы, ЭлементФормы);	
	ОткрытьФорму("Справочник.ФизическиеЛица.Форма.ИНАГРО_ФормаВыбораСотрудников", ПараметрыОткрытияФормы, ЭлементФормы);	
	// ИНАГРО --
КонецПроцедуры

// Открывает форму подбора значений из классификатора
//
// Параметры:
//			ИмяСправочника - Строка, имя справочника как оно задано в конфигураторе
//  		ПолныйПутьКМакету - Строка - формат полного пути:
//								"Документ.<ИмяДокумента>.<ИмяМакета>"
//								"Обработка.<ИмяОбработки>.<ИмяМакета>"
//								"ОбщийМакет.<ИмяМакета>"
//			Форма
//			СоответствиеПолей - Соответствие, задает соответствие имен реквизитов справочника\
//							именам полей классификатора
//
Процедура ОткрытьФормуПодбораИзКлассификатора(ИмяСправочника, ПолныйПутьКМакету, Заголовок, Форма, СоответствиеПолей = Неопределено, ОповещениеЗавершения = Неопределено) Экспорт
	
	СтруктураПараметров = Новый Структура("ИмяСправочника,ПолныйПутьКМакету,Заголовок",
		ИмяСправочника,
		ПолныйПутьКМакету,
		Заголовок);
		
	Если СоответствиеПолей <> Неопределено Тогда
		СтруктураПараметров.Вставить("СоответствиеПолей", СоответствиеПолей);
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура("Форма, ОповещениеЗавершения", Форма, ОповещениеЗавершения);
	Оповещение = Новый ОписаниеОповещения("ОткрытьФормуПодбораИзКлассификатораЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ОткрытьФорму("ОбщаяФорма.ЗаполнениеСправочниковИзКлассификаторов", СтруктураПараметров, Форма, , , , Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

// Открывает форму выбора справочника Сотрудники с отбором только работающих
// сотрудников в периоде.
//
// Параметры:
//		ВладелецФормыВыбора 				- Форма или элемент формы, для которого открывается форма выбора.
//		Организация 						- СправочникСсылка.Организации, если не указана, в отбор попадают все сотрудники компании.
//		Подразделение 						- СправочникСсылка.ПодразделенияОрганизации, если не указано - в отбор попадают все сотрудники.
//		НачалоПериодаПримененияОтбора 		- Дата, начало периода для отбора сотрудников.
//		ОкончаниеПериодаПримененияОтбора 	- Дата, окончание периода для отбора сотрудников.
//      МножественныйВыбор 					- Булево, если ложь - форма откроется для выбора одного сотрудника.
//		АдресСпискаПодобранныхСотрудников	- Строка, адрес массива уже подобранных сотрудников во временном хранилище.
//		СтруктураОтбора						- Структура, аналогичная структуре отбора в параметрах формы списка.
//
Процедура ВыбратьСотрудниковРаботающихВПериоде(ВладелецФормыВыбора, Организация = Неопределено, Подразделение = Неопределено, НачалоПериодаПримененияОтбора = '00010101', ОкончаниеПериодаПримененияОтбора = '00010101', МножественныйВыбор = Истина, АдресСпискаПодобранныхСотрудников = "", СтруктураОтбора = Неопределено) Экспорт
	
	Если СтруктураОтбора = Неопределено Тогда
		СтруктураОтбора = Новый Структура;
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(НачалоПериодаПримененияОтбора) Тогда
		СтруктураОтбора.Вставить("НачалоПериодаПримененияОтбора", НачалоПериодаПримененияОтбора);
		СтруктураОтбора.Вставить("ОкончаниеПериодаПримененияОтбора", ОкончаниеПериодаПримененияОтбора);
	КонецЕсли; 
	
	ОткрытьФормуВыбораСотрудников(ВладелецФормыВыбора, Организация, Подразделение, МножественныйВыбор, СтруктураОтбора, АдресСпискаПодобранныхСотрудников);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Продолжение процедуры ОткрытьФормуПодбораИзКлассификатора
//
Процедура ОткрытьФормуПодбораИзКлассификатораЗавершение(Результат, ДополнительныеПараметры) Экспорт 
	
	ДополнительныеПараметры.Форма.Элементы.Список.Обновить();
	
	Если ДополнительныеПараметры.ОповещениеЗавершения <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеЗавершения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытьФормуВыбораСотрудников(ВладелецФормыВыбора, Организация, Подразделение, МножественныйВыбор, СтруктураОтбора, АдресСпискаПодобранныхСотрудников)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("МножественныйВыбор", МножественныйВыбор);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", НЕ МножественныйВыбор);
		
	Если ЗначениеЗаполнено(Организация) Тогда
		СтруктураОтбора.Вставить("ТекущаяОрганизация", Организация);
		СтруктураОтбора.Вставить("ГоловнаяОрганизация", Организация);
	КонецЕсли; 
		
	Если ЗначениеЗаполнено(Подразделение) Тогда
		СтруктураОтбора.Вставить("ТекущееПодразделение", Подразделение);
	КонецЕсли; 
	
	Если СтруктураОтбора.Количество() > 0 Тогда
		ПараметрыФормы.Вставить("Отбор", СтруктураОтбора);
	КонецЕсли;
	
	Если МножественныйВыбор И НЕ ПустаяСтрока(АдресСпискаПодобранныхСотрудников) Тогда
		ПараметрыФормы.Вставить("АдресСпискаПодобранныхСотрудников", АдресСпискаПодобранныхСотрудников);
	КонецЕсли; 
	
	ОткрытьФорму("Справочник.Сотрудники.ФормаВыбора", ПараметрыФормы, ВладелецФормыВыбора);	
	
КонецПроцедуры


Процедура ОповеститьОбИзмененииРабочегоМеста(Форма) Экспорт
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("ФизическоеЛицо", Форма.Объект.ФизическоеЛицо);
	ПараметрыОповещения.Вставить("Сотрудник", Форма.Объект.Сотрудник);
	
	Оповестить("ИзменениеДанныхМестаРаботы", ПараметрыОповещения, Форма);
	
КонецПроцедуры
