
// Возвращает текст запроса, рассчитывающий затраты сырья по спецификации и количеству продукции.
//
// Исходные данные выбираются из временной таблицы Выпуск, которая
// - должна содержать поля Спецификация и КоличествоПродукции 
// - не должна содержать поля Номенклатура, Количество, ЕдиницаИзмерения, СтатьяЗатрат
//
// Результат помещается во временную таблицу ЗатратыСырья, которая содержит:
// - сведения о потребном сырье: 
//   поля Номенклатура, Количество, ЕдиницаИзмерения, СтатьяЗатрат
// - все поля исходной таблицы Выпуск
//
Функция ТекстЗапросаВременнаяТаблицаЗатратыСырья() Экспорт
	
	// Для редактирования конструктором запроса следует заменить * на любое имя поля 
	// (например, "ВсеПоля"), после редактирования выполнить обратную замену
	Возврат
	"ВЫБРАТЬ
	|	Выпуск.*,
	|	ИсходныеКомплектующие.Номенклатура.СтатьяЗатрат КАК СтатьяЗатрат,
	|	ИсходныеКомплектующие.Номенклатура КАК Номенклатура,
	|	ИсходныеКомплектующие.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ИсходныеКомплектующие.Коэффициент КАК Коэффициент,
	|	ВЫБОР
	|		КОГДА ИсходныеКомплектующие.Ссылка.Количество = 0
	|			ТОГДА 0
	|		ИНАЧЕ (Выпуск.КоличествоПродукции * Выпуск.Коэффициент * ИсходныеКомплектующие.Количество / ИсходныеКомплектующие.Ссылка.Количество) / ИсходныеКомплектующие.Ссылка.Коэффициент
	|	КОНЕЦ КАК Количество
	|ПОМЕСТИТЬ ЗатратыСырья
	|ИЗ
	|	Выпуск КАК Выпуск
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СпецификацииНоменклатуры.ИсходныеКомплектующие КАК ИсходныеКомплектующие
	|		ПО Выпуск.Спецификация = ИсходныеКомплектующие.Ссылка" 
	+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

// Функция производит расчет сырья, необходимый для производства готовой
// продукции, указанной в документе - основание
Функция РассчитатьРасходСырьяПоСпецификации(ОснованиеСсылка, ДокументаОснованиеТЧ = "Продукция") Экспорт
	
	ДокументаОснование = ОснованиеСсылка.Метаданные().Имя;
	
	Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СпецификацииНоменклатурыИсходныеКомплектующие.Номенклатура КАК Номенклатура,
		|	ДокументОснование.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
		|	ДокументОснование.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
		|	СпецификацииНоменклатурыИсходныеКомплектующие.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	СпецификацииНоменклатурыИсходныеКомплектующие.Коэффициент КАК Коэффициент,
		|	ВЫБОР
		|		КОГДА СпецификацииНоменклатуры.Количество = 0
		|			ТОГДА 0
		|		ИНАЧЕ (ДокументОснование.КоличествоПродукции * ДокументОснование.Коэффициент) * СпецификацииНоменклатурыИсходныеКомплектующие.Количество / (СпецификацииНоменклатуры.Количество * СпецификацииНоменклатуры.Коэффициент)
		|	КОНЕЦ КАК Количество
		|ИЗ
		|	(ВЫБРАТЬ
		|		ДокументОснованиеТаблица.Спецификация КАК Спецификация,
		|		ДокументОснованиеТаблица.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
		|		ДокументОснованиеШапка.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
		|		ДокументОснованиеТаблица.Коэффициент КАК Коэффициент, 
		|		ДокументОснованиеТаблица.Количество КАК КоличествоПродукции
		|	ИЗ
		|		Документ.ОтчетПроизводстваЗаСмену.Продукция КАК ДокументОснованиеТаблица
		|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетПроизводстваЗаСмену КАК ДокументОснованиеШапка
		|			ПО ДокументОснованиеТаблица.Ссылка = ДокументОснованиеШапка.Ссылка
		|	ГДЕ
		|		ДокументОснованиеТаблица.Ссылка = &Ссылка
		|		И ДокументОснованиеТаблица.Спецификация <> &Спецификация) КАК ДокументОснование
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпецификацииНоменклатуры КАК СпецификацииНоменклатуры
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СпецификацииНоменклатуры.ИсходныеКомплектующие КАК СпецификацииНоменклатурыИсходныеКомплектующие
		|			ПО СпецификацииНоменклатурыИсходныеКомплектующие.Ссылка = СпецификацииНоменклатуры.Ссылка
		|		ПО ДокументОснование.Спецификация = СпецификацииНоменклатуры.Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", ОснованиеСсылка);
		Запрос.УстановитьПараметр("Спецификация", Справочники.СпецификацииНоменклатуры.ПустаяСсылка());
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ОтчетПроизводстваЗаСмену", ДокументаОснование);
		Если ДокументаОснование = "РеализацияУслугПоПереработке" Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст,"ДокументОснованиеТаблица.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,", "");
			Запрос.Текст = СтрЗаменить(Запрос.Текст,"ДокументОснование.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,", "");
			Запрос.Текст = СтрЗаменить(Запрос.Текст,"ДокументОснование.ПодразделениеОрганизации КАК ПодразделениеОрганизации,", "");
			Запрос.Текст = СтрЗаменить(Запрос.Текст,"ДокументОснованиеШапка.ПодразделениеОрганизации КАК ПодразделениеОрганизации,", "");
			ДокументаОснованиеТЧ = "Услуги";
		КонецЕсли;	
		Если ДокументаОснование = "АктОбОказанииПроизводственныхУслуг" Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст,"ДокументОснованиеТаблица.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,", "ДокументОснованиеШапка.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,");
			ДокументаОснованиеТЧ = "Услуги";
		КонецЕсли;
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Продукция", ДокументаОснованиеТЧ);
		
		Если ДокументаОснованиеТЧ = "Услуги" Тогда
		
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "ДокументОснованиеТаблица.Коэффициент", "1");
		
		КонецЕсли;
		
		
		Возврат Запрос.Выполнить();
		
КонецФункции