// Рассчитывает сумму НДС исходя из суммы и флагов налогообложения
//
// Параметры: 
//  Сумма            - число, сумма от которой надо рассчитывать налоги, 
//  СуммаВключаетНДС - булево, признак включения НДС в сумму ("внутри" или "сверху"),
//  СтавкаНДС        - число , процентная ставка НДС,
//
// Возвращаемое значение:
//  Число, полученная сумма НДС
//
Функция РассчитатьСуммуНДС(Сумма, СуммаВключаетНДС, СтавкаНДС) Экспорт

	Если СуммаВключаетНДС Тогда
		СуммаБезНДС = 100 * Сумма / (100 + СтавкаНДС);
		СуммаНДС = Сумма - СуммаБезНДС;
	Иначе
		СуммаБезНДС = Сумма;
	КонецЕсли;

	Если НЕ СуммаВключаетНДС Тогда
		СуммаНДС = СуммаБезНДС * СтавкаНДС / 100;
	КонецЕсли;
	
	Возврат СуммаНДС;

КонецФункции // РассчитатьСуммуНДС()

// Производит пересчет цен при изменении флагов учета налогов.
// Пересчет зависит от способа заполнения цен, при заполнении По ценам номенклатуры (при продаже) 
// хочется избегать ситуаций, когда компания  «теряет деньги» при пересчете налогов. 
// Поэтому если в документе флаг "Учитывать налог" выключен, то цены должны браться напрямую из справочника, 
// потому что хочется продавать по той же цене, независимо от режима налогообложения. 
// Например, если отпускная цена задана с НП для избежания ошибок округления, то это не значит, 
// что при отпуске без НП мы должны продать дешевле. Если же флаг учета налога в документе включен, 
// то цены должны пересчитываться при подстановке в документ: 
// налог должен включаться или не включаться в зависимости от флага включения налога в типе цен.
// При заполнении по ценам контрагентов (при покупке) хочется хранить цены поставщиков. 
// Поэтому нужно пересчитывать всегда по установленным флагам в документе и в типе цен. 
// Это гарантирует, что при записи цен в регистр и последующем их чтении, 
// например, при заполнении следующего документа, мы с точностью до ошибок округления при пересчете 
// получим те же самые цены.
//
// Параметры: 
//  Цена                - число, пересчитываемое значение цены, 
//  ЦенаВключаетНДС     - булево, определяет содержит ли переданное значение цены НДС,
//  СуммаВключаетНДС    - булево, определяет должно ли новое значение цены включать НДС,
//  СтавкаНДС           - число, ставка НДС, 
//
// Возвращаемое значение:
//  Число, новое значение цены.
//
Функция ПересчитатьЦенуПриИзмененииФлаговНалогов(Цена, ЦенаВключаетНДС,
						СуммаВключаетНДС, СтавкаНДС) Экспорт

	// Инициализация переменных
	НадоВключитьНДС  = Ложь;
	НадоИсключитьНДС = Ложь;
	НоваяЦена		 = Цена;

	Если СуммаВключаетНДС
		И (НЕ ЦенаВключаетНДС) Тогда
		
		// Надо добавлять НДС       
		НадоВключитьНДС = Истина;
	ИначеЕсли (НЕ СуммаВключаетНДС)
		И ЦенаВключаетНДС  Тогда
		
		// Надо исключать НДС       
		НадоИсключитьНДС = Истина;
	КонецЕсли;
		
	Если НадоИсключитьНДС Тогда
		НоваяЦена = (НоваяЦена * 100) / (100 + СтавкаНДС);
	КонецЕсли;

	Если НадоВключитьНДС Тогда
		НоваяЦена = (НоваяЦена * (100 + СтавкаНДС)) / 100;
	КонецЕсли;

	Возврат НоваяЦена;

КонецФункции // ПересчитатьЦенуПриИзмененииФлаговНалогов()

Процедура РассчитатьПропорциональныйНДС(Объект, МассивИменТабличныхЧастей, ПлательщикНДС, КоэффициентПропорциональногоНДС, ДополнительныеПараметрыРасчета = Неопределено) Экспорт
	
	ИмяКолонкиСуммаНДС = "СуммаНДС";
	Если ДополнительныеПараметрыРасчета <> Неопределено Тогда
		Если ДополнительныеПараметрыРасчета.Свойство("ИмяКолонкиСуммаНДС") Тогда
			ИмяКолонкиСуммаНДС = ДополнительныеПараметрыРасчета.ИмяКолонкиСуммаНДС;
		КонецЕсли;
	КонецЕсли;
	
	Объект.СуммаНДСПропорциональноКредит = 0;
	Объект.СуммаНДСПропорциональноВсего  = 0;
	
	Если Не ПлательщикНДС Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ИмяТабличнойЧасти Из МассивИменТабличныхЧастей Цикл
		СтрокиПропорциональногоНДС = Объект[ИмяТабличнойЧасти].НайтиСтроки(
			Новый Структура(
				"НалоговоеНазначение", 
				ПредопределенноеЗначение("Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_Пропорционально")
			)
		);
		Для Каждого СтрокаПропорциональногоНДС Из СтрокиПропорциональногоНДС Цикл
			Объект.СуммаНДСПропорциональноВсего = Объект.СуммаНДСПропорциональноВсего + СтрокаПропорциональногоНДС[ИмяКолонкиСуммаНДС];	
		КонецЦикла;		
	КонецЦикла;
	
	Объект.СуммаНДСПропорциональноКредит = Объект.СуммаНДСПропорциональноВсего * КоэффициентПропорциональногоНДС;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБРАБОТКИ ОШИБОК ОКРУГЛЕНИЯ НДС

// Процедура корректирует построчные ошибки округления НДС так, 
// чтобы итоговый НДС по ставке совпадал с расчитанным НДС по базе
//
// Параметры:
//  ТабличнаяЧасть       - табличная часть, для которой следует пересчитать НДС
//  ДокументОбъект       - объект редактируемого документа.
//
Процедура ПересчитатьНДСсУчетомПогрешностиОкругления(ТабличнаяЧасть, ДокументСсылка, СуммаВключаетНДС, ПогрешностиОкругления, ПредставлениеТабличнойЧасти = "", ПредставлениеВалюты = "", ИмяРеквизитаСумма    = "Сумма", ИмяРеквизитаСуммаНДС = "СуммаНДС", ДополнениеКТексту ="") Экспорт
    Перем СуммаНДСДоОкругления;    // для проверки наличия изменения в сумме НДС по документу
    Перем СуммаНДСПослеОкругления; // для проверки наличия изменения в сумме НДС по документу
	

	// Запомним сумма и сумму НДС до округления
	СуммаНДСДоОкругления = ТабличнаяЧасть.Итог(ИмяРеквизитаСуммаНДС);
	СуммаДоОкругления    = ТабличнаяЧасть.Итог(ИмяРеквизитаСумма) + СуммаНДСДоОкругления;
	
	Для Каждого СтрокаТаблицы Из ТабличнаяЧасть Цикл
		
		СтрокаТаблицы[ИмяРеквизитаСуммаНДС] = РассчитатьСуммуНДСсУчетомПогрешности(СтрокаТаблицы[ИмяРеквизитаСумма],
	                                                   Истина,
	                                                   СуммаВключаетНДС,
	                                                   СтрокаТаблицы.СтавкаНДС,
	                                                   ПогрешностиОкругления);
	КонецЦикла;
	
	// Рассчитаем сумму НДС после устранения ошибок округления
	СуммаНДСПослеОкругления = ТабличнаяЧасть.Итог(ИмяРеквизитаСуммаНДС);
	СуммаПослеОкругления    = ТабличнаяЧасть.Итог(ИмяРеквизитаСумма) + СуммаНДСПослеОкругления;

	// Сравним суммы до и после округления
	Если СуммаНДСПослеОкругления <> СуммаНДСДоОкругления Тогда
		// Если суммы отличаются, сообщим об этом пользователю
		СтрокаСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='После исправления погрешностей округления сумма НДС%1 табличной части %2 изменилась (было %3, стало %4)';uk='Після виправлення погрішностей округлення сума ПДВ%1 табличної частини %2 змінилася (було %3, стало %4)'"), ДополнениеКТексту, ПредставлениеТабличнойЧасти, ОбщегоНазначенияБПВызовСервера.ФорматСумм(СуммаНДСДоОкругления, ПредставлениеВалюты), ОбщегоНазначенияБПВызовСервера.ФорматСумм(СуммаНДСПослеОкругления,ПредставлениеВалюты));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщения);
		
		// изменилась общая сумма документа - сообщим об этом пользователю 
		Если (Не СуммаВключаетНДС) Тогда
			СтрокаСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='После исправления погрешностей округления общая сумма%1 табличной части %2 изменилась (было %3, стало %4)';uk='Після виправлення погрішностей округлення загальна сума%1 табличної частини %2 змінилася (було %3, стало %4)'"), ДополнениеКТексту, ПредставлениеТабличнойЧасти, ОбщегоНазначенияБПВызовСервера.ФорматСумм(СуммаДоОкругления,ПредставлениеВалюты), ОбщегоНазначенияБПВызовСервера.ФорматСумм(СуммаПослеОкругления,ПредставлениеВалюты));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщения);
		КонецЕсли;	
	КонецЕсли;

КонецПроцедуры // ПересчитатьНДСсУчетомПогрешностиОкругления


// Рассчитывает сумму НДС исходя из суммы и флагов налогообложения
//
// Параметры: 
//  Сумма            - число, сумма от которой надо рассчитывать налоги, 
//  УчитыватьНДС     - булево, признак учета НДС в сумме, 
//  СуммаВключаетНДС - булево, признак включения НДС в сумму ("внутри" или "сверху"),
//  СтавкаНДС        - число , процентная ставка НДС,
//
// Возвращаемое значение:
//  Число, полученная сумма НДС
//
Функция РассчитатьСуммуНДСсУчетомПогрешности(Сумма, УчитыватьНДС, СуммаВключаетНДС, ПеречислениеСтавкаНДС,
	               СоответствиеПогрешностей = Неопределено) Экспорт

	СтавкаНДС = УчетНДС.ПолучитьСтавкуНДС(ПеречислениеСтавкаНДС);
				   
	Если УчитыватьНДС Тогда 
		Если СуммаВключаетНДС Тогда
			СуммаНДС = ОбщегоНазначенияРед12.ОкруглитьСУчетомПогрешности(Сумма * СтавкаНДС / (100 + СтавкаНДС), 2, , СоответствиеПогрешностей, ПеречислениеСтавкаНДС);
		Иначе
			СуммаНДС = ОбщегоНазначенияРед12.ОкруглитьСУчетомПогрешности(Сумма * СтавкаНДС / 100              , 2, , СоответствиеПогрешностей, ПеречислениеСтавкаНДС);
		КонецЕсли;
	Иначе
		СуммаНДС = 0;
	КонецЕсли;

	Возврат СуммаНДС;

КонецФункции // РассчитатьСуммуНДССУчетомПогрешности()
