

// Функция возвращает значение характера затрат, соответствующего счету затрат.
//
// Параметры
//  СчетЗатрат - ПланСчетовСсылка - счет затрат, по которому определяется характер затрат.
//  СтатьяЗатрат - СправочникСсылка.СтатьиЗатрат - статья затрат для уточнения характера затрат.
//
// Возвращаемое значение:
//   Строка – характер затрат, соответствующий счету затрат.
//
Функция ПолучитьХарактерЗатратПоСчетуЗатрат(СчетЗатрат, СтатьяЗатрат = Неопределено, Дата = Неопределено) Экспорт	
	// Проверка счета затрат.
	Если НЕ ЗначениеЗаполнено(СчетЗатрат) Тогда
		ПравильныйСчетЗатрат = Ложь;
	ИначеЕсли ТипЗнч(СчетЗатрат) <> Тип("ПланСчетовСсылка.Хозрасчетный") Тогда
		СтрокаСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Счет затрат %1 не соответствует плану счетов: ""Хозрасчетный"".
|Функция <ПолучитьХарактерЗатратПоСчетуЗатрат>';uk='Рахунок витрат %1 не відповідає плану рахунків: ""Госпрозрахунковий"".
|Функція <Получитьхарактерзатратпосчетузатрат>'"), СчетЗатрат);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщения);
		ПравильныйСчетЗатрат = Ложь;
	// Если все проверки пройдены, значит передан правильный счет затрат.
	Иначе
		ПравильныйСчетЗатрат = Истина;
	КонецЕсли;
	
	Если ПравильныйСчетЗатрат Тогда
			
		Если 	  СчетЗатрат = ПланыСчетов.Хозрасчетный.КапитальноеСтроительство
			  ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.ИзготовлениеОсновныхСредств
			  ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.ИзготовлениеДругихНеоборотныхМатериальныхАктивов
			  ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.ОбслуживаниеИРемонт
			  ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.ИзготовлениеНематериальныхАктивов
			  ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.ПриобретениеВыращиваниеДолгосрочныхБиологическихАктивовАмортизируются // ИНАГРО
			  ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ИзготовлениеНематериальныхАктивов)
			  ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.КапитальноеСтроительство)
			  ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ИзготовлениеОсновныхСредств)
			  ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ИзготовлениеДругихНеоборотныхМатериальныхАктивов)
			  ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ОбслуживаниеИРемонт) Тогда
			  
			ХарактерЗатрат = "Строительство";
			
		ИначеЕсли  СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.Производство)
			 И НЕ (СчетЗатрат = ПланыСчетов.Хозрасчетный.ОбслуживаниеИРемонт ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ОбслуживаниеИРемонт)) Тогда
			  
			ХарактерЗатрат   = "Производство";
			
		ИначеЕсли СчетЗатрат = ПланыСчетов.Хозрасчетный.БракВПроизводстве
			  ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.БракВПроизводстве) Тогда
		
			ХарактерЗатрат = "Производство";
			
		ИначеЕсли СчетЗатрат = ПланыСчетов.Хозрасчетный.ЗатратыБудущихПериодов
			  ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ЗатратыБудущихПериодов) Тогда
			  
			ХарактерЗатрат = "РБП";
			
		ИначеЕсли СчетЗатрат = ПланыСчетов.Хозрасчетный.ТранспортноЗаготовительныеРасходыМатериалы
			ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.ТранспортноЗаготовительныеРасходыТовары 
		    ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.ТранспортноЗаготовительныеРасходыТоварыСуммовойУчет  
			ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ТранспортноЗаготовительныеРасходыМатериалы)
			ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ТранспортноЗаготовительныеРасходыТовары) 
			ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ТранспортноЗаготовительныеРасходыТоварыСуммовойУчет) Тогда
			
			ХарактерЗатрат = "ТЗР";     
			
		ИначеЕсли СчетЗатрат = ПланыСчетов.Хозрасчетный.МатериальныеЗатраты
			  ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.ЗатратыНаОплатуТруда
			  ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.ОтчисленияНаСоциальныеМероприятия
			  ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.Амортизация
			  ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.ДругиеОперационныеЗатраты
			  ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.ДругиеЗатратыПоЭлементам
			  ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.МатериальныеЗатраты)
			  ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ЗатратыНаОплатуТруда)
			  ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ОтчисленияНаСоциальныеМероприятия)
			  ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.Амортизация)
			  ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ДругиеОперационныеЗатраты)
			  ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ДругиеЗатратыПоЭлементам) Тогда
			
			ХарактерЗатрат = "Затраты";
			
		ИначеЕсли СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.СебестоимостьРеализации) Тогда
		
			ХарактерЗатрат = "Затраты";
			
		ИначеЕсли СчетЗатрат = ПланыСчетов.Хозрасчетный.ОбщепроизводственныеРасходы
			  ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ОбщепроизводственныеРасходы) Тогда
			  
			  Если ЗначениеЗаполнено(Дата)
				  И Дата < НалоговыйУчетПовтИсп.ДатаНачалаРаспределенияОПЗвНУ() Тогда
				  // В начале, Налоговый кодекс относил ОПЗ на затраты периода, а не включал в себестоимость продукции
				  // Закон 3609 в п.43 это изменил. 
				  // дата вступления в силу изменений 01.08.2011: см. п.2 переходных положений
				  //           2. Норми підпунктів 40 - 53, 55 - 59 ... цього Закону застосовуються з першого числа звітного 
				  //           (податкового) місяця, у якому набирає чинності цей Закон.
				  ХарактерЗатрат = "Затраты";
		  	  Иначе	  
				   // распределяемые в НУ
				   ХарактерЗатрат = "ОПЗ";  
			  КонецЕсли;
			  
		ИначеЕсли СчетЗатрат = ПланыСчетов.Хозрасчетный.АдминистративныеРасходы
			  ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.АдминистративныеРасходы)
			  ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.РасходыНаСбыт
			  ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.РасходыНаСбыт)
			  ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.ДругиеЗатратыОперационнойДеятельностиГруппа
			  ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ДругиеЗатратыОперационнойДеятельностиГруппа) Тогда
			  
			  ХарактерЗатрат = "Затраты";	
		 ИначеЕсли СчетЗатрат = ПланыСчетов.Хозрасчетный.ФинансовыеЗатраты
              ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.ПотериОтУчастияВКапитале
              ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.ДругиеЗатратыДеятельности
              ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.НалогНаПрибыль
              ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.ЧрезвычайныеЗатраты
			  ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ФинансовыеЗатраты)
			  ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ПотериОтУчастияВКапитале)
			  ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ДругиеЗатратыДеятельности)
			  ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.НалогНаПрибыль)
			  ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ЧрезвычайныеЗатраты) Тогда
			  
			  ХарактерЗатрат = "Затраты";
			
		Иначе
			ХарактерЗатрат = "Прочие";
		КонецЕсли;
	Иначе
		ХарактерЗатрат = "Прочие";
	КонецЕсли;
	
	Возврат ХарактерЗатрат;
	
КонецФункции // ПолучитьХарактерЗатратПоСчетуЗатрат()
 
