////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Функция ПолучитьТекстОбщегоЗапроса(ИсточникДанных) Экспорт
	
	ТекстЗапроса = "";
	
	Если ВРег(ИсточникДанных) = ВРег("НачисленияУдержания") Тогда
		
		ТекстЗапроса = 
		"
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка) КАК Сотрудник,
		|	ВзаиморасчетыССотрудниками.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ВзаиморасчетыССотрудниками.Организация КАК Организация,
		|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК Подразделение,
		|	ВзаиморасчетыССотрудниками.СуммаВзаиморасчетовНачальныйОстаток КАК НачальныйОстаток,
		|	ВзаиморасчетыССотрудниками.СуммаВзаиморасчетовКонечныйОстаток КАК КонечныйОстаток,
		|	ВзаиморасчетыССотрудниками.Период КАК Период
		|ПОМЕСТИТЬ ВТСальдоПоРасчетамССотрудниками
		|ИЗ
		|	РегистрНакопления.ВзаиморасчетыССотрудниками.ОстаткиИОбороты(&НачалоПериода {(&НачалоПериода)}, &КонецПериода {(&КонецПериода)}, Месяц, , {(ФизическоеЛицо), (Организация)}) КАК ВзаиморасчетыССотрудниками
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	НАЧАЛОПЕРИОДА(НачисленияИУдержания.Период, МЕСЯЦ) КАК МесяцНачисления,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ПериодВзаиморасчетов,
		|	НачисленияИУдержания.ФизическоеЛицо КАК ФизическоеЛицо,
		|	НачисленияИУдержания.Подразделение КАК Подразделение,
		|	ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(НачисленияИУдержания.НачислениеУдержание) = ТИП(ПланВидовРасчета.Начисления)
		|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ГруппыНачисленияУдержанияВыплаты.Начислено)
		|   ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ГруппыНачисленияУдержанияВыплаты.Удержано) 
		|	КОНЕЦ КАК Группа,
		|	НачисленияИУдержания.НачислениеУдержание КАК ВидРасчета,
		|	НачисленияИУдержания.Сумма КАК Сумма,
		|	НачисленияИУдержания.ОтработаноДней КАК ОтработаноДней,
		|	НачисленияИУдержания.ОтработаноЧасов КАК ОтработаноЧасов,
		|	0 КАК ОплаченоДней,
		|	0 КАК ОплаченоЧасов,
		|	НачисленияИУдержания.Организация КАК Организация,
		|	НачисленияИУдержания.Сотрудник КАК Сотрудник, 
		|	НачисленияИУдержания.ПериодДействия КАК ПериодДействия,
		|	НачисленияИУдержания.Регистратор КАК Регистратор,
		|	NULL КАК Ведомость
		|ПОМЕСТИТЬ ВТПолныеДанныеОтчета
		|ИЗ
		|	РегистрНакопления.НачисленияУдержанияПоСотрудникам КАК НачисленияИУдержания
		|ГДЕ
		|	НачисленияИУдержания.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|{ГДЕ
		|	НачисленияИУдержания.Сотрудник,
		|	НачисленияИУдержания.ФизическоеЛицо,
		|	НачисленияИУдержания.Организация}
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(ВзаиморасчетыССотрудниками.Период, МЕСЯЦ),
		|	НАЧАЛОПЕРИОДА(ВзаиморасчетыССотрудниками.ПериодВзаиморасчетов, МЕСЯЦ),
		|	ВзаиморасчетыССотрудниками.ФизическоеЛицо,
		|	ВзаиморасчетыССотрудниками.Подразделение,
		|   ЗНАЧЕНИЕ(Перечисление.ГруппыНачисленияУдержанияВыплаты.Выплачено),
		|	ВзаиморасчетыССотрудниками.СпособВыплаты,
		|	ВзаиморасчетыССотрудниками.СуммаВзаиморасчетов,
		|	NULL,
		|	NULL,
		|	NULL,
		|	NULL,
		|	ВзаиморасчетыССотрудниками.Организация,
		|	ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка),
		|	НАЧАЛОПЕРИОДА(ВзаиморасчетыССотрудниками.Период, МЕСЯЦ),
		|	ВзаиморасчетыССотрудниками.Регистратор,
		|	ВзаиморасчетыССотрудниками.Ведомость
		|ИЗ
		|	РегистрНакопления.ВзаиморасчетыССотрудниками КАК ВзаиморасчетыССотрудниками
		|ГДЕ
		|	ВзаиморасчетыССотрудниками.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ВзаиморасчетыССотрудниками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И ВзаиморасчетыССотрудниками.КодОперации = ЗНАЧЕНИЕ(Перечисление.КодыОперацийВзаиморасчетыСРаботникамиОрганизаций.Выплата)
		|{ГДЕ
		|	ВзаиморасчетыССотрудниками.ФизическоеЛицо,
		|	ВзаиморасчетыССотрудниками.Организация}
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(НДФЛПоСотрудникамОбороты.Период, МЕСЯЦ),
		|	ДАТАВРЕМЯ(1, 1, 1),
		|	НДФЛПоСотрудникамОбороты.ФизическоеЛицо,
		|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК Подразделение,
		|   ЗНАЧЕНИЕ(Перечисление.ГруппыНачисленияУдержанияВыплаты.Удержано),
		|	""" + НСтр("ru='НДФЛ';uk='ПДФО'")+ """,
		|	НДФЛПоСотрудникамОбороты.НалогОборот,
		|	NULL,
		|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка),
		|	NULL,
		|	NULL,
		|	НДФЛПоСотрудникамОбороты.Организация,
		|   ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка),
		|	НАЧАЛОПЕРИОДА(НДФЛПоСотрудникамОбороты.НалоговыйПериод, МЕСЯЦ),
		|	NULL,
		|	NULL
		|ИЗ
		|   РегистрНакопления.НДФЛПоСотрудникам.Обороты(&НачалоПериода {(&НачалоПериода)}, &КонецПериода {(&КонецПериода)}, Месяц, ДоходНДФЛ.ВидСтавкиРезидента <> ЗНАЧЕНИЕ(Перечисление.ВидыСтавокНДФЛ.ВоенныйСбор) {(Организация), (ФизическоеЛицо)}) КАК НДФЛПоСотрудникамОбороты
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(НДФЛПоСотрудникамОбороты.Период, МЕСЯЦ),
		|	ДАТАВРЕМЯ(1, 1, 1),
		|	НДФЛПоСотрудникамОбороты.ФизическоеЛицо,
		|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК Подразделение,
		|   ЗНАЧЕНИЕ(Перечисление.ГруппыНачисленияУдержанияВыплаты.Удержано),
		|	""" + НСтр("ru='Военный сбор';uk='Військовий збір'")+ """,
		|	НДФЛПоСотрудникамОбороты.НалогОборот,
		|	NULL,
		|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка),
		|	NULL,
		|	NULL,
		|	НДФЛПоСотрудникамОбороты.Организация,
		|   ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка),
		|	НАЧАЛОПЕРИОДА(НДФЛПоСотрудникамОбороты.НалоговыйПериод, МЕСЯЦ),
		|	NULL,
		|	NULL
		|ИЗ
		|   РегистрНакопления.НДФЛПоСотрудникам.Обороты(&НачалоПериода {(&НачалоПериода)}, &КонецПериода {(&КонецПериода)}, Месяц, ДоходНДФЛ.ВидСтавкиРезидента = ЗНАЧЕНИЕ(Перечисление.ВидыСтавокНДФЛ.ВоенныйСбор) {(Организация), (ФизическоеЛицо)}) КАК НДФЛПоСотрудникамОбороты
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(ЕСВПоСотрудникамОбороты.Период, МЕСЯЦ),
		|	ДАТАВРЕМЯ(1, 1, 1),
		|	ЕСВПоСотрудникамОбороты.ФизическоеЛицо,
		|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК Подразделение,
		|   ЗНАЧЕНИЕ(Перечисление.ГруппыНачисленияУдержанияВыплаты.Удержано),
		|	""" + НСтр("ru='ЕСВ';uk='ЄСВ'") + """ +
		|	ВЫБОР КОГДА ЕСВПоСотрудникамОбороты.ВидЕСВ = ЗНАЧЕНИЕ(Перечисление.ВидыЕСВ.ОсновнаяЗарплата) 
		|			Тогда """ + НСтр("ru=' (основная зарплата)';uk=' (основна зарплата)'") + """ 
		|		КОГДА ЕСВПоСотрудникамОбороты.ВидЕСВ = ЗНАЧЕНИЕ(Перечисление.ВидыЕСВ.ПоДоговорамГПХ)
		|			Тогда """ + НСтр("ru=' (договор ГПХ)';uk=' (договір ЦПХ)'") + """ 
		|		КОГДА ЕСВПоСотрудникамОбороты.ВидЕСВ = ЗНАЧЕНИЕ(Перечисление.ВидыЕСВ.Больничные)
		|			Тогда """ + НСтр("ru=' (больничные)';uk=' (лікарняні)'") + """
		|		КОГДА ЕСВПоСотрудникамОбороты.ВидЕСВ = ЗНАЧЕНИЕ(Перечисление.ВидыЕСВ.Декретные)
		|			Тогда """ + НСтр("ru=' (декретные)';uk=' (декретні)'") + """
		|	КОНЕЦ,
		|	ЕСВПоСотрудникамОбороты.СуммаОборот,
		|	NULL,
		|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка),
		|	NULL,
		|	NULL,
		|	ЕСВПоСотрудникамОбороты.Организация,
		|   ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка),
		|	НАЧАЛОПЕРИОДА(ЕСВПоСотрудникамОбороты.НалоговыйПериод, МЕСЯЦ),
		|	NULL,
		|	NULL
		|ИЗ
		|   РегистрНакопления.ЕСВПоСотрудникам.Обороты(&НачалоПериода {(&НачалоПериода)}, &КонецПериода {(&КонецПериода)}, Месяц, СпособРасчета = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаВзносов.Взносы)  {(Организация), (ФизическоеЛицо)}) КАК ЕСВПоСотрудникамОбороты
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СальдоПоРасчетамССотрудниками.Период,
		|	ДАТАВРЕМЯ(1, 1, 1),
		|	СальдоПоРасчетамССотрудниками.ФизическоеЛицо,
		|	СальдоПоРасчетамССотрудниками.Подразделение,
		|	ЗНАЧЕНИЕ(Перечисление.ГруппыНачисленияУдержанияВыплаты.КонечноеСальдо),
		|	NULL,
		|	СальдоПоРасчетамССотрудниками.КонечныйОстаток,
		|	NULL,
		|	NULL,
		|	NULL,
		|	NULL,
		|	СальдоПоРасчетамССотрудниками.Организация,
		|	СальдоПоРасчетамССотрудниками.Сотрудник,
		|	NULL,
		|	NULL,
		|	NULL
		|ИЗ
		|	ВТСальдоПоРасчетамССотрудниками КАК СальдоПоРасчетамССотрудниками
		|ГДЕ
		|	СальдоПоРасчетамССотрудниками.Организация ЕСТЬ НЕ NULL 
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СальдоПоРасчетамССотрудниками.Период,
		|	ДАТАВРЕМЯ(1, 1, 1),
		|	СальдоПоРасчетамССотрудниками.ФизическоеЛицо,
		|	СальдоПоРасчетамССотрудниками.Подразделение,
		|	ЗНАЧЕНИЕ(Перечисление.ГруппыНачисленияУдержанияВыплаты.НачальноеСальдо),
		|	NULL,
		|	СальдоПоРасчетамССотрудниками.НачальныйОстаток,
		|	NULL,
		|	NULL,
		|	NULL,
		|	NULL,
		|	СальдоПоРасчетамССотрудниками.Организация,
		|	СальдоПоРасчетамССотрудниками.Сотрудник,
		|	NULL,
		|	NULL,
		|	NULL
		|ИЗ
		|	ВТСальдоПоРасчетамССотрудниками КАК СальдоПоРасчетамССотрудниками
		|ГДЕ
		|	СальдоПоРасчетамССотрудниками.Организация ЕСТЬ НЕ NULL 
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	КОНЕЦПЕРИОДА(ПолныеДанные.МесяцНачисления,МЕСЯЦ) КАК Период,
		|	ПолныеДанные.Сотрудник
		|ПОМЕСТИТЬ ВТСотрудникиСПериодом
		|ИЗ
		|	ВТПолныеДанныеОтчета КАК ПолныеДанные
		|ГДЕ
		|   ПолныеДанные.Сотрудник <> ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	КОНЕЦПЕРИОДА(ПолныеДанные.МесяцНачисления,МЕСЯЦ) КАК Период,
		|	ПолныеДанные.Организация,
		|	ПолныеДанные.ФизическоеЛицо
		|ПОМЕСТИТЬ ВТФизическиеЛицаСПериодом
		|ИЗ
		|	ВТПолныеДанныеОтчета КАК ПолныеДанные
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДАТАВРЕМЯ(1, 1, 1) КАК Период,
		|	ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка) КАК Сотрудник,
		|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
		|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) КАК ФизическоеЛицо
		|ПОМЕСТИТЬ Представления_ОсновныеСотрудникиОрганизации
		|ИЗ
		|	ВТФизическиеЛицаСПериодом КАК ВТФизическиеЛицаСПериодом
		|ГДЕ
		|	""ТолькоРазрешенные"" = ИСТИНА
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СотрудникиСПериодом.Период,
		|	СотрудникиСПериодом.Сотрудник
		|ПОМЕСТИТЬ ВТВсеСотрудникиСПериодом
		|ИЗ
		|	ВТСотрудникиСПериодом КАК СотрудникиСПериодом
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ОсновныеСотрудникиОрганизации.Период,
		|	ОсновныеСотрудникиОрганизации.Сотрудник
		|ИЗ
		|	Представления_ОсновныеСотрудникиОрганизации КАК ОсновныеСотрудникиОрганизации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДАТАВРЕМЯ(1, 1, 1) КАК Период,
		|	ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка) КАК Сотрудник,
		|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
		|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК Подразделение,
		|	ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка) КАК Должность,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ПустаяСсылка) КАК ВидЗанятости,
		|	НЕОПРЕДЕЛЕНО КАК ТарифнаяСтавка
		|ПОМЕСТИТЬ Представления_КадровыеДанныеСотрудников
		|ИЗ
		|	ВТВсеСотрудникиСПериодом КАК ВТВсеСотрудникиСПериодом
		|ГДЕ
		|	""ТолькоРазрешенные"" = ИСТИНА
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПолныеДанные.МесяцНачисления,
		|	ПолныеДанные.ПериодВзаиморасчетов,
		|	ПолныеДанные.ФизическоеЛицо,
		|	ПолныеДанные.Подразделение,
		|	ПолныеДанные.Группа,
		|	ПолныеДанные.ВидРасчета,
		|	ПолныеДанные.Сумма,
		|	ПолныеДанные.ОтработаноДней,
		|	ПолныеДанные.ОтработаноЧасов,
		|	ПолныеДанные.ОплаченоДней,
		|	ПолныеДанные.ОплаченоЧасов,
		|	ПолныеДанные.Организация,
		|	ВЫБОР КОГДА ПолныеДанные.Сотрудник = ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)
		|		ТОГДА ОсновныеСотрудникиОрганизации.Сотрудник
		|		ИНАЧЕ ПолныеДанные.Сотрудник
		|	КОНЕЦ КАК Сотрудник,
		|	ПолныеДанные.ПериодДействия,
		|	ПолныеДанные.Регистратор,
		|	ПолныеДанные.Ведомость,
		|	ОсновныеСотрудникиОрганизации.Сотрудник КАК ОсновноеМестоРаботы
		|ПОМЕСТИТЬ ВТПолныеДанныеОтчетаСОсновными
		|ИЗ
		|	ВТПолныеДанныеОтчета КАК ПолныеДанные
		|		ЛЕВОЕ СОЕДИНЕНИЕ Представления_ОсновныеСотрудникиОрганизации КАК ОсновныеСотрудникиОрганизации
		|		ПО ПолныеДанные.ФизическоеЛицо = ОсновныеСотрудникиОрганизации.ФизическоеЛицо
		|		   И ПолныеДанные.Организация = ОсновныеСотрудникиОрганизации.Организация
		|          И (КОНЕЦПЕРИОДА(ПолныеДанные.МесяцНачисления, МЕСЯЦ) = ОсновныеСотрудникиОрганизации.Период) 
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПолныеДанные.МесяцНачисления,
		|	ПолныеДанные.ПериодВзаиморасчетов,
		|	ПолныеДанные.ФизическоеЛицо,
		|	ПолныеДанные.Подразделение,
		|	ПолныеДанные.Группа,
		|	ПолныеДанные.ВидРасчета,
		|	ПолныеДанные.Сумма,
		|	ПолныеДанные.ОтработаноДней,
		|	ПолныеДанные.ОтработаноЧасов,
		|	ПолныеДанные.ОплаченоДней,
		|	ПолныеДанные.ОплаченоЧасов,
		|	ПолныеДанные.Организация,
		|	ПолныеДанные.Сотрудник,
		|	ПолныеДанные.ПериодДействия,
		|	ПолныеДанные.Регистратор,
		|	ПолныеДанные.Ведомость,
		|	ПолныеДанные.ОсновноеМестоРаботы,
		|	КадровыеДанныеСотрудников.Подразделение КАК ПодразделениеНаКонецПериода,
		|	КадровыеДанныеСотрудников.Должность КАК Должность,
		|	КадровыеДанныеСотрудников.ТарифнаяСтавка,
		|	КадровыеДанныеСотрудников.ВидЗанятости КАК ВидЗанятости,
		|   ДанныеОсновногоМестаРаботы.Подразделение КАК ПодразделениеОсновноеМестоРаботы
		|ПОМЕСТИТЬ ОбщиеЗапросы_НачисленияУдержания
		|ИЗ
		|	ВТПолныеДанныеОтчетаСОсновными КАК ПолныеДанные
		|		ЛЕВОЕ СОЕДИНЕНИЕ Представления_КадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников
		|		ПО ПолныеДанные.Сотрудник = КадровыеДанныеСотрудников.Сотрудник
		|			И (КОНЕЦПЕРИОДА(ПолныеДанные.МесяцНачисления, МЕСЯЦ) = КадровыеДанныеСотрудников.Период)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Представления_КадровыеДанныеСотрудников КАК ДанныеОсновногоМестаРаботы
		|		ПО ДанныеОсновногоМестаРаботы.Сотрудник = ПолныеДанные.ОсновноеМестоРаботы
		|			И (ДанныеОсновногоМестаРаботы.Период = КОНЕЦПЕРИОДА(ПолныеДанные.МесяцНачисления, МЕСЯЦ))
		|";
		
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПолучитьЗапросПоПредставлению(ТекстЗапроса, СоответствиеПараметров) Экспорт
	
	Запрос = Неопределено;
	
	ПозицияСловаПоместить = Найти(ВРег(ТекстЗапроса), "ПОМЕСТИТЬ ");
	
	ИмяИсточникаДанных = ЗарплатаКадрыОбщиеНаборыДанных.ПервоеСловоНачинаяСПозицииВТексте(ТекстЗапроса, ПозицияСловаПоместить + СтрДлина("ПОМЕСТИТЬ "));
	МассивПодстрокИмениИсточникаДанных = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИмяИсточникаДанных, "_");
	ИмяВременнойТаблицы = МассивПодстрокИмениИсточникаДанных[0];
	ИмяПредставления = МассивПодстрокИмениИсточникаДанных[1];
	Если МассивПодстрокИмениИсточникаДанных.Количество() > 2 Тогда
		ИмяПараметра = МассивПодстрокИмениИсточникаДанных[2];
	Иначе
		ИмяПараметра = Неопределено;
	КонецЕсли;
				
	МассивИменВТИсточниковПараметров = ЗарплатаКадрыОбщиеНаборыДанных.МассивИменВТИсточниковПараметров(ТекстЗапроса, ИмяИсточникаДанных);
	ПараметрыЗапроса = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыЗапроса(ТекстЗапроса, ИмяИсточникаДанных);
				
	ПредставлениеТолькоРазрешенные = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрЗапроса("ТолькоРазрешенные", ПараметрыЗапроса);
	ТолькоРазрешенные = ВРег(ПредставлениеТолькоРазрешенные) = "ИСТИНА";
	
	Если ВРег(ИмяПредставления) = "ПЕРИОДЫ" Тогда
		
		НачалоИнтервала = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрЗапроса("НачалоИнтервала", ПараметрыЗапроса);
        ОкончаниеИнтервала = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрЗапроса("ОкончаниеИнтервала", ПараметрыЗапроса);
		
		Периодичность = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрЗапроса("Периодичность", ПараметрыЗапроса);
		Если Периодичность = Неопределено Тогда
			Периодичность = "МЕСЯЦ";
		КонецЕсли;
		
		ИмяПоляПериод = "Период";
		
		ПредставлениеИспользоватьКонецПериода = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрЗапроса("ИспользоватьКонецПериода", ПараметрыЗапроса);
		Если Периодичность = Неопределено Тогда
			ИспользоватьКонецПериода = Ложь;
		Иначе
			ИспользоватьКонецПериода = ВРег(ПредставлениеИспользоватьКонецПериода) = "ИСТИНА";
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = ЗарплатаКадры.ТекстЗапросаВТПериоды(
			НачалоИнтервала,
			ОкончаниеИнтервала,
			Периодичность,
			ИмяПоляПериод,
			ИмяИсточникаДанных,
			ИспользоватьКонецПериода);
		
	ИначеЕсли ВРег(ИмяПредставления) = "СРЕЗПОСЛЕДНИХ" Тогда
		
		ИмяВТИсточникДанных = МассивИменВТИсточниковПараметров[0];
		КоллекцияПолей = ЗарплатаКадры.КоллекцииПолейДляПодготовкиТекстаЗапроса(ИмяПараметра, СоответствиеПараметров[ИмяВТИсточникДанных]);
		
		Отбор = ЗарплатаКадрыОбщиеНаборыДанных.КоллекцияОтборПоПараметрам(ПараметрыЗапроса);
		
		Запрос = ЗарплатаКадры.ЗапросВТИмяРегистраСрез(ТолькоРазрешенные, КоллекцияПолей, ИмяПараметра, ИмяВТИсточникДанных, ИмяИсточникаДанных, Отбор, , , Истина);
		
	ИначеЕсли ВРег(ИмяПредставления) = "СРЕЗПЕРВЫХ" Тогда
		
		ИмяВТИсточникДанных = МассивИменВТИсточниковПараметров[0];
		КоллекцияПолей = ЗарплатаКадры.КоллекцииПолейДляПодготовкиТекстаЗапроса(ИмяПараметра, СоответствиеПараметров[ИмяВТИсточникДанных]);
		
		Отбор = ЗарплатаКадрыОбщиеНаборыДанных.КоллекцияОтборПоПараметрам(ПараметрыЗапроса);
		
		Запрос = ЗарплатаКадры.ЗапросВТИмяРегистраСрез(ТолькоРазрешенные, КоллекцияПолей, ИмяПараметра, ИмяВТИсточникДанных, ИмяИсточникаДанных, Отбор, , , Ложь);
		
	ИначеЕсли ВРег(ИмяПредставления) = "ТАБЛИЦАРЕГИСТРА" Тогда
		
		ИмяВТИсточникДанных = МассивИменВТИсточниковПараметров[0];
		КоллекцияПолей = ЗарплатаКадры.КоллекцииПолейДляПодготовкиТекстаЗапроса(ИмяПараметра, СоответствиеПараметров[ИмяВТИсточникДанных]);
		
		Запрос = ЗарплатаКадры.ЗапросВТИмяРегистра(ТолькоРазрешенные, КоллекцияПолей, ИмяПараметра, ИмяВТИсточникДанных, ИмяИсточникаДанных);
		
	ИначеЕсли ВРег(ИмяПредставления) = "КАДРОВЫЕДАННЫЕСОТРУДНИКОВ" Тогда
		
		Отбор = ЗарплатаКадрыОбщиеНаборыДанных.КоллекцияОтборПоПараметрам(ПараметрыЗапроса);
		
		КоллекцияПолей =  ЗарплатаКадрыОбщиеНаборыДанных.МассивПолейЗапроса(Лев(ТекстЗапроса, ПозицияСловаПоместить - 1));
		
		Запрос = КадровыйУчет.ЗапросВТКадровыеДанныеСотрудников(
			ТолькоРазрешенные,
			ИмяИсточникаДанных,
			МассивИменВТИсточниковПараметров[0],
			КадровыйУчет.ПоляОтбораСотрудников(),
			КоллекцияПолей,
			Отбор);	
			
	ИначеЕсли ВРег(ИмяПредставления) = "ОСНОВНЫЕСОТРУДНИКИОРГАНИЗАЦИИ" Тогда
		
		Запрос = КадровыйУчет.ЗапросВТОсновныеСотрудникиОрганизации(
			ТолькоРазрешенные,
			ИмяИсточникаДанных,
		    МассивИменВТИсточниковПараметров[0]);			
		
	КонецЕсли;
		
	Возврат Запрос;	
	
КонецФункции

