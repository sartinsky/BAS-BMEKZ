////////////////////////////////////////////////////////////////////////////////
// ПоступлениеТоваровУслугФормыКлиентСервер: клиентские и серверные процедуры и 
// функции, вызываемые из форм документа "Поступление товаров и услуг".
//  
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЙ ПРОГРАММНЫЙ ИНТЕРФЕЙС

Процедура СформироватьНадписьЦеныИВалюта(Форма) Экспорт
	
	Объект = Форма.Объект;
	СтруктураНадписи = Новый Структура();
	СтруктураНадписи.Вставить("ТипЦен",           Объект.ТипЦен);
	СтруктураНадписи.Вставить("ВалютаДокумента",  Объект.ВалютаДокумента);
	СтруктураНадписи.Вставить("Курс",             Объект.КурсВзаиморасчетов);
	СтруктураНадписи.Вставить("Кратность",        Объект.КратностьВзаиморасчетов);
	Если Объект.ВидОперации <> ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку") И Форма.ПлательщикНДС Тогда
		СтруктураНадписи.Вставить("СуммаВключаетНДС", Объект.СуммаВключаетНДС);
	КонецЕсли;
	СтруктураНадписи.Вставить("ВалютаРегламентированногоУчета", Форма.ВалютаРегламентированногоУчета);
	Форма.ЦеныИВалюта = ОбщегоНазначенияБПКлиентСервер.СформироватьНадписьЦеныИВалюта(СтруктураНадписи);

КонецПроцедуры

// Возвращает значение необязательного реквизита формы по его имени.
//
// Параметры:
//  Форма - УправляемаяФорма - Форма, для которой определяется реквизит.
//  ИмяРеквизита - Строка, содержащая одно из возможных имен реквизита:
//		НТТ
//		ПорядокУчетаРасчетов
//		РазделениеПоСтавкамВРознице
//		РассчитыватьСуммаВРознице
//		СчетаРасчетовЗаполнены
//		ТоварыСвернуты
//		УчетВПродажныхЦенах
//		ЭтоКомиссия
//
// Возвращаемое значение:
//  Тип Произвольный - значение реквизита.
//
Функция ПолучитьРеквизитФормы(Форма, ИмяРеквизита) Экспорт

	ЧастиИмениФормы 	= СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Форма.ИмяФормы, ".");
	ИмяФормы 			= ЧастиИмениФормы[ЧастиИмениФормы.Количество() - 1];
	ИмяРеквизитаВРег 	= ВРег(ИмяРеквизита);
	Результат 			= Неопределено;
	
	Если ИмяРеквизитаВРег = "НТТ" Тогда
		// Склад типа НТТ.
		Если ИмяФормы = "ФормаДокументаОбщая" ИЛИ ИмяФормы = "ФормаДокументаТовары" Тогда
			Результат = Форма.НТТ;
		Иначе
			Результат = Ложь;
		КонецЕсли;
	
	ИначеЕсли ИмяРеквизитаВРег = ВРег("ПорядокУчетаРасчетов") Тогда
		// Надпись со счетами расчетов.
		Если ИмяФормы = "ФормаДокументаТовары" ИЛИ ИмяФормы = "ФормаДокументаУслуги" Тогда
			Результат = Форма.ПорядокУчетаРасчетов;
		Иначе
			Результат = "";
		КонецЕсли;

	ИначеЕсли ИмяРеквизитаВРег = ВРег("РазделениеПоСтавкамВРознице") Тогда
		// Признак необходимости разделения в рознице по ставкам НДС.
		Если ИмяФормы = "ФормаДокументаОбщая" ИЛИ ИмяФормы = "ФормаДокументаТовары" Тогда
			Результат = Форма.РазделениеПоСтавкамВРознице;
		Иначе
			Результат = Ложь;
		КонецЕсли;

	ИначеЕсли ИмяРеквизитаВРег = ВРег("РассчитыватьСуммаВРознице") Тогда
		// Признак необходимости расчета суммы в рознице по продажным ценам.
		Если ИмяФормы = "ФормаДокументаОбщая" ИЛИ ИмяФормы = "ФормаДокументаТовары" Тогда
			Результат = Форма.РассчитыватьСуммаВРознице;
		Иначе
			Результат = Ложь;
		КонецЕсли;

	ИначеЕсли ИмяРеквизитаВРег = ВРег("СчетаРасчетовЗаполнены") Тогда
		// Признак того, что счета расчетов заполнены корректно.
		Если ИмяФормы = "ФормаДокументаТовары" ИЛИ ИмяФормы = "ФормаДокументаУслуги" Тогда
			Результат = Форма.СчетаРасчетовЗаполнены;
		Иначе
			Результат = Ложь;
		КонецЕсли;

	ИначеЕсли ИмяРеквизитаВРег = ВРег("ТоварыСвернуты") Тогда
		// Признак того, что счета при поступлении в розницу номенклатура в строках не указывается,
		// вводятся общие цифры.
		Если ИмяФормы = "ФормаДокументаОбщая" ИЛИ ИмяФормы = "ФормаДокументаТовары" Тогда
			Результат = Форма.ТоварыСвернуты;
		Иначе
			Результат = Ложь;
		КонецЕсли;

	ИначеЕсли ИмяРеквизитаВРег = ВРег("УчетВПродажныхЦенах") Тогда
		// Признак того, что в учетной политике установлена розница в продажных ценах.
		Если ИмяФормы = "ФормаДокументаОбщая" ИЛИ ИмяФормы = "ФормаДокументаТовары" Тогда
			Результат = Форма.УчетВПродажныхЦенах;
		Иначе
			Результат = Ложь;
		КонецЕсли;

	ИначеЕсли ИмяРеквизитаВРег = ВРег("ЭтоКомиссия") Тогда
		// Признак поступления товаров на комиссию по продаже.
		Если ИмяФормы = "ФормаДокументаОбщая" Тогда
			Результат = Форма.ЭтоКомиссия;
		Иначе
			Результат = Ложь;
		КонецЕсли;
	
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

// Устанавливает значение необязательного реквизита формы по имени.
// 
// Параметры:
//  Форма - УправляемаяФорма - Форма, для которой определяется реквизит.
//  ИмяРеквизита - Строка, содержащая одно из возможных имен реквизита:
//		НТТ
//		ПорядокУчетаРасчетов
//		РазделениеПоСтавкамВРознице
//		РассчитыватьСуммаВРознице
//		СчетаРасчетовЗаполнены
//		ТоварыСвернуты
//		УчетВПродажныхЦенах
//		ЭтоКомиссия
//	ЗначениеРеквизита - Произвольный, устанавливаемое значение реквизита.
//
Процедура УстановитьРеквизитФормы(Форма, ИмяРеквизита, ЗначениеРеквизита) Экспорт

	ЧастиИмениФормы 	= СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Форма.ИмяФормы, ".");
	ИмяФормы 			= ЧастиИмениФормы[ЧастиИмениФормы.Количество() - 1];
	ИмяРеквизитаВРег 	= ВРег(ИмяРеквизита);
	
	Если ИмяРеквизитаВРег = "НТТ" Тогда
		// Склад типа НТТ.
		Если ИмяФормы = "ФормаДокументаОбщая" ИЛИ ИмяФормы = "ФормаДокументаТовары" Тогда
			Форма.НТТ = ЗначениеРеквизита;
		КонецЕсли;
	
	ИначеЕсли ИмяРеквизитаВРег = ВРег("ПорядокУчетаРасчетов") Тогда
		// Надпись со счетами расчетов.
		Если ИмяФормы = "ФормаДокументаТовары" ИЛИ ИмяФормы = "ФормаДокументаУслуги" Тогда
			Форма.ПорядокУчетаРасчетов = ЗначениеРеквизита;
		КонецЕсли;

	ИначеЕсли ИмяРеквизитаВРег = ВРег("РазделениеПоСтавкамВРознице") Тогда
		// Признак необходимости разделения в рознице по ставкам НДС.
		Если ИмяФормы = "ФормаДокументаОбщая" ИЛИ ИмяФормы = "ФормаДокументаТовары" Тогда
			Форма.РазделениеПоСтавкамВРознице = ЗначениеРеквизита;
		КонецЕсли;

	ИначеЕсли ИмяРеквизитаВРег = ВРег("РассчитыватьСуммаВРознице") Тогда
		// Признак необходимости расчета суммы в рознице по продажным ценам.
		Если ИмяФормы = "ФормаДокументаОбщая" ИЛИ ИмяФормы = "ФормаДокументаТовары" Тогда
			Форма.РассчитыватьСуммаВРознице = ЗначениеРеквизита;
		КонецЕсли;

	ИначеЕсли ИмяРеквизитаВРег = ВРег("СчетаРасчетовЗаполнены") Тогда
		// Признак того, что счета расчетов заполнены корректно.
		Если ИмяФормы = "ФормаДокументаТовары" ИЛИ ИмяФормы = "ФормаДокументаУслуги" Тогда
			Форма.СчетаРасчетовЗаполнены = ЗначениеРеквизита;
		КонецЕсли;

	ИначеЕсли ИмяРеквизитаВРег = ВРег("ТоварыСвернуты") Тогда
		// Признак того, что счета при поступлении в розницу номенклатура в строках не указывается,
		// вводятся общие цифры.
		Если ИмяФормы = "ФормаДокументаОбщая" ИЛИ ИмяФормы = "ФормаДокументаТовары" Тогда
			Форма.ТоварыСвернуты = ЗначениеРеквизита;
		КонецЕсли;

	ИначеЕсли ИмяРеквизитаВРег = ВРег("УчетВПродажныхЦенах") Тогда
		// Признак того, что в учетной политике установлена розница в продажных ценах.
		Если ИмяФормы = "ФормаДокументаОбщая" ИЛИ ИмяФормы = "ФормаДокументаТовары" Тогда
			Форма.УчетВПродажныхЦенах = ЗначениеРеквизита;
		КонецЕсли;

	ИначеЕсли ИмяРеквизитаВРег = ВРег("ЭтоКомиссия") Тогда
		// Признак поступления товаров на комиссию по продаже.
		Если ИмяФормы = "ФормаДокументаОбщая" Тогда
			Форма.ЭтоКомиссия = ЗначениеРеквизита;
		КонецЕсли;
	
	КонецЕсли;

КонецПроцедуры

Процедура БланкиСтрогоУчетаРассчитатьНоминальныеРеквизиты(СтрокаТабличнойЧасти, ДанныеОбъекта) Экспорт
	
	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Номенклатура) Тогда
		//заполнение для бланков, которые учитываются по номинальной стоимости
		Если НЕ СтрокаТабличнойЧасти.УчитываетсяПоНоминальнойСтоимости Тогда
			
			Если  ДанныеОбъекта.ПлательщикНДС
				И СтрокаТабличнойЧасти.НалоговоеНазначение = ПредопределенноеЗначение("Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_Облагаемая") Тогда
				СуммаВключенногоНДС = 0;	
			Иначе
				СуммаВключенногоНДС = СтрокаТабличнойЧасти.СуммаНДС;
			КонецЕсли;
			
			СтрокаТабличнойЧасти.СуммаНоминальная = СтрокаТабличнойЧасти.Сумма - СуммаВключенногоНДС;
			СтрокаТабличнойЧасти.ЦенаНоминальная  = СтрокаТабличнойЧасти.Цена - СуммаВключенногоНДС/?(СтрокаТабличнойЧасти.Количество=0,1,СтрокаТабличнойЧасти.Количество);
			
			//заполнение для бланков, которые НЕ учитываются по номинальной стоимости	
		Иначе
			СтрокаТабличнойЧасти.СуммаНоминальная = СтрокаТабличнойЧасти.ЦенаНоминальная*СтрокаТабличнойЧасти.Количество;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры 

          