////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ


Процедура ВедомостьУстановитьВидимостьПоСпособуВыплаты(Форма) Экспорт
	
	СтруктураВидимости = Новый Структура();
	СтруктураВидимости.Вставить("Зарплата", Истина);
	СтруктураВидимости.Вставить("ЗарплатаПодробно", Истина);
	СтруктураВидимости.Вставить("НДФЛ", Истина);
	СтруктураВидимости.Вставить("Взносы", Истина);
	СтруктураВидимости.Вставить("РассчитыватьНалоги", Ложь);
	
	СпособВыплаты = Форма.Объект.СпособВыплаты;
	
	//Здесь указываем, когда и какие табличные части не нужны
	Если СпособВыплаты = ПредопределенноеЗначение("Справочник.ВидыВыплат.Очередная") 
	или СпособВыплаты = ПредопределенноеЗначение("Справочник.ВидыВыплат.ОчереднаяПогашениеДолгов")  Тогда
		СтруктураВидимости.Взносы  = Ложь;
	ИначеЕсли СпособВыплаты = ПредопределенноеЗначение("Справочник.ВидыВыплат.ОчереднаяВзносы") 
	или СпособВыплаты = ПредопределенноеЗначение("Справочник.ВидыВыплат.ОчереднаяПогашениеДолговВзносы")  
	Тогда
		СтруктураВидимости.Зарплата  = Ложь;
		СтруктураВидимости.ЗарплатаПодробно  = Ложь;
		СтруктураВидимости.НДФЛ  = Ложь;
	ИначеЕсли СпособВыплаты = ПредопределенноеЗначение("Справочник.ВидыВыплат.НДФЛБезВыплатыЗарплаты")
	или СпособВыплаты = ПредопределенноеЗначение("Справочник.ВидыВыплат.НДФЛКонтрагентам")  	
	Тогда	
		СтруктураВидимости.Зарплата  = Ложь;
		СтруктураВидимости.ЗарплатаПодробно  = Ложь;
		СтруктураВидимости.Взносы  = Ложь;
	ИначеЕсли СпособВыплаты = ПредопределенноеЗначение("Справочник.ВидыВыплат.Депоненты")
	Тогда	
		СтруктураВидимости.Зарплата  = Истина;
		СтруктураВидимости.ЗарплатаПодробно  = Истина;
		СтруктураВидимости.Взносы  = Ложь;
		СтруктураВидимости.НДФЛ  = Ложь;
	КонецЕсли;
	
	//Здесь указываем, когда может потребоваться расчет налогов и взносов
	СтруктураВидимости.РассчитыватьНалоги = Форма.РассчитыватьНалоги;
	
	Форма.Элементы.ЗарплатаГруппа.Видимость = СтруктураВидимости.Зарплата;
	Форма.Элементы.ЗарплатаПодробноГруппа.Видимость = СтруктураВидимости.ЗарплатаПодробно;
	Форма.Элементы.НДФЛГруппа.Видимость = СтруктураВидимости.НДФЛ;
	Форма.Элементы.ВзносыГруппа.Видимость = СтруктураВидимости.Взносы;
	Форма.Элементы.ВзносыФОТГруппа.Видимость = СтруктураВидимости.Взносы;
	Форма.Элементы.РассчитатьНалоги.Видимость = СтруктураВидимости.РассчитыватьНалоги;
	Форма.Элементы.ЗарплатаПодробноСуммаГрязными.Видимость = СтруктураВидимости.РассчитыватьНалоги;
	Форма.Элементы.ЗарплатаПодробноВедомость.Видимость = (СпособВыплаты = ПредопределенноеЗначение("Справочник.ВидыВыплат.Депоненты"));
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции ведомостей на выплату зарплаты

// Обработчики событий формы

Процедура ВедомостьОбработкаОповещения(Форма, ИмяСобытия, Параметр, Источник) Экспорт
	
	Если ИмяСобытия = ВзаиморасчетыССотрудникамиКлиент.ИмяСобытияИзмененияОплатыВедомости() Тогда
		Форма.УстановитьДоступностьЭлементов();
	КонецЕсли;
	
КонецПроцедуры

// Обработчики событий элементов шапки

Процедура ВедомостьСпособВыплатыПриИзменении(Форма, Элемент) Экспорт
	
	ВзаиморасчетыССотрудникамиКлиент.ВедомостьУстановитьВидимостьПоСпособуВыплаты(Форма);
	
КонецПроцедуры


// Редактирование и представление параметров расчета

Процедура ВедомостьИзменитьПараметрыРасчета(Форма, ОповещениеЗавершения = Неопределено) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма", Форма);
	ДополнительныеПараметры.Вставить("СтароеОкругление", Форма.Объект.Округление);
	ДополнительныеПараметры.Вставить("ОповещениеЗавершения", ОповещениеЗавершения);
	
	Оповещение = Новый ОписаниеОповещения("ВедомостьИзменитьПараметрыРасчетаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	Форма.ПоказатьВыборИзМеню(Оповещение, Форма.СпособыОкругления);
	
КонецПроцедуры

Процедура ВедомостьИзменитьПараметрыРасчетаЗавершение(Выбор, ДополнительныеПараметры) Экспорт

	Форма = ДополнительныеПараметры.Форма;
	СтароеОкругление = ДополнительныеПараметры.СтароеОкругление;
	ОповещениеЗавершения = ДополнительныеПараметры.ОповещениеЗавершения;
	
	Если Выбор <> Неопределено И Выбор.Значение <> СтароеОкругление Тогда
		Форма.Объект.Округление = Выбор.Значение;
		Форма.Рассчитать(Неопределено)
	КонецЕсли;
	
	Если ОповещениеЗавершения <> Неопределено Тогда 
		ВыполнитьОбработкуОповещения(ОповещениеЗавершения, Выбор = Неопределено);
	КонецЕсли;
	
КонецПроцедуры	
	
Функция ВедомостьПредставлениеПараметровРасчета(Форма) Экспорт

	ПредставлениеПараметровРасчета = Строка(Форма.Объект.Округление);
	
	Возврат ПредставлениеПараметровРасчета;
	
КонецФункции

