Процедура ПередЗаписьюДокументаДляУдаленияДвиженийПередЗаписью(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	Источник.ДополнительныеСвойства.Вставить("ЭтоНовый", Источник.ЭтоНовый());
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение 
		И НЕ Источник.ДополнительныеСвойства.ЭтоНовый 
		И Источник.Проведен Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Док.Дата КАК Дата
		|ИЗ
		|	Документ." + Источник.Метаданные().Имя + " КАК Док
		|ГДЕ
		|	Док.Ссылка = &Ссылка";
		Запрос.УстановитьПараметр("Ссылка", Источник.Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		Источник.ДополнительныеСвойства.Вставить("ДатаДокументаСдвинутаВперед", Источник.Дата > Выборка.Дата);
	Иначе
		Источник.ДополнительныеСвойства.Вставить("ДатаДокументаСдвинутаВперед", Ложь);
	КонецЕсли;
	
КонецПроцедуры

// Процедура очищает реквизит РучнаяКорректировка при копировании отредактированных вручную документов
//
Процедура ПриКопированииДокументаСВозможностьюРучногоРедактированияПриКопировании(Источник, ОбъектКопирования) Экспорт
	
	Источник.РучнаяКорректировка = Ложь;
	
КонецПроцедуры

// РЕГИСТРАЦИЯ РЕКВИЗИТОВ ДОКУМЕНТОВ ПОСТУПЛЕНИЯ И ОПЛАТЫ

// Обработчик подписки на событие ПриЗаписиДокументаРегистрацияДанныхПервичныхДокументовИП
Процедура ПриЗаписиДокументаРегистрацияДанныхПервичныхДокументов(Источник, Отказ) Экспорт
	Перем НомерДокумента, ДатаДокумента;
	
	Если Источник.ОбменДанными.Загрузка = Истина
		И Источник.ДополнительныеСвойства.Свойство("РегистрироватьДанныеПервичныхДокументов")
		И Источник.ДополнительныеСвойства.РегистрироватьДанныеПервичныхДокументов = Ложь Тогда
		Возврат;
	КонецЕсли;	

	Ссылка	= Источник.Ссылка;
	
	ИменаРеквизитов	= "Организация, Номер, Дата";
	
	МетаданныеДокумента	= Источник.Метаданные();
	Если НЕ МетаданныеДокумента.Реквизиты.Найти("РучнаяКорректировка") = Неопределено Тогда
		ИменаРеквизитов	= ИменаРеквизитов + ", РучнаяКорректировка";
	КонецЕсли;
	
	Если НЕ МетаданныеДокумента.Реквизиты.Найти("НомерВходящегоДокумента") = Неопределено Тогда
		ИменаРеквизитов	= ИменаРеквизитов + ", НомерВходящегоДокумента";
		Если НЕ МетаданныеДокумента.Реквизиты.Найти("ДатаВходящегоДокумента") = Неопределено Тогда
			ИменаРеквизитов	= ИменаРеквизитов + ", ДатаВходящегоДокумента";
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.ОприходованиеТоваров") Тогда
		ИменаРеквизитов	= ИменаРеквизитов + ", ИнвентаризацияТоваровНаСкладе";
	КонецЕсли;
	
	Реквизиты	= ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, ИменаРеквизитов);
	
	Если Реквизиты.Свойство("РучнаяКорректировка") Тогда
		Если Реквизиты.РучнаяКорректировка Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Реквизиты.Организация) Тогда
		Возврат;
	КонецЕсли;
	
	//Установка управляемой блокировки
	ПараметрыБлокировки	= Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрСведений", "ДанныеПервичныхДокументов");
	ЗначенияБлокировки	= Новый Структура("Документ", Ссылка);
	ОбщегоНазначенияБПВызовСервера.УстановитьУправляемуюБлокировку(ПараметрыБлокировки, ЗначенияБлокировки);
	
	НомерДокумента	= "";
	ДатаДокумента	= '00010101';
	
	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.ОприходованиеТоваров") И ЗначениеЗаполнено(Реквизиты.ИнвентаризацияТоваровНаСкладе) Тогда
		
		РеквизитыИнвентаризации	= ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Реквизиты.ИнвентаризацияТоваровНаСкладе, "Номер, Дата");
		
		НомерДокумента	= ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(РеквизитыИнвентаризации.Номер, Истина, Истина);
		ДатаДокумента	= РеквизитыИнвентаризации.Дата;
		
	ИначеЕсли Реквизиты.Свойство("НомерВходящегоДокумента") Тогда
		
		НомерДокумента	= СокрЛП(Реквизиты.НомерВходящегоДокумента);
		
		Если Реквизиты.Свойство("ДатаВходящегоДокумента") Тогда
			ДатаДокумента	= Реквизиты.ДатаВходящегоДокумента;
		КонецЕсли;
		
	Иначе
		
		НомерДокумента	= ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(Реквизиты.Номер, Истина, Истина);
		ДатаДокумента	= Реквизиты.Дата;
		
	КонецЕсли;
	
	НаборЗаписейРегистра = РегистрыСведений.ДанныеПервичныхДокументов.СоздатьНаборЗаписей();	
	НаборЗаписейРегистра.Отбор.Документ.Установить(Ссылка);
	
	МенеджерЗаписиРегистра = НаборЗаписейРегистра.Добавить();
	МенеджерЗаписиРегистра.Организация		 = Реквизиты.Организация;
	МенеджерЗаписиРегистра.Документ			 = Ссылка;
	МенеджерЗаписиРегистра.Номер             = НомерДокумента;
	МенеджерЗаписиРегистра.Дата              = ДатаДокумента;
	МенеджерЗаписиРегистра.НомерРегистратора = Реквизиты.Номер;
	МенеджерЗаписиРегистра.ДатаРегистратора	 = Реквизиты.Дата;
	
	НаборЗаписейРегистра.Записать(Истина);		
	
КонецПроцедуры

