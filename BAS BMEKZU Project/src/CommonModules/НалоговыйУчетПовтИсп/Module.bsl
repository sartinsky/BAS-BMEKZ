Функция ДатаНачалаРаспределенияОПЗвНУ() Экспорт
	
	ДатаВступления = Константы.ДатаНачалаРаспределенияОПЗвНУ.Получить();	
	
	Если Не ЗначениеЗаполнено(ДатаВступления) Тогда
		ДатаВступления = '2011-08-01';
	КонецЕсли;
	
	Возврат ДатаВступления;	

КонецФункции 

Функция ДатаВступленияВСилуПриказа1379() Экспорт
	
	ДатаВступления = Константы.ДатаВступленияВСилуПриказа1379.Получить();	
	
	Если Не ЗначениеЗаполнено(ДатаВступления) Тогда
		ДатаВступления = '2011-12-16';
	КонецЕсли;
	
	Возврат ДатаВступления;	

КонецФункции 

Функция ДатаНачалаИспользованияФормыННПоЛекарствам() Экспорт
	
	ДатаНачала = Константы.ДатаНачалаИспользованияФормыННПоЛекарствам.Получить();	
	Если Не ЗначениеЗаполнено(ДатаНачала) Тогда
		ДатаНачала = '20141201';
	КонецЕсли;
	
	Возврат ДатаНачала;	

КонецФункции 

Функция НастройкаПечатиНалоговыхДокументов(Организация, Дата) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЕСТЬNULL(Настройки.ВыводитьНомерДоговораБезКлеточек, ЛОЖЬ) КАК ВыводитьНомерДоговораБезКлеточек,
	               |	ЕСТЬNULL(Настройки.ДополнятьНаименованиеАдресДаннымиГоловнойОрганизацииВНН, ЛОЖЬ) КАК ДополнятьНаименованиеАдресДаннымиГоловнойОрганизацииВНН,
	               |	ЕСТЬNULL(Настройки.НеДополнятьНаименованиеНомеромГТДВНалоговыхДокументах, ЛОЖЬ) КАК НеДополнятьНаименованиеНомеромГТДВНалоговыхДокументах,
	               |	ЕСТЬNULL(Настройки.ПечататьДатыВНалоговыхНакладныхСРазделителями, ЛОЖЬ) КАК ПечататьДатыВНалоговыхНакладныхСРазделителями,
	               |	ЕСТЬNULL(Настройки.ПечататьКодУКТВЭДБезПробелов, ЛОЖЬ) КАК ПечататьКодУКТВЭДБезПробелов,
	               |	ЕСТЬNULL(Настройки.ПечататьТитульныйЛистДляПриложения2КНалоговойНакладной, ЛОЖЬ) КАК ПечататьТитульныйЛистДляПриложения2КНалоговойНакладной,
				   |	ЕСТЬNULL(Настройки.СимволПустыхПоказателейВНалоговойНакладной, ""X"") КАК СимволПустыхПоказателейВНалоговойНакладной,
				   |	ЕСТЬNULL(Настройки.ВыгрузкаПечатьНН_ПоФормеНаДатуДокумента, ЛОЖЬ) КАК ВыгрузкаПечатьНН_ПоФормеНаДатуДокумента
	               |ИЗ
	               |	(ВЫБРАТЬ 1) КАК СтрокаДанных
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаПечатиНалоговыхДокументов.СрезПоследних(&Дата, Организация = &Организация) КАК Настройки
	               |		ПО (ИСТИНА)";
				   
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	ТаблицаНастройки = Запрос.Выполнить().Выгрузить();
	СтрокаНастройки = ТаблицаНастройки[0];
	
	СтруктураНастройки = Новый Структура;
	
	Для каждого Настройка Из ТаблицаНастройки.Колонки Цикл
	
		СтруктураНастройки.Вставить(Настройка.Имя, СтрокаНастройки[Настройка.Имя]);	
	
	КонецЦикла;
	
	Возврат СтруктураНастройки;

КонецФункции
 
Функция НастройкаНумерацииНалоговыхДокументов(Организация, Дата) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЕСТЬNULL(Настройки.ВестиМесячнуюНумерациюНалоговыхДокументов, ЛОЖЬ) 							КАК ВестиМесячнуюНумерациюНалоговыхДокументов,
				   |	ЕСТЬNULL(Настройки.ВестиДневнуюНумерациюНалоговыхДокументов, ЛОЖЬ) 								КАК ВестиДневнуюНумерациюНалоговыхДокументов,
				   |	ЕСТЬNULL(Настройки.ВестиНумерациюНалоговыхДокументовБезУчетаОбособленныхПодразделений, ЛОЖЬ) 	КАК ВестиНумерациюНалоговыхДокументовБезУчетаОбособленныхПодразделений,
				   |	ЕСТЬNULL(Настройки.ВестиРаздельнуюНумерациюНалоговыхДокументов, ЛОЖЬ) 							КАК ВестиРаздельнуюНумерациюНалоговыхДокументов,
	               |	ЕСТЬNULL(Настройки.ВестиРаздельнуюНумерациюНалоговыхДокументовПоНеНДСОперациям, ЛОЖЬ) 			КАК ВестиРаздельнуюНумерациюНалоговыхДокументовПоНеНДСОперациям,
	               |	ЕСТЬNULL(Настройки.ВестиРаздельнуюНумерациюНалоговыхДокументовПоСпецРежимам, ЛОЖЬ) 				КАК ВестиРаздельнуюНумерациюНалоговыхДокументовПоСпецРежимам,
	               |	ЕСТЬNULL(Настройки.ВестиРаздельнуюНумерациюНалоговыхНакладныхПоОбычнымЦенам, ЛОЖЬ) 				КАК ВестиРаздельнуюНумерациюНалоговыхНакладныхПоОбычнымЦенам
	               |ИЗ
	               |	(ВЫБРАТЬ 1) КАК СтрокаДанных
	               |	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаНумерацииНалоговыхДокументов.СрезПоследних(&Дата, Организация = &Организация) КАК Настройки
	               |		ПО (ИСТИНА)";
				   
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Организация", Организация);
				   
	ТаблицаНастройки = Запрос.Выполнить().Выгрузить();
	СтрокаНастройки = ТаблицаНастройки[0];
	
	СтруктураНастройки = Новый Структура;
	
	Для каждого Настройка Из ТаблицаНастройки.Колонки Цикл
	
		СтруктураНастройки.Вставить(Настройка.Имя, СтрокаНастройки[Настройка.Имя]);	
	
	КонецЦикла;
	
	Возврат СтруктураНастройки;

КонецФункции

Функция ЭтоНалоговоеНазначениеНДС(НалоговоеНазначение) Экспорт
	
	Если НалоговоеНазначение = Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность 
		ИЛИ НалоговоеНазначение = Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяХозДеятельность
		ИЛИ НалоговоеНазначение = Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_Облагаемая  
		ИЛИ НалоговоеНазначение = Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_Пропорционально Тогда
		
		Возврат Истина;
		
	Иначе 
		Возврат Ложь;
		
	КонецЕсли; 
	
КонецФункции
