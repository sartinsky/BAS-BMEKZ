////////////////////////////////////////////////////////////////////////////////
// Отражение зарплаты в бухучете базовая реализация
// 
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЙ ПРОГРАММНЫЙ ИНТЕРФЕЙС

Процедура СформироватьДвиженияПоДокументу(Движения, Отказ, Организация, ПериодРегистрации, Дата, ДанныеДляПроведения, СтрокаСписокТаблиц) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ФормироватьПроводкиПоЗарплатеВКонцеПериода", Новый Структура("Организация", Организация)) Тогда
		
		// получим структуру с результатами отражения зарплаты
		// структура РезультатОтраженияЗарплатыДляБухучета
		// ключ - имя таблицы, значение - таблица значений с полученными данными
		// в структуре присутствуют все таблицы, а заполнены только те, которые входят в СтрокаСписокТаблиц
		ДанныеДляОтражения = ОтражениеЗарплатыВБухучете.ДанныеДляОтраженияЗарплатыВБухучете(ПериодРегистрации, Организация, СтрокаСписокТаблиц, ДанныеДляПроведения);
		
		// формирование проводок
		ОтражениеЗарплатыВБухучетеПереопределяемый.СформироватьДвижения(Движения, Отказ, Организация, ПериодРегистрации, Дата, ДанныеДляОтражения);
		
	КонецЕсли;

КонецПроцедуры

// получим отражение в бухучете начислений и поместим во временную таблицу ВТБухучетНачислений
//	МенеджерВременныхТаблиц должен содержать таблицу ВТНачисления
//	Описание таблицы ВТНачисления
// 		ПериодРегистрации
// 		Организация
// 		Сотрудник
// 		ФизическоеЛицо
// 		Подразделение
// 		ПериодПринятияРасходов
// 		ДатаНачалаСобытия
// 		ВидОперации
// 		Начисление
// 		Сумма
//
// Поля таблицы ВТБухучетНачислений
// 	ПериодРегистрации
// 	Сотрудник
// 	ФизическоеЛицо
// 	Подразделение
// 	ПериодПринятияРасходов
// 	Начисление
// 	ВидОперации
// 	ВидНачисленияОплатыТрудаДляНУ
// 	СпособОтраженияЗарплатыВБухучете
// 	Сумма
//
Процедура СоздатьВТБухучетНачислений(Организация, ПериодРегистрации, МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НачисленнаяЗарплатаИВзносы.ПериодРегистрации,
	|	НачисленнаяЗарплатаИВзносы.Сотрудник,
	|	НачисленнаяЗарплатаИВзносы.ФизическоеЛицо,
	|	НачисленнаяЗарплатаИВзносы.Подразделение,
	|	НачисленнаяЗарплатаИВзносы.Начисление,
	|	НачисленнаяЗарплатаИВзносы.ВидОперации КАК ВидОперации,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Начисления.СпособОтраженияВБухучете, ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВРеглУчете.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВРеглУчете.ПустаяСсылка)
	|			ТОГДА Начисления.СпособОтраженияВБухучете
	|		КОГДА ЕСТЬNULL(БухучетСотрудников.СпособОтраженияЗарплатыВБухучете, ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВРеглУчете.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВРеглУчете.ПустаяСсылка)
	|			ТОГДА БухучетСотрудников.СпособОтраженияЗарплатыВБухучете
	|		КОГДА ЕСТЬNULL(БухучетПодразделений.СпособОтраженияЗарплатыВБухучете, ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВРеглУчете.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВРеглУчете.ПустаяСсылка)
	|			ТОГДА БухучетПодразделений.СпособОтраженияЗарплатыВБухучете
	|		ИНАЧЕ ЕСТЬNULL(БухучетОрганизации.СпособОтраженияЗарплатыВБухучете, ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВРеглУчете.ОтражениеНачисленийПоУмолчанию))
	|	КОНЕЦ КАК СпособОтраженияЗарплатыВБухучете,
	|	НачисленнаяЗарплатаИВзносы.СтатьяНалоговойДекларации,
	|	НачисленнаяЗарплатаИВзносы.Налог,
	|	НачисленнаяЗарплатаИВзносы.Сумма,
	|	НачисленнаяЗарплатаИВзносы.СуммаВзносов
	|ПОМЕСТИТЬ ВТБУНачисленнаяЗарплатаИВзносы
	|ИЗ
	|	ВТНачисленнаяЗарплатаИВзносы КАК НачисленнаяЗарплатаИВзносы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления КАК Начисления
	|		ПО НачисленнаяЗарплатаИВзносы.Начисление = Начисления.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыСотрудников.СрезПоследних(
	|				&ПериодРегистрации,
	|				Сотрудник В
	|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|						ВТНачисленнаяЗарплатаИВзносы.Сотрудник
	|					ИЗ
	|						ВТНачисленнаяЗарплатаИВзносы)) КАК БухучетСотрудников
	|		ПО НачисленнаяЗарплатаИВзносы.Сотрудник = БухучетСотрудников.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыПодразделений.СрезПоследних(&ПериодРегистрации, Подразделение.Владелец = &Организация) КАК БухучетПодразделений
	|		ПО НачисленнаяЗарплатаИВзносы.Подразделение = БухучетПодразделений.Подразделение
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыОрганизаций.СрезПоследних(&ПериодРегистрации, Организация = &Организация) КАК БухучетОрганизации
	|		ПО (ИСТИНА)
	|";

	Запрос.Выполнить();

КонецПроцедуры

Процедура СоздатьВТБухучетВзносов(Организация, ПериодРегистрации, МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
//На случай если в таблице есть строка без начислений, а только с взносами
//В этом случае попробуем найти подходящую из других начислений
	|	НачисленнаяЗарплатаИВзносы.ФизическоеЛицо,
	|	НачисленнаяЗарплатаИВзносы.Сотрудник,	
	|	НачисленнаяЗарплатаИВзносы.Подразделение,
	|	НачисленнаяЗарплатаИВзносы.СпособОтраженияЗарплатыВБухучете,
	|	СУММА(НачисленнаяЗарплатаИВзносы.Сумма) КАК СУММА
	|ПОМЕСТИТЬ ВТДругиеНачисленияВДокументе
	|ИЗ
	|	ВТБУНачисленнаяЗарплатаИВзносы КАК НачисленнаяЗарплатаИВзносы
	|ГДЕ
	|   НачисленнаяЗарплатаИВзносы.Сумма <> 0
	|СГРУППИРОВАТЬ ПО
	|	НачисленнаяЗарплатаИВзносы.ФизическоеЛицо,
	|	НачисленнаяЗарплатаИВзносы.Сотрудник,	
	|	НачисленнаяЗарплатаИВзносы.Подразделение,
	|	НачисленнаяЗарплатаИВзносы.СпособОтраженияЗарплатыВБухучете
	|;	
	|	
	|///////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачисленнаяЗарплатаИВзносы.ФизическоеЛицо,
	|	НачисленнаяЗарплатаИВзносы.СпособОтраженияЗарплатыВБухучете,
	|	МАКСИМУМ(НачисленнаяЗарплатаИВзносы.Сумма) КАК Сумма
	|ПОМЕСТИТЬ ВТДругиеНачисленияВДокументеМаксимум
	|ИЗ
	|	ВТДругиеНачисленияВДокументе КАК НачисленнаяЗарплатаИВзносы
	|СГРУППИРОВАТЬ ПО
	|	НачисленнаяЗарплатаИВзносы.ФизическоеЛицо,
	|	НачисленнаяЗарплатаИВзносы.СпособОтраженияЗарплатыВБухучете
	|;	
	|	
	|///////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачисленнаяЗарплатаИВзносы.ФизическоеЛицо,
	|	НачисленнаяЗарплатаИВзносы.СпособОтраженияЗарплатыВБухучете,
	|	МАКСИМУМ(НачисленнаяЗарплатаИВзносы.Сотрудник) КАК Сотрудник,	
	|	МАКСИМУМ(НачисленнаяЗарплатаИВзносы.Подразделение) КАК Подразделение	
	|ПОМЕСТИТЬ ВТДругиеНачисленияВДокументеПодходящие
	|ИЗ
	|	ВТДругиеНачисленияВДокументе КАК НачисленнаяЗарплатаИВзносы
	|   ЛЕВОЕ СОЕДИНЕНИЕ
	|	ВТДругиеНачисленияВДокументеМаксимум КАК НачисленнаяЗарплатаИВзносыМаксимум
	|   ПО НачисленнаяЗарплатаИВзносы.ФизическоеЛицо = НачисленнаяЗарплатаИВзносыМаксимум.ФизическоеЛицо
	|    И НачисленнаяЗарплатаИВзносы.СпособОтраженияЗарплатыВБухучете = НачисленнаяЗарплатаИВзносыМаксимум.СпособОтраженияЗарплатыВБухучете
	|    И НачисленнаяЗарплатаИВзносы.Сумма = НачисленнаяЗарплатаИВзносыМаксимум.Сумма
	|СГРУППИРОВАТЬ ПО
	|	НачисленнаяЗарплатаИВзносы.ФизическоеЛицо,
	|	НачисленнаяЗарплатаИВзносы.СпособОтраженияЗарплатыВБухучете
	|;	
	|	
	|///////////////////////////////////////////////////////////////////////////	
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НачисленнаяЗарплатаИВзносы.ПериодРегистрации,
	|	ВЫБОР
	|		КОГДА НачисленнаяЗарплатаИВзносы.Сотрудник = ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(ДругиеНачисленияВДокументеПодходящие.Сотрудник, ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка))
	|		ИНАЧЕ НачисленнаяЗарплатаИВзносы.Сотрудник
	|   КОНЕЦ КАК Сотрудник,
	|	ВЫБОР
	|		КОГДА НачисленнаяЗарплатаИВзносы.Подразделение = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(ДругиеНачисленияВДокументеПодходящие.Подразделение, ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка))
	|		ИНАЧЕ НачисленнаяЗарплатаИВзносы.Подразделение
	|   КОНЕЦ КАК Подразделение,
	|	НачисленнаяЗарплатаИВзносы.ФизическоеЛицо,
	|	НачисленнаяЗарплатаИВзносы.Начисление,
	|	НачисленнаяЗарплатаИВзносы.ВидОперации,
	|	НачисленнаяЗарплатаИВзносы.СпособОтраженияЗарплатыВБухучете,
	|	НачисленнаяЗарплатаИВзносы.СтатьяНалоговойДекларации,
	|	НачисленнаяЗарплатаИВзносы.Налог,
	|	НачисленнаяЗарплатаИВзносы.Сумма,
	|	НачисленнаяЗарплатаИВзносы.СуммаВзносов,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СпособыОтражения.Ссылка, ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВРеглУчете.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВРеглУчете.ПустаяСсылка)
	|			ТОГДА НачисленнаяЗарплатаИВзносы.СпособОтраженияЗарплатыВБухучете
	|		КОГДА СпособыОтражения.СтратегияОтраженияЕСВФОТ = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеЕСВ.ОсобымСпособом)
	|			ТОГДА СпособыОтражения.СпособОтраженияЕСВФОТ
	|		КОГДА СпособыОтражения.СтратегияОтраженияЕСВФОТ = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеЕСВ.КакОсновноеНачислениеСотрудника)
	|			ТОГДА 
	|				ВЫБОР 
	|					КОГДА ЕСТЬNULL(БухучетСотрудников.СпособОтраженияЗарплатыВБухучете, ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВРеглУчете.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВРеглУчете.ПустаяСсылка)
	|						ТОГДА БухучетСотрудников.СпособОтраженияЗарплатыВБухучете
	|					КОГДА ЕСТЬNULL(БухучетПодразделений.СпособОтраженияЗарплатыВБухучете, ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВРеглУчете.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВРеглУчете.ПустаяСсылка)
	|						ТОГДА БухучетПодразделений.СпособОтраженияЗарплатыВБухучете
	|					ИНАЧЕ ЕСТЬNULL(БухучетОрганизации.СпособОтраженияЗарплатыВБухучете, ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВРеглУчете.ОтражениеНачисленийПоУмолчанию))
	|               КОНЕЦ
	|		ИНАЧЕ НачисленнаяЗарплатаИВзносы.СпособОтраженияЗарплатыВБухучете
	|	КОНЕЦ КАК СпособОтраженияЗарплатыВБухучетеВзносы
	|ПОМЕСТИТЬ ВТБУНачисленнаяЗарплатаИБУВзносы
	|ИЗ
	|	ВТБУНачисленнаяЗарплатаИВзносы КАК НачисленнаяЗарплатаИВзносы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДругиеНачисленияВДокументеПодходящие КАК ДругиеНачисленияВДокументеПодходящие
	|		ПО НачисленнаяЗарплатаИВзносы.СпособОтраженияЗарплатыВБухучете = ДругиеНачисленияВДокументеПодходящие.СпособОтраженияЗарплатыВБухучете
	|		 И НачисленнаяЗарплатаИВзносы.ФизическоеЛицо = ДругиеНачисленияВДокументеПодходящие.ФизическоеЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОтраженияЗарплатыВРеглУчете КАК СпособыОтражения
	|		ПО НачисленнаяЗарплатаИВзносы.СпособОтраженияЗарплатыВБухучете = СпособыОтражения.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления КАК Начисления
	|		ПО НачисленнаяЗарплатаИВзносы.Начисление = Начисления.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыСотрудников.СрезПоследних(
	|				&ПериодРегистрации,
	|				Сотрудник В
	|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|						ВТБУНачисленнаяЗарплатаИВзносы.Сотрудник
	|					ИЗ
	|						ВТБУНачисленнаяЗарплатаИВзносы)) КАК БухучетСотрудников
	|		ПО НачисленнаяЗарплатаИВзносы.Сотрудник = БухучетСотрудников.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыПодразделений.СрезПоследних(&ПериодРегистрации, Подразделение.Владелец = &Организация) КАК БухучетПодразделений
	|		ПО НачисленнаяЗарплатаИВзносы.Подразделение = БухучетПодразделений.Подразделение
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыОрганизаций.СрезПоследних(&ПериодРегистрации, Организация = &Организация) КАК БухучетОрганизации
	|		ПО (ИСТИНА)
	|";

	Запрос.Выполнить();

КонецПроцедуры

Процедура СоздатьВТБухучетЕСВ(Организация, ПериодРегистрации, МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	&ПериодРегистрации,
	|	ДанныеЕСВ.Организация,
	|	ДанныеЕСВ.ФизическоеЛицо,
	|	ДанныеЕСВ.ВидЕСВ,
	|	ДанныеЕСВ.СтатьяНалоговойДекларации,
	|	ДанныеЕСВ.Налог,
	|	ДанныеЕСВ.СчетУчета,
	|	ДанныеЕСВ.Сумма,
	|	ДанныеЕСВ.ВидОперации,
	|	БухучетВзносов.СпособОтраженияВРеглУчете КАК СпособОтраженияВБухУчете
	|ПОМЕСТИТЬ ВТБухучетЕСВ
	|ИЗ
	|	ВТЕСВПоСотрудникам КАК ДанныеЕСВ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтражениеВзносовВРеглУчете.СрезПоследних(&ПериодРегистрации) КАК БухучетВзносов
	|		ПО ДанныеЕСВ.Налог = БухучетВзносов.Налог
	|";

	Запрос.Выполнить();

КонецПроцедуры

Процедура СоздатьВТБухучетНДФЛ(Организация, ПериодРегистрации, МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	&ПериодРегистрации,
	|	ДанныеНДФЛ.Организация,
	|	ДанныеНДФЛ.ФизическоеЛицо,
	|	ДанныеНДФЛ.ДоходНДФЛ,
	|	ДанныеНДФЛ.СчетУчета,
	|	ДанныеНДФЛ.Сумма,
	|	ДанныеНДФЛ.ВидОперации,
	|	ДоходыНДФЛ.СпособОтраженияВРеглУчете КАК СпособОтраженияВБухУчете
	|ПОМЕСТИТЬ ВТБухучетНДФЛ
	|ИЗ
	|	ВТТаблицаНДФЛ КАК ДанныеНДФЛ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыДоходовНДФЛ КАК ДоходыНДФЛ
	|		ПО ДанныеНДФЛ.ДоходНДФЛ = ДоходыНДФЛ.Ссылка
	|";

	Запрос.Выполнить();

КонецПроцедуры
