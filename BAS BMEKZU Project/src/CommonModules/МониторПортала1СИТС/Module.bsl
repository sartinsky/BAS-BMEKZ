
#Область СлужебныйПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Общего назначения

// Определяет возможность использования Монитора
// в соответствии с текущим режимом работы информационной базы
// и правами пользователя.
//
// Возвращаемое значение:
//	Булево - Истина - возможно использование, Ложь - в противном случае.
//
Функция ДоступноИспользованиеМонитора() Экспорт
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат ПравоДоступа("Просмотр", Метаданные.Обработки.МониторПортала1СИТС);
	
КонецФункции

#Область ИнтеграцияСБиблиотекойСтандартныхПодсистем

// Добавляет необходимые параметры работы клиента при запуске.
// Добавленные параметры доступны в
// СтандартныеПодсистемыКлиент.ПараметрыРаботыКлиентаПриЗапуске().ИнтернетПоддержкаПользователей.<ИмяПараметра>;
// Используется в том случае, если подсистема реализует сценарий, выполняемый
// при начале работы системы.
// Вызывается из ИнтернетПоддержкаПользователей.ПараметрыРаботыКлиентаПриЗапуске().
//
// Параметры:
//	Параметры - Структура - заполняемые параметры;
//
Процедура ПараметрыРаботыКлиентаПриЗапуске(Параметры) Экспорт
	
	Если Не ДоступноИспользованиеМонитора() Тогда
		Возврат;
	КонецЕсли;
	
	ОбщиеПараметры = ОбщиеПараметрыМонитора();
	Если Не ОбщиеПараметры.ИспользоватьОтображениеПриНачалеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	ИнтернетПоддержкаПодключена =
		ИнтернетПоддержкаПользователей.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки();
	Если ИнтернетПоддержкаПодключена Тогда
		ЗначениеНастройкиПоказыватьПриНачалеРаботы = ЗначениеНастройкиПоказыватьПриНачалеРаботы();
		Если ЗначениеНастройкиПоказыватьПриНачалеРаботы = 2 Тогда
			// Никогда не показывать при начале работы.
			Возврат;
		КонецЕсли;
	Иначе
		// Если Интернет-поддержка не подключена.
		Если ИнтернетПоддержкаПользователей.ДоступноПодключениеИнтернетПоддержки() Тогда
			// Есть право подключения Интернет-поддержки - при начале работы
			// будет предложено подключить Интернет-поддержку.
			ЗначениеНастройкиПоказыватьПриНачалеРаботы = ЗначениеНастройкиПоказыватьПриНачалеРаботы();
			Если ЗначениеНастройкиПоказыватьПриНачалеРаботы = 2 Тогда
				// Никогда не показывать при начале работы.
				Возврат;
			КонецЕсли;
		Иначе
			// Нет права подключения Интернет-поддержки - не обрабатывать
			// событие начала работы с программой.
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыМонитора = Новый Структура;
	ПараметрыМонитора.Вставить("ПоказыватьПриНачалеРаботы"  , Истина);
	ПараметрыМонитора.Вставить("ИнтернетПоддержкаПодключена", ИнтернетПоддержкаПодключена);
	ПараметрыМонитора.Вставить("ПриНаличииНовойИнформации", (ЗначениеНастройкиПоказыватьПриНачалеРаботы = 1));
	Параметры.Вставить("МониторПортала1СИТС", ПараметрыМонитора);
	
КонецПроцедуры

// Интеграция с подсистемой СтандартныеПодсистемы.БазоваяФункциональность.
//
Процедура ПриЗаполненииРазрешенийНаДоступКВнешнимРесурсам(ЗапросыРазрешений) Экспорт
	
	НовыеРазрешения = Новый Массив;
	
	Разрешение = РаботаВБезопасномРежиме.РазрешениеНаИспользованиеИнтернетРесурса(
		"HTTPS",
		ХостСервиса(0),
		443,
		НСтр("ru='Сервис Монитор Портала ИТС (зона ru)';uk='Сервіс Монітор Порталу ІТС (зона ru)'"));
	НовыеРазрешения.Добавить(Разрешение);
	
	Разрешение = РаботаВБезопасномРежиме.РазрешениеНаИспользованиеИнтернетРесурса(
		"HTTPS",
		ХостСервиса(1),
		443,
		НСтр("ru='Сервис Монитор Портала ИТС (зона eu)';uk='Сервіс Монітор Порталу ІТС (зона eu)'"));
	НовыеРазрешения.Добавить(Разрешение);
	
	ЗапросыРазрешений.Добавить(РаботаВБезопасномРежиме.ЗапросНаИспользованиеВнешнихРесурсов(НовыеРазрешения));
	
КонецПроцедуры

// См. процедуру
// ОбщегоНазначенияПереопределяемый.ПриДобавленииПереименованийОбъектовМетаданных().
//
Процедура ПриДобавленииПереименованийОбъектовМетаданных(Итог) Экспорт
	
	ОбщегоНазначения.ДобавитьПереименование(
		Итог,
		"2.2.5.1",
		"Подсистема.ИнтернетПоддержкаПользователей.Подсистема.МониторИнтернетПоддержки",
		"Подсистема.ИнтернетПоддержкаПользователей.Подсистема.МониторПортала1СИТС",
		"ИнтернетПоддержкаПользователей");
	
	ОбщегоНазначения.ДобавитьПереименование(
		Итог,
		"2.2.5.1",
		"Роль.ИспользованиеМонитораИПП",
		"Роль.ИспользованиеМонитораПортала1СИТС",
		"ИнтернетПоддержкаПользователей");
	
КонецПроцедуры

// Вызывается из обработчика ПриСозданииНаСервере() панели администрирования
// БСП, выполняется настройку отображения элементов управления для подсистем
// библиотеки ИПП.
//
// Параметры:
//	Форма - УправляемаяФорма - форма панели управления.
//
Процедура ИнтернетПоддержкаИСервисы_ПриСозданииНаСервере(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	Элементы.БИПМониторИнтернетПоддержки.Видимость = ДоступноИспользованиеМонитора();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОбщиеПараметрыМонитора() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ИспользоватьОтображениеПриНачалеРаботы", Истина);
	МониторПортала1СИТСПереопределяемый.ПриОпределенииОбщихПараметровМонитора(Результат);
	
	Возврат Результат;
	
КонецФункции

Функция ЗначениеНастройкиПоказыватьПриНачалеРаботы() Экспорт
	
	НастройкаЗапуска = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"ИнтернетПоддержкаПользователей",
		"ВсегдаПоказыватьПриСтартеПрограммы");
	
	Если НастройкаЗапуска = Неопределено Тогда
		Возврат 0; // Всегда
	ИначеЕсли НастройкаЗапуска = Истина Тогда
		ПоказыватьПриОбновлении = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ИнтернетПоддержкаПользователей",
			"ПоказПриСтартеТолькоПриИзменении");
		Если ПоказыватьПриОбновлении = Истина Тогда
			Возврат 1; // Только при наличии новой информации.
		Иначе
			Возврат 0; // Всегда
		КонецЕсли;
	Иначе
		Возврат 2; // Никогда
	КонецЕсли;
	
	Возврат 0;
	
КонецФункции

Функция ОтпечатокДанныхМонитора(Логин, ДанныеМонитора)
	
	Если Логин = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеСтрока =
		Логин
		+ ", "
		+ ЗначениеЗаполнено(ДанныеМонитора.programName) + ", "
		+ ДанныеМонитора.supportConditionsImplStatus.status + ", "
		+ ДанныеМонитора.serviceContractsStatus.status + ", "
		+ ДанныеМонитора.updateInfo.updateAvailable;
	
	Хеширование = Новый ХешированиеДанных(ХешФункция.SHA256);
	Хеширование.Добавить(ДанныеСтрока);
	Возврат Base64Строка(Хеширование.ХешСумма);
	
КонецФункции

Процедура СохранитьОтпечатокДанныхМонитора(Логин, ДанныеМонитора) Экспорт
	
	Если ДанныеМонитора <> Неопределено
		И ДанныеМонитора.authResult.authenticated Тогда
		Отпечаток = ОтпечатокДанныхМонитора(Логин, ДанныеМонитора);
	Иначе
		Отпечаток = Неопределено;
	КонецЕсли;
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		"МониторПортала1СИТС",
		"ОтпечатокДанныхМонитора",
		Лев(Отпечаток, 48));
	
КонецПроцедуры

Функция ДанныеМонитораИзменены(Логин, ДанныеМонитора)
	
	Отпечаток = ОтпечатокДанныхМонитора(Логин, ДанныеМонитора);
	
	Если Отпечаток = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СохраненныйОтпечаток = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"МониторПортала1СИТС",
		"ОтпечатокДанныхМонитора");
	
	Возврат (СохраненныйОтпечаток <> Отпечаток);
	
КонецФункции

Процедура ПроверитьНаличиеНовойИнформацииВФоне(ПараметрыМетода, АдресРезультата) Экспорт
	
	РезультатОперации = ДанныеМонитора();
	Если ПустаяСтрока(РезультатОперации.ИмяОшибки) Тогда
		
		ДанныеМонитора = РезультатОперации.Данные;
		Если ДанныеМонитора.authResult.authenticated Тогда
			
			Если ДанныеМонитора.programName = Неопределено Тогда
				ПоместитьВоВременноеХранилище("НетИзменений", АдресРезультата);
				ЗаписатьИнформациюВЖурналРегистрации(
					НСтр("ru='Интернет-поддержка продукта не оказывается.
                        |Не удалось проверить наличие новой информации в сервисе Монитора Портала ИТС.'
                        |;uk='Інтернет-підтримка продукту не надається.
                        |Не вдалося перевірити наявність нової інформації в сервісі Монітора Порталу ІТС.'"));
				Возврат;
			КонецЕсли;
			
			СообщениеОбОшибкеДанных = ОписаниеОшибкиПолученияДанныхМонитора(ДанныеМонитора);
			Если Не ПустаяСтрока(СообщениеОбОшибкеДанных) Тогда
				
				ЗаписатьОшибкуВЖурналРегистрации(
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru='Не удалось проверить изменение информации Монитора Портала ИТС из-за ошибки получения данных:
                            |%1'
                            |;uk='Не вдалося перевірити зміну інформації Монітора Порталу ІТС через помилки одержання даних:
                            |%1'"),
						СообщениеОбОшибкеДанных));
				ПоместитьВоВременноеХранилище("Ошибка", АдресРезультата);
				
			Иначе
				
				Если ДанныеМонитораИзменены(РезультатОперации.Логин, РезультатОперации.Данные) Тогда
					ПоместитьВоВременноеХранилище("ДанныеИзменены", АдресРезультата);
				Иначе
					ПоместитьВоВременноеХранилище("НетИзменений", АдресРезультата);
				КонецЕсли;
				
			КонецЕсли;
			
		Иначе
			
			ЗаписатьОшибкуВЖурналРегистрации(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Не удалось проверить изменение информации Монитора Портала ИТС.
                        |Заполнены некорректные данные аутентификации.
                        |%1'
                        |;uk='Не вдалося перевірити зміну інформації Монітора Порталу ІТС.
                        |Заповнені некоректні дані аутентифікації.
                        |%1'"),
					РезультатОперации.ИнформацияОбОшибке));
			ПоместитьВоВременноеХранилище("Ошибка", АдресРезультата);
			
		КонецЕсли;
		
	ИначеЕсли РезультатОперации.ИмяОшибки = "НеЗаполненыДанныеАутентификации" Тогда
		
		ПоместитьВоВременноеХранилище("НетИзменений", АдресРезультата);
		
	Иначе
		
		ЗаписатьОшибкуВЖурналРегистрации(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Не удалось проверить изменение информации Монитора Портала ИТС.
                    |%1'
                    |;uk='Не вдалося перевірити зміну інформації Монітора Порталу ІТС.
                    |%1'"),
				РезультатОперации.ИнформацияОбОшибке));
		ПоместитьВоВременноеХранилище("Ошибка", АдресРезультата);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ОписаниеОшибкиПолученияДанныхМонитора(ДанныеМонитора) Экспорт
	
	Результат = "";
	
	Если ДанныеМонитора.supportConditionsImplStatus <> Неопределено
		И ДанныеМонитора.supportConditionsImplStatus.blockStatus <> "200"
		И ДанныеМонитора.supportConditionsImplStatus.blockStatus <> 200 Тогда
		Результат = Результат + ?(ПустаяСтрока(Результат), "", Символы.ПС)
			+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='- состояние выполнения условий сопровождения (blockStatus: %1);';uk='- стан виконання умов супроводу (blockStatus: %1);'"),
				ДанныеМонитора.supportConditionsImplStatus.blockStatus);
	КонецЕсли;
	
	Если ДанныеМонитора.serviceContractsStatus <> Неопределено
		И ДанныеМонитора.serviceContractsStatus.blockStatus <> "200"
		И ДанныеМонитора.serviceContractsStatus.blockStatus <> 200 Тогда
		Результат = Результат + ?(ПустаяСтрока(Результат), "", Символы.ПС)
			+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='- подключение сервисов 1С (blockStatus: %1);';uk='- подключение сервисов 1С (blockStatus: %1);'"),
				ДанныеМонитора.serviceContractsStatus.blockStatus);
	КонецЕсли;
	
	Если ДанныеМонитора.updateInfo <> Неопределено
		И ДанныеМонитора.updateInfo.blockStatus <> "200"
		И ДанныеМонитора.updateInfo.blockStatus <> 200 Тогда
		Результат = Результат + ?(ПустаяСтрока(Результат), "", Символы.ПС)
			+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='- наличие обновления программы (blockStatus: %1);';uk='- наявність оновлення програми (blockStatus: %1);'"),
				ДанныеМонитора.updateInfo.blockStatus);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаписатьОшибкуВЖурналРегистрации(СообщениеОбОшибке) Экспорт
	
	ЗаписьЖурналаРегистрации(
		ИмяСобытияЖурналаРегистрации(),
		УровеньЖурналаРегистрации.Ошибка,
		,
		,
		СообщениеОбОшибке);
	
КонецПроцедуры

Процедура ЗаписатьПредупреждениеВЖурналРегистрации(СообщениеОбОшибке) Экспорт
	
	ЗаписьЖурналаРегистрации(
		ИмяСобытияЖурналаРегистрации(),
		УровеньЖурналаРегистрации.Предупреждение,
		,
		,
		СообщениеОбОшибке);
	
КонецПроцедуры

Процедура ЗаписатьИнформациюВЖурналРегистрации(СообщениеЖурнала) Экспорт
	
	ЗаписьЖурналаРегистрации(
		ИмяСобытияЖурналаРегистрации(),
		УровеньЖурналаРегистрации.Информация,
		,
		,
		СообщениеЖурнала);
	
КонецПроцедуры

Функция ИмяСобытияЖурналаРегистрации()
	
	Возврат НСтр("ru='Монитор Портала ИТС';uk='Монітор Порталу ІТС'",ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	
КонецФункции

#Область ВызовОперацийСервиса

Функция ДанныеМонитора() Экспорт
	
	Результат = НовыйРезультатВызоваОперации();
	
	Если Не ДоступноИспользованиеМонитора() Тогда
		ВызватьИсключение НСтр("ru='Использование Монитора недоступно в текущем режиме работы.';uk='Використання Монітору недоступне в поточному режимі роботи.'");
	КонецЕсли;
	
	НастройкиСоединения = ИнтернетПоддержкаПользователей.НастройкиСоединенияССерверами();
	Результат.Вставить("Логин", Неопределено);
	Результат.Вставить("Домен", НастройкиСоединения.ДоменРасположенияСерверовИПП);
	
	ИмяПрограммы                  = Неопределено;
	ДанныеАутентификации          = Неопределено;
	ПередВызовомОперацииСервиса(ИмяПрограммы, ДанныеАутентификации, Результат);
	Если Не ПустаяСтрока(Результат.ИмяОшибки) Тогда
		Возврат Результат;
	КонецЕсли;
	
	ВерсияКонфигурации = ИнтернетПоддержкаПользователей.ВерсияКонфигурации();
	ВерсияПлатформы    = ИнтернетПоддержкаПользователейКлиентСервер.ТекущаяВерсияПлатформы1СПредприятие();
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ПолучениеОбновленийПрограммы") Тогда
		ПроверятьОбновления = Истина;
	Иначе
		ПроверятьОбновления = Ложь;
	КонецЕсли;
	
	ВызватьОперациюДанныеМонитора(
		ДанныеАутентификации,
		НастройкиСоединения.ДоменРасположенияСерверовИПП,
		ИмяПрограммы,
		ВерсияКонфигурации,
		ВерсияПлатформы,
		ПроверятьОбновления,
		Результат);
	
	Если Не ПустаяСтрока(Результат.ИмяОшибки) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если Результат.Данные = Неопределено Тогда
		Результат.ИмяОшибки          = "Ошибка";
		Результат.СообщениеОбОшибке  = НСтр("ru='Некорректный (пустой) ответ сервиса.';uk='Некоректна (порожня) відповідь сервісу.'");
		Результат.ИнформацияОбОшибке = Результат.СообщениеОбОшибке;
		Возврат Результат;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ДеталиДанныхМонитора(
	Логин,
	Договоры1СИТС,
	АктивацияСервисовСопровождения,
	ДоговорыНаСервисы) Экспорт
	
	Результат = НовыйРезультатВызоваОперации();
	
	Если Не ДоступноИспользованиеМонитора() Тогда
		ВызватьИсключение НСтр("ru='Использование Монитора недоступно в текущем режиме работы.';uk='Використання Монітору недоступне в поточному режимі роботи.'");
	КонецЕсли;
	
	ИмяПрограммы         = Неопределено;
	ДанныеАутентификации = Неопределено;
	ПередВызовомОперацииСервиса(ИмяПрограммы, ДанныеАутентификации, Результат);
	Если Не ПустаяСтрока(Результат.ИмяОшибки) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если НРег(СокрЛП(Логин)) <> НРег(СокрЛП(ДанныеАутентификации.Логин)) Тогда
		Результат.ИмяОшибки          = "ИзмененыДанныеАутентификации";
		Результат.СообщениеОбОшибке  = НСтр("ru='Изменились данные аутентификации пользователя Интернет-поддержки.
            |Обновите содержимое Монитора.'
            |;uk='Змінилися дані автентифікації користувача Інтернет-підтримки.
            |Оновіть вміст Монітора.'");
		Результат.ИнформацияОбОшибке = Результат.СообщениеОбОшибке;
		Возврат Результат;
	КонецЕсли;
	
	ВызватьОперациюДеталиДанныхМонитора(
		ДанныеАутентификации,
		ИмяПрограммы,
		Договоры1СИТС,
		АктивацияСервисовСопровождения,
		ДоговорыНаСервисы,
		Результат);
	
	Если Не ПустаяСтрока(Результат.ИмяОшибки) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если Результат.Данные = Неопределено Тогда
		Результат.ИмяОшибки          = "Ошибка";
		Результат.СообщениеОбОшибке  = НСтр("ru='Некорректный (пустой) ответ сервиса.';uk='Некоректна (порожня) відповідь сервісу.'");
		Результат.ИнформацияОбОшибке = Результат.СообщениеОбОшибке;
		Возврат Результат;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ПередВызовомОперацииСервиса(ИмяПрограммы, ДанныеАутентификации, РезультатОперации)
	
	ИмяПрограммы = ИнтернетПоддержкаПользователей.СлужебнаяИмяПрограммы();
	Если ИмяПрограммы = "Unknown" Тогда
		РезультатОперации.ИмяОшибки = "НеЗаполненоИмяПрограммы";
		РезультатОперации.СообщениеОбОшибке  = НСтр("ru='Не заполнено имя программы.';uk='Не заповнено ім''я програми.'");
		РезультатОперации.ИнформацияОбОшибке =
			НСтр("ru='Не заполнено имя программы в методе ИнтернетПоддержкаПользователейПереопределяемый.ПриОпределенииИмениПрограммы().';uk='Не заповнено ім''я програми в методі ИнтернетПоддержкаПользователейПереопределяемый.ПриОпределенииИмениПрограммы().'");
	КонецЕсли;
	
	Если ДанныеАутентификации = Неопределено Тогда
		УстановитьПривилегированныйРежим(Истина);
		ДанныеАутентификации = ИнтернетПоддержкаПользователей.ДанныеАутентификацииПользователяИнтернетПоддержки();
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Если ДанныеАутентификации = Неопределено Тогда
		РезультатОперации.ИмяОшибки = "НеЗаполненыДанныеАутентификации";
		РезультатОперации.СообщениеОбОшибке = НСтр("ru='Не заполнены данные аутентификации.';uk='Не заповнені дані аутентифікації.'");
		РезультатОперации.ИнформацияОбОшибке = РезультатОперации.СообщениеОбОшибке;
		Возврат;
	ИначеЕсли РезультатОперации.Свойство("Логин") Тогда
		РезультатОперации.Логин = ДанныеАутентификации.Логин;
	КонецЕсли;
	
КонецПроцедуры

Процедура ВызватьОперациюДанныеМонитора(
	ДанныеАутентификации,
	Домен,
	ИмяПрограммы,
	ВерсияКонфигурации,
	ВерсияПлатформы,
	ПолучитьИнформациюОбОбновлении,
	Результат)
	
	URLОперации = URLОперацииСервиса("monitor", Домен) + "/" + ИмяПрограммы;
	ТелоЗапроса = НовыйТелоЗапросаСодержимоеМонитора(
		ДанныеАутентификации,
		ВерсияКонфигурации,
		ВерсияПлатформы,
		ПолучитьИнформациюОбОбновлении);
	
	ТелоОтвета = "";
	ВызватьОперациюСервиса(URLОперации, Домен, ТелоЗапроса, Результат, ТелоОтвета);
	
	Если Не ПустаяСтрока(Результат.ИмяОшибки) Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(ТелоОтвета);
		Результат.Данные = ПрочитатьJSON(ЧтениеJSON);
		ЧтениеJSON.Закрыть();
		
		Если Результат.Данные.authResult.blockStatus = "429" Или Результат.Данные.authResult.blockStatus = 429 Тогда
			Результат.ИмяОшибки         = "Ошибка";
			Результат.СообщениеОбОшибке = НСтр("ru='Превышено количество попыток аутентификации с некорректным логином или паролем.
                |Проверьте правильность логина и пароля и повторите попытку через 30 минут.'
                |;uk='Перевищено кількість спроб аутентифікації з некоректним логіном або паролем.
                |Перевірте правильність логіна і пароля та повторіть спробу через 30 хвилин.'");
			Результат.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Ошибка при вызове операции %1 сервиса Монитора Портала ИТС.
                    |Сервис сообщил об ошибке - authResult.blockStatus: %2'
                    |;uk='Помилка підчас виклику операції %1 сервісу Монітора Порталу ІТС.
                    |Сервіс повідомив про помилку - authResult.blockStatus: %2'"),
				URLОперации,
				Результат.Данные.authResult.blockStatus);
		ИначеЕсли (Результат.Данные.authResult.blockStatus <> "200" И Результат.Данные.authResult.blockStatus <> 200) Тогда
			Результат.ИмяОшибки         = "ВнутренняяОшибкаСервиса";
			Результат.СообщениеОбОшибке = НСтр("ru='Внутренняя ошибка сервиса.
                |Повторите попытку подключения позднее.'
                |;uk='Внутрішня помилка сервісу.
                |Повторіть спробу підключення пізніше.'");
			Результат.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Ошибка при вызове операции %1 сервиса Монитора Портала ИТС.
                    |Сервис сообщил об ошибке - authResult.blockStatus: %2'
                    |;uk='Помилка підчас виклику операції %1 сервісу Монітора Порталу ІТС.
                    |Сервіс повідомив про помилку - authResult.blockStatus: %2'"),
				URLОперации,
				Результат.Данные.authResult.blockStatus);
		КонецЕсли;
		
	Исключение
		
		ПриОшибкеОбработкиОтветаСервиса(ИнформацияОбОшибке(), URLОперации, ТелоОтвета, Результат);
		
	КонецПопытки;
	
КонецПроцедуры

Процедура ВызватьОперациюДеталиДанныхМонитора(
	ДанныеАутентификации,
	ИмяПрограммы,
	Договоры1СИТС,
	АктивацияСервисовСопровождения,
	ДоговорыНаСервисы,
	Результат)
	
	НастройкиСоединения = ИнтернетПоддержкаПользователей.НастройкиСоединенияССерверами();
	URLОперации = URLОперацииСервиса("support-info", НастройкиСоединения.ДоменРасположенияСерверовИПП)
		+ "/" + ИмяПрограммы;
	ТелоЗапроса = НовыйТелоЗапросаДеталиМонитора(
		ДанныеАутентификации,
		Договоры1СИТС,
		АктивацияСервисовСопровождения,
		ДоговорыНаСервисы);
	
	ТелоОтвета = "";
	
	ВызватьОперациюСервиса(URLОперации, НастройкиСоединения.ДоменРасположенияСерверовИПП, ТелоЗапроса, Результат, ТелоОтвета);
	
	Если Не ПустаяСтрока(Результат.ИмяОшибки) Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(ТелоОтвета);
		
		ИменаСвойствСоЗначениямиДата = Новый Массив;
		ИменаСвойствСоЗначениямиДата.Добавить("from");
		ИменаСвойствСоЗначениямиДата.Добавить("to");
		Результат.Данные = ПрочитатьJSON(ЧтениеJSON, , ИменаСвойствСоЗначениямиДата, ФорматДатыJSON.ISO);
		ЧтениеJSON.Закрыть();
		
		// Проверка состояния получения данных в сервисе.
		ОшибкаСервисаПриПолученииДанных = Ложь;
		Если (Договоры1СИТС Или АктивацияСервисовСопровождения)
			И Результат.Данные.supportConditionsImplStatus.blockStatus <> "200"
			И Результат.Данные.supportConditionsImplStatus.blockStatus <> 200 Тогда
			
			ОшибкаСервисаПриПолученииДанных = Истина;
			Результат.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Ошибка при вызове операции %1 сервиса Монитора Портала ИТС.
                    |Сервис сообщил об ошибке - supportConditionsImplStatus.blockStatus: %2'
                    |;uk='Помилка підчас виклику операції %1 сервісу Монітора Порталу ІТС.
                    |Сервіс повідомив про помилку - supportConditionsImplStatus.blockStatus: %2'"),
				URLОперации,
				Результат.Данные.supportConditionsImplStatus.blockStatus);
			
		ИначеЕсли Договоры1СИТС
			И Результат.Данные.itsContracts <> Неопределено
			И Результат.Данные.itsContracts.blockStatus <> "200"
			И Результат.Данные.itsContracts.blockStatus <> 200 Тогда
			
			ОшибкаСервисаПриПолученииДанных = Истина;
			Результат.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Ошибка при вызове операции %1 сервиса Монитора Портала ИТС.
                    |Сервис сообщил об ошибке - itsContracts.blockStatus: %2'
                    |;uk='Помилка підчас виклику операції %1 сервісу Монітора Порталу ІТС.
                    |Сервіс повідомив про помилку - itsContracts.blockStatus: %2'"),
				URLОперации,
				Результат.Данные.itsContracts.blockStatus);
			
		ИначеЕсли АктивацияСервисовСопровождения
			И Результат.Данные.supportServiceActivations <> Неопределено
			И Результат.Данные.supportServiceActivations.blockStatus <> "200"
			И Результат.Данные.supportServiceActivations.blockStatus <> 200 Тогда
			
			ОшибкаСервисаПриПолученииДанных = Истина;
			Результат.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Ошибка при вызове операции %1 сервиса Монитора Портала ИТС.
                    |Сервис сообщил об ошибке - supportServiceActivations.blockStatus: %2'
                    |;uk='Помилка підчас виклику операції %1 сервісу Монітора Порталу ІТС.
                    |Сервіс повідомив про помилку - supportServiceActivations.blockStatus: %2'"),
				URLОперации,
				Результат.Данные.supportServiceActivations.blockStatus);
			
		ИначеЕсли ДоговорыНаСервисы
			И Результат.Данные.serviceContractsStatus.blockStatus <> "200"
			И Результат.Данные.serviceContractsStatus.blockStatus <> 200 Тогда
			
			ОшибкаСервисаПриПолученииДанных = Истина;
			Результат.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Ошибка при вызове операции %1 сервиса Монитора Портала ИТС.
                    |Сервис сообщил об ошибке - serviceContractsStatus.blockStatus: %2'
                    |;uk='Помилка підчас виклику операції %1 сервісу Монітора Порталу ІТС.
                    |Сервіс повідомив про помилку - serviceContractsStatus.blockStatus: %2'"),
				URLОперации,
				Результат.Данные.serviceContractsStatus.blockStatus);
			
		ИначеЕсли ДоговорыНаСервисы
			И Результат.Данные.serviceContracts <> Неопределено
			И Результат.Данные.serviceContracts.blockStatus <> "200"
			И Результат.Данные.serviceContracts.blockStatus <> 200 Тогда
			
			ОшибкаСервисаПриПолученииДанных = Истина;
			Результат.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Ошибка при вызове операции %1 сервиса Монитора Портала ИТС.
                    |Сервис сообщил об ошибке - serviceContracts.blockStatus: %2'
                    |;uk='Помилка підчас виклику операції %1 сервісу Монітора Порталу ІТС.
                    |Сервіс повідомив про помилку - serviceContracts.blockStatus: %2'"),
				URLОперации,
				Результат.Данные.serviceContracts.blockStatus);
			
		КонецЕсли;
		
		Если ОшибкаСервисаПриПолученииДанных Тогда
			Результат.ИмяОшибки = "ВнутренняяОшибкаСервиса";
			Результат.СообщениеОбОшибке = НСтр("ru='Внутренняя ошибка сервиса.
                |Повторите попытку подключения позднее.'
                |;uk='Внутрішня помилка сервісу.
                |Повторіть спробу підключення пізніше.'");
		КонецЕсли;
		
	Исключение
		
		ПриОшибкеОбработкиОтветаСервиса(ИнформацияОбОшибке(), URLОперации, ТелоОтвета, Результат);
		
	КонецПопытки;
	
КонецПроцедуры

Функция НовыйТелоЗапросаСодержимоеМонитора(
	ДанныеАутентификации,
	ВерсияКонфигурации,
	ВерсияПлатформы,
	ПолучитьИнформациюОбОбновлении)
	
	ЗаписьДанныхСообщения = Новый ЗаписьJSON;
	ЗаписьДанныхСообщения.УстановитьСтроку();
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	ЗаписатьДанныеАутентификации(ЗаписьДанныхСообщения, ДанныеАутентификации);
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("programVersion");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ВерсияКонфигурации);
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("platformVersion");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ВерсияПлатформы);
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("getUpdateAvailabilityStatus");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ПолучитьИнформациюОбОбновлении);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("params");
	ЗаписатьДополнительныеПараметрыЗапроса(ЗаписьДанныхСообщения);
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	Возврат ЗаписьДанныхСообщения.Закрыть();
	
КонецФункции

Функция НовыйТелоЗапросаДеталиМонитора(
	ДанныеАутентификации,
	Договоры1СИТС,
	АктивацияСервисовСопровождения,
	ДоговорыНаСервисы)
	
	ЗаписьДанныхСообщения = Новый ЗаписьJSON;
	ЗаписьДанныхСообщения.УстановитьСтроку();
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	ЗаписатьДанныеАутентификации(ЗаписьДанныхСообщения, ДанныеАутентификации);
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("getItsContracts");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(Договоры1СИТС);
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("getSupportServiceActivations");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(АктивацияСервисовСопровождения);
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("getServiceContracts");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ДоговорыНаСервисы);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("params");
	ЗаписатьДополнительныеПараметрыЗапроса(ЗаписьДанныхСообщения);
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	Возврат ЗаписьДанныхСообщения.Закрыть();
	
КонецФункции

Процедура ЗаписатьДанныеАутентификации(ЗаписьДанныхСообщения, ДанныеАутентификации)
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("auth");
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("login");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ДанныеАутентификации.Логин);
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("password");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ДанныеАутентификации.Пароль);
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
КонецПроцедуры

Процедура ЗаписатьДополнительныеПараметрыЗапроса(ЗаписьПараметровЗапроса)
	
	ДопПараметрыЗапроса = ИнтернетПоддержкаПользователей.ДополнительныеПараметрыВызоваОперацииСервиса(Ложь);
	
	ЗаписьПараметровЗапроса.ЗаписатьНачалоОбъекта();
	
	ТипСтрока = Тип("Строка");
	ТипЧисло  = Тип("Число");
	ТипБулево = Тип("Булево");
	ТипДата   = Тип("Дата");
	Для Каждого КлючЗначение Из ДопПараметрыЗапроса Цикл
		ЗаписьПараметровЗапроса.ЗаписатьИмяСвойства(КлючЗначение.Ключ);
		Если КлючЗначение.Значение = Неопределено Тогда
			ЗаписьПараметровЗапроса.ЗаписатьЗначение(Неопределено);
		Иначе
			ТипЗнчПараметра = ТипЗнч(КлючЗначение.Значение);
			Если ТипЗнчПараметра = ТипСтрока
				Или ТипЗнчПараметра = ТипЧисло
				Или ТипЗнчПараметра = ТипБулево Тогда
				ЗаписьПараметровЗапроса.ЗаписатьЗначение(КлючЗначение.Значение);
			ИначеЕсли ТипЗнчПараметра = ТипДата Тогда
				ЗаписьПараметровЗапроса.ЗаписатьЗначение(ЗаписатьДатуJSON(КлючЗначение.Значение, ФорматДатыJSON.ISO));
			Иначе
				ЗаписьПараметровЗапроса.ЗаписатьЗначение(Строка(КлючЗначение.Значение));
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ЗаписьПараметровЗапроса.ЗаписатьКонецОбъекта();
	
КонецПроцедуры

Процедура ВызватьОперациюСервиса(URLОперации, Домен, ТелоЗапроса, Результат, ТелоОтвета)
	
	НастройкиСоединения = ИнтернетПоддержкаПользователей.НастройкиСоединенияССерверами();
	ПроверитьДоступностьСервиса(Домен, Результат);
	Если Не ПустаяСтрока(Результат.ИмяОшибки) Тогда
		Возврат;
	КонецЕсли;
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json");
	ПараметрыПолученияСодержимого = Новый Структура;
	ПараметрыПолученияСодержимого.Вставить("Метод"                   , "POST");
	ПараметрыПолученияСодержимого.Вставить("ФорматОтвета"            , 1);
	ПараметрыПолученияСодержимого.Вставить("Заголовки"               , Заголовки);
	ПараметрыПолученияСодержимого.Вставить("ДанныеДляОбработки"      , ТелоЗапроса);
	ПараметрыПолученияСодержимого.Вставить("ФорматДанныхДляОбработки", 1);
	ПараметрыПолученияСодержимого.Вставить("Таймаут"                 , 60);
	
	РезультатЗагрузкиСодержимого = ИнтернетПоддержкаПользователейКлиентСервер.ЗагрузитьСодержимоеИзИнтернет(
		URLОперации,
		,
		,
		ПараметрыПолученияСодержимого);
	
	Результат.КодСостояния = РезультатЗагрузкиСодержимого.КодСостояния;
	
	Если ПустаяСтрока(РезультатЗагрузкиСодержимого.КодОшибки) Тогда
		
		ТелоОтвета = РезультатЗагрузкиСодержимого.Содержимое;
		
		ДлинаТелаОтвета = СтрДлина(ТелоОтвета);
		Если ДлинаТелаОтвета > 100000 Тогда
			
			Результат.ИмяОшибки         = "ВнутренняяОшибкаСервиса";
			Результат.СообщениеОбОшибке = НСтр("ru='Некорректный ответ сервиса.';uk='Некоректний відповідь сервісу.'");
			Результат.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Превышена допустимая длина тела ответа 100000 символов.
                    |Длина тела ответа: %1 символов.'
                    |;uk='Перевищена допустима довжина тіла відповіді 100000 символів.
                    |Довжина тіла відповіді: %1 символів.'"),
				ДлинаТелаОтвета);
			
		КонецЕсли;
		
	Иначе
		
		ПриОшибкеВызоваОперацииСервиса(РезультатЗагрузкиСодержимого, URLОперации, Результат);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОшибкеОбработкиОтветаСервиса(ИнформацияОбОшибке, URLОперации, ТелоОтвета, Результат)
	
	Результат.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='Ошибка при вызове операции %1 сервиса Монитора Портала ИТС.
            |Ошибка при обработке ответа сервиса. %2
            |Тело ответа: %3'
            |;uk='Помилка підчас виклику операції %1 сервісу Монітора Порталу ІТС.
            |Помилка при обробці відповіді сервісу. %2
            |Тіло відповіді: %3'"),
		URLОперации,
		ПодробноеПредставлениеОшибки(ИнформацияОбОшибке),
		Лев(ТелоОтвета, 5120));
	
	Результат.ИмяОшибки         = "ВнутренняяОшибкаСервиса";
	Результат.СообщениеОбОшибке = НСтр("ru='Некорректный ответ сервиса.';uk='Некоректний відповідь сервісу.'");
	
КонецПроцедуры

Процедура ПриОшибкеВызоваОперацииСервиса(РезультатЗагрузкиСодержимого, URLОперации, Результат)
	
	ШапкаИнформацииОбОшибке =
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Ошибка при вызове операции %1 сервиса Монитора Портала ИТС.';uk='Помилка підчас виклику операції %1 сервісу Монітора Порталу ІТС.'"),
			URLОперации);
	
	Если РезультатЗагрузкиСодержимого.КодОшибки = "ConnectError" Тогда
		
		Результат.ИмяОшибки = "ОшибкаСоединения";
		Результат.СообщениеОбОшибке =
			НСтр("ru='Ошибка соединения с сервисом.';uk='Помилка з''єднання з сервісом.'")
			+ Символы.ПС
			+ РезультатЗагрузкиСодержимого.СообщениеОбОшибке;
		
	ИначеЕсли  РезультатЗагрузкиСодержимого.КодОшибки = "ConnectError" Тогда
		
		Результат.ИмяОшибки = "ВнутренняяОшибкаСервиса";
		Результат.СообщениеОбОшибке = НСтр("ru='В сервисе возникли неполадки.';uk='У сервісі виникли неполадки.'")
			+ Символы.ПС
			+ РезультатЗагрузкиСодержимого.СообщениеОбОшибке;
		
	ИначеЕсли РезультатЗагрузкиСодержимого.КодСостояния = 401
		Или РезультатЗагрузкиСодержимого.КодСостояния = 403 Тогда
		
		Результат.ИмяОшибки = "НекорректныеДанныеАутентификации";
		Если ПустаяСтрока(Результат.СообщениеОбОшибке) Тогда
			Результат.СообщениеОбОшибке = НСтр("ru='Неверный логин или пароль пользователя Интернет-поддержки.';uk='Невірний логін або пароль користувача Інтернет-підтримки.'");
		КонецЕсли;
		
	ИначеЕсли РезультатЗагрузкиСодержимого.КодСостояния = 429 Тогда
		
		Результат.ИмяОшибки = "Ошибка";
		Если ПустаяСтрока(Результат.СообщениеОбОшибке) Тогда
			Результат.СообщениеОбОшибке = НСтр("ru='Превышено количество попыток аутентификации с некорректным логином или паролем.
            |Проверьте правильность логина и пароля и повторите попытку через 30 минут.'
            |;uk='Перевищено кількість спроб аутентифікації з некоректним логіном або паролем.
            |Перевірте правильність логіна і пароля та повторіть спробу через 30 хвилин.'");
		КонецЕсли;
		
	Иначе
		
		Результат.ИмяОшибки = "Ошибка";
		Результат.СообщениеОбОшибке = РезультатЗагрузкиСодержимого.СообщениеОбОшибке;
		
	КонецЕсли;
	
	Результат.ИнформацияОбОшибке = ШапкаИнформацииОбОшибке
		+ Символы.ПС + Результат.СообщениеОбОшибке
		+ Символы.ПС + РезультатЗагрузкиСодержимого.ИнформацияОбОшибке;
	
КонецПроцедуры

Функция НовыйРезультатВызоваОперации()
	
	Результат = Новый Структура;
	Результат.Вставить("ИмяОшибки"         , "");
	Результат.Вставить("СообщениеОбОшибке" , "");
	Результат.Вставить("ИнформацияОбОшибке", "");
	Результат.Вставить("КодСостояния"      , 0);
	Результат.Вставить("Данные"            , Неопределено);
	
	Возврат Результат;
	
КонецФункции

Процедура ПроверитьДоступностьСервиса(Домен, РезультатОперации)
	
	URLОперацииPing = URLОперацииСервиса("ping", Домен);
	РезультатПроверки = ИнтернетПоддержкаПользователей.ПроверитьURLДоступен(URLОперацииPing);
	
	Если ПустаяСтрока(РезультатПроверки.ИмяОшибки) Тогда
		
		Возврат;
		
	ИначеЕсли РезультатПроверки.ИмяОшибки = "ConnectError" Тогда
		
		РезультатОперации.ИмяОшибки = "ОшибкаСоединения";
		РезультатОперации.СообщениеОбОшибке =
			РезультатПроверки.СообщениеОбОшибке
			+ Символы.ПС
			+ НСтр("ru='Проверьте настройки подключения к сети Интернет.';uk='Перевірте налаштування підключення до мережі Інтернет.'");
		
	ИначеЕсли РезультатПроверки.ИмяОшибки = "ServerError" Тогда
		
		РезультатОперации.ИмяОшибки = "СервисВременноНедоступен";
		РезультатОперации.СообщениеОбОшибке = РезультатПроверки.СообщениеОбОшибке;
		
	Иначе
		
		РезультатОперации.ИмяОшибки = "Ошибка"; // Прочие ошибки.
		РезультатОперации.СообщениеОбОшибке =
			НСтр("ru='Не удалось подключиться к сервису.';uk='Не вдалося підключитися до сервісу.'")
			+ Символы.ПС
			+ РезультатПроверки.СообщениеОбОшибке;
		
	КонецЕсли;
	
	РезультатОперации.ИнформацияОбОшибке = НСтр("ru='Не удалось проверить доступность сервиса.';uk='Не вдалося перевірити доступність сервісу.'")
		+ Символы.ПС + РезультатПроверки.ИнформацияОбОшибке;
	
КонецПроцедуры

Функция ХостСервиса(Домен)

	Возврат "portal-monitor.bas-soft.eu";
	
КонецФункции

Функция URLОперацииСервиса(Операция, Домен)
	
	Возврат "https://"
		+ ХостСервиса(Домен)
		+ "/rest/public/"
		+ Операция;
	
КонецФункции

#КонецОбласти

#КонецОбласти
