////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

////////////////////////////////////////////////////////////////////////////////
// Имена событий

// Возвращает имя события оповещения об изменении состояния оплаты ведомости на выплату зарплаты
// Уместно использовать в платёжных документах в вызовах Оповестить:
// 	Оповестить(ВзаиморасчетыССотрудникамиКлиент.ИмяСобытияИзмененияОплатыВедомости());
//
//	Возвращаемое значение
//		строка
//
Функция ИмяСобытияИзмененияОплатыВедомости() Экспорт
	Возврат "ИзменениеОплатыВедомости"
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура ВедомостьУстановитьВидимостьПоСпособуВыплаты(Форма) Экспорт
	ВзаиморасчетыССотрудникамиКлиентВнутренний.ВедомостьУстановитьВидимостьПоСпособуВыплаты(Форма);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции ведомостей на выплату зарплаты

// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ВедомостьПриОткрытии(Форма, Отказ) Экспорт
	
	ВедомостьУстановитьПредставлениеПараметровРасчета(Форма);
	ВедомостьОбновитьОписаниеСостоянияДокумента(Форма);
	ВедомостьУстановитьВидимостьПоСпособуВыплаты(Форма);
	
КонецПроцедуры

Процедура ВедомостьОбработкаОповещения(Форма, ИмяСобытия, Параметр, Источник) Экспорт
	ВзаиморасчетыССотрудникамиКлиентВнутренний.ВедомостьОбработкаОповещения(Форма, ИмяСобытия, Параметр, Источник)	
КонецПроцедуры

// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

Процедура ВедомостьСпособВыплатыПриИзменении(Форма, Элемент) Экспорт
	ВзаиморасчетыССотрудникамиКлиентВнутренний.ВедомостьСпособВыплатыПриИзменении(Форма, Элемент);
КонецПроцедуры

Процедура КомментарийНачалоВыбора(Форма, Элемент, ДанныеВыбора, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования,
		Форма.ЭтотОбъект,
		"Объект.Комментарий"
	);
	
КонецПроцедуры

// ОБРАБОТЧИКИ СОБЫТИЙ КОМАНД ФОРМЫ

Процедура ВедомостьЗаполнить(Форма) Экспорт
	
	ОчиститьСообщения();
	Форма.ЗаполнитьНаСервере()
	
КонецПроцедуры	

Процедура ВедомостьРассчитать(Форма) Экспорт
	
	ОчиститьСообщения();
	Форма.РассчитатьНаСервере()
	
КонецПроцедуры	


Процедура ВедомостьРассчитатьНалоги(Форма) Экспорт
	
	ОчиститьСообщения();
	Форма.РассчитатьНалогиНаСервере()
	
КонецПроцедуры	

Процедура ВедомостьПараметрыРасчетаИзменить(Форма, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ДополнительныеПараметры = Новый Структура("Форма", Форма);
	Оповещение = Новый ОписаниеОповещения("ВедомостьПараметрыРасчетаИзменитьЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ВзаиморасчетыССотрудникамиКлиентВнутренний.ВедомостьИзменитьПараметрыРасчета(Форма, Оповещение);
	
КонецПроцедуры

Процедура ВедомостьПараметрыРасчетаИзменитьЗавершение(Отказ, ДополнительныеПараметры) Экспорт

	Если Не Отказ Тогда
		Форма = ДополнительныеПараметры.Форма;
		ВедомостьУстановитьПредставлениеПараметровРасчета(Форма);
		Форма.ТекущийЭлемент = Форма.Элементы.Заполнить;
	КонецЕсли;
	
КонецПроцедуры

// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ <Зарплата>

Процедура ВедомостьЗарплатаПриАктивизацииСтроки(Форма, Элемент) Экспорт

	ЗарплатаТекущиеДанные = Форма.Элементы.Зарплата.ТекущиеДанные;
	ЗарплатаПрежниеДанные = Форма.ЗарплатаПрежниеДанные;
	
	Если ЗарплатаТекущиеДанные <> Неопределено Тогда
///////////////		ЗаполнитьЗначенияСвойств(ЗарплатаПрежниеДанные, ЗарплатаТекущиеДанные); 
	КонецЕсли;
	
КонецПроцедуры

Процедура ВедомостьЗарплатаПриНачалеРедактирования(Форма, Элемент, НоваяСтрока, Копирование) Экспорт
	
	Если НоваяСтрока И Не Копирование Тогда
		Элемент.ТекущиеДанные.Подразделение = Форма.Объект.Подразделение;
	КонецЕсли;
	
КонецПроцедуры

Процедура ВедомостьЗарплатаКВыплатеПриИзменении(Форма, Элемент) Экспорт
	
	ЗарплатаТекущиеДанные = Форма.Элементы.Зарплата.ТекущиеДанные;
	ЗарплатаПрежниеДанные = Форма.ЗарплатаПрежниеДанные;

	Если ЗарплатаТекущиеДанные.КВыплате <> ЗарплатаПрежниеДанные.КВыплате Тогда
		ЗарплатаТекущиеДанные.Авторасчет = Ложь;
		ЗарплатаПрежниеДанные.КВыплате	= ЗарплатаТекущиеДанные.КВыплате;
	КонецЕсли;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Показ и управление состоянием документа

Процедура ВедомостьОбновитьОписаниеСостоянияДокумента(Форма)   
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Показ параметров расчета

Процедура ВедомостьУстановитьПредставлениеПараметровРасчета(Форма) Экспорт
	
	Форма.ПараметрыРасчетаИнфо = ВзаиморасчетыССотрудникамиКлиентВнутренний.ВедомостьПредставлениеПараметровРасчета(Форма);
	
КонецПроцедуры	
