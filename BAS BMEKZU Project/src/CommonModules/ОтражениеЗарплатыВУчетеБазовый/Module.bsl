////////////////////////////////////////////////////////////////////////////////
// Отражение зарплаты в учете
// Базовая реализация изменяемого поведения
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Создает временную таблицу с полями
//  ФизическоеЛицо
//  ДокументОснование
//  Контрагент
//  Удержание
//
Процедура СоздатьВТУдержанияПоСотрудникамКонтрагент(МенеджерВременныхТаблиц) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	УдержанияПоСотрудникам.ФизическоеЛицо,
	|	УдержанияПоСотрудникам.ДокументОснование,
	|	УдержанияПоСотрудникам.Удержание,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Контрагент
	|ПОМЕСТИТЬ ВТУдержанияПоСотрудникамКонтрагент
	|ИЗ
	|	ВТУдержанияПоСотрудникам КАК УдержанияПоСотрудникам";
	
	Запрос.Выполнить();

КонецПроцедуры

Процедура ДополнитьТаблицуНДФЛ(ПериодРегистрации, Организация, МенеджерВременныхТаблиц) Экспорт

	// в базовой реализации не требуется дополнение
	
КонецПроцедуры

Процедура СоздатьВТНачисленияВидОперации(МенеджерВременныхТаблиц) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокНачислений.НачислениеУдержание КАК НачислениеУдержание,
	|	ВЫБОР
	|		КОГДА Начисления.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОплатаОтпуска)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.НачисленоОтпускныеДоход)
	|		КОГДА Начисления.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОплатаБольничногоЛистаЗаСчетРаботодателя)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.НачисленоБольничные)
	|		КОГДА Начисления.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОплатаБольничногоЛиста)
	|		 ИЛИ Начисления.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОплатаБольничногоНесчастныйСлучайНаПроизводстве)
	|		 ИЛИ Начисления.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОтпускПоБеременностиИРодам)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.НачисленоЗаСчетФССДоход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.НачисленоДоход)
	|	КОНЕЦ КАК ВидОперации
	|ПОМЕСТИТЬ ВТНачисленияВидОперации
	|ИЗ
	|	ВТНачисления КАК СписокНачислений
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления КАК Начисления
	|		ПО СписокНачислений.НачислениеУдержание = Начисления.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НачислениеУдержание";
	
	Запрос.Выполнить();

КонецПроцедуры

Процедура СоздатьВТУдержанияВидОперации(МенеджерВременныхТаблиц) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;

	Запрос.Текст = 
	
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокУдержаний.НачислениеУдержание КАК НачислениеУдержание,
	|	ВЫБОР
	|		КОГДА Удержания.КатегорияУдержания = ЗНАЧЕНИЕ(Перечисление.КатегорииУдержаний.Алименты)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.Алименты)
	|		КОГДА Удержания.КатегорияУдержания = ЗНАЧЕНИЕ(Перечисление.КатегорииУдержаний.ИсполнительныйЛист)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ИсполнительныеЛисты)
	|		КОГДА Удержания.КатегорияУдержания = ЗНАЧЕНИЕ(Перечисление.КатегорииУдержаний.ПочтовыйСбор)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ПочтовыйСбор)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ПрочиеУдержания)
	|	КОНЕЦ КАК ВидОперации
	|ПОМЕСТИТЬ ВТУдержанияВидОперации
	|ИЗ
	|	ВТУдержания КАК СписокУдержаний
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.Удержания КАК Удержания
	|		ПО СписокУдержаний.НачислениеУдержание = Удержания.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НачислениеУдержание";
	
	Запрос.Выполнить();

КонецПроцедуры


