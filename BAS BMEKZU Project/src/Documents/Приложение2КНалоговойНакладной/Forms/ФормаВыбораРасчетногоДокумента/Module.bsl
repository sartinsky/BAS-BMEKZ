
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Дата") Тогда
		Дата = Параметры.Дата;	
	КонецЕсли;
	
	Если Параметры.Свойство("Организация") Тогда
		Организация = Параметры.Организация;	
	КонецЕсли;
	
	Если Параметры.Свойство("Контрагент") Тогда
		Контрагент = Параметры.Контрагент;	
	КонецЕсли;
	
	Если Параметры.Свойство("ДоговорКонтрагента") Тогда
		ДоговорКонтрагента = Параметры.ДоговорКонтрагента;	
	КонецЕсли;
	
	Если Параметры.Свойство("НалоговаяНакладная") Тогда
		НалоговаяНакладная = Параметры.НалоговаяНакладная;	
	КонецЕсли;
	
	Элементы.ИнфНадписьРеквизитыДокумента.Заголовок = 
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Организация: %1, 
|Контрагент: %2, 
|Договор: %3. ';uk='Організація: %1, 
|Контрагент: %2, 
|Договір: %3.'"), СокрЛП(Организация), СокрЛП(Контрагент), СокрЛП(ДоговорКонтрагента))

	
КонецПроцедуры

&НаКлиенте
Процедура РасчетныйДокументНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОбъекта = Новый Структура;
	ПараметрыОбъекта.Вставить("Дата"                 , КонецДня(ТекущаяДата()));
	ПараметрыОбъекта.Вставить("НачалоПериода"        , НачалоМесяца(Дата));
	ПараметрыОбъекта.Вставить("ДоговорКонтрагента"   , ДоговорКонтрагента);
	ПараметрыОбъекта.Вставить("Контрагент"           , Контрагент);
	ПараметрыОбъекта.Вставить("Организация"          , Организация);
	ПараметрыОбъекта.Вставить("ТипыДокументов"       , "Метаданные.Документы.Приложение2КНалоговойНакладной.Реквизиты.Сделка.Тип");
	
	ПараметрыФормы = Новый Структура("ПараметрыОбъекта", ПараметрыОбъекта);
	ОткрытьФорму("Документ.ДокументРасчетовСКонтрагентом.ФормаВыбора", ПараметрыФормы, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	Если Не ЗначениеЗаполнено(РасчетныйДокумент) Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru='Расчетный документ не выбран! Продолжить редактирование?';uk='Розрахунковий документ не обраний! Продовжити редагування?'");
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура("Основание, Сделка", НалоговаяНакладная, РасчетныйДокумент);	
	ОткрытьФорму("Документ.Приложение2КНалоговойНакладной.Форма.ФормаДокумента", ПараметрыОткрытия);
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		Возврат;
	Иначе
		Закрыть();
	КонецЕсли; 
	
КонецПроцедуры	