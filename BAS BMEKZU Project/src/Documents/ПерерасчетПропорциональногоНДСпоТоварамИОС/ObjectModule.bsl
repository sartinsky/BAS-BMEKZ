#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Выполняет движения по регистрам.
//
Процедура ДвиженияПоРегистрам(ТаблицаПоАвансамПоставщикам, ТаблицаПоПерерасчетуОС, ТаблицаОстатков15Счета, СтруктураШапкиДокумента, Отказ, Заголовок)
	
	ДвиженияПоЗатратамПоТоварам(СтруктураШапкиДокумента, Отказ, Заголовок);
	
	ДвиженияПоОстаткам15Счета(ТаблицаОстатков15Счета, СтруктураШапкиДокумента, Отказ, Заголовок);
	
	ПересчетПропорциональногоНДСПоВзаиморасчетам(ТаблицаПоАвансамПоставщикам, СтруктураШапкиДокумента, Отказ, Заголовок);
	
	ДвиженияПоОС(ТаблицаПоПерерасчетуОС, СтруктураШапкиДокумента, Отказ, Заголовок);
	
	ФормированиеДокументаУстановкаКоэффициентаПропорциональногоОтнесенияНДСНаКредит(СтруктураШапкиДокумента, Отказ, Заголовок);
	
	// технологическая операция по регистру учета пропорционального НДС по ОС
	ЗакрытиеРегистраСтоимостьПриобретенияОСПропорциональноОблагаемыхНДС(СтруктураШапкиДокумента, Отказ, Заголовок);
	
	Если Дата >= '20160101' Тогда
		ДвиженияПоРегиструОжидаемогоИПодтвержденногоНДСПродаж(СтруктураШапкиДокумента, Отказ, Заголовок);	
	КонецЕсли;
	
КонецПроцедуры // ДвиженияПоРегистрам()

Процедура ДвиженияПоЗатратамПоТоварам(СтруктураШапкиДокумента, Отказ, Заголовок)
	
	Если СуммаКорректировкиБУ = 0 И СуммаКорректировкиНУ = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Проводка = Движения.Хозрасчетный.Добавить();

	Проводка.Период      = Дата;

	Проводка.Организация = СтруктураШапкиДокумента.Организация;
	Проводка.Содержание  = НСтр("ru='Корректировка пропорц. НДС по товарам/услугам и ОС';uk='Коригування пропорц. ПДВ по товарам/послугам та ОЗ'",Локализация.КодЯзыкаИнформационнойБазы());

	Проводка.СчетДт      = СтруктураШапкиДокумента.СчетДт;
	БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,1, СтруктураШапкиДокумента.СубконтоДт1);
	БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,2, СтруктураШапкиДокумента.СубконтоДт2);
	БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,3, СтруктураШапкиДокумента.СубконтоДт3);
	
	Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыльДо2015 Тогда
		Проводка.НалоговоеНазначениеДт = СтруктураШапкиДокумента.НалоговоеНазначениеДоходовИЗатрат;
		Если НЕ СтруктураШапкиДокумента.НалоговоеНазначениеДоходовИЗатрат = Справочники.НалоговыеНазначенияАктивовИЗатрат.НКУ_НеХозДеятельность Тогда
			Проводка.СуммаНУДт = СуммаКорректировкиНУ;	
		КонецЕсли;
	КонецЕсли;
	
	// проводку будем делать транзитом через счет корректировки НК, так, чтобы проводка по счету НДС (6412) была по дебету.
	//Проводка.СчетКт      = СтруктураШапкиДокумента.СчетНДС;
	Проводка.СчетКт      = СтруктураШапкиДокумента.СчетКорректировкиНДС;

	Проводка.Сумма       = СуммаКорректировкиБУ;
	
	Если СтруктураШапкиДокумента.Дата >= '20160101' Тогда
		//c 2016 года  проводки формируются через корректировки обязательств!
		Проводка.СчетКт      = СтруктураШапкиДокумента.СчетНДСУсловнаяПродажа;
	Иначе	
		
		// проводку будем делать транзитом через счет корректировки НК, так, чтобы проводка по счету НДС (6412) была по дебету.
		Проводка = Движения.Хозрасчетный.Добавить();

		Проводка.Период      = Дата;

		Проводка.Организация = СтруктураШапкиДокумента.Организация;
		Проводка.Содержание  = НСтр("ru='Корректировка пропорц. НДС по товарам/услугам';uk='Коригування пропорц. ПДВ по товарам/послугам'",Локализация.КодЯзыкаИнформационнойБазы());

		Проводка.СчетДт      = СтруктураШапкиДокумента.СчетНДС;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,1, СтруктураШапкиДокумента.СубконтоКт1);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,2, СтруктураШапкиДокумента.СубконтоКт2);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,3, СтруктураШапкиДокумента.СубконтоКт3);

		Проводка.СчетКт      = СтруктураШапкиДокумента.СчетКорректировкиНДС;

		Проводка.Сумма       = - СуммаКорректировкиБУ;
		
	КонецЕсли;
	
	// сразу отнесем на фин. рез
	Проводка = Движения.Хозрасчетный.Добавить();

	Проводка.Период      = Дата;

	Проводка.Организация = СтруктураШапкиДокумента.Организация;
	Проводка.Содержание  = НСтр("ru='Отнесение на фин. результат результата пересчета пропорц. НДС по товарам/услугам';uk='Віднесення на фін. результат перерахунку пропорц. ПДВ по товарам/послугам'",Локализация.КодЯзыкаИнформационнойБазы());

	Проводка.СчетДт      = ПланыСчетов.Хозрасчетный.РезультатОперационнойДеятельности;
	Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыльДо2015 Тогда
		Проводка.НалоговоеНазначениеДт = СтруктураШапкиДокумента.НалоговоеНазначениеДоходовИЗатрат;
		Если НЕ СтруктураШапкиДокумента.НалоговоеНазначениеДоходовИЗатрат = Справочники.НалоговыеНазначенияАктивовИЗатрат.НКУ_НеХозДеятельность Тогда
			Проводка.СуммаНУДт = СуммаКорректировкиНУ;	
		КонецЕсли;
	КонецЕсли;
	
	Проводка.СчетКт      = СтруктураШапкиДокумента.СчетДт;
	БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,1, СтруктураШапкиДокумента.СубконтоДт1);
	БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,2, СтруктураШапкиДокумента.СубконтоДт2);
	БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,3, СтруктураШапкиДокумента.СубконтоДт3);

	Проводка.Сумма       = СуммаКорректировкиБУ;
	
	Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыльДо2015 Тогда
		Проводка.НалоговоеНазначениеКт = СтруктураШапкиДокумента.НалоговоеНазначениеДоходовИЗатрат;
		Если НЕ СтруктураШапкиДокумента.НалоговоеНазначениеДоходовИЗатрат = Справочники.НалоговыеНазначенияАктивовИЗатрат.НКУ_НеХозДеятельность Тогда
			Проводка.СуммаНУКт = СуммаКорректировкиНУ;	
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДвиженияПоОстаткам15Счета(ТаблицаОстатков15Счета, СтруктураШапкиДокумента, Отказ, Заголовок)

	Для каждого Строка Из ТаблицаОстатков15Счета Цикл
		
		Если Строка.СуммаКорректировкиНДС = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Проводка = Движения.Хозрасчетный.Добавить();

		Проводка.Период      = Дата;

		Проводка.Организация = СтруктураШапкиДокумента.Организация;
		Проводка.Содержание  = НСтр("ru='Корректировка пропорц. НДС по инвестициям';uk='Коригування пропорц. ПДВ по инвестициям'",Локализация.КодЯзыкаИнформационнойБазы());

		Проводка.СчетДт      = Строка.Счет;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"ОбъектыСтроительства", Строка.НеоборотныйАктив);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"НематериальныеАктивы", Строка.НеоборотныйАктив);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"Номенклатура", 		 Строка.НеоборотныйАктив);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"БиологическиеАктивы",  Строка.НеоборотныйАктив);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"Склады",  			 Строка.Склад);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"Партии",  			 Строка.Партия);
		//БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеПозиции, Строка.НоменклатурнаяПозиция);
		
		Проводка.НалоговоеНазначениеДт = Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_Пропорционально;
		Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыльДо2015 Тогда
			Если Строка.КорректироватьПоНУ Тогда
				Проводка.СуммаНУДт = - Строка.СуммаКорректировкиНДС;	
			КонецЕсли;
		КонецЕсли;
		
		Проводка.СчетКт      = СтруктураШапкиДокумента.СчетКорректировкиНДС;

		Проводка.Сумма       = - Строка.СуммаКорректировкиНДС;
		
		Если СтруктураШапкиДокумента.дата > '20160101' Тогда
			//c 2016 года  проводки формируются через корректировки обязательств!
			Проводка.СчетКт  = СтруктураШапкиДокумента.СчетНДСУсловнаяПродажа;
		Иначе	
		
			Проводка = Движения.Хозрасчетный.Добавить();

			Проводка.Период      = Дата;

			Проводка.Организация = СтруктураШапкиДокумента.Организация;
			Проводка.Содержание  = НСтр("ru='Корректировка пропорц. НДС по инвестициям';uk='Коригування пропорц. ПДВ по инвестициям'",Локализация.КодЯзыкаИнформационнойБазы());

			Проводка.СчетДт      = СтруктураШапкиДокумента.СчетНДС;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,1, СтруктураШапкиДокумента.СубконтоКт1);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,2, СтруктураШапкиДокумента.СубконтоКт2);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,3, СтруктураШапкиДокумента.СубконтоКт3);

			Проводка.СчетКт      = СтруктураШапкиДокумента.СчетКорректировкиНДС;

			Проводка.Сумма       = Строка.СуммаКорректировкиНДС;
			
		КонецЕсли;	
		
	КонецЦикла;

КонецПроцедуры

Процедура ПересчетПропорциональногоНДСПоВзаиморасчетам(ТаблицаПоАвансамПоставщикам, СтруктураШапкиДокумента, Отказ, Заголовок)
	
	Если Месяц(Дата) <> 12 Тогда
		Возврат;
	КонецЕсли;
	
	///////////////////////////////////////////////////////
	// сфорируем движения по регистру Приобретение налоговый учет по договорам с простым налоговым учетом (чтобы правильно закрылись поставки следующего года)
	НаборДвижений = Движения.ПриобретенияНалоговыйУчет;
	ТаблицаДвижений = НаборДвижений.ВыгрузитьКолонки();
	
	ТаблицаПоАвансамПоставщикамКопия = ТаблицаПоАвансамПоставщикам.Скопировать();
	
	// оставим строки по которым есть сумма корректировки и договора - с простым налоговым учетом
	Инд = 0;
	Пока ТаблицаПоАвансамПоставщикамКопия.Количество() > Инд Цикл
	
		Строка = ТаблицаПоАвансамПоставщикамКопия[Инд];
		Если Строка.СуммаНДСПропорционально = 0 Тогда
			ТаблицаПоАвансамПоставщикамКопия.Удалить(Строка);
		ИначеЕсли Строка.СложныйНалоговыйУчет = Истина Тогда
			ТаблицаПоАвансамПоставщикамКопия.Удалить(Строка);
		Иначе
			Инд = Инд + 1;
		КонецЕсли;
	
	КонецЦикла;
	
	// записывать в регистр будем только сумму корректировки пропорционального НДС
	ОбщегоНазначенияБПВызовСервера.ЗагрузитьВТаблицуЗначений(ТаблицаПоАвансамПоставщикамКопия, ТаблицаДвижений);
	ТаблицаДвижений.ЗаполнитьЗначения(Организация, "Организация");
	
	Если НЕ Отказ И ТаблицаДвижений.Количество() > 0 Тогда
			
		НаборДвижений.мПериод          = Дата;
		НаборДвижений.мТаблицаДвижений = ТаблицаДвижений;
		
		Движения.ПриобретенияНалоговыйУчет.ВыполнитьПриход();
		Движения.ПриобретенияНалоговыйУчет.Записать();
			
	КонецЕсли;
	
	///////////////////////////////////////////////////////
	// теперь сформируем движения по регистру Ожидаемого и подтвержденного НДС по договрам со сложным налоговым учетом (чтобы правильно закрылись поставки следующего года)
	НаборДвижений = Движения.ОжидаемыйИПодтвержденныйНДСПриобретений;
	ТаблицаДвижений = НаборДвижений.ВыгрузитьКолонки();
	
	ТаблицаПоАвансамПоставщикамКопия = ТаблицаПоАвансамПоставщикам.Скопировать();
	
	// оставим строки по которым есть сумма корректировки и договора - с простым налоговым учетом
	Инд = 0;
	Пока ТаблицаПоАвансамПоставщикамКопия.Количество() > Инд Цикл
	
		Строка = ТаблицаПоАвансамПоставщикамКопия[Инд];
		Если Строка.СуммаНДСПропорционально = 0 Тогда
			ТаблицаПоАвансамПоставщикамКопия.Удалить(Строка);
		ИначеЕсли Строка.СложныйНалоговыйУчет = Ложь Тогда
			ТаблицаПоАвансамПоставщикамКопия.Удалить(Строка);
		Иначе
			Инд = Инд + 1;
		КонецЕсли;
	
	КонецЦикла;
	
	// записывать в регистр будем только сумму корректировки пропорционального НДС
	ОбщегоНазначенияБПВызовСервера.ЗагрузитьВТаблицуЗначений(ТаблицаПоАвансамПоставщикамКопия, ТаблицаДвижений);
	ТаблицаДвижений.ЗаполнитьЗначения(Организация, "Организация");
	ТаблицаДвижений.ЗаполнитьЗначения(Перечисления.КодыОперацийОжидаемыйИПодтвержденныйНДСПриобретений.КорректировкаРегламентная, "КодОперации");
	
	Если НЕ Отказ И ТаблицаДвижений.Количество() > 0 Тогда
			
		НаборДвижений.мПериод          = Дата;
		НаборДвижений.мТаблицаДвижений = ТаблицаДвижений;
		
		Движения.ОжидаемыйИПодтвержденныйНДСПриобретений.ВыполнитьРасход();
		Движения.ОжидаемыйИПодтвержденныйНДСПриобретений.Записать();
			
	КонецЕсли;
	
	
	////////////////////////////////////////////////////////
	// теперь получим таблицу для корректировок счета 6441(простой учет)/6442(сложный учет)
	Для каждого Строка Из ТаблицаПоАвансамПоставщикам Цикл
		
		Проводка = Движения.Хозрасчетный.Добавить();

		Проводка.Период      = Дата;

		Проводка.Организация = СтруктураШапкиДокумента.Организация;
		Проводка.Содержание  = НСтр("ru='Корректировка пропорц. НДС по предоплатам за товары/услуги';uk='Коригування пропорц. ПДВ по передоплатам за товари/послуги'",Локализация.КодЯзыкаИнформационнойБазы());

		Проводка.СчетДт      = СтруктураШапкиДокумента.СчетНДС;
		Если СтруктураШапкиДокумента.дата > '20160101' Тогда
			//c 2016 года  проводки формируются через корректировки обязательств!
			Проводка.СчетДт  = СтруктураШапкиДокумента.СчетНДСУсловнаяПродажа;
		Иначе
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,1, СтруктураШапкиДокумента.СубконтоКт1);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,2, СтруктураШапкиДокумента.СубконтоКт2);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,3, СтруктураШапкиДокумента.СубконтоКт3);
		КонецЕсли;

		Проводка.СчетКт      = Строка.СчетУчетаНДС;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,"Контрагенты",        				Строка.Контрагент);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,"Договоры",           				Строка.ДоговорКонтрагента);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,"ДокументыРасчетовСКонтрагентами", Строка.Сделка);

		Проводка.Сумма          = Строка.СуммаНДСПропорционально * ?(Строка.Событие = Перечисления.СобытияПриобретенияНалоговыйУчет.ВозвратОплатыПоставщиком, -1, 1);
		
	КонецЦикла;
	
	// теперь сформируем автоматические движения, аналогичные движения по авансам, но только по остаткам отгрузок, которые переходят на новый год.
	// Это необходимо для корректного закрытия регистров последующей оплатой в новом году (актуально только для простого налогового учета)
	
	// НО, только для обычного режима (в рег. ПриобретенияНалоговыйУчет не ведется в разрезе спец. режима)
	Если НЕ СтруктураШапкиДокумента.СпецРежимНалогообложения = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// заполним таблицу с "авансами" отгрузок поставщикам 
	Запрос = новый Запрос();
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Период", КонецМесяца(Дата));
	Запрос.УстановитьПараметр("ПропорциональныйНДС", Перечисления.ВидыДеятельностиНДС.ПропорциональноОблагаемая);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВЫБОР
	               |		КОГДА ПриобретенияНалоговыйУчетОстатки.Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПриобретенияНалоговыйУчет.ВозвратОплатыПоставщиком)
	               |				ИЛИ ПриобретенияНалоговыйУчетОстатки.Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПриобретенияНалоговыйУчет.ВозвратПоставщику)
	               |			ТОГДА ИСТИНА
	               |		ИНАЧЕ ЛОЖЬ
	               |	КОНЕЦ КАК ЭтоВозврат,
	               |	ВЫБОР
	               |		КОГДА ПриобретенияНалоговыйУчетОстатки.Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПриобретенияНалоговыйУчет.ВозвратОплатыПоставщиком)
	               |				ИЛИ ПриобретенияНалоговыйУчетОстатки.Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПриобретенияНалоговыйУчет.ОплатаПоставщику)
	               |			ТОГДА ИСТИНА
	               |		ИНАЧЕ ЛОЖЬ
	               |	КОНЕЦ КАК ЭтоОплата,
	               |	ПриобретенияНалоговыйУчетОстатки.СуммаНДСОстаток КАК СуммаНДС,
	               |	ПриобретенияНалоговыйУчетОстатки.СуммаНДСПропорциональноОстаток КАК СуммаНДСПропорционально,
	               |	ПриобретенияНалоговыйУчетОстатки.СтавкаНДС,
	               |	ПриобретенияНалоговыйУчетОстатки.Сделка,
	               |	ПриобретенияНалоговыйУчетОстатки.ДоговорКонтрагента.Владелец КАК Контрагент,
	               |	ПриобретенияНалоговыйУчетОстатки.ДоговорКонтрагента,
				   |	ПриобретенияНалоговыйУчетОстатки.Амортизируется
	               |ПОМЕСТИТЬ ПростойУчет
	               |ИЗ
	               |	РегистрНакопления.ПриобретенияНалоговыйУчет.Остатки(
	               |			&Период,
	               |			Организация = &Организация
	               |				И ВидДеятельностиНДС = &ПропорциональныйНДС
	               |				) КАК ПриобретенияНалоговыйУчетОстатки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПростойУчет.Контрагент,
	               |	ПростойУчет.ДоговорКонтрагента,
				   |	ПростойУчет.Амортизируется,
				   |	ПростойУчет.Сделка,
	               |	ПростойУчет.ЭтоВозврат,
	               |	ПростойУчет.СтавкаНДС,
	               |	СУММА(ВЫБОР
	               |			КОГДА ПростойУчет.ЭтоОплата = ЛОЖЬ
	               |				ТОГДА ПростойУчет.СуммаНДС
	               |			ИНАЧЕ -ПростойУчет.СуммаНДС
	               |		КОНЕЦ) КАК СуммаНДС,
	               |	СУММА(ВЫБОР
	               |			КОГДА ПростойУчет.ЭтоОплата = ЛОЖЬ
	               |				ТОГДА ПростойУчет.СуммаНДСПропорционально
	               |			ИНАЧЕ -ПростойУчет.СуммаНДСПропорционально
	               |		КОНЕЦ) КАК СуммаНДСПропорционально
	               |ПОМЕСТИТЬ АвансыПростойУчет
	               |ИЗ
	               |	ПростойУчет КАК ПростойУчет
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ПростойУчет.Контрагент,
	               |	ПростойУчет.ДоговорКонтрагента,
	               |	ПростойУчет.Амортизируется,
	               |	ПростойУчет.Сделка,
	               |	ПростойУчет.ЭтоВозврат,
	               |	ПростойУчет.СтавкаНДС
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	АвансыПростойУчет.Контрагент,
	               |	АвансыПростойУчет.ДоговорКонтрагента,
				   |	АвансыПростойУчет.Амортизируется,
				   |	АвансыПростойУчет.Сделка,
	               |	ВЫБОР
	               |		КОГДА АвансыПростойУчет.ЭтоВозврат = ИСТИНА
	               |			ТОГДА Истина
	               |		ИНАЧЕ Ложь
	               |	КОНЕЦ КАК ЭтоВозврат,
	               |	АвансыПростойУчет.СтавкаНДС,
	               |	АвансыПростойУчет.СуммаНДС КАК НДСПоставки,
	               |	АвансыПростойУчет.СуммаНДСПропорционально КАК НДСПропорциональноКредитДоПерерасчета,
				   |	&ПропорциональныйНДС КАК ВидДеятельностиНДС,
				   |	Истина КАК ДляХозяйственнойДеятельности
	               |ИЗ
	               |	АвансыПростойУчет КАК АвансыПростойУчет
	               |ГДЕ
	               |	  АвансыПростойУчет.СуммаНДС > 0
	               |	И АвансыПростойУчет.СуммаНДСПропорционально <> 0
	               |";
	
	ТаблицаАвансовыхОтгрузок = Запрос.Выполнить().Выгрузить();
	ТаблицаАвансовыхОтгрузок.Колонки.Добавить("Событие");
	ТаблицаАвансовыхОтгрузок.Колонки.Добавить("СуммаНДСПропорционально");
	
	Для каждого Строка Из ТаблицаАвансовыхОтгрузок Цикл
	
		Строка.Событие = ?(Строка.ЭтоВозврат = Ложь,
						   Перечисления.СобытияПриобретенияНалоговыйУчет.ПоступлениеОтПоставщика,
						   Перечисления.СобытияПриобретенияНалоговыйУчет.ВозвратПоставщику);
						   
		Строка.СуммаНДСПропорционально = Окр(Строка.НДСПоставки * СтруктураШапкиДокумента.Коэффициент / 100, 2) - Строка.НДСПропорциональноКредитДоПерерасчета;
	
	КонецЦикла;
	
	// оставим строки по которым есть сумма корректировки
	Инд = 0;
	Пока ТаблицаАвансовыхОтгрузок.Количество() > Инд Цикл
	
		Строка = ТаблицаАвансовыхОтгрузок[Инд];
		Если Строка.СуммаНДСПропорционально = 0 Тогда
			ТаблицаАвансовыхОтгрузок.Удалить(Строка);
		Иначе
			Инд = Инд + 1;
		КонецЕсли;
	
	КонецЦикла;
	
	НаборДвижений = Движения.ПриобретенияНалоговыйУчет;
	ТаблицаДвижений = НаборДвижений.ВыгрузитьКолонки();
	
	ОбщегоНазначенияБПВызовСервера.ЗагрузитьВТаблицуЗначений(ТаблицаАвансовыхОтгрузок, ТаблицаДвижений);
	ТаблицаДвижений.ЗаполнитьЗначения(Организация, "Организация");
	
	Если НЕ Отказ И ТаблицаДвижений.Количество() > 0 Тогда
			
		НаборДвижений.мПериод          = Дата;
		НаборДвижений.мТаблицаДвижений = ТаблицаДвижений;
		
		Движения.ПриобретенияНалоговыйУчет.ВыполнитьПриход();
		Движения.ПриобретенияНалоговыйУчет.Записать();
			
	КонецЕсли;
				   
	
КонецПроцедуры

Процедура ДвиженияПоОС(ТаблицаПоПерерасчетуОС, СтруктураШапкиДокумента, Отказ, Заголовок)
	
	// 1. по СправочникСсылка.ОсновныеСредства
	ЗапросПараметрыАмортизацииОС = Новый Запрос();
	ЗапросПараметрыАмортизацииОС.УстановитьПараметр("Период", 		Дата);
	ЗапросПараметрыАмортизацииОС.УстановитьПараметр("Организация", 	Организация);
	ЗапросПараметрыАмортизацииОС.УстановитьПараметр("СписокОС", 	ПараметрыПерерасчетаОС.ВыгрузитьКолонку("НеоборотныйАктив"));
	
	ЗапросПараметрыАмортизацииОС.Текст = "ВЫБРАТЬ
	                                     |	ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.СрокПолезногоИспользования,
	                                     |	ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.ОбъемПродукцииРабот,
	                                     |	ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.СрокИспользованияДляВычисленияАмортизации,
	                                     |	ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.СтоимостьДляВычисленияАмортизации,
	                                     |	ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.ОбъемПродукцииРаботДляВычисленияАмортизации,
	                                     |	ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.ЛиквидационнаяСтоимость,
	                                     |	ПараметрыАмортизацииОСНалоговыйУчетСрезПоследних.СрокПолезногоИспользования КАК СрокПолезногоИспользованияНУ,
	                                     |	ПараметрыАмортизацииОСНалоговыйУчетСрезПоследних.СрокИспользованияДляВычисленияАмортизации КАК СрокИспользованияДляВычисленияАмортизацииНУ,
	                                     |	ПараметрыАмортизацииОСНалоговыйУчетСрезПоследних.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизацииНУ,
	                                     |	ЕстьNULL(ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.ОсновноеСредство, ПараметрыАмортизацииОСНалоговыйУчетСрезПоследних.ОсновноеСредство) КАК ОсновноеСредство,
										 |	СчетаБухгалтерскогоУчетаОС.СчетУчета                                КАК СчетУчетаБУ,
										 |	СчетаБухгалтерскогоУчетаОС.СчетНачисленияАмортизации                КАК СчетНачисленияАмортизацииБУ
	                                     |ИЗ
	                                     |	РегистрСведений.ПараметрыАмортизацииОСБухгалтерскийУчет.СрезПоследних(
	                                     |			&Период,
	                                     |			Организация = &Организация
	                                     |				И ОсновноеСредство В (&СписокОС)) КАК ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних
										 |ЛЕВОЕ СОЕДИНЕНИЕ
										 |	РегистрСведений.СчетаБухгалтерскогоУчетаОС.СрезПоследних(
										 |		            &Период,
										 |		            Организация = &Организация
										 |		            И ОсновноеСредство В (&СписокОС)) КАК СчетаБухгалтерскогоУчетаОС
										 |ПО ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.ОсновноеСредство = СчетаБухгалтерскогоУчетаОС.ОсновноеСредство
										 |		ПОЛНОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСНалоговыйУчет.СрезПоследних(
	                                     |				&Период,
	                                     |				Организация = &Организация
	                                     |					И ОсновноеСредство В (&СписокОС)) КАК ПараметрыАмортизацииОСНалоговыйУчетСрезПоследних
	                                     |		ПО ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.ОсновноеСредство = ПараметрыАмортизацииОСНалоговыйУчетСрезПоследних.ОсновноеСредство";
	
	ЗапросПараметрыАмортизацииОС = ЗапросПараметрыАмортизацииОС.Выполнить().Выгрузить();

	Для каждого СтрокаТЧ Из ТаблицаПоПерерасчетуОС Цикл
		
		Если НЕ ТипЗнч(СтрокаТЧ.НеоборотныйАктив) = Тип("СправочникСсылка.ОсновныеСредства") Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтрокаТЧ.ОССписан = Истина Тогда
			ДвиженияПоКорректировкеКредитаПоСписаннымОС(СтруктураШапкиДокумента, СтрокаТЧ);
			Продолжить;
   		КонецЕсли;
		
		// Движения по регистру ПараметрыАмортизации - изменена первоначальная стоимость ОС
		СтрокаПараметров = ЗапросПараметрыАмортизацииОС.Найти(СтрокаТЧ.НеоборотныйАктив, "ОсновноеСредство");
		Если СтрокаПараметров = Неопределено 
			 ИЛИ НЕ ЗначениеЗаполнено(СтрокаПараметров.СчетУчетаБУ)
			 ИЛИ НЕ ЗначениеЗаполнено(СтрокаПараметров.СчетНачисленияАмортизацииБУ) Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='По ОС %1 в строке %2 не обнаружены данные по учету! По данному ОС проводки не сформированы!';uk='По ОЗ %1 у рядку %2 не знайдені дані по обліку! За даним ОЗ проведення не сформовані!'"), СтрокаТЧ.НеоборотныйАктив, СтрокаТЧ.НомерСтроки);
			Поле = "ПараметрыПерерасчетаОС[" + Формат((СтрокаТЧ.НомерСтроки-1), "ЧН=0; ЧГ=") + "].НеоборотныйАктив";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", );
			Продолжить;	
		КонецЕсли;
		
		СтатьяЗатрат = СтруктураШапкиДокумента.СтатьяОС;
		Если СтрокаПараметров.СчетНачисленияАмортизацииБУ.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ИзносДругихНеоборотныхМатериальныхАктивов) Тогда
			СтатьяЗатрат = СтруктураШапкиДокумента.СтатьяМНМА;
		КонецЕсли;
		ДвижениеПоОСПоПерерасчетам(СтруктураШапкиДокумента, СтрокаТЧ, СтрокаПараметров, "ОсновныеСредства", СтатьяЗатрат);
		
		// зафиксируем изменения в регистрах учета параметров ОС
		Движение = Движения.ПараметрыАмортизацииОСБухгалтерскийУчет.Добавить();
		
		ЗаполнитьЗначенияСвойств(Движение, СтрокаПараметров);
		
		Движение.Период                                       = Дата;
		Движение.ОсновноеСредство                             = СтрокаТЧ.НеоборотныйАктив;
		Движение.Организация                                  = Организация;
		
		Движение.СтоимостьДляВычисленияАмортизации    		  = Движение.СтоимостьДляВычисленияАмортизации - СтрокаТЧ.СуммаКорректировкиНДС;
		
		// Движения по регистру ПараметрыАмортизацииНУ - изменена первоначальная стоимость ОС
		Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыль Тогда
			
			Движение = Движения.ПараметрыАмортизацииОСНалоговыйУчет.Добавить();
			
			Движение.Период 									= Дата;
			Движение.ОсновноеСредство 							= СтрокаТЧ.НеоборотныйАктив;
			Движение.Организация 								= Организация;
			
			Движение.СрокПолезногоИспользования 				= СтрокаПараметров.СрокПолезногоИспользованияНУ;
			
			Движение.СрокИспользованияДляВычисленияАмортизации 	= СтрокаПараметров.СрокИспользованияДляВычисленияАмортизацииНУ;
			
			Движение.СтоимостьДляВычисленияАмортизации 		    = СтрокаПараметров.СтоимостьДляВычисленияАмортизацииНУ - СтрокаТЧ.СуммаКорректировкиНДС;						
			
		КонецЕсли;
	
	КонецЦикла;
	
	// 2. по СправочникСсылка.НематериальныеАктивы
	ЗапросПараметрыАмортизацииНМА = Новый Запрос();
	ЗапросПараметрыАмортизацииНМА.УстановитьПараметр("Период", 		Дата);
	ЗапросПараметрыАмортизацииНМА.УстановитьПараметр("Организация", 	Организация);
	ЗапросПараметрыАмортизацииНМА.УстановитьПараметр("НематериальныйАктив", 	ПараметрыПерерасчетаОС.ВыгрузитьКолонку("НеоборотныйАктив"));
	
	ЗапросПараметрыАмортизацииНМА.Текст = "ВЫБРАТЬ
	                                     |	ПервоначальныеСведенияНМАБухгалтерскийУчет.ПервоначальнаяСтоимость,
	                                     |	ПервоначальныеСведенияНМАБухгалтерскийУчет.НачислятьАмортизацию,
	                                     |	ПервоначальныеСведенияНМАБухгалтерскийУчет.СпособНачисленияАмортизации,
	                                     |	ПервоначальныеСведенияНМАБухгалтерскийУчет.СрокПолезногоИспользования,
	                                     |	ПервоначальныеСведенияНМАБухгалтерскийУчет.ОбъемПродукцииРаботДляВычисленияАмортизации,
	                                     |	ПервоначальныеСведенияНМАБухгалтерскийУчет.ЛиквидационнаяСтоимость,
	                                     |	ПервоначальныеСведенияНМАНалоговыйУчет.ПервоначальнаяСтоимостьНУ КАК ПервоначальнаяСтоимостьНУ,
	                                     |	ПервоначальныеСведенияНМАНалоговыйУчет.НачислятьАмортизацию КАК НачислятьАмортизацию,
										 |	ПервоначальныеСведенияНМАНалоговыйУчет.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
										 |	ПервоначальныеСведенияНМАНалоговыйУчет.НалоговаяГруппаОС КАК НалоговаяГруппаОС,
										 |	ПервоначальныеСведенияНМАНалоговыйУчет.НалоговоеНазначение КАК НалоговоеНазначение,
	                                     |	ЕстьNULL(ПервоначальныеСведенияНМАНалоговыйУчет.НематериальныйАктив, ПервоначальныеСведенияНМАБухгалтерскийУчет.НематериальныйАктив) КАК НематериальныйАктив,
										 |	СчетаБухгалтерскогоУчетаНМА.СчетУчета                                КАК СчетУчетаБУ,
										 |	СчетаБухгалтерскогоУчетаНМА.СчетНачисленияАмортизации                КАК СчетНачисленияАмортизацииБУ
	                                     |ИЗ
	                                     |	РегистрСведений.ПервоначальныеСведенияНМАБухгалтерскийУчет.СрезПоследних(
	                                     |			&Период,
	                                     |			Организация = &Организация
	                                     |				И НематериальныйАктив В (&НематериальныйАктив)) КАК ПервоначальныеСведенияНМАБухгалтерскийУчет
										 |ЛЕВОЕ СОЕДИНЕНИЕ
										 |	РегистрСведений.СчетаБухгалтерскогоУчетаНМА.СрезПоследних(
										 |		            &Период,
										 |		            Организация = &Организация
										 |		            И НематериальныйАктив В (&НематериальныйАктив)) КАК СчетаБухгалтерскогоУчетаНМА
										 |ПО ПервоначальныеСведенияНМАБухгалтерскийУчет.НематериальныйАктив = СчетаБухгалтерскогоУчетаНМА.НематериальныйАктив
										 |		ПОЛНОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияНМАНалоговыйУчет.СрезПоследних(
	                                     |				&Период,
	                                     |				Организация = &Организация
	                                     |					И НематериальныйАктив В (&НематериальныйАктив)) КАК ПервоначальныеСведенияНМАНалоговыйУчет
	                                     |		ПО ПервоначальныеСведенияНМАБухгалтерскийУчет.НематериальныйАктив = ПервоначальныеСведенияНМАНалоговыйУчет.НематериальныйАктив";
	
	ЗапросПараметрыАмортизацииНМА = ЗапросПараметрыАмортизацииНМА.Выполнить().Выгрузить();

	Для каждого СтрокаТЧ Из ТаблицаПоПерерасчетуОС Цикл
		
		Если НЕ ТипЗнч(СтрокаТЧ.НеоборотныйАктив) = Тип("СправочникСсылка.НематериальныеАктивы") Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтрокаТЧ.ОССписан = Истина Тогда
			ДвиженияПоКорректировкеКредитаПоСписаннымОС(СтруктураШапкиДокумента, СтрокаТЧ);
			Продолжить;
   		КонецЕсли;
		
		// Движения по регистру ПараметрыАмортизации - изменена первоначальная стоимость ОС
		СтрокаПараметров = ЗапросПараметрыАмортизацииНМА.Найти(СтрокаТЧ.НеоборотныйАктив, "НематериальныйАктив");
		Если СтрокаПараметров = Неопределено 
			 ИЛИ НЕ ЗначениеЗаполнено(СтрокаПараметров.СчетУчетаБУ)
			 ИЛИ НЕ ЗначениеЗаполнено(СтрокаПараметров.СчетНачисленияАмортизацииБУ) Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='По НМА %1 в строке %2 не обнаружены данные по учету! По данному НМА проводки не сформированы!';uk='По НМА %1 у рядку %2 не знайдені дані по обліку! За даним НМА проведення не сформовані!'"), СтрокаТЧ.НеоборотныйАктив, СтрокаТЧ.НомерСтроки);
			Поле = "ПараметрыПерерасчетаОС[" + Формат((СтрокаТЧ.НомерСтроки-1), "ЧН=0; ЧГ=") + "].НеоборотныйАктив";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", );
			Продолжить;	
		КонецЕсли;
		
		ДвижениеПоОСПоПерерасчетам(СтруктураШапкиДокумента, СтрокаТЧ, СтрокаПараметров, "НематериальныеАктивы", СтруктураШапкиДокумента.СтатьяНМА);
	
		// зафиксируем изменения в регистрах учета параметров НМА
		Движение = Движения.ПервоначальныеСведенияНМАБухгалтерскийУчет.Добавить();
		
		ЗаполнитьЗначенияСвойств(Движение, СтрокаПараметров);
		                                                                                     
		Движение.Период                                       = Дата;
		Движение.НематериальныйАктив                          = СтрокаТЧ.НеоборотныйАктив;
		Движение.Организация                                  = Организация;
		
		Движение.ПервоначальнаяСтоимость   		  			  = Движение.ПервоначальнаяСтоимость - СтрокаТЧ.СуммаКорректировкиНДС;
		
		// Движения по регистру ПараметрыАмортизацииНУ - изменена первоначальная стоимость ОС
		Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыль Тогда
			
			Движение = Движения.ПервоначальныеСведенияНМАНалоговыйУчет.Добавить();
			
			ЗаполнитьЗначенияСвойств(Движение, СтрокаПараметров);
			
			Движение.Период 							= Дата;
			Движение.НематериальныйАктив 				= СтрокаТЧ.НеоборотныйАктив;
			Движение.Организация 						= Организация;
			
			Движение.ПервоначальнаяСтоимостьНУ 		    = СтрокаПараметров.ПервоначальнаяСтоимостьНУ - СтрокаТЧ.СуммаКорректировкиНДС;
			
		КонецЕсли;
	
	КонецЦикла;
	
	// 3. по СправочникСсылка.Номенклатура
	ЗапросПараметрыАмортизацииМНМА = Новый Запрос();
	ЗапросПараметрыАмортизацииМНМА.УстановитьПараметр("Период", 	КонецМесяца(Дата));
	ЗапросПараметрыАмортизацииМНМА.УстановитьПараметр("Организация",Организация);
	ЗапросПараметрыАмортизацииМНМА.УстановитьПараметр("Счет1112", 	ПланыСчетов.Хозрасчетный.БиблиотечныеФондыКоличественно);
	ЗапросПараметрыАмортизацииМНМА.УстановитьПараметр("Счет1122", 	ПланыСчетов.Хозрасчетный.МалоценныеНеоборотныеМатериальныеАктивыКоличественно);
	ЗапросПараметрыАмортизацииМНМА.УстановитьПараметр("Счет1322", 	ПланыСчетов.Хозрасчетный.ИзносДругихНеоборотныхМатериальныхАктивовКоличественно);
	ЗапросПараметрыАмортизацииМНМА.УстановитьПараметр("МНМА", 		ПараметрыПерерасчетаОС.ВыгрузитьКолонку("НеоборотныйАктив"));
	ЗапросПараметрыАмортизацииМНМА.УстановитьПараметр("Номенклатура", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);	
	
	ЗапросПараметрыАмортизацииМНМА.УстановитьПараметр("Пропорциональное", 	Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_Пропорционально);
	
	ЗапросПараметрыАмортизацииМНМА.Текст = "ВЫБРАТЬ
	                                       |	ВЫБОР
	                                       |		КОГДА ХозрасчетныйОстатки.Счет В ИЕРАРХИИ (&Счет1322)
	                                       |			ТОГДА ХозрасчетныйОстатки.Счет
	                                       |	КОНЕЦ КАК СчетНачисленияАмортизацииБУ,
	                                       |	ВЫБОР
	                                       |		КОГДА НЕ ХозрасчетныйОстатки.Счет В ИЕРАРХИИ (&Счет1322)
	                                       |			ТОГДА ХозрасчетныйОстатки.Счет
	                                       |	КОНЕЦ КАК СчетУчетаБУ,
	                                       |	ХозрасчетныйОстатки.Субконто1 КАК МНМА,
	                                       |	ХозрасчетныйОстатки.СуммаОстаток КАК Остаток
	                                       |ПОМЕСТИТЬ ВложенныйЗапрос
	                                       |ИЗ
	                                       |	РегистрБухгалтерии.Хозрасчетный.Остатки(
	                                       |			&Период,
	                                       |			Счет В ИЕРАРХИИ (&Счет1112)
	                                       |				ИЛИ Счет В ИЕРАРХИИ (&Счет1122)
	                                       |				ИЛИ Счет В ИЕРАРХИИ (&Счет1322),
	                                       |			&Номенклатура,
	                                       |			Организация = &Организация
	                                       |				И НАлоговоеНазначение = &Пропорциональное
	                                       |				И Субконто1 В (&МНМА)) КАК ХозрасчетныйОстатки
	                                       |;
	                                       |
	                                       |////////////////////////////////////////////////////////////////////////////////
	                                       |ВЫБРАТЬ
	                                       |	МАКСИМУМ(ВложенныйЗапрос.СчетУчетаБУ) КАК СчетУчетаБУ,
	                                       |	МАКСИМУМ(ВложенныйЗапрос.СчетНачисленияАмортизацииБУ) КАК СчетНачисленияАмортизацииБУ,
	                                       |	ВложенныйЗапрос.МНМА
	                                       |ИЗ
	                                       |	ВложенныйЗапрос КАК ВложенныйЗапрос
	                                       |
	                                       |СГРУППИРОВАТЬ ПО
	                                       |	ВложенныйЗапрос.МНМА
	                                       |
										   |";
										   
										   
	ЗапросПараметрыАмортизацииМНМА = ЗапросПараметрыАмортизацииМНМА.Выполнить().Выгрузить();

	Для Каждого СтрокаТЧ Из ТаблицаПоПерерасчетуОС Цикл
		
		Если НЕ ТипЗнч(СтрокаТЧ.НеоборотныйАктив) = Тип("СправочникСсылка.Номенклатура") Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтрокаТЧ.ОССписан = Истина Тогда
			ДвиженияПоКорректировкеКредитаПоСписаннымОС(СтруктураШапкиДокумента, СтрокаТЧ);
			Продолжить;
   		КонецЕсли;
		
		// Движения по регистру ПараметрыАмортизации - изменена первоначальная стоимость ОС
		СтрокаПараметров = ЗапросПараметрыАмортизацииМНМА.Найти(СтрокаТЧ.НеоборотныйАктив, "МНМА");
		Если СтрокаПараметров = Неопределено 
			 ИЛИ НЕ ЗначениеЗаполнено(СтрокаПараметров.СчетУчетаБУ)
			 ИЛИ НЕ ЗначениеЗаполнено(СтрокаПараметров.СчетНачисленияАмортизацииБУ) Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='По МНМА %1 в строке %2 не обнаружены данные по учету! По данному МНМА проводки не сформированы!';uk='По МНМА %1 у рядку %2 не знайдені дані по обліку! За даним МНМА проведення не сформовані!'"), СтрокаТЧ.НеоборотныйАктив, СтрокаТЧ.НомерСтроки);
			Поле = "ПараметрыПерерасчетаОС[" + Формат((СтрокаТЧ.НомерСтроки-1), "ЧН=0; ЧГ=") + "].НеоборотныйАктив";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", );
			Продолжить;	
		КонецЕсли;
		
		ДвижениеПоОСПоПерерасчетам(СтруктураШапкиДокумента, СтрокаТЧ, СтрокаПараметров, "Номенклатура", СтруктураШапкиДокумента.СтатьяМНМА);
	
	КонецЦикла;	
	
	
	// 4. запись в регистр СтоимостьПриобретенияОСПропорциональноОблагаемыхНДС суммы корректировки НДС
	НаборДвижений = Движения.СтоимостьПриобретенияОСПропорциональноОблагаемыхНДС;
	ТаблицаДвижений = НаборДвижений.ВыгрузитьКолонки();
	
	Для каждого СтрокаТЧ Из ТаблицаПоПерерасчетуОС Цикл
	    СтрокаРегистра = ТаблицаДвижений.Добавить();
		СтрокаРегистра.НеоборотныйАктив = СтрокаТЧ.НеоборотныйАктив;
		
		СтрокаРегистра.ДатаНачалаИспользования = СтрокаТЧ.ДатаНачалаИспользования;
		СтрокаРегистра.ДатаФормированияКредита = СтрокаТЧ.ДатаФормированияКредита;
		
		СтрокаРегистра.СуммаНДСПропорциональноКредит = СтрокаТЧ.СуммаКорректировкиНДС;
	КонецЦикла;
	
	ТаблицаДвижений.ЗаполнитьЗначения(Организация, "Организация");
	ТаблицаДвижений.ЗаполнитьЗначения(СпецРежимНалогообложения, "СпецРежимНалогообложения");
	
	Если НЕ Отказ И ТаблицаДвижений.Количество() > 0 Тогда
			
		НаборДвижений.мПериод          = Дата;
		НаборДвижений.мТаблицаДвижений = ТаблицаДвижений;
			
		Движения.СтоимостьПриобретенияОСПропорциональноОблагаемыхНДС.ВыполнитьПриход();
		Движения.СтоимостьПриобретенияОСПропорциональноОблагаемыхНДС.Записать();
			
	КонецЕсли;
	
КонецПроцедуры

Процедура ДвиженияПоКорректировкеКредитаПоСписаннымОС(СтруктураШапкиДокумента, СтрокаТЧ)
	
	Если СтрокаТЧ.СуммаКорректировкиНДС = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПроводкаСуммаКорректировкиБУ = СтрокаТЧ.СуммаКорректировкиБУ;
	ПроводкаСуммаКорректировкиНУ = СтрокаТЧ.СуммаКорректировкиНУ;
	
	Проводка = Движения.Хозрасчетный.Добавить();

	Проводка.Период      = Дата;

	Проводка.Организация = СтруктураШапкиДокумента.Организация;
	Проводка.Содержание  = НСтр("ru='Корректировка пропорц. НДС по ОС - списанными или учитываемым по справедливой стоимости ';uk='Коригування пропорц. ПДВ по ОЗ - списаним або тим що обліковуються за справедливою вартістю'",Локализация.КодЯзыкаИнформационнойБазы());

	Проводка.СчетДт      = СтруктураШапкиДокумента.СчетДт;
	БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,1, СтруктураШапкиДокумента.СубконтоДт1);
	БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,2, СтруктураШапкиДокумента.СубконтоДт2);
	БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,3, СтруктураШапкиДокумента.СубконтоДт3);
	
	Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыльДо2015 Тогда
		Проводка.НалоговоеНазначениеДт = СтруктураШапкиДокумента.НалоговоеНазначениеДоходовИЗатрат;
		Если НЕ СтруктураШапкиДокумента.НалоговоеНазначениеДоходовИЗатрат = Справочники.НалоговыеНазначенияАктивовИЗатрат.НКУ_НеХозДеятельность Тогда
			Проводка.СуммаНУДт = ПроводкаСуммаКорректировкиНУ;	
		КонецЕсли;
	КонецЕсли;
	
	Проводка.СчетКт      = СтруктураШапкиДокумента.СчетКорректировкиНДС;
	Проводка.Сумма       = ПроводкаСуммаКорректировкиБУ;
	
	Проводка = Движения.Хозрасчетный.Добавить();

	Проводка.Период      = Дата;

	Проводка.Организация = СтруктураШапкиДокумента.Организация;
	Проводка.Содержание  = НСтр("ru='Корректировка пропорц. НДС по ОС - списанными или учитываемым по справедливой стоимости ';uk='Коригування пропорц. ПДВ по ОЗ - списаним або тим що обліковуються за справедливою вартістю'",Локализация.КодЯзыкаИнформационнойБазы());

	Проводка.СчетДт      = СтруктураШапкиДокумента.СчетНДС;
	БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,1, СтруктураШапкиДокумента.СубконтоКт1);
	БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,2, СтруктураШапкиДокумента.СубконтоКт2);
	БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,3, СтруктураШапкиДокумента.СубконтоКт3);

	Проводка.СчетКт      = СтруктураШапкиДокумента.СчетКорректировкиНДС;

	Проводка.Сумма       = - ПроводкаСуммаКорректировкиБУ;

	// сразу отнесем на фин. рез
	Проводка = Движения.Хозрасчетный.Добавить();

	Проводка.Период      = Дата;

	Проводка.Организация = СтруктураШапкиДокумента.Организация;
	Проводка.Содержание  = НСтр("ru='Отнесение на фин. результат результата пересчета пропорц. НДС по списанным ОС';uk='Віднесення на фін. результат перерахунку пропорц. ПДВ по списаним ОЗ'",Локализация.КодЯзыкаИнформационнойБазы());

	Проводка.СчетДт      = ПланыСчетов.Хозрасчетный.РезультатОперационнойДеятельности;
	Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыльДо2015 Тогда
		Проводка.НалоговоеНазначениеДт = СтруктураШапкиДокумента.НалоговоеНазначениеДоходовИЗатрат;
		Если НЕ СтруктураШапкиДокумента.НалоговоеНазначениеДоходовИЗатрат = Справочники.НалоговыеНазначенияАктивовИЗатрат.НКУ_НеХозДеятельность Тогда
			Проводка.СуммаНУДт = ПроводкаСуммаКорректировкиНУ;	
		КонецЕсли;
	КонецЕсли;
	
	Проводка.СчетКт      = СтруктураШапкиДокумента.СчетДт;
	БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,1, СтруктураШапкиДокумента.СубконтоДт1);
	БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,2, СтруктураШапкиДокумента.СубконтоДт2);
	БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,3, СтруктураШапкиДокумента.СубконтоДт3);

	Проводка.Сумма       = ПроводкаСуммаКорректировкиБУ;
	
	Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыльДо2015 Тогда
		Проводка.НалоговоеНазначениеКт = СтруктураШапкиДокумента.НалоговоеНазначениеДоходовИЗатрат;
		Если НЕ СтруктураШапкиДокумента.НалоговоеНазначениеДоходовИЗатрат = Справочники.НалоговыеНазначенияАктивовИЗатрат.НКУ_НеХозДеятельность Тогда
			Проводка.СуммаНУКт = ПроводкаСуммаКорректировкиНУ;	
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДвижениеПоОСПоПерерасчетам(СтруктураШапкиДокумента, СтрокаТЧ, СтрокаПараметров, ВидСубконто, СтатьяЗатрат);
		
	Если СтрокаТЧ.СуммаКорректировкиНДС <> 0 Тогда
	
		Если СтрокаТЧ.ДатаНачалаИспользования >= НачалоГода(Дата) Тогда   
			
			// проводки по плану счетов по корректировке начальной стоимости ОС на сумму налогового кредита
			Проводка = Движения.Хозрасчетный.Добавить();
			Проводка.Активность 	= Истина;
			Проводка.Организация 	= Организация;
			Проводка.Период 		= Дата;
			
			Проводка.СчетДт = СтрокаПараметров.СчетУчетаБУ;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, ВидСубконто, СтрокаТЧ.НеоборотныйАктив);
			Проводка.Содержание = НСтр("ru='Изменение балансовой стоимости в связи с перерасчетом НДС';uk=""Зміна балансової вартості у зв'язку з перерахунком ПДВ""", Локализация.КодЯзыкаИнформационнойБазы());
			
			Проводка.НалоговоеНазначениеДт = Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_Пропорционально;
			Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыль Тогда
				Проводка.СуммаНУДт	= -СтрокаТЧ.СуммаКорректировкиНДС;
			КонецЕсли;
			
			Проводка.СчетКт      = СтруктураШапкиДокумента.СчетНДСУсловнаяПродажа;

			Проводка.Сумма       = -СтрокаТЧ.СуммаКорректировкиНДС;
			
		Иначе

			// проводки по плану счетов по корректировке начальной стоимости ОС на сумму налогового кредита
			Проводка = Движения.Хозрасчетный.Добавить();
			Проводка.Активность 	= Истина;
			Проводка.Организация 	= Организация;
			Проводка.Период 		= Дата;
			
			Проводка.СчетДт = СтрокаПараметров.СчетУчетаБУ;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, ВидСубконто, СтрокаТЧ.НеоборотныйАктив);
			Проводка.Содержание = НСтр("ru='Изменение балансовой стоимости в связи с перерасчетом НДС';uk=""Зміна балансової вартості у зв'язку з перерахунком ПДВ""", Локализация.КодЯзыкаИнформационнойБазы());
			
			Проводка.НалоговоеНазначениеДт = Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_Пропорционально;
			Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыль Тогда
				Проводка.СуммаНУДт	= -СтрокаТЧ.СуммаКорректировкиНДС;
			КонецЕсли;
			
			Проводка.СчетКт      = СтруктураШапкиДокумента.СчетКорректировкиНДС;

			Проводка.Сумма       = -СтрокаТЧ.СуммаКорректировкиНДС;
			
			// транзитом на 6412
			Проводка = Движения.Хозрасчетный.Добавить();
			Проводка.Период      = Дата;

			Проводка.Организация = СтруктураШапкиДокумента.Организация;
			Проводка.Содержание  = НСтр("ru='Корректировка пропорц. НДС по ОС';uk='Коригування пропорц. ПДВ по ОС'",Локализация.КодЯзыкаИнформационнойБазы());

			Проводка.СчетДт      = СтруктураШапкиДокумента.СчетНДС;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,1, СтруктураШапкиДокумента.СубконтоКт1);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,2, СтруктураШапкиДокумента.СубконтоКт2);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,3, СтруктураШапкиДокумента.СубконтоКт3);

			Проводка.СчетКт      = СтруктураШапкиДокумента.СчетКорректировкиНДС;

			Проводка.Сумма       = СтрокаТЧ.СуммаКорректировкиНДС;
		
		КонецЕсли;
	
	КонецЕсли;
	
	// проводка на затраты в связи с пересчетом накопленной амортизации
	Если    СтрокаТЧ.СуммаКорректировкиБУ <> 0
		ИЛИ СтрокаТЧ.СуммаКорректировкиНУ <> 0 Тогда
	
		// проводки по затратам по пересчету накопленной амортизации при корректировке НДС
		Проводка = Движения.Хозрасчетный.Добавить();
		Проводка.Активность 	= Истина;
		Проводка.Организация 	= Организация;
		Проводка.Период 		= Дата;
		Проводка.Содержание = НСтр("ru='Изменение накопленной амортизации в связи с перерасчетом НДС';uk=""Зміна накопиченої амортизації у зв'язку з перерахунком ПДВ""", Локализация.КодЯзыкаИнформационнойБазы());
		
		Проводка.Сумма = СтрокаТЧ.СуммаКорректировкиБУ;
		
		Проводка.СчетДт = СтруктураШапкиДокумента.СчетДт;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,1, СтруктураШапкиДокумента.СубконтоДт1);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,2, СтруктураШапкиДокумента.СубконтоДт2);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,3, СтруктураШапкиДокумента.СубконтоДт3);

		// статью затрат определим для каждого вида необоротных активов
		Если ТипЗнч(СтатьяЗатрат) = Тип("СправочникСсылка.СтатьиЗатрат") Тогда
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт, "СтатьиЗатрат", СтатьяЗатрат);
		Иначе	
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт, "СтатьиНеоперационныхРасходов", СтатьяЗатрат);
		КонецЕсли;
		
		Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыльДо2015 Тогда
			Проводка.НалоговоеНазначениеДт = СтруктураШапкиДокумента.НалоговоеНазначениеДоходовИЗатрат;
			Если НЕ СтруктураШапкиДокумента.НалоговоеНазначениеДоходовИЗатрат = Справочники.НалоговыеНазначенияАктивовИЗатрат.НКУ_НеХозДеятельность Тогда
				Проводка.СуммаНУДт = СтрокаТЧ.СуммаКорректировкиНУ;	
			КонецЕсли;
		КонецЕсли;
		
		Проводка.СчетКт = СтрокаПараметров.СчетНачисленияАмортизацииБУ;
		
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, ВидСубконто, СтрокаТЧ.НеоборотныйАктив);
		
		Проводка.НалоговоеНазначениеКт = Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_Пропорционально;
		Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыль Тогда
			Проводка.СуммаНУКт	= СтрокаТЧ.СуммаКорректировкиНУ;
		КонецЕсли;
		
		
		// сразу отнесем на фин. рез
		Проводка = Движения.Хозрасчетный.Добавить();

		Проводка.Период      = Дата;

		Проводка.Организация = СтруктураШапкиДокумента.Организация;
		Проводка.Содержание  = НСтр("ru='Отнесение на фин. результат результата пересчета пропорц. НДС по ОС';uk='Віднесення на фін. результат перерахунку пропорц. ПДВ по ОЗ'",Локализация.КодЯзыкаИнформационнойБазы());

		Проводка.СчетДт      = ПланыСчетов.Хозрасчетный.РезультатОперационнойДеятельности;
		Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыльДо2015 Тогда
			Проводка.НалоговоеНазначениеДт = СтруктураШапкиДокумента.НалоговоеНазначениеДоходовИЗатрат;
			Если НЕ СтруктураШапкиДокумента.НалоговоеНазначениеДоходовИЗатрат = Справочники.НалоговыеНазначенияАктивовИЗатрат.НКУ_НеХозДеятельность Тогда
				Проводка.СуммаНУДт =  СтрокаТЧ.СуммаКорректировкиНУ;	
			КонецЕсли;
		КонецЕсли;
		
		Проводка.СчетКт      = СтруктураШапкиДокумента.СчетДт;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,1, СтруктураШапкиДокумента.СубконтоДт1);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,2, СтруктураШапкиДокумента.СубконтоДт2);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,3, СтруктураШапкиДокумента.СубконтоДт3);

		Проводка.Сумма       =  СтрокаТЧ.СуммаКорректировкиБУ;
		
		Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыльДо2015 Тогда
			Проводка.НалоговоеНазначениеКт = СтруктураШапкиДокумента.НалоговоеНазначениеДоходовИЗатрат;
			Если НЕ СтруктураШапкиДокумента.НалоговоеНазначениеДоходовИЗатрат = Справочники.НалоговыеНазначенияАктивовИЗатрат.НКУ_НеХозДеятельность Тогда
				Проводка.СуммаНУКт =  СтрокаТЧ.СуммаКорректировкиНУ;	
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры		

Процедура ФормированиеДокументаУстановкаКоэффициентаПропорциональногоОтнесенияНДСНаКредит(СтруктураШапкиДокумента, Отказ, Заголовок)
	
	Если СтруктураШапкиДокумента.ФормироватьДокументУстановкиКоэффициентаНаСледующийГод = Ложь
		ИЛИ Месяц(Дата) <> 12 Тогда
		Возврат;
	КонецЕсли;

	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Организация", СтруктураШапкиДокумента.Организация);
	Запрос.УстановитьПараметр("СпецРежимНалогообложения", СпецРежимНалогообложения);
	Запрос.УстановитьПараметр("Период", НачалоГода(ДобавитьМесяц(СтруктураШапкиДокумента.Дата, 12)));
	
	Запрос.Текст = "ВЫБРАТЬ Первые 1
	               |	КоэффициентыПропорциональногоОтнесенияНДСНаКредит.Регистратор,
	               |	КоэффициентыПропорциональногоОтнесенияНДСНаКредит.Регистратор.Коэффициент КАК Коэффициент,
	               |	КоэффициентыПропорциональногоОтнесенияНДСНаКредит.Регистратор.СуммаОблагаемыхОпераций КАК СуммаОблагаемыхОпераций,
	               |	КоэффициентыПропорциональногоОтнесенияНДСНаКредит.Регистратор.СуммаВсехОпераций КАК СуммаВсехОпераций,
				   //	если движение сделал не документ установки коэффициента, а универсальный - мы это определим по отсутствию реквизита
				   |	ЕстьNULL(КоэффициентыПропорциональногоОтнесенияНДСНаКредит.Регистратор.ПоДаннымПрошлогоГода, НЕОПРЕДЕЛЕНО) КАК ПоДаннымПрошлогоГода
	               |ИЗ
	               |	РегистрСведений.КоэффициентыПропорциональногоОтнесенияНДСНаКредит КАК КоэффициентыПропорциональногоОтнесенияНДСНаКредит
	               |ГДЕ
	               |	КоэффициентыПропорциональногоОтнесенияНДСНаКредит.Организация = &Организация
	               |	И КоэффициентыПропорциональногоОтнесенияНДСНаКредит.СпецРежимНалогообложения = &СпецРежимНалогообложения
	               |	И КоэффициентыПропорциональногоОтнесенияНДСНаКредит.Период = &Период";
				                                    
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		// если запись в регистр сделал не правильный документ - не будем его переоформлять
		Если Выборка.ПоДаннымПрошлогоГода = Неопределено Тогда
			СтрокаСообщения = НСтр("ru='Невозможно установить коэффициент пропорционального НДС на следующий год!
|Запись в регистр сформирована не документом ""Установка коэффициента пропорционального НДС""';uk='Неможливо встановити коефіцієнт пропорційного ПДВ на наступний рік!
|Запис у регістр сформований не документом ""Встановлення коефіцієнта пропорційного ПДВ""'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщения);
						   
			Возврат;			  	 
		
		КонецЕсли;
		
		// проверим, нужно ли переоформлять этот документ
		Если    Выборка.ПоДаннымПрошлогоГода = Истина 
			И  Выборка.Коэффициент 				= СтруктураШапкиДокумента.Коэффициент/100
			И  Выборка.СуммаОблагаемыхОпераций 	= СтруктураШапкиДокумента.СуммаОблагаемыхОпераций
			И  Выборка.СуммаВсехОпераций		= СтруктураШапкиДокумента.СуммаВсехОпераций Тогда
		
			возврат;	
		
		КонецЕсли;
		
		Док = Выборка.Регистратор.ПолучитьОбъект();
		
	Иначе
		
		Док = Документы.УстановкаКоэффициентаПропорциональногоОтнесенияНДСНаКредит.СоздатьДокумент();
		
	КонецЕсли;
				   
	Док.Дата 		= НачалоГода(ДобавитьМесяц(СтруктураШапкиДокумента.Дата, 12));
	Док.Организация = СтруктураШапкиДокумента.Организация;
	Док.СпецРежимНалогообложения = СтруктураШапкиДокумента.СпецРежимНалогообложения;
	Док.Ответственный = СтруктураШапкиДокумента.Ответственный;
	Док.Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Сформирован автоматически документом %1';uk='Сформований автоматично документом %1'"), СтруктураШапкиДокумента.Ссылка);
	Док.МесяцРасчета = 12;
	Док.ПоДаннымПрошлогоГода = Истина;
	Док.СуммаОблагаемыхОпераций = СтруктураШапкиДокумента.СуммаОблагаемыхОпераций;
	Док.СуммаВсехОпераций = СтруктураШапкиДокумента.СуммаВсехОпераций;
	Док.Коэффициент = СтруктураШапкиДокумента.Коэффициент / 100;
	
	Попытка
		Док.Записать(РежимЗаписиДокумента.Проведение);
	Исключение
		СтрокаСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Невозможно установить коэффициент пропорционального НДС на следующий год! 
| ошибка: %1';uk='Неможливо встановити коефіцієнт пропорційного ПДВ на наступний рік! 
| помилка: %1'"), ОписаниеОшибки());
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщения);
	КонецПопытки;
	
КонецПроцедуры

Процедура ЗакрытиеРегистраСтоимостьПриобретенияОСПропорциональноОблагаемыхНДС(СтруктураШапкиДокумента, Отказ, Заголовок)

	// закроем остатки в регистре, по ОС, которые эксплуатируются более 36 месяцев
	
	НаборДвижений = Движения.СтоимостьПриобретенияОСПропорциональноОблагаемыхНДС;
	ТаблицаДвижений = НаборДвижений.ВыгрузитьКолонки();
	
	Запрос = новый Запрос();
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СпецРежимНалогообложения", СпецРежимНалогообложения);
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("ДатаНачалаИспользования", НачалоГода(ДобавитьМесяц(Дата, - 36)));
	
    Запрос.Текст = "ВЫБРАТЬ
                   |	СтоимостьПриобретенияОСПропорциональноОблагаемыхНДСОстатки.НеоборотныйАктив,
                   |	СтоимостьПриобретенияОСПропорциональноОблагаемыхНДСОстатки.ДатаНачалаИспользования,
                   |	СтоимостьПриобретенияОСПропорциональноОблагаемыхНДСОстатки.ДатаФормированияКредита,
                   |	СтоимостьПриобретенияОСПропорциональноОблагаемыхНДСОстатки.БазаНДСОстаток КАК БазаНДС,
                   |	СтоимостьПриобретенияОСПропорциональноОблагаемыхНДСОстатки.СуммаНДСОстаток  КАК СуммаНДС,
                   |	СтоимостьПриобретенияОСПропорциональноОблагаемыхНДСОстатки.СуммаНДСПропорциональноКредитОстаток  КАК СуммаНДСПропорциональноКредит
                   |ИЗ
                   |	РегистрНакопления.СтоимостьПриобретенияОСПропорциональноОблагаемыхНДС.Остатки(&Период, ДатаНачалаИспользования < &ДатаНачалаИспользования И Организация = &Организация И СпецРежимНалогообложения = &СпецРежимНалогообложения) КАК СтоимостьПриобретенияОСПропорциональноОблагаемыхНДСОстатки
				   |";
		
	ОбщегоНазначенияБПВызовСервера.ЗагрузитьВТаблицуЗначений(Запрос.Выполнить().Выгрузить(), ТаблицаДвижений);
	ТаблицаДвижений.ЗаполнитьЗначения(Организация, "Организация");
	ТаблицаДвижений.ЗаполнитьЗначения(СпецРежимНалогообложения, "СпецРежимНалогообложения");
	
	Если НЕ Отказ И ТаблицаДвижений.Количество() > 0 Тогда
			
		НаборДвижений.мПериод          = Дата;
		НаборДвижений.мТаблицаДвижений = ТаблицаДвижений;
			
		Движения.СтоимостьПриобретенияОСПропорциональноОблагаемыхНДС.ВыполнитьРасход();
		Движения.СтоимостьПриобретенияОСПропорциональноОблагаемыхНДС.Записать();
			
	КонецЕсли;
	
КонецПроцедуры

Процедура ДвиженияПоРегиструОжидаемогоИПодтвержденногоНДСПродаж(СтруктураШапкиДокумента, Отказ, Заголовок)

	НаборДвижений = Движения.ОжидаемыйИПодтвержденныйНДСПродаж;
	ТаблицаДвижений = НаборДвижений.ВыгрузитьКолонки();
	
	Запрос = новый Запрос();
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
    Запрос.Текст = "ВЫБРАТЬ
                   |	ДокП2.СтавкаНДС КАК СтавкаНДС,
				   |	СУММА(-ДокП2.ИзменениеСуммыНДС) КАК СуммаНДС
                   |ИЗ
                   |	Документ.ПерерасчетПропорциональногоНДСпоТоварамИОС.НалоговыеДокументы КАК НалоговыеДокументы
                   |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Приложение2КНалоговойНакладной.Услуги КАК ДокП2
                   |		ПО (ДокП2.Ссылка = НалоговыеДокументы.П2)
                   |ГДЕ
                   |	НалоговыеДокументы.Ссылка = &Ссылка
                   |
                   |СГРУППИРОВАТЬ ПО
                   |	ДокП2.СтавкаНДС";
		
	ОбщегоНазначенияБПВызовСервера.ЗагрузитьВТаблицуЗначений(Запрос.Выполнить().Выгрузить(), ТаблицаДвижений);
	
	ТаблицаДвижений.ЗаполнитьЗначения(Организация, "Организация");
	ТаблицаДвижений.ЗаполнитьЗначения(Перечисления.СобытияОжидаемыйИПодтвержденныйНДСПродаж.УсловнаяПродажаВозврат, "СобытиеНДС");
	ТаблицаДвижений.ЗаполнитьЗначения(Перечисления.КодыОперацийОжидаемыйИПодтвержденныйНДСПродаж.ОжидаемыйНДС, "КодОперации");
	
	////Если РегламентированнаяОтчетность.ИДКонфигурации() = "УПП" Тогда
	////	ТаблицаДвижений.ЗаполнитьЗначения(Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_Облагаемая, "НалоговоеНазначение");
	////КонецЕсли;		
	
	Если НЕ Отказ И ТаблицаДвижений.Количество() > 0 Тогда
			
		НаборДвижений.мПериод          = Дата;
		НаборДвижений.мТаблицаДвижений = ТаблицаДвижений;
			
		Движения.ОжидаемыйИПодтвержденныйНДСПродаж.ВыполнитьПриход();
		Движения.ОжидаемыйИПодтвержденныйНДСПродаж.Записать();
			
	КонецЕсли;	
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////

Процедура ПроверитьЧтоДокументВПериодеОдин(СтруктураШапкиДокумента, Отказ, Заголовок)
	
	Запрос = Новый Запрос();
	
	Запрос.УстановитьПараметр("НачПериода", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("КонПериода", КонецМесяца(Дата));
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СпецРежимНалогообложения", СпецРежимНалогообложения);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПерерасчетПропорциональногоНДСпоТоварамИОС.Ссылка
	               |ИЗ
	               |	Документ.ПерерасчетПропорциональногоНДСпоТоварамИОС КАК ПерерасчетПропорциональногоНДСпоТоварамИОС
	               |ГДЕ
	               |	ПерерасчетПропорциональногоНДСпоТоварамИОС.Дата МЕЖДУ &НачПериода И  &КонПериода
	               |	И ПерерасчетПропорциональногоНДСпоТоварамИОС.Проведен
	               |	И ПерерасчетПропорциональногоНДСпоТоварамИОС.Организация = &Организация
				   |	И ПерерасчетПропорциональногоНДСпоТоварамИОС.СпецРежимНалогообложения = &СпецРежимНалогообложения
	               |	И НЕ ПерерасчетПропорциональногоНДСпоТоварамИОС.Ссылка = &Ссылка";
	
	Если НЕ Запрос.Выполнить().Пустой() Тогда
		ТекстСообщения = НСтр("ru='В данном периоде по данной организации уже имеется проведенный документ ""Перерасчет пропорционального НДС по товарам и ОС""!';uk='У поточному періоді по даній організації вже існує інший проведенй документ ""Перерахунок пропорційного ПДВ за товарами та ОЗ""'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, , , Отказ);
	КонецЕсли;
	
КонецПроцедуры

Функция ПодготовитьТаблицуПоАвансамПоставщикам(РезультатЗапросаПоАвансамПоставщикам, СтруктураШапкиДокумента)
	
	ТаблицаПоАвансамПоставщикам = РезультатЗапросаПоАвансамПоставщикам.Выгрузить();

	ТаблицаПоАвансамПоставщикам.Колонки.Добавить("ДляХозяйственнойДеятельности");
	ТаблицаПоАвансамПоставщикам.ЗаполнитьЗначения(Истина, "ДляХозяйственнойДеятельности");
	
	ТаблицаПоАвансамПоставщикам.Колонки.Добавить("ВидДеятельностиНДС");
	ТаблицаПоАвансамПоставщикам.ЗаполнитьЗначения(Перечисления.ВидыДеятельностиНДС.ПропорциональноОблагаемая, "ВидДеятельностиНДС");
	
	ТаблицаПоАвансамПоставщикам.Колонки.Добавить("Событие");
	ТаблицаПоАвансамПоставщикам.Колонки.Добавить("СобытиеНДС");
	
	ТаблицаПоАвансамПоставщикам.Колонки.Добавить("СуммаНДСПропорционально");
	
	Для каждого Строка Из ТаблицаПоАвансамПоставщикам Цикл
		
		Если Строка.РасчетыВозврат = Перечисления.РасчетыВозврат.Расчеты Тогда
			Строка.Событие    = Перечисления.СобытияПриобретенияНалоговыйУчет.ОплатаПоставщику;
			Строка.СобытиеНДС = Перечисления.СобытияОжидаемыйИПодтвержденныйНДСПриобретений.Поступление;
		Иначе
			Строка.Событие = Перечисления.СобытияПриобретенияНалоговыйУчет.ВозвратОплатыПоставщиком;
			Строка.СобытиеНДС = Перечисления.СобытияОжидаемыйИПодтвержденныйНДСПриобретений.Возврат;
		КонецЕсли;	
		
		Строка.СуммаНДСПропорционально = Строка.НДСПропорциональноКредит - Строка.НДСПропорциональноКредитДоПерерасчета;
	
	КонецЦикла;
	
	Возврат ТаблицаПоАвансамПоставщикам;
	
КонецФункции

Функция ПодготовитьТаблицуПоПерерасчетуОС(РезультатЗапросаПоПерерасчету, СтруктураШапкиДокумента)

	ТаблицаПоПерерасчету = РезультатЗапросаПоПерерасчету.Выгрузить();

	Возврат ТаблицаПоПерерасчету;

КонецФункции // ПодготовитьТаблицуТоваров()

Процедура ПроверитьЗаполнениеТабличнойЧастиПоПерерасчетуОС(Отказ)

	Для Каждого Строка Из ПараметрыПерерасчетаОС Цикл
		
		Поле = "ПараметрыПерерасчетаОС[" + Формат(Строка.НомерСтроки - 1, "ЧН=0; ЧГ=") + "].ДатаНачалаИспользования";								
		
		Если Не ЗначениеЗаполнено(Строка.ДатаНачалаИспользования) Тогда

			СтрокаНачалаСообщенияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='В строке номер ""%1"" табличной части ""Перерасчет ОС (за 1, 2, 3 год использования)"": ';uk='У рядку номер ""%1"" табличної частини ""Перерахунок ОЗ (за 1, 2, 3 рік використання)"": '"), СокрЛП(Строка.НомерСтроки));
			
			СтрокаСообщения = НСтр("ru='Не заполнено значение реквизита ""Начало использования""!';uk='Не заповнене значення реквізиту ""Початок використання""!'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаНачалаСообщенияОбОшибке + СтрокаСообщения,ЭтотОбъект, Поле, "Объект",Отказ);
			
		ИначеЕсли  НачалоГода(Строка.ДатаНачалаИспользования) < НачалоГода(ДобавитьМесяц(Дата, -36)) Тогда
				
				СтрокаНачалаСообщенияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='В строке номер ""%1"" табличной части ""Перерасчет ОС (за 1, 2, 3 год использования)"": ';uk='У рядку номер ""%1"" табличної частини ""Перерахунок ОЗ (за 1, 2, 3 рік використання)"": '"), СокрЛП(Строка.НомерСтроки));
				
				СтрокаСообщения = НСтр("ru='Указано некорректное значение реквизита ""Начало использования"". Прошло уже более 3-х лет после года ввода в эксплуатацию!';uk='Зазначене некоректне значення реквізиту ""Початок використання"". Пройшло вже більше 3-х років після року введення у експлуатацію!'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаНачалаСообщенияОбОшибке + СтрокаСообщения,ЭтотОбъект, Поле, "Объект",Отказ);
				
		ИначеЕсли  КонецГода(Строка.ДатаНачалаИспользования) > КонецГода(Дата) Тогда		
				
				СтрокаНачалаСообщенияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='В строке номер ""%1"" табличной части ""ОС, введенные в эксплуатацию в текущем году"": ';uk='У рядку номер ""%1"" табличної частини ""ОС, що введені у експлуатацію в поточному році"": '"), СокрЛП(Строка.НомерСтроки));
				
				СтрокаСообщения = НСтр("ru='Указано некорректное значение реквизита ""Начало использования"". Дата ввода в эксплуатацию не может быть позже конца текущего месяца!';uk='Зазначене некоректне значення реквізиту ""Початок використання"". Дата введення в експлуатацію не може бути більшою за кінець поточного місяця!'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаНачалаСообщенияОбОшибке + СтрокаСообщения,ЭтотОбъект, Поле, "Объект",Отказ);
				
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры // ПроверитьЗаполнениеТабличнойЧастиТовары()

Функция ПодготовитьТаблицуПоОстаткам15Счета(РезультатЗапросаПоОстаткам15Счета, СтруктураШапкиДокумента)

	ТаблицаПоОстаткам15Счета = РезультатЗапросаПоОстаткам15Счета.Выгрузить();

	Возврат ТаблицаПоОстаткам15Счета;

КонецФункции // ПодготовитьТаблицуТоваров()

// Процедура определяет параметры регл. учетной политики
//
Процедура ПодготовитьПараметрыУчетнойПолитики(СтруктураШапкиДокумента, Отказ, Заголовок)
	
	СтруктураШапкиДокумента.Вставить("ЕстьНалогНаПрибыль"             , УчетнаяПолитика.ПлательщикНалогаНаПрибыль(СтруктураШапкиДокумента.Организация, КонецМесяца(СтруктураШапкиДокумента.Дата)));
	СтруктураШапкиДокумента.Вставить("ЕстьНалогНаПрибыльДо2015"       , УчетнаяПолитика.ПлательщикНалогаНаПрибыльДо2015(СтруктураШапкиДокумента.Организация, КонецМесяца(СтруктураШапкиДокумента.Дата)));
	СтруктураШапкиДокумента.Вставить("ЕстьНДС"                        , УчетнаяПолитика.ПлательщикНДС(СтруктураШапкиДокумента.Организация, КонецМесяца(СтруктураШапкиДокумента.Дата)));
		
КонецПроцедуры // ПодготовитьПараметрыУчетнойПолитики

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Процедура - обработчик события "ОбработкаПроведения".
//
Процедура ОбработкаПроведения(Отказ, Режим)
 	
	// Сформируем структуру реквизитов шапки документа.
	СтруктураШапкиДокумента = ОбщегоНазначенияРед12.СформироватьСтруктуруШапкиДокумента(ЭтотОбъект);

	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначенияБПВызовСервера.ПредставлениеДокументаПриПроведении(Ссылка);
	
	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ДОКУМЕНТА
	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	Если РучнаяКорректировка Тогда
		Возврат;
	КонецЕсли;
	
	ПодготовитьПараметрыУчетнойПолитики(СтруктураШапкиДокумента, Отказ, Заголовок);

	ПроверитьЧтоДокументВПериодеОдин(СтруктураШапкиДокумента, Отказ, Заголовок);

	// Получим необходимые данные для проведения и проверки заполенения данные по табличной части "АвансыПоставщикам".
	СтруктураПолей = Новый Структура;
	СтруктураПростыхПолей = Новый Структура;
	СтруктураПолей.Вставить("Контрагент", 					"Контрагент");
	СтруктураПолей.Вставить("ДоговорКонтрагента", 			"ДоговорКонтрагента");
	СтруктураПолей.Вставить("Амортизируется", 			    "Амортизируется");
	СтруктураПолей.Вставить("СложныйНалоговыйУчет", 		"ДоговорКонтрагента.СложныйНалоговыйУчет");
	СтруктураПолей.Вставить("Сделка", 						"Сделка");
	СтруктураПолей.Вставить("РасчетыВозврат", 				"РасчетыВозврат");
	СтруктураПолей.Вставить("СтавкаНДС", 					"СтавкаНДС");
	СтруктураПолей.Вставить("НДСПропорциональноКредитДоПерерасчета", "НДСПропорциональноКредитДоПерерасчета");
	СтруктураПолей.Вставить("НДСПропорциональноКредит", 	"НДСПропорциональноКредит");
	СтруктураПолей.Вставить("СчетУчетаНДС", 				"СчетУчетаНДС");
	
	РезультатЗапросаПоАвансамПоставщикам 	= ОбщегоНазначенияРед12.СформироватьЗапросПоТабличнойЧасти(ЭтотОбъект, "АвансыПоставщикам", СтруктураПолей, СтруктураПростыхПолей);
	ТаблицаПоАвансамПоставщикам 		  	= ПодготовитьТаблицуПоАвансамПоставщикам(РезультатЗапросаПоАвансамПоставщикам, СтруктураШапкиДокумента);
		
	// Получим необходимые данные для проведения и проверки заполенения данные по табличной части "ПараметрыПерерасчетаОС".
	СтруктураПолей = Новый Структура;
	СтруктураПростыхПолей = Новый Структура;
	СтруктураПолей.Вставить("СуммаВсехОпераций", 			"СуммаВсехОпераций");
	СтруктураПолей.Вставить("СуммаОблагаемыхОпераций", 		"СуммаОблагаемыхОпераций");
	СтруктураПолей.Вставить("Коэффициент", 					"Коэффициент");
	СтруктураПолей.Вставить("СуммаПоставки", 				"СуммаПоставки");
	СтруктураПолей.Вставить("НДСПоставки", 					"НДСПоставки");
	СтруктураПолей.Вставить("НДСПропорциональноКредитДоПерерасчета", "НДСПропорциональноКредитДоПерерасчета");
	СтруктураПолей.Вставить("НДСПропорциональноКредит", 	"НДСПропорциональноКредит");
	СтруктураПолей.Вставить("СуммаКорректировкиНДС", 		"СуммаКорректировкиНДС");
	СтруктураПолей.Вставить("НеоборотныйАктив", 			"НеоборотныйАктив");
	СтруктураПолей.Вставить("ДатаНачалаИспользования", 		"ДатаНачалаИспользования");
	СтруктураПолей.Вставить("ДатаФормированияКредита", 		"ДатаФормированияКредита");
	СтруктураПолей.Вставить("СуммаКорректировкиБУ", 		"СуммаКорректировкиБУ");
	СтруктураПолей.Вставить("СуммаКорректировкиНУ", 		"СуммаКорректировкиНУ");
	СтруктураПолей.Вставить("ОССписан", 					"ОССписан");
	
	РезультатЗапросаПоПерерасчетуОС = ОбщегоНазначенияРед12.СформироватьЗапросПоТабличнойЧасти(ЭтотОбъект, "ПараметрыПерерасчетаОС", СтруктураПолей, СтруктураПростыхПолей);
	ТаблицаПоПерерасчетуОС 		  = ПодготовитьТаблицуПоПерерасчетуОС(РезультатЗапросаПоПерерасчетуОС, СтруктураШапкиДокумента);
	
	// Получим необходимые данные для проведения и проверки заполенения данные по табличной части "Остатки15Счета".
	СтруктураПолей = Новый Структура;
	СтруктураПростыхПолей = Новый Структура;
	СтруктураПолей.Вставить("Счет", 				 "Счет");
	СтруктураПолей.Вставить("НеоборотныйАктив", 	 "НеоборотныйАктив");
	СтруктураПолей.Вставить("НоменклатурнаяПозиция", "НоменклатурнаяПозиция");
	СтруктураПолей.Вставить("Склад", 				 "Склад");
	СтруктураПолей.Вставить("Партия", 				 "Партия");
	СтруктураПолей.Вставить("СуммаКорректировкиНДС", "СуммаКорректировкиНДС");
	СтруктураПолей.Вставить("КорректироватьПоНУ",    "КорректироватьПоНУ");
	
	РезультатЗапросаПоОстаткам15Счета = ОбщегоНазначенияРед12.СформироватьЗапросПоТабличнойЧасти(ЭтотОбъект, "Остатки15Счета", СтруктураПолей, СтруктураПростыхПолей);
	ТаблицаПоОстаткам15Счета 		  = ПодготовитьТаблицуПоОстаткам15Счета(РезультатЗапросаПоОстаткам15Счета, СтруктураШапкиДокумента);
	
	Если Не Отказ Тогда
		ДвиженияПоРегистрам(ТаблицаПоАвансамПоставщикам, ТаблицаПоПерерасчетуОС, ТаблицаПоОстаткам15Счета, СтруктураШапкиДокумента, Отказ, Заголовок);
	КонецЕсли;

	ПроведениеСервер.ПодготовитьНаборыЗаписейКЗаписиДвижений(ЭтотОбъект);
	
КонецПроцедуры // ОбработкаПроведения()

//Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
//	Дата = КонецМесяца(Дата);
//КонецПроцедуры // ПередЗаписью

Процедура ОбработкаУдаленияПроведения(Отказ)

	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	Движения.Записать();

КонецПроцедуры // ОбработкаУдаленияПроведения

Процедура ЗаполнитьПоДокументуОснованию(Основание)
	
	// Заполним реквизиты из стандартного набора по документу основанию.
	ЗаполнениеДокументов.ЗаполнитьПоОснованию(ЭтотОбъект, Основание);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ДанныеЗаполнения <> Неопределено И ТипДанныхЗаполнения <> Тип("Структура") 
		И Метаданные().ВводитсяНаОсновании.Содержит(ДанныеЗаполнения.Метаданные()) Тогда
		ЗаполнитьПоДокументуОснованию(ДанныеЗаполнения);
	КонецЕсли;
	
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	
	ФормироватьДокументУстановкиКоэффициентаНаСледующийГод = (Месяц(Дата) = 12);
	Дата = КонецМесяца(Дата);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	МассивНепроверяемыхРеквизитов = Новый Массив;

	ПлательщикНДС = УчетнаяПолитика.ПлательщикНДС(Организация, Дата);
	ПлательщикНалогаНаПрибыльДо2015  = УчетнаяПолитика.ПлательщикНалогаНаПрибыльДо2015(Организация, Дата);
	
	Если ПлательщикНалогаНаПрибыльДо2015 Тогда
		Если ЗначениеЗаполнено(СчетДт) Тогда
			ЕстьСубконтоСтатьяЗатратДоходов = Ложь;
			Для НомСубконто = 1 По 3 Цикл
				Если СчетДт.ВидыСубконто.Количество()<НомСубконто Тогда
					МассивНепроверяемыхРеквизитов.Добавить("СубконтоДт" + НомСубконто);
					Продолжить;
				КонецЕсли;
				
				ВидСубконто = СчетДт.ВидыСубконто[НомСубконто-1].ВидСубконто;
				Если НЕ (ВидСубконто = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат
					 ИЛИ ВидСубконто = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиДоходов
					 ИЛИ ВидСубконто = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиНеоперационныхРасходов) Тогда
					 МассивНепроверяемыхРеквизитов.Добавить("СубконтоДт" + НомСубконто);
				КонецЕсли;
				Если ВидСубконто = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат
					 ИЛИ ВидСубконто = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиНеоперационныхРасходов Тогда
					ЕстьСубконтоСтатьяЗатратДоходов = Истина;
				КонецЕсли;
			КонецЦикла;
			
			Если ЕстьСубконтоСтатьяЗатратДоходов Тогда
				 ЕстьОС   = Ложь;
				 ЕстьНМА  = Ложь;
				 ЕстьМНМА = Ложь;
				 Для Каждого СтрокаТЧ Из ПараметрыПерерасчетаОС Цикл
					 
					 Если Не ЕстьОС 
						 И ТипЗнч(СтрокаТЧ.НеоборотныйАктив) = Тип("СправочникСсылка.ОсновныеСредства") Тогда
					 	ЕстьОС = Истина;
						Продолжить;
					 КонецЕсли;
					 
					 Если Не ЕстьНМА 
						 И ТипЗнч(СтрокаТЧ.НеоборотныйАктив) = Тип("СправочникСсылка.НематериальныеАктивы") Тогда
					 	ЕстьНМА = Истина;
						Продолжить;
					 КонецЕсли;
					 
					 Если Не ЕстьМНМА 
						 И ТипЗнч(СтрокаТЧ.НеоборотныйАктив) = Тип("СправочникСсылка.Номенклатура") Тогда
					 	ЕстьМНМА = Истина;
						Продолжить;
					 КонецЕсли;
				 
				 КонецЦикла;
				 
				 Если ЕстьОС Тогда
				 	ПроверяемыеРеквизиты.Добавить("СтатьяОС");
				 КонецЕсли;
				 Если ЕстьНМА Тогда
				 	ПроверяемыеРеквизиты.Добавить("СтатьяНМА");
				 КонецЕсли;
				 Если ЕстьМНМА Тогда
				 	ПроверяемыеРеквизиты.Добавить("СтатьяМНМА");
				 КонецЕсли;
				
			КонецЕсли;	
			
		Иначе 
			МассивНепроверяемыхРеквизитов.Добавить("СубконтоДт1");
			МассивНепроверяемыхРеквизитов.Добавить("СубконтоДт2");
			МассивНепроверяемыхРеквизитов.Добавить("СубконтоДт3");
		КонецЕсли;
		
	Иначе 
		МассивНепроверяемыхРеквизитов.Добавить("НалоговоеНазначениеДоходовИЗатрат");
		МассивНепроверяемыхРеквизитов.Добавить("СубконтоДт1");
		МассивНепроверяемыхРеквизитов.Добавить("СубконтоДт2");
		МассивНепроверяемыхРеквизитов.Добавить("СубконтоДт3");
	КонецЕсли; 
	
	ПроверитьЗаполнениеТабличнойЧастиПоПерерасчетуОС(Отказ);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);

	Если Дата < '20160101' Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СчетНДСУсловнаяПродажа");	
	КонецЕсли;
	
	РеквизитыП2 = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(НалоговыеДокументы.ВыгрузитьКолонку("П2"), "Проведен, СуммаНДСДокумента");
	
	ИтогКОрректировкиНДС = 0;
	Для каждого СтрокаТД Из НалоговыеДокументы Цикл
		
		Строка = РеквизитыП2[СтрокаТД.П2];
		
		Префикс = "НалоговыеДокументы[" + Формат(СтрокаТД.НомерСтроки - 1, "ЧН=0; ЧГ=") + "].";
		СтрокаНачалаСообщенияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='В строке номер ""%1"" табличной части ""Налоговые документы"": ';uk='У рядку номер ""%1"" табличної частини ""Податкові документи"": '"), СокрЛП(СтрокаТД.НомерСтроки));
		
		Если НЕ Строка.Проведен = Истина Тогда
			
			ТекстСообщения = НСтр("ru='Указан непроведенный документ ""Корректировка к Сводной налоговой накладной"". Его необходимо провести вручную!';uk='Вказаний непроведенный документ ""Коригування до Зведеної податкової накладної"". Його необхідно провести вручну!'");
			
			Поле = Префикс + "П2";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаНачалаСообщенияОбОшибке + ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
			
		КонецЕсли;
		
		ИтогКОрректировкиНДС = ИтогКОрректировкиНДС + Строка.СуммаНДСДокумента;
		
	КонецЦикла;
	
	ИтогКОрректировкиНДС = -ИтогКорректировкиНДС;
	
	Если НЕ ИтогКОрректировкиНДС = СуммаКорректировкиНДС Тогда
		
		СообщениеОбОшибке = НСтр("ru='Сумма корректировки НДС на закладке ""Налоговые документы"" не совпадает с суммой корректировки НДС, указанной на закладке ""Перерасчет товаров/услуг и ОС""!';uk='Сума коригування ПДВ на закладці ""Податкові документи"" не збігається з сумою коригування ПДВ, яка зазначена на закладці ""Перерахунок товарів/послуг та ОЗ""!'");
		                                                      // разрешаем проводить!!!
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке, ЭтотОбъект, "СуммаКорректировкиНДС", "Объект");
	
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)

	Дата = НачалоДня(ОбщегоНазначенияБП.ПолучитьРабочуюДату());
	Ответственный = Пользователи.ТекущийПользователь();

КонецПроцедуры

#КонецЕсли

