#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПроцедурыИФункцииПечати
	
// Заполняет список команд печати.
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Бригадный наряд
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "БригадныйНаряд";
	КомандаПечати.Представление = НСтр("ru='Бригадный наряд';uk='Бригадний наряд'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.СписокФорм    = "ФормаДокумента,ФормаСписка";
	
	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Реестр";
	КомандаПечати.Представление = НСтр("ru='Реестр документов';uk='Реєстр документів'");
	КомандаПечати.ЗаголовокФормы= НСтр("ru='Реестр документов ""Бригадный наряд""';uk='Реєстр документів ""Бригадний наряд""'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм    = "ФормаСписка";
	КомандаПечати.Порядок       = 100;
		
КонецПроцедуры

Функция ПечатьБригадныйНаряд(МассивОбъектов, ОбъектыПечати, ПараметрыВывода)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ИНАГРО_БригадныйНаряд";
	
	Для Каждого Ссылка Из МассивОбъектов Цикл
		
		// запоминаем области макета
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ИНАГРО_БригадныйНаряд.ПФ_MXL_БригадныйНаряд");
		
		// печать производится на языке, указанном в настройках пользователя
		КодЯзыкаПечать = ПараметрыВывода.КодЯзыкаДляМногоязычныхПечатныхФорм;
		Макет.КодЯзыкаМакета = КодЯзыкаПечать;
		
		ЗапросШапка = Новый Запрос;
		ЗапросШапка.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ИНАГРО_БригадныйНаряд.Организация,
		|	ИНАГРО_БригадныйНаряд.ПодразделениеОрганизации,
		|	ИНАГРО_БригадныйНаряд.Ответственный,
		|	ИНАГРО_БригадныйНаряд.ВидРасчета,
		|	ИНАГРО_БригадныйНаряд.Номер,
		|	ИНАГРО_БригадныйНаряд.Дата,
		|	ИНАГРО_БригадныйНаряд.ПериодРегистрации
		|ИЗ
		|	Документ.ИНАГРО_БригадныйНаряд КАК ИНАГРО_БригадныйНаряд
		|ГДЕ
		|	ИНАГРО_БригадныйНаряд.Ссылка = &ТекущийДокумент";
		
		ЗапросШапка.УстановитьПараметр("ТекущийДокумент", Ссылка);

		Шапка = ЗапросШапка.Выполнить().Выбрать();
		Шапка.Следующий();
		
		ЗапросТЧ = Новый Запрос;
		ЗапросТЧ.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		                 |	ИНАГРО_БригадныйНаряд.НомерСтроки,
		                 |	ИНАГРО_БригадныйНаряд.Сотрудник,
		                 |	ИНАГРО_БригадныйНаряд.ОтработаноЧасов КАК Часов,
		                 |	ИНАГРО_БригадныйНаряд.КТУ,
		                 |	ИНАГРО_БригадныйНаряд.Результат,
		                 |	ИНАГРО_БригадныйНаряд.ОтработаноДней,
		                 |	ИНАГРО_БригадныйНаряд.ДатаНачала,
		                 |	ИНАГРО_БригадныйНаряд.ДатаОкончания
		                 |ИЗ
		                 |	Документ.ИНАГРО_БригадныйНаряд.Начисления КАК ИНАГРО_БригадныйНаряд
		                 |ГДЕ
		                 |	ИНАГРО_БригадныйНаряд.Ссылка = &ТекущийДокумент";
		
		ЗапросТЧ.УстановитьПараметр("ТекущийДокумент", Ссылка);

		ТаблицаТЧ = ЗапросТЧ.Выполнить().Выгрузить();
		
		СведенияОПоставщике = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Шапка.Организация, Шапка.Дата,,,КодЯзыкаПечать);
		
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
		ОбластьМакета.Параметры.Заполнить(Шапка);
		ОбластьМакета.Параметры.Организация = СведенияОПоставщике.ПолноеНаименование;
		
		ОбластьМакета.Параметры.МесяцНачисления = Формат(Шапка.ПериодРегистрации, "ДФ='MMMM yyyy'"+";Л="+КодЯзыкаПечать);
		ОбластьМакета.Параметры.ТекстЗаголовка = ОбщегоНазначенияБПВызовСервера.СформироватьЗаголовокДокумента(Шапка, НСтр("ru='Бригадный наряд';uk='Бригадний наряд'",КодЯзыкаПечать),КодЯзыкаПечать);
		ТабДокумент.Вывести(ОбластьМакета);
		
		ОбластьСтроки = Макет.ПолучитьОбласть("Строка");
		
		НомерСтроки = 0;
		
		Для Каждого СтрокаТабличнойЧасти Из ТаблицаТЧ Цикл	
			
			НомерСтроки = НомерСтроки + 1;
			
			ОбластьСтроки.Параметры.Заполнить(СтрокаТабличнойЧасти);
			ОбластьСтроки.Параметры.НомерСтроки = НомерСтроки;
			ОбластьСтроки.Параметры.ДатаНачала = Формат(СтрокаТабличнойЧасти.ДатаНачала,"ДФ=dd.MM.yyyy");
			ОбластьСтроки.Параметры.ДатаОкончания = Формат(СтрокаТабличнойЧасти.ДатаОкончания,"ДФ=dd.MM.yyyy");
			ТабДокумент.Вывести(ОбластьСтроки);
			
		КонецЦикла;
		
		Если ТаблицаТЧ <> Неопределено Тогда
			
			Всего  = ТаблицаТЧ.Итог("Результат");
			ВсегоЧасов  = ТаблицаТЧ.Итог("Часов");
			ВсегоДней  = ТаблицаТЧ.Итог("ОтработаноДней");
			
		Иначе
			
			Всего  = 0;
			ВсегоЧасов  = 0;
			ВсегоДней  = 0;
			
		КонецЕсли;
		
		ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
		ОбластьМакета.Параметры.Всего = ОбщегоНазначенияБПВызовСервера.ФорматСумм(Всего);
		ОбластьМакета.Параметры.ВсегоЧасов = ОбщегоНазначенияБПВызовСервера.ФорматСумм(ВсегоЧасов);
		ОбластьМакета.Параметры.ВсегоДней = ОбщегоНазначенияБПВызовСервера.ФорматСумм(ВсегоДней);
		ОбластьМакета.Параметры.Ответственный = Шапка.Ответственный;
		ТабДокумент.Вывести(ОбластьМакета);
			
	КонецЦикла;	
	
	Возврат ТабДокумент;
	
КонецФункции 
	
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Устанавливаем признак доступности печати покомплектно.
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	// Проверяем, нужно ли для макета формировать табличный документ.
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "БригадныйНаряд")  Тогда
		
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "БригадныйНаряд", НСтр("ru='Бригадный наряд';uk='Бригадний наряд'"), 
		ПечатьБригадныйНаряд(МассивОбъектов, ОбъектыПечати, ПараметрыВывода), , "Документ.ИНАГРО_БригадныйНаряд.ПФ_MXL_БригадныйНаряд", ,Истина);
		
	КонецЕсли; 	

КонецПроцедуры

Функция ПолучитьДополнительныеРеквизитыДляРеестра() Экспорт
	
	Результат = Новый Структура;	
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли
