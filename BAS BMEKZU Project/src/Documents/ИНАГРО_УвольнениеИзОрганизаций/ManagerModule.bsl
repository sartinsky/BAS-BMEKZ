#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПроцедурыИФункцииПечати

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Типовая форма П5
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "УвольнениеИзОрганизации_П4";
	КомандаПечати.Представление = НСтр("ru='Увольнение из организации';uk='Звільнення з організації'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.СписокФорм    = "ФормаДокумента,ФормаСписка";
	
	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Реестр";
	КомандаПечати.Представление = НСтр("ru='Реестр документов';uk='Реєстр документів'");
	КомандаПечати.ЗаголовокФормы= НСтр("ru='Реестр документов ""Увольнение из организации""';uk='Реєстр документів ""Звільнення з організації""'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм    = "ФормаСписка";
	КомандаПечати.Порядок       = 100;

КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Устанавливаем признак доступности печати покомплектно.
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	// Проверяем, нужно ли для макета формировать табличный документ.
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "УвольнениеИзОрганизации_П4")  Тогда
		
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "УвольнениеИзОрганизации_П4", НСтр("ru='Увольнение из организации';uk='Звільнення з організації'"), 
		ПечатьДоговорНаВыполнениеРаботСФизЛицом(МассивОбъектов, ОбъектыПечати, ПараметрыВывода), , "Документ.ИНАГРО_УвольнениеИзОрганизаций.ПФ_MXL_П4_от_05_12_2008", ,Истина);
		
	КонецЕсли; 	
	
КонецПроцедуры
	
Функция СформироватьЗапросДляПечати(Режим, Ссылка, масСотрудники = Неопределено)
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ДокументСсылка", Ссылка);
	Запрос.УстановитьПараметр("Руководитель", Перечисления.ОтветственныеЛицаОрганизаций.Руководитель);
	Запрос.УстановитьПараметр("ДатаДокумента", Ссылка.Дата);
	Запрос.УстановитьПараметр("Организация", Ссылка.Организация);
	Запрос.УстановитьПараметр("Сотрудники", масСотрудники);
	
	Если Режим = "ПоРеквизитамДокумента" Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ИНАГРО_УвольнениеИзОрганизаций.Номер КАК НомерДок,
		|	ИНАГРО_УвольнениеИзОрганизаций.Дата КАК ДатаДок,
		|	ОтветственныеЛицаОрганизацийСрезПоследних.Должность КАК ДолжностьРуководителя,
		|	ИНАГРО_УвольнениеИзОрганизаций.Организация.НаименованиеПолное КАК НазваниеОрганизации,
		|	ВЫБОР
		|		КОГДА ФИОФизическихЛицСрезПоследних.ФизическоеЛицо ЕСТЬ NULL
		|			ТОГДА ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо.Наименование
		|		ИНАЧЕ ФИОФизическихЛицСрезПоследних.Фамилия + ВЫБОР
		|				КОГДА ПОДСТРОКА(ФИОФизическихЛицСрезПоследних.Имя, 1, 1) <> """"
		|					ТОГДА "" "" + ПОДСТРОКА(ФИОФизическихЛицСрезПоследних.Имя, 1, 1) + "".""
		|				ИНАЧЕ """"
		|			КОНЕЦ + ВЫБОР
		|				КОГДА ПОДСТРОКА(ФИОФизическихЛицСрезПоследних.Отчество, 1, 1) <> """"
		|					ТОГДА "" "" + ПОДСТРОКА(ФИОФизическихЛицСрезПоследних.Отчество, 1, 1) + "".""
		|				ИНАЧЕ """"
		|			КОНЕЦ
		|	КОНЕЦ КАК ФИОРуководителя
		|ИЗ
		|	Документ.ИНАГРО_УвольнениеИзОрганизаций КАК ИНАГРО_УвольнениеИзОрганизаций
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(&ДатаДокумента, ) КАК ОтветственныеЛицаОрганизацийСрезПоследних
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизическихЛиц.СрезПоследних(&ДатаДокумента, ) КАК ФИОФизическихЛицСрезПоследних
		|			ПО ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо = ФИОФизическихЛицСрезПоследних.ФизическоеЛицо
		|		ПО (ОтветственныеЛицаОрганизацийСрезПоследних.ОтветственноеЛицо = &Руководитель)
		|			И ИНАГРО_УвольнениеИзОрганизаций.Организация = ОтветственныеЛицаОрганизацийСрезПоследних.СтруктурнаяЕдиница
		|ГДЕ
		|	ИНАГРО_УвольнениеИзОрганизаций.Ссылка = &ДокументСсылка";
				
	ИначеЕсли Режим = "ПоТабличнойЧастиДокумента" Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВЫБОР
		|		КОГДА ФИОФизЛицСрезПоследних.Фамилия ЕСТЬ NULL
		|			ТОГДА УвольненияИзОрганизацийРаботники.Сотрудник.ФизическоеЛицо.Наименование
		|		ИНАЧЕ ФИОФизЛицСрезПоследних.Фамилия + "" "" + ФИОФизЛицСрезПоследних.Имя + "" "" + ФИОФизЛицСрезПоследних.Отчество
		|	КОНЕЦ КАК Работник,
		|	УвольненияИзОрганизацийРаботники.НомерСтроки КАК НомерСтроки,
		|	УвольненияИзОрганизацийРаботники.Сотрудник КАК Сотрудник,
		|	УвольненияИзОрганизацийРаботники.ДатаУвольнения КАК ДатаУвольнения,
		|	УвольненияИзОрганизацийРаботники.ОснованиеУвольнения.СтатьяЗакона КАК ОснованиеУвольнения,
		|	УвольненияИзОрганизацийРаботники.ПричинаУвольнения КАК ПричинаУвольнения,
		|	УвольненияИзОрганизацийРаботники.Сотрудник.ИНАГРО_ТабельныйНомер КАК ТабельныйНомер,
		|	Работники.ПодразделениеОрганизации КАК Подразделение,
		|	Работники.Должность КАК Должность
		|ИЗ
		|	Документ.ИНАГРО_УвольнениеИзОрганизаций.РаботникиОрганизации КАК УвольненияИзОрганизацийРаботники
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизическихЛиц.СрезПоследних(
		|				&ДатаДокумента,
		|				ФизическоеЛицо В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						УвольненияИзОрганизацийРаботники.Сотрудник
		|					ИЗ
		|						Документ.ИНАГРО_УвольнениеИзОрганизаций.РаботникиОрганизации КАК УвольненияИзОрганизацийРаботники
		|					ГДЕ
		|						УвольненияИзОрганизацийРаботники.Ссылка = &ДокументСсылка)) КАК ФИОФизЛицСрезПоследних
		|		ПО УвольненияИзОрганизацийРаботники.Сотрудник.ФизическоеЛицо = ФИОФизЛицСрезПоследних.ФизическоеЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			СписокДат.Сотрудник КАК Сотрудник,
		|			СписокДат.Сотрудник.ФизическоеЛицо КАК ФизЛицо,
		|			РаботникиОрганизации.ПодразделениеОрганизации.Наименование КАК ПодразделениеОрганизации,
		|			РаботникиОрганизации.Должность.Наименование КАК Должность,
		|			РаботникиОрганизации.Сотрудник.ИНАГРО_ТабельныйНомер КАК ТабельныйНомер
		|		ИЗ
		|			(ВЫБРАТЬ
		|				РаботникиВнутри.Сотрудник КАК Сотрудник,
		|				РаботникиВнутри.Сотрудник.ФизическоеЛицо КАК ФизЛицо,
		|				МАКСИМУМ(РаботникиВнутри.Период) КАК ДатаПоследнегоИзменения
		|			ИЗ
		|				РегистрСведений.ИНАГРО_РаботникиОрганизаций КАК РаботникиВнутри
		|					ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИНАГРО_УвольнениеИзОрганизаций.РаботникиОрганизации КАК Док
		|					ПО РаботникиВнутри.Сотрудник = Док.Сотрудник
		|						И (РаботникиВнутри.Организация = &Организация)
		|						И РаботникиВнутри.Период < Док.ДатаУвольнения
		|			ГДЕ
		|				Док.Ссылка = &ДокументСсылка
		|			
		|			СГРУППИРОВАТЬ ПО
		|				РаботникиВнутри.Сотрудник,
		|				РаботникиВнутри.Сотрудник.ФизическоеЛицо) КАК СписокДат
		|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИНАГРО_РаботникиОрганизаций КАК РаботникиОрганизации
		|				ПО СписокДат.Сотрудник = РаботникиОрганизации.Сотрудник
		|					И СписокДат.ДатаПоследнегоИзменения = РаботникиОрганизации.Период) КАК Работники
		|		ПО УвольненияИзОрганизацийРаботники.Сотрудник = Работники.Сотрудник " +
		?(масСотрудники = Неопределено, "", " И (УвольненияИзОрганизацийРаботники.Сотрудник В(&Сотрудники))") + "
		|ГДЕ
		|	УвольненияИзОрганизацийРаботники.Ссылка = &ДокументСсылка";

	Иначе
		Возврат Неопределено
	КонецЕсли;
	
	Возврат Запрос.Выполнить();	
	
КонецФункции

Функция ПечатьДоговорНаВыполнениеРаботСФизЛицом(МассивОбъектов, ОбъектыПечати, ПараметрыВывода)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_УвольнениеИзОрганизации_П4";
		
	масСотрудники = Неопределено;
	Если ТипЗнч(МассивОбъектов[0]) = Тип("СправочникСсылка.Сотрудники") Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ИНАГРО_РаботникиОрганизацийСрезПоследних.Регистратор КАК Документ
		|ИЗ
		|	РегистрСведений.ИНАГРО_РаботникиОрганизаций.СрезПоследних(
		|			,
		|			Сотрудник В (&МассивОбъектов)
		|				И ПричинаИзмененияСостояния = ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)) КАК ИНАГРО_РаботникиОрганизацийСрезПоследних";
		
		Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
		масСотрудники = МассивОбъектов;
		МассивОбъектов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Документ");
	КонецЕсли;
	Для каждого Ссылка Из МассивОбъектов Цикл
		
		ВыборкаДляШапки   = СформироватьЗапросДляПечати("ПоРеквизитамДокумента", Ссылка).Выбрать();
		ВыборкаРаботники = СформироватьЗапросДляПечати("ПоТабличнойЧастиДокумента", Ссылка, масСотрудники).Выбрать();
		СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Ссылка.Организация, Ссылка.Дата);
		
		// запоминаем области макета
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ИНАГРО_УвольнениеИзОрганизаций.ПФ_MXL_П4_от_05_12_2008");
		
		// печать производится на языке, указанном в настройках пользователя
		КодЯзыкаПечать = ПараметрыВывода.КодЯзыкаДляМногоязычныхПечатныхФорм;
		Макет.КодЯзыкаМакета = КодЯзыкаПечать;

		ОбластьМакета = Макет.ПолучитьОбласть("Форма");
		
		// выводим данные о руководителях организации
		Если ВыборкаДляШапки.Следующий() Тогда
			ОбластьМакета.Параметры.Заполнить(ВыборкаДляШапки); // данные из шапки
		КонецЕсли;
		
		// Начинаем формировать выходной документ
		Пока ВыборкаРаботники.Следующий() Цикл
			
			// Каждый приказ на отдельной странице.
			Если ТабДок.ВысотаТаблицы > 0 Тогда
				ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			
			ОбластьМакета.Параметры.Заполнить(ВыборкаДляШапки); // данные из шапки
			ОбластьМакета.Параметры.НазваниеОрганизации = СведенияОбОрганизации.ПолноеНаименование;
			ОбластьМакета.Параметры.ЕДРПОУ = СведенияОбОрганизации.КодПоЕДРПОУ;
			ОбластьМакета.Параметры.НомерДок = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ВыборкаДляШапки.НомерДок, Истина, Истина);
			
			// Данные по работнику.
			ОбластьМакета.Параметры.Заполнить(ВыборкаРаботники);
			
			ТабДок.Вывести(ОбластьМакета);

		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции
	
Функция ПолучитьДополнительныеРеквизитыДляРеестра() Экспорт
	
	Результат = Новый Структура;	
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли