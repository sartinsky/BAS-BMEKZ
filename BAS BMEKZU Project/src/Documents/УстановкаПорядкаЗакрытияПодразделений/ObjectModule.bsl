#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ
//

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);

	Дата = НачалоМесяца(ЭтотОбъект.Дата);

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)

	Дата = НачалоМесяца(ОбщегоНазначенияБП.ПолучитьРабочуюДату());
	Ответственный = Пользователи.ТекущийПользователь();

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Процедура - обработчик события "ОбработкаПроведения"
//
Процедура ОбработкаПроведения(Отказ, Режим)

	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ДОКУМЕНТА

	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	
	ПорядокЗакрытияПодразделений = Движения.ПорядокЗакрытияПодразделенийОрганизаций;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",      Ссылка);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Подразделения.Ссылка КАК Подразделение,
	|	ВЫБОР 
	|		КОГДА ПорядокЗакрытия.НомерПередела ЕСТЬ NULL 
	|		ТОГДА 0
	|	ИНАЧЕ
	|		ПорядокЗакрытия.НомерПередела
	|	КОНЕЦ КАК НомерПередела
	|ИЗ
	|	Справочник.ПодразделенияОрганизаций КАК Подразделения
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			УстановкаПорядкаЗакрытие.Подразделение КАК Подразделение,
	|			УстановкаПорядкаЗакрытие.НомерСтроки КАК НомерПередела
	|		ИЗ
	|			Документ.УстановкаПорядкаЗакрытияПодразделений.ПорядокЗакрытия КАК УстановкаПорядкаЗакрытие
	|		ГДЕ
	|			УстановкаПорядкаЗакрытие.Ссылка = &Ссылка
	|		) КАК ПорядокЗакрытия
	|	ПО
	|		Подразделения.Ссылка = ПорядокЗакрытия.Подразделение
	|ГДЕ
	|	Не Подразделения.ПометкаУдаления
	|	И Подразделения.Владелец = &Организация
	|";
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();

	Для Каждого Строка Из РезультатЗапроса Цикл

		НоваяСтрока = ПорядокЗакрытияПодразделений.Добавить();

		НоваяСтрока.Период        = НачалоМесяца(Дата);
		НоваяСтрока.Подразделение = Строка.Подразделение;
		НоваяСтрока.НомерПередела = Строка.НомерПередела;
		НоваяСтрока.Организация   = Организация;

	КонецЦикла;

	ПроведениеСервер.ПодготовитьНаборыЗаписейКЗаписиДвижений(ЭтотОбъект);
	
КонецПроцедуры // ОбработкаПроведения()

Процедура ОбработкаУдаленияПроведения(Отказ)

	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	Движения.Записать();

КонецПроцедуры

#КонецЕсли