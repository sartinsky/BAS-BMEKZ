
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Подсистема "Управление доступом"

// Процедура ЗаполнитьНаборыЗначенийДоступа по свойствам объекта заполняет наборы значений доступа
// в таблице с полями:
//    НомерНабора     - Число                                     (необязательно, если набор один),
//    ВидДоступа      - ПланВидовХарактеристикСсылка.ВидыДоступа, (обязательно),
//    ЗначениеДоступа - Неопределено, СправочникСсылка или др.    (обязательно),
//    Чтение          - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Добавление      - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Изменение       - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Удаление        - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//
//  Вызывается из процедуры УправлениеДоступомСлужебный.ЗаписатьНаборыЗначенийДоступа(),
// если объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьНаборыЗначенийДоступа" и
// из таких же процедур объектов, у которых наборы значений доступа зависят от наборов этого
// объекта (в этом случае объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьЗависимыеНаборыЗначенийДоступа").
//
// Параметры:
//  Таблица      - ТабличнаяЧасть,
//                 РегистрСведенийНаборЗаписей.НаборыЗначенийДоступа,
//                 ТаблицаЗначений, возвращаемая УправлениеДоступом.ТаблицаНаборыЗначенийДоступа().
//
Процедура ЗаполнитьНаборыЗначенийДоступа(Таблица) Экспорт
	
	ЗарплатаКадры.ЗаполнитьНаборыПоОрганизацииИФизическимЛицам(ЭтотОбъект, Таблица, "Организация", "ФизическиеЛица.ФизическоеЛицо");
	
КонецПроцедуры

// Подсистема "Управление доступом"

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если РучнаяКорректировка Тогда
		ТекстСообщения = НСтр("ru='Движения документа не могут быть автоматически актуализированы. При необходимости снимите признак ручной корректировки.';uk='Рухи документа не можуть бути автоматично актуалізовані. При необхідності зніміть ознаку ручного коригування.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект);
		Возврат;
	КонецЕсли;	
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	Если ПредварительныйРасчет Тогда
		Возврат;
	КонецЕсли;	
	
	ДанныеДляПроведения = ДанныеДляПроведения();
	
	// Учет начисленной зарплаты
	УчетНачисленнойЗарплаты.ЗарегистрироватьНачисленияУдержания(ЭтотОбъект.Движения, Отказ, Организация, МесяцНачисления,
			ДанныеДляПроведения.НачисленияПоСотрудникам, ДанныеДляПроведения.УдержанияПоСотрудникам, НеОпределено, НеОпределено);

	// Страховые взносы
	УчетСтраховыхВзносов.СформироватьДвиженияВзносы(Движения, Отказ, Организация, МесяцНачисления, Перечисления.СпособыРасчетаВзносов.Взносы, ДанныеДляПроведения.Взносы);
	УчетСтраховыхВзносов.СформироватьДвиженияВзносы(Движения, Отказ, Организация, МесяцНачисления, Перечисления.СпособыРасчетаВзносов.ВзносыФОТ, ДанныеДляПроведения.ВзносыФОТ);		
	
	// НДФЛ
	УчетНДФЛ.СформироватьДвиженияНДФЛ(Движения, Отказ, Организация, МесяцНачисления, ДанныеДляПроведения.НДФЛ);
	
	// Взаиморасчеты
	ВзаиморасчетыССотрудниками.ЗарегистрироватьНачисленнуюЗарплату(
			Движения, Отказ, 
			Организация, МесяцНачисления);
	
	Если Не Отказ Тогда
		
		// формирование проводок
		ДанныеДляПроведения = Новый Структура;
		ДанныеДляПроведения.Вставить("НачисленияУдержанияПоСотрудникам",Движения.НачисленияУдержанияПоСотрудникам.Выгрузить());
		ДанныеДляПроведения.Вставить("ЕСВПоСотрудникам",Движения.ЕСВПоСотрудникам.Выгрузить());
		ДанныеДляПроведения.Вставить("НДФЛПоСотрудникам",Движения.НДФЛПоСотрудникам.Выгрузить());
		ДанныеДляПроведения.Вставить("ВзаиморасчетыПоУдержаниям",Движения.ВзаиморасчетыПоУдержаниям.Выгрузить());
		СтрокаСписокТаблиц = "НачисленнаяЗарплатаИВзносы, УдержанныйЕСВ, УдержанныйНДФЛ, УдержаннаяЗарплата";
		ОтражениеЗарплатыВБухучетеБазовый.СформироватьДвиженияПоДокументу(Движения, Отказ, Организация, МесяцНачисления, Дата, ДанныеДляПроведения, СтрокаСписокТаблиц);
		
	КонецЕсли;
		
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Функция ДанныеДляПроведения()
	
	ДанныеДляПроведенияНачисленияЗарплаты = РасчетЗарплаты.СоздатьДанныеДляПроведенияНачисленияЗарплаты();
	
	РасчетЗарплаты.ЗаполнитьНачисления(ДанныеДляПроведенияНачисленияЗарплаты, Ссылка);
	РасчетЗарплаты.ЗаполнитьУдержания(ДанныеДляПроведенияНачисленияЗарплаты, Ссылка);
	РасчетЗарплаты.ЗаполнитьСписокФизическихЛиц(ДанныеДляПроведенияНачисленияЗарплаты, Ссылка);
	
	РасчетЗарплаты.ЗаполнитьДанныеНДФЛ(ДанныеДляПроведенияНачисленияЗарплаты, Ссылка);
	РасчетЗарплаты.ЗаполнитьДанныеСтраховыхВзносов(ДанныеДляПроведенияНачисленияЗарплаты, Ссылка);
	
	Возврат ДанныеДляПроведенияНачисленияЗарплаты;
	
КонецФункции

#КонецЕсли
