
Процедура ПередУдалением(Отказ)
	//Отказ = Не МонопольныйРежим();
КонецПроцедуры


Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Состояние = Перечисления.скEDI_СостоянияДополнительныхЭлектронныхДокументовДФС.Создан;
	Ответственный = ПараметрыСеанса.ТекущийПользователь;
КонецПроцедуры


Процедура ПриКопировании(ОбъектКопирования)
	Ответственный = ПараметрыСеанса.ТекущийПользователь;
	Состояние = Перечисления.скEDI_СостоянияДополнительныхЭлектронныхДокументовДФС.Создан;
	ИмяФайлаДФС = "";
	КтоОформилЗапрос = Неопределено;
	Подписи.Очистить();
	ВыпискаСтрока2Сумма = 0;
	ДатаОтправки = Неопределено;
	ДатаПолученияОтвета = Неопределено;
	ПричинаОтклоненияДФС = "";
КонецПроцедуры

Функция ЗагрузитьВытяг(ОрганизацияПолучатель, ДанныеДокумента, УзелDECLARHEAD, УзелDECLARBODY, ДатаСобытия) Экспорт
	тДатаВитягу = "";
	тВремяВитягу = "";
	тВыпискаСтрока2Сумма = "";
	Для Каждого ДочернийУзелDECLARBODY Из УзелDECLARBODY.ДочерниеУзлы Цикл
		Если ВРег(ДочернийУзелDECLARBODY.ИмяУзла) = ВРег("HFILL") Тогда
			тДатаВитягу = ДочернийУзелDECLARBODY.ТекстовоеСодержимое;
		ИначеЕсли ВРег(ДочернийУзелDECLARBODY.ИмяУзла) = ВРег("HTIME") Тогда
			тВремяВитягу = ДочернийУзелDECLARBODY.ТекстовоеСодержимое;
		ИначеЕсли ВРег(ДочернийУзелDECLARBODY.ИмяУзла) = ВРег("R02G03") Тогда
			тВыпискаСтрока2Сумма = ДочернийУзелDECLARBODY.ТекстовоеСодержимое;
		КонецЕсли;
	КонецЦикла;
	
	дГод = 0;
	дМесяц = 0;
	дДень = 0;
	Если скEDI_ОбщегоНазначения.ПолучитьГодМесяцДеньИзДатыФорматаДДММГГГГ(тДатаВитягу, дГод, дМесяц, дДень) Тогда
		дЧас = 0;
		дМинута = 0;
		дСекунда = 0;
		Если скEDI_ОбщегоНазначения.ПолучитьВремяФорматаЧЧММСС(тВремяВитягу, дЧас, дМинута, дСекунда) Тогда
			ДатаВремяВитягу = Дата(дГод, дМесяц, дДень, дЧас, дМинута, дСекунда);
		Иначе
			Возврат Ложь;
		КонецЕсли;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
	ДатаФормированияОтвета = ДатаВремяВитягу;
	ДатаПолученияОтвета = ДатаСобытия;
	Если ЗначениеЗаполнено(тВыпискаСтрока2Сумма) Тогда
		ВыпискаСтрока2Сумма = Число(тВыпискаСтрока2Сумма);
	Иначе
		ВыпискаСтрока2Сумма = 0;
	КонецЕсли;
	
	Состояние = Перечисления.скEDI_СостоянияДополнительныхЭлектронныхДокументовДФС.ПолученоОтветОтДФС;
	
	лМассивПодписей = скEDI_ОбщегоНазначения.ПолучитьМассивПодписейИзМассиваСоответствий(ДанныеДокумента.Получить("Signers"));
	//ИнформацияОПолученомДокументе = ИнформацияОПолученомДокументе + "
	//		|
	//		|Підписи:";
	//ДополнитьОписаниеДаннымиПоПодписямКонтрагента(лМассивПодписей, ИнформацияОПолученомДокументе, Неопределено, Ложь, Ложь);
	лМассивДанныхПоПодписямКонтрагента = скEDI_ОбщегоНазначения.СформироватьМассивДанныхПоПодписямКонтрагента(лМассивПодписей);
	Для Каждого лИнформацияОПодписи Из лМассивДанныхПоПодписямКонтрагента Цикл
		лНоваяСтрокаПодписиКонтрагента = ПодписиКонтрагента.Добавить();
		ЗаполнитьЗначенияСвойств(лНоваяСтрокаПодписиКонтрагента, лИнформацияОПодписи);
	КонецЦикла;
	
	Записать(РежимЗаписиДокумента.Проведение);
	
	скEDI_ВнешниеХранилища.СохранитьСодержимоеДополнительногоЭлектронногоДокументаДФС(Ссылка, Перечисления.скEDI_ТипыСодержимогоДополнительныхЭлектронныхДокуметовДФС.Ответ, ДанныеДокумента.Получить("Body"), "", ДатаСобытия);
	//СодержимоеЭлектронныхДокументовМенеджерЗаписи = РегистрыСведений.скEDI_СодержимоеДополнительныхЭлектронныхДокументовДФС.СоздатьМенеджерЗаписи();
	//СодержимоеЭлектронныхДокументовМенеджерЗаписи.ЭлектронныйДокумент = Ссылка;
	//СодержимоеЭлектронныхДокументовМенеджерЗаписи.ТипСодержимого = Перечисления.скEDI_ТипыСодержимогоДополнительныхЭлектронныхДокуметовДФС.Ответ;
	//СодержимоеЭлектронныхДокументовМенеджерЗаписи.ТелоДокумента = ДанныеДокумента.Получить("Body");
	//СодержимоеЭлектронныхДокументовМенеджерЗаписи.ИзображениеДокумента = "";
	//СодержимоеЭлектронныхДокументовМенеджерЗаписи.Дата = ДатаСобытия;
	//СодержимоеЭлектронныхДокументовМенеджерЗаписи.Записать(Истина);
	Возврат Истина;
КонецФункции

Процедура ОбработкаПроведения(Отказ, Режим)
	// регистр скEDI_ВипискиПоЛимитуРегистрацииВЕРНН
	Движения.скEDI_ВипискиПоЛимитуРегистрацииВЕРНН.Записывать = Истина;
	Если Состояние = Перечисления.скEDI_СостоянияДополнительныхЭлектронныхДокументовДФС.ПолученоОтветОтДФС Тогда
		Движение = Движения.скEDI_ВипискиПоЛимитуРегистрацииВЕРНН.Добавить();
		Движение.Организация = ОрганизацияEDI;
		Движение.Период = ДатаФормированияОтвета;
		Движение.Строка2Сумма = ВыпискаСтрока2Сумма;
	КонецЕсли;
КонецПроцедуры
