#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	Параметры.Свойство("Организация",        Организация);
	Параметры.Свойство("Склад",              Склад);
	Параметры.Свойство("Владелец",           Владелец);
	Параметры.Свойство("ДоговорКонтрагента", ДоговорКонтрагента);
	Параметры.Свойство("Номенклатура",       Номенклатура);
	Параметры.Свойство("ТипЖурнала",         ТипЖурнала);
	Параметры.Свойство("Урожай",             Урожай);
	Параметры.Свойство("ВидХранения",        ВидХранения);
	Параметры.Свойство("Силос",              Силос);
	Параметры.Свойство("ДатаОкончания",      ДатаОкончания);
	Параметры.Свойство("ДатаНачала",         ДатаНачала); 
	
КонецПроцедуры 

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	
	СтруктураВозврата = Новый Структура("АдресХарактеристикиВХранилище, КвоЛабАнализов");
	
	ЗаполнитьНаСервере(СтруктураВозврата);
	
	Закрыть(СтруктураВозврата);

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериод(Команда)

	ПараметрыФормыВыбора = Новый Структура("НачалоПериода, КонецПериода", ДатаНачала, ДатаОкончания);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериода", ПараметрыФормыВыбора, Элементы.ВыбратьПериод, , , , ОписаниеОповещения);
	
КонецПроцедуры 

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДатаНачала	  = РезультатВыбора.НачалоПериода;
	ДатаОкончания = РезультатВыбора.КонецПериода;		
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьНаСервере(СтруктураВозврата)
	
	Запрос = Новый Запрос;	
	
	Фильтр = "";
	
	Если ЗначениеЗаполнено(Организация) Тогда
		Фильтр = Фильтр + " И ИНАГРО_ЖурналЛабораторныхАнализов.ЛабораторныйАнализ.Организация В ИЕРАРХИИ(&Организация)"
	КонецЕсли;
	
	Если ДатаНачала <> '00010101000000' И ДатаОкончания <> '00010101000000' Тогда
		Фильтр = Фильтр + " И ИНАГРО_ЖурналЛабораторныхАнализов.ЛабораторныйАнализ.Дата >= &ДатаНачала И ИНАГРО_ЖурналЛабораторныхАнализов.ЛабораторныйАнализ.Дата <= &ДатаОкончания";
	ИначеЕсли ДатаНачала <> '00010101000000' Тогда
		Фильтр = Фильтр + " И ИНАГРО_ЖурналЛабораторныхАнализов.ЛабораторныйАнализ.Дата >= &ДатаНачала ";
	ИначеЕсли ДатаОкончания <> '00010101000000' Тогда
		Фильтр = Фильтр + " И ИНАГРО_ЖурналЛабораторныхАнализов.ЛабораторныйАнализ.Дата <= &ДатаОкончания ";		
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(Владелец) Тогда
		Фильтр = Фильтр + " И ИНАГРО_ЖурналЛабораторныхАнализов.ЛабораторныйАнализ.Владелец В ИЕРАРХИИ(&Владелец)"
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Склад) Тогда
		Фильтр = Фильтр + " И ИНАГРО_ЖурналЛабораторныхАнализов.ЛабораторныйАнализ.Склад  В ИЕРАРХИИ(&Склад)"
	КонецЕсли;

	Если ЗначениеЗаполнено(Силос) Тогда
		Фильтр = Фильтр + " И ИНАГРО_ЖурналЛабораторныхАнализов.ЛабораторныйАнализ.Силос  В ИЕРАРХИИ(&Силос)"
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Номенклатура) Тогда
		Фильтр = Фильтр + " И ИНАГРО_ЖурналЛабораторныхАнализов.ЛабораторныйАнализ.Номенклатура В ИЕРАРХИИ(&Номенклатура)"
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Урожай) Тогда
		Фильтр = Фильтр + " И ИНАГРО_ЖурналЛабораторныхАнализов.ЛабораторныйАнализ.Урожай  В ИЕРАРХИИ(&Урожай)"
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТипЖурнала) Тогда
		Фильтр = Фильтр + " И ИНАГРО_ЖурналЛабораторныхАнализов.ЛабораторныйАнализ.ТипЖурнала  В ИЕРАРХИИ(&ТипЖурнала)"
	КонецЕсли;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ИНАГРО_ЖурналЛабораторныхАнализов.ДокументРегистратор.Ссылка КАК ДокСсылка,
		|	ЕСТЬNULL(ИНАГРО_ЖурналЛабораторныхАнализов.Вес, 0) КАК ФизическийВес,
		|	ЕСТЬNULL(ИНАГРО_ЖурналЛабораторныхАнализов.ЛабораторныйАнализ.Влажность, 0) КАК Влажность,
		|	ЕСТЬNULL(ИНАГРО_ЖурналЛабораторныхАнализов.ЛабораторныйАнализ.СорнаяПримесь, 0) КАК СорнаяПримесь,
		|	ЕСТЬNULL(ИНАГРО_ЖурналЛабораторныхАнализов.ЛабораторныйАнализ.ЗерноваяПримесь, 0) КАК ЗерноваяПримесь,
		|	ИНАГРО_ЖурналЛабораторныхАнализов.ЛабораторныйАнализ.Ссылка КАК ЛабораторныйАнализ
		|ИЗ
		|	РегистрСведений.ИНАГРО_ЖурналЛабораторныхАнализов КАК ИНАГРО_ЖурналЛабораторныхАнализов
		|ГДЕ
		|	ИНАГРО_ЖурналЛабораторныхАнализов.Регистратор.Проведен = ИСТИНА" + Фильтр;	
		
	Запрос.УстановитьПараметр("Организация",   Организация);
	Запрос.УстановитьПараметр("Владелец",      Владелец);
	Запрос.УстановитьПараметр("Склад",         Склад);
	Запрос.УстановитьПараметр("Силос",         Силос);
	Запрос.УстановитьПараметр("Номенклатура",  Номенклатура);
	Запрос.УстановитьПараметр("Урожай",        Урожай);
	Запрос.УстановитьПараметр("ТипЖурнала",    ТипЖурнала);
	Запрос.УстановитьПараметр("ДатаНачала",    НачалоДня(ДатаНачала));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецДня(ДатаОкончания));	
	
	РезТаблица = Запрос.Выполнить().Выгрузить();
		
	Если РезТаблица.Количество() > 0 Тогда
		
		ТаблицаХарактеристик = Новый ТаблицаЗначений;
		ТаблицаХарактеристик.Колонки.Добавить("Характеристика");
		ТаблицаХарактеристик.Колонки.Добавить("Вес");
		ТаблицаХарактеристик.Колонки.Добавить("ЦентнероПроценты");
		ТаблицаХарактеристик.Колонки.Добавить("КвоЗначенийПоказателя");
		ТаблицаХарактеристик.Колонки.Добавить("КачествоЗерна");
		
		КвоЛабАнализов = 0;
		Для Каждого СтрРезТаблицы Из РезТаблица Цикл
			Для Каждого СтрЛабАнализа Из СтрРезТаблицы.ЛабораторныйАнализ.Результаты Цикл
				Если ТипЗнч(СтрЛабАнализа.Значение) <> Тип("Число") Тогда
					Продолжить;
				КонецЕсли;
				НоваяСтрока = ТаблицаХарактеристик.Добавить();
				НоваяСтрока.Характеристика        = СтрЛабАнализа.Характеристика;
				НоваяСтрока.Вес                   = СтрРезТаблицы.ФизическийВес;
				НоваяСтрока.ЦентнероПроценты      = СтрРезТаблицы.ФизическийВес * СтрЛабАнализа.Значение/100;
				НоваяСтрока.КачествоЗерна	      = СтрЛабАнализа.Значение;
				НоваяСтрока.КвоЗначенийПоказателя = 1;
			КонецЦикла;
			КвоЛабАнализов = КвоЛабАнализов + 1;
		КонецЦикла;
		
		Если КвоЛабАнализов = 1 Тогда
			//
		Иначе
			ТаблицаХарактеристик.Свернуть("Характеристика", "ЦентнероПроценты, КвоЗначенийПоказателя, Вес");
		КонецЕсли;	
		
		СтруктураВозврата.Вставить("АдресХарактеристикиВХранилище", ПоместитьХарактеристикиВХранилище(ТаблицаХарактеристик));
		СтруктураВозврата.Вставить("КвоЛабАнализов",                КвоЛабАнализов);
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ПоместитьХарактеристикиВХранилище(ТаблицаХарактеристик)

	Возврат ПоместитьВоВременноеХранилище(ТаблицаХарактеристик, УникальныйИдентификатор);

КонецФункции

#КонецОбласти
