
&НаКлиенте
Процедура Заполнить(Команда)
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("НалоговоеНазначение", НалоговоеНазначение);
	СтруктураРезультата.Вставить("НалоговоеНазначениеНовое", НалоговоеНазначениеНовое);
	СтруктураРезультата.Вставить("СчетУчета", СчетУчета);
	СтруктураРезультата.Вставить("ОчиститьТабличнуюЧасть", ОчиститьТабличнуюЧасть);

	ОповеститьОВыборе(СтруктураРезультата);
	
	ОчиститьТабличнуюЧасть = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗакрыватьПриВыборе = Ложь;
	
	Если Параметры.Свойство("Организация") Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 2
		|	УчетнаяПолитикаОрганизаций.ПлательщикНДС
		|		И НЕ УчетнаяПолитикаОрганизаций.ПлательщикНДСПриостановлен КАК ПлательщикНДС
		|ИЗ
		|	РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
		|ГДЕ
		|	УчетнаяПолитикаОрганизаций.Организация = &Организация
		|	И УчетнаяПолитикаОрганизаций.Период <= &Период
		|
		|УПОРЯДОЧИТЬ ПО
		|	УчетнаяПолитикаОрганизаций.Период УБЫВ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	УчетнаяПолитикаОрганизаций.ПлательщикНДС
		|		И НЕ УчетнаяПолитикаОрганизаций.ПлательщикНДСПриостановлен КАК ПлательщикНДС
		|ИЗ
		|	РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
		|ГДЕ
		|	УчетнаяПолитикаОрганизаций.Организация = &Организация
		|	И УчетнаяПолитикаОрганизаций.Период > &Период
		|
		|УПОРЯДОЧИТЬ ПО
		|	УчетнаяПолитикаОрганизаций.Период";
		Запрос.УстановитьПараметр("Организация", Параметры.Организация);
		Запрос.УстановитьПараметр("Период", Параметры.Дата);
		
		Результаты = Запрос.ВыполнитьПакет();
		ПолитикаДо = Результаты[0].Выгрузить();
		ПолитикаПосле = Результаты[1].Выгрузить();
		
		Если ПолитикаПосле.Количество() И НЕ ПолитикаПосле[0].ПлательщикНДС Тогда
			
			//переходим на необлагаемые
			НалоговоеНазначение = Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_Облагаемая;
			НалоговоеНазначениеНовое = Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяХозДеятельность;
			
		ИначеЕсли ПолитикаДо.Количество() = 2 И ПолитикаДо[0].ПлательщикНДС И НЕ ПолитикаДо[1].ПлательщикНДС Тогда
			
			//возвращаемся на облагаемое
			НалоговоеНазначение = Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяХозДеятельность;
			НалоговоеНазначениеНовое = Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_Облагаемая;
			
		Иначе
			
			//по умолчанию у плательщика все назначения облагаемые
			НалоговоеНазначение = Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_Облагаемая;
			НалоговоеНазначениеНовое = Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяХозДеятельность;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)

	Если ЗначениеЗаполнено(НалоговоеНазначение) И НалоговоеНазначение = НалоговоеНазначениеНовое Тогда
		
		ТекстСообщения = НСтр("ru='Налоговые назначения совпадают!';uk='Податкові призначення співпадають!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "НалоговоеНазначение",, Отказ);
		
	КонецЕсли;
	
КонецПроцедуры
