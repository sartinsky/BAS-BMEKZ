#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Подсистема "Управление доступом"

// Процедура ЗаполнитьНаборыЗначенийДоступа по свойствам объекта заполняет наборы значений доступа
// в таблице с полями:
//    НомерНабора     - Число                                     (необязательно, если набор один),
//    ВидДоступа      - ПланВидовХарактеристикСсылка.ВидыДоступа, (обязательно),
//    ЗначениеДоступа - Неопределено, СправочникСсылка или др.    (обязательно),
//    Чтение          - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Добавление      - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Изменение       - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Удаление        - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//
//  Вызывается из процедуры УправлениеДоступомСлужебный.ЗаписатьНаборыЗначенийДоступа(),
// если объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьНаборыЗначенийДоступа" и
// из таких же процедур объектов, у которых наборы значений доступа зависят от наборов этого
// объекта (в этом случае объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьЗависимыеНаборыЗначенийДоступа").
//
// Параметры:
//  Таблица      - ТабличнаяЧасть,
//                 РегистрСведенийНаборЗаписей.НаборыЗначенийДоступа,
//                 ТаблицаЗначений, возвращаемая УправлениеДоступом.ТаблицаНаборыЗначенийДоступа().
//
Процедура ЗаполнитьНаборыЗначенийДоступа(Таблица) Экспорт
	
	ЗарплатаКадры.ЗаполнитьНаборыПоОрганизацииИФизическимЛицам(ЭтотОбъект, Таблица, "Организация", "ФизическоеЛицо");
	
КонецПроцедуры

// Подсистема "Управление доступом"

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	// Не требуется заполнять ОбособленноеПодразделение если в организации не используются обособленные подразделения
	ПараметрыФО = Новый Структура("Организация", Организация);
	//*Если Не ПолучитьФункциональнуюОпцию("ИспользоватьОбособленныеПодразделения", ПараметрыФО) Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ОбособленноеПодразделение");
	//*КонецЕсли;
	
	Если Не ИзменитьПодразделениеИДолжность Тогда
		
		// Не требуется заполнять подразделение и количество ставок, если оно не изменяется
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ОбособленноеПодразделение");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Подразделение");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Должность");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ВидЗанятости");
		
	КонецЕсли;
	
	КадровыйУчет.ПроверитьВозможностьПроведенияПоКадровомуУчету(
		ЭтотОбъект.ДатаНачала,
		ЭтотОбъект.Сотрудник,
		ЭтотОбъект.Ссылка,
		Отказ,
		Перечисления.ВидыКадровыхСобытий.Перемещение);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	
	
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ДанныеДляПроведения = ПолучитьДанныеДляПроведения();
	
	Если ИзменитьПодразделениеИДолжность Тогда
		КадровыйУчет.СформироватьКадровыеДвижения(ЭтотОбъект, Движения, ДанныеДляПроведения.КадровыеДвижения);
	КонецЕсли;
	
	Если ИзменитьНачисления Тогда
		СтруктураПлановыхНачислений = Новый Структура;
		СтруктураПлановыхНачислений.Вставить("ДанныеОПлановыхНачислениях", ДанныеДляПроведения.ПлановыеНачисления);
		
		РасчетЗарплаты.СформироватьДвиженияПлановыхНачислений(ЭтотОбъект, Движения, СтруктураПлановыхНачислений);
		
		СтруктураПлановыхУдержаний = Новый Структура;
		СтруктураПлановыхУдержаний.Вставить("ДанныеОПлановыхУдержаниях", ДанныеДляПроведения.ПлановыеУдержания);
		
		РасчетЗарплаты.СформироватьДвиженияПлановыхУдержаний(ЭтотОбъект, Движения, СтруктураПлановыхУдержаний);
		
		СтруктураЕСВСотрудников = Новый Структура;
		СтруктураЕСВСотрудников.Вставить("ДанныеОЕСВСотрудников", ДанныеДляПроведения.ЕСВСотрудников);
		
		РасчетЗарплаты.СформироватьДвиженияЕСВСотрудников(ЭтотОбъект, Движения, СтруктураЕСВСотрудников);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ДатаНачала = ТекущаяДатаСеанса();
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Сотрудники") Тогда
		ЗарплатаКадры.ЗаполнитьПоОснованиюСотрудником(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		
		Если ДанныеЗаполнения.Свойство("Сотрудник") Тогда
			Сотрудник = ДанныеЗаполнения.Сотрудник;
		КонецЕсли;
			
		Если ДанныеЗаполнения.Свойство("ДатаНачала") Тогда
			ДатаНачала = ДанныеЗаполнения.ДатаНачала;
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Функция ПолучитьДанныеДляПроведения()
		
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Регистратор", ЭтотОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДатаСобытия", ЭтотОбъект.ДатаНачала);
	Запрос.УстановитьПараметр("Сотрудник", ЭтотОбъект.Сотрудник);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КадровыйПеревод.ДатаНачала КАК ДатаСобытия,
	|	КадровыйПеревод.Сотрудник,
	|	КадровыйПеревод.Сотрудник.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	КадровыйПеревод.ОбособленноеПодразделение КАК Организация,
	|	КадровыйПеревод.Подразделение КАК Подразделение,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Перемещение) КАК ВидСобытия,
	|	КадровыйПеревод.Должность КАК Должность,
	|	КадровыйПеревод.ВидЗанятости КАК ВидЗанятости,
	|	КадровыйПеревод.ФизическоеЛицо,
	|	КадровыйПеревод.ПринятНаНовоеРабочееМесто КАК ПринятНаНовоеРабочееМесто,
	|	КадровыйПеревод.ИзменениеДолжности КАК ИзменениеДолжности,
	|	ВЫБОР
	|		КОГДА КадровыйПеревод.ИзменениеДолжности
	|			ТОГДА КадровыйПеревод.ПредыдущаяДолжность 
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК ПредыдущаяДолжность
	|ИЗ
	|	Документ.КадровыйПеревод КАК КадровыйПеревод
	|ГДЕ
	|	КадровыйПеревод.Ссылка = &Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КадровыйПереводНачисления.Ссылка.ДатаНачала КАК ДатаСобытия,
	|	КадровыйПереводНачисления.Ссылка.Сотрудник КАК Сотрудник,
	|	КадровыйПереводНачисления.Начисление,
	|	КадровыйПереводНачисления.Размер,
	|	КадровыйПереводНачисления.Ссылка.ФизическоеЛицо,
	|	КадровыйПереводНачисления.Ссылка.Организация КАК Организация,
	|	КадровыйПереводНачисления.Начисление.КатегорияНачисленияИлиНеоплаченногоВремени КАК КатегорияНачисления,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Перемещение) КАК ВидСобытия
	|ИЗ
	|	Документ.КадровыйПеревод.Начисления КАК КадровыйПереводНачисления
	|ГДЕ
	|	КадровыйПереводНачисления.Ссылка = &Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&ДатаСобытия,
	|	ПлановыеНачисленияСрезПоследних.Сотрудник,
	|	ПлановыеНачисленияСрезПоследних.Начисление,
	|	0,
	|	ПлановыеНачисленияСрезПоследних.ФизическоеЛицо,
	|	NULL КАК Организация,
	|	ПлановыеНачисленияСрезПоследних.Начисление.КатегорияНачисленияИлиНеоплаченногоВремени КАК КатегорияНачисления,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Перемещение) КАК ВидСобытия
	|ИЗ
	|	РегистрСведений.ПлановыеНачисления.СрезПоследних(
	|			&ДатаСобытия,
	|			Сотрудник = &Сотрудник
	|				И Регистратор <> &Регистратор) КАК ПлановыеНачисленияСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КадровыйПеревод.Начисления КАК КадровыйПереводНачисления
	|		ПО ПлановыеНачисленияСрезПоследних.Начисление = КадровыйПереводНачисления.Начисление
	|			И (КадровыйПереводНачисления.Ссылка = &Регистратор)
	|ГДЕ
	|	КадровыйПереводНачисления.Начисление ЕСТЬ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КадровыйПереводУдержания.Ссылка.ДатаНачала КАК ДатаСобытия,
	|	КадровыйПереводУдержания.Ссылка.Сотрудник КАК Сотрудник,
	|	КадровыйПереводУдержания.Удержание,
	|	КадровыйПереводУдержания.Контрагент,
	|	КадровыйПереводУдержания.Размер,
	|	КадровыйПереводУдержания.Ссылка.ФизическоеЛицо
	|ИЗ
	|	Документ.КадровыйПеревод.Удержания КАК КадровыйПереводУдержания
	|ГДЕ
	|	КадровыйПереводУдержания.Ссылка = &Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&ДатаСобытия,
	|	ПлановыеУдержанияСрезПоследних.Сотрудник,
	|	ПлановыеУдержанияСрезПоследних.Удержание,
	|	ПлановыеУдержанияСрезПоследних.Контрагент,
	|	0,
	|	ПлановыеУдержанияСрезПоследних.ФизическоеЛицо
	|ИЗ
	|	РегистрСведений.ПлановыеУдержания.СрезПоследних(
	|			&ДатаСобытия,
	|			Сотрудник = &Сотрудник
	|				И Регистратор <> &Регистратор) КАК ПлановыеУдержанияСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КадровыйПеревод.Удержания КАК КадровыйПереводУдержания
	|		ПО ПлановыеУдержанияСрезПоследних.Удержание = КадровыйПереводУдержания.Удержание
	|		 И ПлановыеУдержанияСрезПоследних.Контрагент = КадровыйПереводУдержания.Контрагент
	|			И (КадровыйПереводУдержания.Ссылка = &Регистратор)
	|ГДЕ
	|	КадровыйПереводУдержания.Удержание ЕСТЬ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КадровыйПеревод.ДатаНачала КАК ДатаСобытия,
	|	КадровыйПеревод.Сотрудник КАК Сотрудник,
	|	КадровыйПеревод.КатегорияЕСВ КАК КатегорияЕСВ,
	|	КадровыйПеревод.ФизическоеЛицо
	|ИЗ
	|	Документ.КадровыйПеревод КАК КадровыйПеревод
	|   ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрСведений.ЕСВСотрудников.СрезПоследних(
	|			&ДатаСобытия,
	|			Сотрудник = &Сотрудник
	|				И Регистратор <> &Регистратор) КАК ЕСВСотрудниковСрезПоследних
	|		ПО ЕСВСотрудниковСрезПоследних.КатегорияЕСВ = КадровыйПеревод.КатегорияЕСВ
	|ГДЕ
	|	КадровыйПеревод.Ссылка = &Регистратор
	|	И КадровыйПеревод.КатегорияЕСВ <> ЗНАЧЕНИЕ(Справочник.КатегорииЗастрахованныхЛицЕСВ.ПустаяСсылка)
	|	И ЕСВСотрудниковСрезПоследних.КатегорияЕСВ ЕСТЬ NULL
	|";
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	ДанныеДляПроведения = Новый Структура; 
	
	// Первый набор данных для проведения - таблица для формирования кадровых движений, истрии графиков, авансов
	ДанныеДляПроведения.Вставить("КадровыеДвижения", РезультатыЗапроса[0].Выгрузить());
	
	// Второй набор данных для проведения - таблица для формирования плановых начислений
	ДанныеДляПроведения.Вставить("ПлановыеНачисления", РезультатыЗапроса[1].Выгрузить());
	
	// Третий набор данных для проведения - таблица для формирования плановых удержаний
	ДанныеДляПроведения.Вставить("ПлановыеУдержания", РезультатыЗапроса[2].Выгрузить());
	
	// Четвертый набор данных для проведения - таблица для формирования ЕСВ
	ДанныеДляПроведения.Вставить("ЕСВСотрудников", РезультатыЗапроса[3].Выгрузить());
	
	Возврат ДанныеДляПроведения;

КонецФункции	

#КонецЕсли