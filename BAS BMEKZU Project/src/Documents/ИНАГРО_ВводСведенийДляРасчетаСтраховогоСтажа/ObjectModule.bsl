#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);	
	
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения); 	
					
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ИНАГРО_ЗарплатаКадрыРасширенный.ЗаполнитьФизЛицо(ЭтотОбъект);

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
		
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначенияБПВызовСервера.ПредставлениеДокументаПриПроведении(Ссылка); 	
	
	РезультатЗапросаПоШапке = СформироватьЗапросПоШапке(Режим);
	ВыборкаПоШапкеДокумента = РезультатЗапросаПоШапке.Выбрать();
	
	Если ВыборкаПоШапкеДокумента.Следующий() Тогда		
	
		Если НЕ Отказ Тогда
			ВыборкаПоСтраховойСтаж = СформироватьЗапросПоСтраховойСтаж().Выбрать();
			// Заполним записи в наборах записей регистров
			ДобавитьСтрокуВДвиженияПоРегиструСведений(ВыборкаПоШапкеДокумента, ВыборкаПоСтраховойСтаж);
		КонецЕсли;
		
	КонецЕсли;
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКЗаписиДвижений(ЭтотОбъект); 

КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	Движения.Записать();
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)

	Дата = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Ответственный = Пользователи.ТекущийПользователь(); 
		
КонецПроцедуры

#КонецОбласти

#Область Проведение

// Формирует запрос по шапке документа
//
// Параметры: 
//  Режим - режим проведения
//
// Возвращаемое значение:
//  Результат запроса
//
Функция СформироватьЗапросПоШапке(Режим)

	Запрос = Новый Запрос;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВводСведенийДляРасчетаСтраховогоСтажа.Дата КАК Дата,
		|	ВводСведенийДляРасчетаСтраховогоСтажа.Организация КАК Организация,
		|	ВводСведенийДляРасчетаСтраховогоСтажа.Сотрудник КАК Сотрудник,
		|	ВводСведенийДляРасчетаСтраховогоСтажа.Ссылка КАК Ссылка,
		|	ВводСведенийДляРасчетаСтраховогоСтажа.ПериодС КАК ПериодС,
		|	ВводСведенийДляРасчетаСтраховогоСтажа.ПериодПо КАК ПериодПо
		|ИЗ
		|	Документ.ИНАГРО_ВводСведенийДляРасчетаСтраховогоСтажа КАК ВводСведенийДляРасчетаСтраховогоСтажа
		|ГДЕ
		|	ВводСведенийДляРасчетаСтраховогоСтажа.Ссылка = &ДокументСсылка";
	
	Запрос.УстановитьПараметр("ДокументСсылка",	Ссылка);

	Возврат Запрос.Выполнить();

КонецФункции

// Формирует запрос по таблице "РаботникиОрганизации" документа
//
// Параметры: 
//	Режим - режим проведения
//
// Возвращаемое значение:
//	Результат запроса. В запросе данные документа дополняются значениями
//	проверяемых параметров из связанного с
//
Функция СформироватьЗапросПоСтраховойСтаж()
	
	Запрос = Новый Запрос;	
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.Ссылка,
		|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.НомерСтроки,
		|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.Год,
		|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.Месяц1,
		|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.Месяц2,
		|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.Месяц3,
		|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.Месяц4,
		|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.Месяц5,
		|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.Месяц6,
		|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.Месяц7,
		|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.Месяц8,
		|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.Месяц9,
		|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.Месяц10,
		|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.Месяц11,
		|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.Месяц12,
		|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.ВсегоМесяцев,
		|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.ВсегоДней,
		|	ВЫБОР
		|		КОГДА ГОД(&ПериодС) = ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.Год
		|				И МЕСЯЦ(&ПериодС) > 1
		|			ТОГДА ЛОЖЬ
		|		КОГДА ГОД(&ПериодПо) = ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.Год
		|				И МЕСЯЦ(&ПериодПо) < 12
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК УчитыватьИтогиГода,
		|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.ПолныйМесяц1,
		|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.ПолныйМесяц2,
		|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.ПолныйМесяц3,
		|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.ПолныйМесяц4,
		|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.ПолныйМесяц5,
		|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.ПолныйМесяц6,
		|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.ПолныйМесяц7,
		|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.ПолныйМесяц8,
		|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.ПолныйМесяц9,
		|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.ПолныйМесяц10,
		|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.ПолныйМесяц11,
		|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.ПолныйМесяц12
		|ИЗ
		|	Документ.ИНАГРО_ВводСведенийДляРасчетаСтраховогоСтажа.СтраховойСтаж КАК ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж
		|ГДЕ
		|	ВводСведенийДляРасчетаСтраховогоСтажаСтраховойСтаж.Ссылка = &ДокументСсылка";		
	
	Запрос.УстановитьПараметр("ДокументСсылка", Ссылка);
	Запрос.УстановитьПараметр("ПериодС",		ПериодС);
	Запрос.УстановитьПараметр("ПериодПо",		ПериодПо);
	
	Возврат Запрос.Выполнить();
	
КонецФункции

// По строке выборки результата запроса по документу формируем движения по регистрам
//
// Параметры:
//	ВыборкаПоШапкеДокумента - выборка из результата запроса по шапке документа,
//	ВыборкаПоРаботникиОрганизации
//
// Возвращаемое значение:
//	Нет.
//
Процедура ДобавитьСтрокуВДвиженияПоРегиструСведений(ВыборкаПоШапкеДокумента, ВыборкаПоСтраховойСтаж)

	Пока ВыборкаПоСтраховойСтаж.Следующий() Цикл
		
		Для НомерМесяца = 1 По 12 Цикл
			
			МесяцЗаписи = Дата(ВыборкаПоСтраховойСтаж.Год, НомерМесяца, 1);
			Если МесяцЗаписи >= ВыборкаПоШапкеДокумента.ПериодС И МесяцЗаписи <= ВыборкаПоШапкеДокумента.ПериодПо Тогда
				Движение = Движения.ИНАГРО_СтраховойСтажПоДаннымПФУ.Добавить();
				// Свойства
				Движение.Период	= Дата;
				// Измерения
				Движение.Физлицо		= ВыборкаПоШапкеДокумента.Сотрудник.ФизическоеЛицо;
				Движение.Организация	= ВыборкаПоШапкеДокумента.Организация;
				Движение.Год			= ВыборкаПоСтраховойСтаж.Год;
				Движение.Месяц			= МесяцЗаписи;
				// Ресурсы
				Движение.ДниСтажа		= ВыборкаПоСтраховойСтаж["Месяц"+НомерМесяца];
				Если ВыборкаПоСтраховойСтаж["ПолныйМесяц"+НомерМесяца] = 1 Тогда
					Движение.МесяцыСтажа = 1
				Иначе
					Движение.МесяцыСтажа = 0
				КонецЕсли;	
			КонецЕсли;
			
		КонецЦикла;
		
		Если ВыборкаПоСтраховойСтаж.УчитыватьИтогиГода Тогда
			
			Движение = Движения.ИНАГРО_СтраховойСтажПоДаннымПФУ.Добавить();
			Движение.Период	= Дата;
			// Измерения
			Движение.Физлицо		= ВыборкаПоШапкеДокумента.Сотрудник.ФизическоеЛицо;
			Движение.Организация	= ВыборкаПоШапкеДокумента.Организация;
			// Ресурсы
			Движение.Год			= ВыборкаПоСтраховойСтаж.Год;
			Движение.ДниСтажа		= ВыборкаПоСтраховойСтаж.ВсегоДней;
			Движение.МесяцыСтажа	= ВыборкаПоСтраховойСтаж.ВсегоМесяцев;
			
		КонецЕсли;
		
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли