#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область ПроцедурыИФункцииПечати

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	// Форма ОЗ-1
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ОЗ1";
	КомандаПечати.Представление = НСтр("ru='Форма ОЗ-1';uk='Форма ОЗ-1'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	
	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Реестр";
	КомандаПечати.Представление = НСтр("ru='Реестр документов';uk='Реєстр документів'");
	КомандаПечати.ЗаголовокФормы= НСтр("ru='Реестр документов ""Перемещение ОС""';uk='Реєстр документів ""Переміщення ОЗ""'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм    = "ФормаСписка";
	КомандаПечати.Порядок       = 100;
	
КонецПроцедуры

// Функция формирует табличный документ с печатной формой ОЗ-1,
//
// Возвращаемое значение:
//  Табличный документ - печатная форма накладной
//
Функция ПечатьОЗ1(МассивОбъектов, ОбъектыПечати, ПараметрыПечати, ИмяМакета)
	УстановитьПривилегированныйРежим(Истина);

	ТабДокумент   = Новый ТабличныйДокумент();
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПеремещениеОС_ОЗ1";
	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_UK_ОЗ1");
	
	ПервыйДокумент = Истина;
	
	Для Каждого Ссылка Из МассивОбъектов Цикл	
		
		Если Не ПервыйДокумент Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;
	
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",         Ссылка);
		Запрос.УстановитьПараметр("ТекДата",        Ссылка.МоментВремени());
		Запрос.УстановитьПараметр("ТекОрганизация", Ссылка.Организация);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПеремещениеОС.Дата                                   КАК ДатаАкта,
		|	ПеремещениеОС.Номер                                  КАК НомерАкта, 
		|	ПеремещениеОС.ПодразделениеОрганизации.Представление КАК ПринялоПодразделение,
		|	ПеремещениеОС.ПодразделениеОрганизации               КАК ПодразделениеПриняло,
		|	ВЫРАЗИТЬ(ПеремещениеОС.Организация.НаименованиеПолное 
		|	                    КАК СТРОКА(1000))     КАК Организация,
		|	ПеремещениеОС.Организация.КодПоЕДРПОУ               КАК ЕДРПОУ
		|ИЗ
		|	Документ.ПеремещениеОС КАК ПеремещениеОС
		|ГДЕ
		|	ПеремещениеОС.Ссылка = &Ссылка";
		ВыборкаПоШапке = Запрос.Выполнить().Выбрать();
		ВыборкаПоШапке.Следующий();

		СписокОС = Ссылка.ОС.ВыгрузитьКолонку("ОсновноеСредство");
		
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка"        , Ссылка);
		Запрос.УстановитьПараметр("СписокОС"      , СписокОС);
		Запрос.УстановитьПараметр("ТекДата"       , Новый Граница(Ссылка.МоментВремени(), ВидГраницы.Исключая));
		Запрос.УстановитьПараметр("ТекОрганизация", Ссылка.Организация);
		Запрос.УстановитьПараметр("СостояниеВвода", Перечисления.СостоянияОС.ВведеноВЭксплуатацию);
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПервоначальныеСведенияБУ.ИнвентарныйНомер           КАК ИнвентарныйНомер,
		|	ПервоначальныеСведенияБУ.ПервоначальнаяСтоимость    КАК ПервоначальнаяСтоимость,
		|	СчетаБухгалтерскогоУчетаОС.СчетУчета                КАК СчетКт,
		|	СчетаБухгалтерскогоУчетаОС.СчетУчета                КАК СчетДт,
		|	ПеремещениеОСОС.ОсновноеСредство.НаименованиеПолное КАК НаименованиеОС,
		|	ПеремещениеОСОС.ОсновноеСредство.ЗаводскойНомер     КАК ЗаводскойНомер,
		|	ПеремещениеОСОС.ОсновноеСредство.ДатаВыпуска        КАК ГодВыпуска,
		|	ПеремещениеОСОС.ОсновноеСредство.НомерПаспорта      КАК НомерПаспорта,
		|	МестонахождениеОС.МОЛ.Код                           КАК КодМОЛа,
		|	МестонахождениеОС.Местонахождение.Представление     КАК СдалоПодразделение,
		|	МестонахождениеОС.Местонахождение                   КАК Подразделение,
		|	СостоянияОС.ДатаСостояния                           КАК ДатаВвода
		|ИЗ
		|	Документ.ПеремещениеОС.ОС КАК ПеремещениеОСОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ 
		|			РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.СрезПоследних(
		|		                    &ТекДата,
		|		                    ОсновноеСредство В (&СписокОС)
		|		                    И Организация = &ТекОрганизация) КАК ПервоначальныеСведенияБУ
		|		ПО ПеремещениеОСОС.ОсновноеСредство = ПервоначальныеСведенияБУ.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ 
		|			РегистрСведений.СчетаБухгалтерскогоУчетаОС.СрезПоследних(
		|		                    &ТекДата,
		|		                    ОсновноеСредство В (&СписокОС)
		|		                    И Организация = &ТекОрганизация) КАК СчетаБухгалтерскогоУчетаОС
		|		ПО ПеремещениеОСОС.ОсновноеСредство = СчетаБухгалтерскогоУчетаОС.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ 
		|			РегистрСведений.МестонахождениеОСБухгалтерскийУчет.СрезПоследних(
		|			                &ТекДата,
		|		                    Организация = &ТекОрганизация
		|			                И ОсновноеСредство В (&СписокОС)) КАК МестонахождениеОС
		|		ПО ПеремещениеОСОС.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ 
		|			(ВЫБРАТЬ
		|				СостоянияОС.ОсновноеСредство КАК ОсновноеСредство,
		|				СостоянияОС.ДатаСостояния    КАК ДатаСостояния
		|			ИЗ
		|				РегистрСведений.СостоянияОСОрганизаций КАК СостоянияОС
		|			ГДЕ
		|				СостоянияОС.Организация = &ТекОрганизация
		|				И СостоянияОС.Состояние = &СостояниеВвода
		|				И СостоянияОС.ОсновноеСредство В(&СписокОС)) КАК СостоянияОС
		|		ПО ПеремещениеОСОС.ОсновноеСредство = СостоянияОС.ОсновноеСредство
		|ГДЕ
		|	ПеремещениеОСОС.Ссылка = &Ссылка";
			
		Результат = Запрос.Выполнить();
		ВыборкаПоОС = Результат.Выбрать();
		
		ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();

		ВыборкаПоКомиссии = ОбщегоНазначенияБПВызовСервера.ПолучитьСведенияОКомиссии(Ссылка);
		
		ДанныеФизЛицаПолучил = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(Ссылка.Организация, Ссылка.Принял, Ссылка.Дата);
		ДанныеФизЛицаП = ИНАГРО_ЗарплатаКадрыРасширенный.ДанныеФизЛица(Ссылка.Организация, Ссылка.Принял, Ссылка.Дата);
		ДанныеФизЛицаСдал = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(Ссылка.Организация, Ссылка.Сдал, Ссылка.Дата);
		ДанныеФизЛицаС = ИНАГРО_ЗарплатаКадрыРасширенный.ДанныеФизЛица(Ссылка.Организация, Ссылка.Сдал, Ссылка.Дата);
		ОтветственныеЛица = ОтветственныеЛицаБП.ОтветственныеЛица(Ссылка.Организация, Ссылка.Дата);
		
		Пока ВыборкаПоОС.Следующий() Цикл

			ОбластьМакета = Макет.ПолучитьОбласть("ОЗ1");
			Параметры     = ОбластьМакета.Параметры;
			Параметры.Заполнить(ВыборкаПоШапке);
			Параметры.Организация = СокрП(ВыборкаПоШапке.Организация);
			Параметры.Валюта      = ВалютаРегламентированногоУчета;
			
			Параметры.Заполнить(ВыборкаПоОС);
			Параметры.Заполнить(ВыборкаПоКомиссии);
			Параметры.ВидОперации = "Внутр. перемі-" + Символы.ПС + "щення";
			
			ОбластьМакета.Параметры.ПолучилДолжность 	= ДанныеФизЛицаП.Должность;
			ОбластьМакета.Параметры.ПолучилФИО 			= ДанныеФизЛицаПолучил.Представление;
			
			ОбластьМакета.Параметры.СдалДолжность 	= ДанныеФизЛицаС.Должность;
			ОбластьМакета.Параметры.СдалФИО 		= ДанныеФизЛицаСдал.Представление;
			
			ОбластьМакета.Параметры.ГлавныйБухгалтер 	= ОтветственныеЛица.ГлавныйБухгалтерПредставление;
			
			ТабДокумент.Вывести(ОбластьМакета);
			
		КонецЦикла;

		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, Ссылка);
		
	КонецЦикла;	
	
	Возврат ТабДокумент;
	
КонецФункции // ПечатьОЗ1()

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт

	// Устанавливаем признак доступности печати покомплектно.
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;

	// Проверяем, нужно ли для макета СчетЗаказа формировать табличный документ.
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ОЗ1") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		ИмяМакета = "";
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ОЗ1",
			НСтр("ru='Форма ОЗ-1';uk='Форма ОЗ-1'"), ПечатьОЗ1(МассивОбъектов, ОбъектыПечати, ПараметрыПечати, ИмяМакета),, ИмяМакета);
	КонецЕсли;

КонецПроцедуры

Функция ПолучитьДополнительныеРеквизитыДляРеестра() Экспорт
	
	Результат = Новый Структура("Информация", "СобытиеОС");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли