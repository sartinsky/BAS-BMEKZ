#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда  

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);	
	
	ИНАГРО_ЭлеваторЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ЭтотОбъект);

	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения); 	
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
		
КонецПроцедуры 

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Перем Заголовок, СтруктураШапкиДокумента;
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
		   
	// Заголовок Для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначенияБПВызовСервера.ПредставлениеДокументаПриПроведении(Ссылка);
		
	// Движения по документу
	Если НЕ Отказ Тогда		
		ДвиженияПоРегиструРасчетыПоУслугам();
	КонецЕсли;	
		
	ПроведениеСервер.ПодготовитьНаборыЗаписейКЗаписиДвижений(ЭтотОбъект);

КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	Движения.Записать();
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)

	Дата = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Ответственный = Пользователи.ТекущийПользователь();
	
КонецПроцедуры

#КонецОбласти

#Область Проведение

Процедура ДвиженияПоРегиструРасчетыПоУслугам()
	
	ТаблицаУслугПоРасходу = Новый ТаблицаЗначений;
	ТаблицаУслугПоРасходу.Колонки.Добавить("ДатаРасчета");
	ТаблицаУслугПоРасходу.Колонки.Добавить("Ссылка"); 
	ТаблицаУслугПоРасходу.Колонки.Добавить("Организация");
	ТаблицаУслугПоРасходу.Колонки.Добавить("Контрагент");
	ТаблицаУслугПоРасходу.Колонки.Добавить("ДоговорКонтрагента");
	ТаблицаУслугПоРасходу.Колонки.Добавить("Номенклатура");
	ТаблицаУслугПоРасходу.Колонки.Добавить("Культура");
	ТаблицаУслугПоРасходу.Колонки.Добавить("Склад");
	ТаблицаУслугПоРасходу.Колонки.Добавить("Урожай");
	ТаблицаУслугПоРасходу.Колонки.Добавить("ВидХранения");
	ТаблицаУслугПоРасходу.Колонки.Добавить("Количество");
	ТаблицаУслугПоРасходу.Колонки.Добавить("Стоимость");  	 
	
	ТаблицаУслугПоПриходу = ТаблицаУслугПоРасходу.Скопировать();
	
	СформироватьТаблицыУслуг(ТаблицаУслугПоПриходу, ТаблицаУслугПоРасходу);
	
	Если ТаблицаУслугПоРасходу.Количество() > 0 Тогда		
		ИНАГРО_Элеватор.ДвиженияПоРегиструРасчетыПоУслугам(Движения, ТаблицаУслугПоРасходу, "Расход");		
	КонецЕсли;  
	
	Если ТаблицаУслугПоПриходу.Количество() > 0 Тогда		
		ИНАГРО_Элеватор.ДвиженияПоРегиструРасчетыПоУслугам(Движения, ТаблицаУслугПоПриходу, "Приход");		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьТаблицыУслуг(ТаблицаУслугПоПриходу, ТаблицаУслугПоРасходу)
	
	Для Каждого СтрокаТабличнойЧасти Из Услуги Цикл
		
		Если СтрокаТабличнойЧасти.Количество = 0 И СтрокаТабличнойЧасти.Стоимость = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтрокаТабличнойЧасти.ВидДвижения = Перечисления.ИНАГРО_ВидыДвижений.Приход Тогда
			НоваяСтрока = ТаблицаУслугПоПриходу.Добавить();
		ИначеЕсли СтрокаТабличнойЧасти.ВидДвижения = Перечисления.ИНАГРО_ВидыДвижений.Расход Тогда 
			НоваяСтрока = ТаблицаУслугПоРасходу.Добавить();
		Иначе 
			Продолжить;
		КонецЕсли;		
		
		НоваяСтрока.ДатаРасчета        = Дата;
		НоваяСтрока.Ссылка             = Ссылка; 
		НоваяСтрока.Организация        = Организация;
		НоваяСтрока.Контрагент         = СтрокаТабличнойЧасти.Контрагент;
		НоваяСтрока.ДоговорКонтрагента = СтрокаТабличнойЧасти.ДоговорКонтрагента;
		НоваяСтрока.Номенклатура       = СтрокаТабличнойЧасти.Номенклатура;
		НоваяСтрока.Культура           = СтрокаТабличнойЧасти.Культура;
		НоваяСтрока.Склад              = СтрокаТабличнойЧасти.Склад;
		НоваяСтрока.Урожай             = СтрокаТабличнойЧасти.Урожай;
		НоваяСтрока.ВидХранения        = СтрокаТабличнойЧасти.ВидХранения;
		НоваяСтрока.Количество         = СтрокаТабличнойЧасти.Количество;
		НоваяСтрока.Стоимость          = СтрокаТабличнойЧасти.Стоимость;
		
	КонецЦикла;
	
КонецПроцедуры	

#КонецОбласти

#КонецЕсли