#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПроцедурыИФункцииПечати

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Типовая форма П5
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ДоговорНаВыполнениеРаботСФизЛицом";
	КомандаПечати.Представление = НСтр("ru='Договор на выполнение работ';uk='Договір на виконання робіт'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.СписокФорм    = "ФормаДокумента,ФормаСписка";
	
	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор  = "Реестр";
	КомандаПечати.Представление  = НСтр("ru='Реестр документов';uk='Реєстр документів'");
	КомандаПечати.ЗаголовокФормы = НСтр("ru='Реестр документов ""Договор на выполнение работ с физ. лицом""';uk='Реєстр документів ""Договір на виконання робіт з фіз. особою""'");
	КомандаПечати.Обработчик     = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм     = "ФормаСписка";
	КомандаПечати.Порядок        = 100;

КонецПроцедуры

Функция СформироватьЗапросДляПечати(Ссылка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ИНАГРО_ДоговорНаВыполнениеРаботСФизЛицом.Номер КАК Номер,
	               |	ИНАГРО_ДоговорНаВыполнениеРаботСФизЛицом.Дата КАК Дата,
	               |	ИНАГРО_ДоговорНаВыполнениеРаботСФизЛицом.Организация.Наименование КАК НазваниеОрганизации,
	               |	ИНАГРО_ДоговорНаВыполнениеРаботСФизЛицом.ДатаНачала КАК ДатаНачала,
	               |	ИНАГРО_ДоговорНаВыполнениеРаботСФизЛицом.ДатаОкончания КАК ДатаОкончания,
	               |	ИНАГРО_ДоговорНаВыполнениеРаботСФизЛицом.ХарактерОплаты КАК ЧастотаВыплат,
	               |	ЕСТЬNULL(ФИОФизическихЛицСрезПоследних.Фамилия + "" "" + ФИОФизическихЛицСрезПоследних.Имя + "" "" + ФИОФизическихЛицСрезПоследних.Отчество, ИНАГРО_ДоговорНаВыполнениеРаботСФизЛицом.Сотрудник.Наименование) КАК РаботникНаименование,
	               |	ИНАГРО_ДоговорНаВыполнениеРаботСФизЛицом.СуммаЗаРаботу КАК СуммаЗаРаботу,
	               |	ДокументыФизическихЛицСрезПоследних.ВидДокумента.Наименование КАК ДокументВид,
	               |	ДокументыФизическихЛицСрезПоследних.Серия КАК ДокументСерия,
	               |	ДокументыФизическихЛицСрезПоследних.Номер КАК ДокументНомер,
	               |	ЕСТЬNULL(ИНАГРО_ДоговорНаВыполнениеРаботСФизЛицом.Организация.ОсновнойБанковскийСчет.НомерСчета, ""__________________"") КАК НомерСчета,
	               |	ЕСТЬNULL(ИНАГРО_ДоговорНаВыполнениеРаботСФизЛицом.Организация.ОсновнойБанковскийСчет.Банк.Наименование, ""_______________________________________________________________________________________"") КАК Банк,
	               |	КонтактнаяИнформация.Представление КАК АдресОрганизации
	               |ИЗ
	               |	Документ.ИНАГРО_ДоговорНаВыполнениеРаботСФизЛицом КАК ИНАГРО_ДоговорНаВыполнениеРаботСФизЛицом
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизическихЛиц.СрезПоследних(&Дата, ФизическоеЛицо = &ФизЛицо) КАК ФИОФизическихЛицСрезПоследних
	               |		ПО (ИСТИНА)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДокументыФизическихЛиц.СрезПоследних(&Дата, Физлицо = &ФизЛицо) КАК ДокументыФизическихЛицСрезПоследних
	               |		ПО (ИСТИНА)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации.КонтактнаяИнформация КАК КонтактнаяИнформация
	               |		ПО ИНАГРО_ДоговорНаВыполнениеРаботСФизЛицом.Организация = КонтактнаяИнформация.Ссылка
	               |			И (КонтактнаяИнформация.Тип = &Адрес)
	               |			И (КонтактнаяИнформация.Вид = &АдресЮридический)
	               |ГДЕ
	               |	ИНАГРО_ДоговорНаВыполнениеРаботСФизЛицом.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка",				Ссылка);
	Запрос.УстановитьПараметр("Адрес",				Перечисления.ТипыКонтактнойИнформации.Адрес);
	Запрос.УстановитьПараметр("АдресЮридический",	Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации);
	Запрос.УстановитьПараметр("ФизЛицо",			Ссылка.Сотрудник.ФизическоеЛицо);
	Запрос.УстановитьПараметр("Дата",				Ссылка.Дата);

	Возврат Запрос.Выполнить();
	
КонецФункции

Функция ПечатьДоговорНаВыполнениеРаботСФизЛицом(МассивОбъектов, ОбъектыПечати, ПараметрыВывода)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ДоговорНаВыполнениеРаботСФизЛицом";
		
	Для каждого Ссылка Из МассивОбъектов Цикл
		
		// запоминаем области макета
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ИНАГРО_ДоговорНаВыполнениеРаботСФизЛицом.ПФ_MXL_Договор");
		
		// печать производится на языке, указанном в настройках пользователя
		КодЯзыкаПечать = ПараметрыВывода.КодЯзыкаДляМногоязычныхПечатныхФорм;
		Макет.КодЯзыкаМакета = КодЯзыкаПечать;	
		
		Выборка = СформироватьЗапросДляПечати(Ссылка).Выбрать();
		
		Если Выборка.Следующий() Тогда
			Макет.Параметры.Заполнить(Выборка);
			Макет.Параметры.Номер = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(Выборка.Номер, Истина, Истина);
			Если ЗначениеЗаполнено(Выборка.ДатаНачала) Тогда
				Макет.параметры.ДатаНачала = Формат(Выборка.ДатаНачала,?(КодЯзыкаПечать="ru", "Л=ru; ДЛФ=DD","Л=uk; ДЛФ=DD"));
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Выборка.ДатаОкончания) Тогда
				Макет.параметры.ДатаОкончания = Формат(Выборка.ДатаОкончания,?(КодЯзыкаПечать="ru", "Л=ru; ДЛФ=DD","Л=uk; ДЛФ=DD"));
			КонецЕсли;
			
		КонецЕсли;
			
		Макет.Параметры.ЧастотаВыплат = НРег(Макет.Параметры.ЧастотаВыплат);
			
		ТабДок.Вывести(Макет);
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Устанавливаем признак доступности печати покомплектно.
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	// Проверяем, нужно ли для макета формировать табличный документ.
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ДоговорНаВыполнениеРаботСФизЛицом")  Тогда
		
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ДоговорНаВыполнениеРаботСФизЛицом", НСтр("ru='Договор на выполнение работ';uk='Договір на виконання робіт'"), 
		ПечатьДоговорНаВыполнениеРаботСФизЛицом(МассивОбъектов, ОбъектыПечати, ПараметрыВывода), , "Документ.ИНАГРО_ДоговорНаВыполнениеРаботСФизЛицом.ПФ_MXL_Договор", ,Истина);
		
	КонецЕсли; 	
	
КонецПроцедуры

Функция ПолучитьДополнительныеРеквизитыДляРеестра() Экспорт
	
	Результат = Новый Структура;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли