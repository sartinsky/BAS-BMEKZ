#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПроцедурыИФункцииПечати

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	// Корректировка списком
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "КорректировкаСписком";
	КомандаПечати.Представление = НСтр("ru='Корректировка списком';uk='Коригування списком'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.СписокФорм    = "ФормаДокумента,ФормаСписка";
	
	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Реестр";
	КомандаПечати.Представление = НСтр("ru='Реестр документов';uk='Реєстр документів'");
	КомандаПечати.ЗаголовокФормы= НСтр("ru='Реестр документов ""Корректировка списком""';uk='Реєстр документів ""Коригування списком""'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм    = "ФормаСписка";
	КомандаПечати.Порядок       = 100;

КонецПроцедуры		

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Устанавливаем признак доступности печати покомплектно.
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	// Проверяем, нужно ли для макета СчетЗаказа формировать табличный документ.
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "КорректировкаСписком") Тогда		
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "КорректировкаСписком", НСтр("ru='КорректировкаСписком';uk='Коригування списком'"), 
		ПечатьДокумента(МассивОбъектов, ОбъектыПечати, ПараметрыВывода),, "Документ.ИНАГРО_КорректировкаСписком.ПФ_MXL_КорректировкаСписком", ,Истина);
		
	КонецЕсли; 	

КонецПроцедуры

Функция ПечатьДокумента(МассивОбъектов, ОбъектыПечати, ПараметрыВывода)
	
	ЗапросШапка = Новый Запрос;	
	ЗапросШапка.Текст =
		"ВЫБРАТЬ
		|	ИНАГРО_КорректировкаСписком.Организация КАК Организация,
		|	ИНАГРО_КорректировкаСписком.Номер КАК Номер,
		|	ИНАГРО_КорректировкаСписком.Дата КАК Дата
		|ИЗ
		|	Документ.ИНАГРО_КорректировкаСписком КАК ИНАГРО_КорректировкаСписком
		|ГДЕ
		|	ИНАГРО_КорректировкаСписком.Ссылка = &Ссылка";	
		
	ЗапросСписок = Новый Запрос;
	ЗапросСписок.Текст = 
		"ВЫБРАТЬ
		|	ИНАГРО_КорректировкаСпискомСписок.Владелец КАК Владелец,
		|	ИНАГРО_КорректировкаСпискомСписок.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	ИНАГРО_КорректировкаСпискомСписок.ВидХранения КАК ВидХранения,
		|	ИНАГРО_КорректировкаСпискомСписок.Склад КАК Склад,
		|	ИНАГРО_КорректировкаСпискомСписок.Номенклатура КАК Номенклатура,
		|	ИНАГРО_КорректировкаСпискомСписок.Урожай КАК Урожай,
		|	ИНАГРО_КорректировкаСпискомСписок.ВидДвижения КАК ВидДвижения,
		|	ИНАГРО_КорректировкаСпискомСписок.Фасованное КАК Фасованное,
		|	ИНАГРО_КорректировкаСпискомСписок.Фасовка КАК Фасовка,
		|	ИНАГРО_КорректировкаСпискомСписок.ЗачетныйВес КАК ЗачетныйВес,
		|	ИНАГРО_КорректировкаСпискомСписок.Вес КАК Вес,
		|	ИНАГРО_КорректировкаСпискомСписок.Влажность КАК Влажность,
		|	ИНАГРО_КорректировкаСпискомСписок.СорнаяПримесь КАК СорнаяПримесь,
		|	ИНАГРО_КорректировкаСпискомСписок.ЗерноваяПримесь КАК ЗерноваяПримесь,
		|	ИНАГРО_КорректировкаСпискомСписок.ВесПоВлажности КАК ВесПоВлажности,
		|	ИНАГРО_КорректировкаСпискомСписок.ВесПоСорнойПримеси КАК ВесПоСорнойПримеси,
		|	ИНАГРО_КорректировкаСпискомСписок.УбыльВесаПриСушке КАК УбыльВесаПриСушке,
		|	ИНАГРО_КорректировкаСпискомСписок.МестоХранения КАК МестоХранения,
		|	ИНАГРО_КорректировкаСпискомСписок.НомерСтроки КАК НомерСтроки,
		|	ИНАГРО_КорректировкаСпискомСписок.Количество КАК Количество
		|ИЗ
		|	Документ.ИНАГРО_КорректировкаСписком.Список КАК ИНАГРО_КорректировкаСпискомСписок
		|ГДЕ
		|	ИНАГРО_КорректировкаСпискомСписок.Ссылка = &Ссылка"; 
	
	ТабДокумент = Новый ТабличныйДокумент;		
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ИНАГРО_КорректировкаСписком_КорректировкаСписком"; 	
	
	ПервыйДокумент = Истина;  	
	
	Для каждого Ссылка Из МассивОбъектов Цикл	
		
		Макет = ПолучитьМакет("ПФ_MXL_КорректировкаСписком");
		
		КодЯзыкаПечать = ПараметрыВывода.КодЯзыкаДляМногоязычныхПечатныхФорм;
		Макет.КодЯзыкаМакета = КодЯзыкаПечать;		
		
		Если Не ПервыйДокумент Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1; 	
		
		ЗапросШапка. УстановитьПараметр("Ссылка",Ссылка);
		ЗапросСписок.УстановитьПараметр("Ссылка",Ссылка);
		
		Шапка = ЗапросШапка.Выполнить().Выбрать();
		Шапка.Следующий();
		
		Список = ЗапросСписок.Выполнить().Выгрузить();  		
		
		Область = Макет.ПолучитьОбласть("Заголовок");
		ТабДокумент.Вывести(Область);
		// Шапка
		ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
		ОбластьШапка.Параметры.Заполнить(Шапка);
		ТабДокумент.Вывести(ОбластьШапка);
		// Список
		Область = Макет.ПолучитьОбласть("СписокШапка");
		ТабДокумент.Вывести(Область);
		Ном = 1;
		ОбластьСписок = Макет.ПолучитьОбласть("Список");
		Для Каждого ТекСтрокаСписок Из Список Цикл
			ОбластьСписок.Параметры.Заполнить(ТекСтрокаСписок);
			ОбластьСписок.Параметры.Ном = Ном;
			ИНАГРО_Элеватор.ПолучитьНаименованияКонтрагента(ОбластьСписок.Параметры.Владелец);
			ТабДокумент.Вывести(ОбластьСписок);
			Ном = Ном + 1;
		КонецЦикла; 
		
		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, Ссылка);
		
	КонецЦикла;
	
	Возврат ТабДокумент;

КонецФункции 

Функция ПолучитьДополнительныеРеквизитыДляРеестра() Экспорт
	
	Результат = Новый Структура;	
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти 

#КонецЕсли