#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Перем мВалютаРегламентированногоУчета;

Перем КурсЗачетаАвансаРегл;

Перем АмортизацияБА Экспорт; // ИНАГРО

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА
 
// Процедура определяет параметры учетной политики
//
Процедура ПодготовитьПараметрыУчетнойПолитики(СтруктураШапкиДокумента, Отказ, Заголовок)
	
	СтруктураШапкиДокумента.Вставить("ЕстьНалогНаПрибыль"             , УчетнаяПолитика.ПлательщикНалогаНаПрибыль(СтруктураШапкиДокумента.Организация, КонецМесяца(СтруктураШапкиДокумента.Дата)));
	СтруктураШапкиДокумента.Вставить("ЕстьНалогНаПрибыльДо2015"		  , УчетнаяПолитика.ПлательщикНалогаНаПрибыльДо2015(СтруктураШапкиДокумента.Организация, КонецМесяца(СтруктураШапкиДокумента.Дата)));
	СтруктураШапкиДокумента.Вставить("ЕстьНДС"                        , УчетнаяПолитика.ПлательщикНДС(СтруктураШапкиДокумента.Организация, КонецМесяца(СтруктураШапкиДокумента.Дата)));
	
КонецПроцедуры // ПодготовитьПараметрыУчетнойПолитики()

// Выгружает результат запроса в табличную часть, добавляет ей необходимые колонки для проведения.
//
// Параметры: 
//  РезультатЗапросаПоТоварам - результат запроса по табличной части "Товары",
//  СтруктураШапкиДокумента   - выборка по результату запроса по шапке документа.
//
// Возвращаемое значение:
//  Сформированная таблиица значений.
//
Функция ПодготовитьТаблицуТоваров(РезультатЗапросаПоТоварам, СтруктураШапкиДокумента, ПогрешностиОкругления)

	ТаблицаТоваров = РезультатЗапросаПоТоварам.Выгрузить();
	
	ТаблицаТоваров.Колонки.Добавить("ВидДеятельностиНДС", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыДеятельностиНДС"));
	
	Если НЕ СтруктураШапкиДокумента.ЕстьНДС Тогда
		
		// для регламентного учета считаем НДС
		ТаблицаТоваров.ЗаполнитьЗначения(Перечисления.СтавкиНДС.БезНДС, "СтавкаНДС");
		ТаблицаТоваров.ЗаполнитьЗначения(0                            , "НДС");
		
	КонецЕсли;

	Для Каждого СтрокаТаблицы Из ТаблицаТоваров Цикл
		
		СтрокаТаблицы.ВидДеятельностиНДС  = УчетНДС.ПолучитьВидДеятельностиНДС(СтрокаТаблицы.СтавкаНДС);
		
	КонецЦикла;

	НалоговыйУчет.ДобавитьКолонкиТоваровРегл(ТаблицаТоваров, СтруктураШапкиДокумента, ПогрешностиОкругления, Ложь);
	
	Возврат ТаблицаТоваров;

КонецФункции // ПодготовитьТаблицуТоваров()

// По результату запроса по шапке документа формируем движения по регистрам.
//
// Параметры: 
//  РежимПроведения           - режим проведения документа (оперативный или неоперативный),
//  СтруктураШапкиДокумента   - выборка из результата запроса по шапке документа,
//  ТаблицаПоТоварам          - таблица значений, содержащая данные для проведения и проверки ТЧ Товары
//  ТаблицаПоТаре             - таблица значений, содержащая данные для проведения и проверки ТЧ "Возвратная тара",
//  Отказ                     - флаг отказа в проведении,
//  Заголовок                 - строка, заголовок сообщения об ошибке проведения.
//
Процедура ДвиженияПоРегистрам(РежимПроведения, СтруктураШапкиДокумента, 
	                          ТаблицаПоТоварам, ТаблицаМестонахождений, Отказ, Заголовок)

	ТекОрганизация = СтруктураШапкиДокумента.Организация;
	ДатаДока       = Дата;
	НомерЖурнала   = НСтр("ru='ОС';uk='ОЗ'",Локализация.КодЯзыкаИнформационнойБазы());

	ПроводкиБУ = Движения.Хозрасчетный;

	ТаблицыДокумента = Новый Структура();
   	ТаблицыДокумента.Вставить("ТаблицаПоТоварам",ТаблицаПоТоварам);
		
	ТаблицаАвансов = БухгалтерскийУчетРасчетовСКонтрагентами.ЗачетАванса(ЭтотОбъект, СтруктураШапкиДокумента , мВалютаРегламентированногоУчета, ТаблицыДокумента , Отказ, Заголовок, НомерЖурнала);
		
	КурсЗачетаАвансаРегл = ?(ТаблицаАвансов.Итог("СуммаВал") = 0, Неопределено, ТаблицаАвансов.Итог("Сумма") / ТаблицаАвансов.Итог("СуммаВал"));
	
	ВыручкаПоБУ = ТаблицаПоТоварам.Скопировать();
	ВыручкаПоБУ.Свернуть("СчетДоходовБУ, 
						  |СубконтоДоходовБУ1,
						  |СубконтоДоходовБУ2,
						  |СубконтоДоходовБУ3,
						  |НалоговоеНазначениеДоходовИЗатрат
						  |",
						  "ПроводкиСуммаСНДСРегл, 
						  |ПроводкиСуммаБезНДСРегл, 
						  |ОстСтоимостьНУ, 
						  |ПроводкиСуммаСНДСВал");

	Для каждого СтрокаТаблицы из ВыручкаПоБУ Цикл

		// Выручка
		Если СтрокаТаблицы.ПроводкиСуммаСНДСРегл = 0
			 И СтрокаТаблицы.ПроводкиСуммаСНДСВал = 0 Тогда
			
			Продолжить;
			
		КонецЕсли;

		Проводка = ПроводкиБУ.Добавить();

		Проводка.Период       = ДатаДока;
		Проводка.Организация  = СтруктураШапкиДокумента.Организация;
		Проводка.Сумма        = СтрокаТаблицы.ПроводкиСуммаСНДСРегл;
		Проводка.Содержание   = НСтр("ru='Реализация ОС';uk='Реалізація ОЗ'",Локализация.КодЯзыкаИнформационнойБазы());
		Проводка.НомерЖурнала = НомерЖурнала;

		Проводка.СчетДт = СтруктураШапкиДокумента.СчетУчетаРасчетовСКонтрагентом;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"Контрагенты", СтруктураШапкиДокумента.Контрагент);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"Договоры",    СтруктураШапкиДокумента.ДоговорКонтрагента);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"ДокументыРасчетовСКонтрагентами", УправлениеВзаиморасчетами.ОпределитьДокументРасчетовСКонтрагентом(СтруктураШапкиДокумента.Ссылка,СтруктураШапкиДокумента.Сделка));
		Проводка.ВалютаДт        = СтруктураШапкиДокумента.ВалютаДокумента;
		Проводка.ВалютнаяСуммаДт = СтрокаТаблицы.ПроводкиСуммаСНДСВал;

		Проводка.СчетКт = СтрокаТаблицы.СчетДоходовБУ;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 1, СтрокаТаблицы.СубконтоДоходовБУ1);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 2, СтрокаТаблицы.СубконтоДоходовБУ2);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 3, СтрокаТаблицы.СубконтоДоходовБУ3);

		СуммаНУДоход = Макс(СтрокаТаблицы.ПроводкиСуммаБезНДСРегл - СтрокаТаблицы.ОстСтоимостьНУ, 0);
		СуммаНУДоходСНДС = Макс(СтрокаТаблицы.ПроводкиСуммаСНДСРегл - СтрокаТаблицы.ОстСтоимостьНУ, 0);
		
		// если доход
		Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыльДо2015 И СуммаНУДоход > 0 Тогда
			
			Проводка.НалоговоеНазначениеКт  = СтрокаТаблицы.НалоговоеНазначениеДоходовИЗатрат;
			Проводка.СуммаНУКт 				= СуммаНУДоходСНДС;
			
		КонецЕсли;
		
	КонецЦикла;

	// Продажи (нал. учет)	
	ТаблицаПоВторомуСобытиюНал = ДвиженияПоРегистрамНалоговогоУчета(СтруктураШапкиДокумента, ТаблицаПоТоварам, Отказ);
	
	// НДС 
	ПроводкиПоНДС(ПроводкиБУ, СтруктураШапкиДокумента, ТаблицаПоТоварам, ТаблицаПоВторомуСобытиюНал, Отказ);
	
	//Учет курсовых разниц
	Если (ВалютаДокумента <> мВалютаРегламентированногоУчета) Тогда
		
		БухгалтерскийУчетРед12.ПереоценкаСчетовДокументаРегл(ЭтотОбъект,СтруктураШапкиДокумента, мВалютаРегламентированногоУчета,Отказ,Заголовок);
		
	КонецЕсли; // Учет курсовых разниц
	
	////////////////////////////////////////////////////
	// Движения по регистрам подсистемы ОсновныеСредства
	
	СостояниеОС             = Движения.СостоянияОСОрганизаций;
	СобытияОС               = Движения.СобытияОСОрганизаций;
	НачислениеАмортизацииБУ = Движения.НачислениеАмортизацииОСБухгалтерскийУчет;
	НачислениеАмортизацииНУ = Движения.НачислениеАмортизацииОСНалоговыйУчет;
	СчетОС                  = ПланыСчетов.Хозрасчетный.ОсновныеСредства;
	ПереоценкаОСБУ 			= Движения.ПереоценкаОСБухгалтерскийУчет;

	УправлениеНеоборотнымиАктивами.ДополнитьТабличнуюЧастьСведениямиОбОСБухНалогРегл(МоментВремени(), ТаблицаПоТоварам,
	                                                  СтруктураШапкиДокумента, 
													  Отказ, Заголовок);

	Если Отказ Тогда
		
		Возврат;
		
	КонецЕсли;								
	
	// Если ранее подготовка к передачи не была проведена
	Если НЕ ЗначениеЗаполнено(СтруктураШапкиДокумента.ДокПодготовкаКПередачеОС) Тогда
			
		// Подготовим таблицу с данными по амортизации для списания амортизации по направлениям затрат
		ТабАмортизации = Новый ТаблицаЗначений;
		ТабАмортизации.Колонки.Добавить("НаправлениеАмортизации", Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияРасходовПоАмортизации"));
		ТабАмортизации.Колонки.Добавить("ОбъектУчета", Новый ОписаниеТипов("СправочникСсылка.ОсновныеСредства"));
		ТабАмортизации.Колонки.Добавить("Сумма", ОбщегоНазначенияБПКлиентСервер.ПолучитьОписаниеТиповЧисла(15, 2));
		ТабАмортизации.Колонки.Добавить("СуммаНУ", ОбщегоНазначенияБПКлиентСервер.ПолучитьОписаниеТиповЧисла(15, 2));
		ТабАмортизации.Колонки.Добавить("СчетАмортизации");
		ТабАмортизации.Колонки.Добавить("НалоговоеНазначение", 		Новый ОписаниеТипов("СправочникСсылка.НалоговыеНазначенияАктивовИЗатрат"));
		ТабАмортизации.Колонки.Добавить("Местонахождение",			Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
		
		Для Каждого СтрокаОС Из ТаблицаПоТоварам Цикл
			
			Если СтрокаОС.АмортизацияЗаМесяцБУ > 0 ИЛИ СтрокаОС.АмортизацияЗаМесяцНУ > 0 Тогда
				
				НоваяСтрока = ТабАмортизации.Добавить();
				
				НоваяСтрока.Сумма                  	= СтрокаОС.АмортизацияЗаМесяцБУ;
				НоваяСтрока.СуммаНУ                	= СтрокаОС.АмортизацияЗаМесяцНУ;
				НоваяСтрока.ОбъектУчета            	= СтрокаОС.ОсновноеСредство;
				НоваяСтрока.НаправлениеАмортизации 	= СтрокаОС.НаправлениеБУ;
				НоваяСтрока.СчетАмортизации        	= СтрокаОС.СчетНачисленияАмортизацииБУ;
				НоваяСтрока.НалоговоеНазначение 	= СтрокаОС.НалоговоеНазначение_ОС;
				
				ТекМестонахождение 					= ТаблицаМестонахождений.Найти(СтрокаОС.ОсновноеСредство,"ОС_БУ");
				НоваяСтрока.Местонахождение 		= ?(ТекМестонахождение = Неопределено, Неопределено, ТекМестонахождение.Местонахождение_БУ);
				
			КонецЕсли;
			
		КонецЦикла;
		
		// Вызов процедуры списания амортизации по направлениям.
		// Создаются движения по начислению амортизации.
		УправлениеНеоборотнымиАктивами.ПолучитьРаспределениеАмортизацииПоНаправлениямРегл(ПроводкиБУ,
		                                                   Отказ,
														   Заголовок,
														   ТабАмортизации,
														   СтруктураШапкиДокумента,
														   НомерЖурнала,
														   НСтр("ru='Начисление амортизации ОС';uk='Нарахування амортизації ОЗ'",Локализация.КодЯзыкаИнформационнойБазы()));
			
	КонецЕсли;

	НеОблНДСДеятельность = Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяХозДеятельность;
	
	// Создание движений документа по БУ	
	Для Каждого СтрокаТЧ Из ТаблицаПоТоварам Цикл

		ТекОС = СтрокаТЧ.ОсновноеСредство;

		НепроизводственныйОС = (СтрокаТЧ.НалоговоеНазначение_ОС = Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность);
		
		// Движения по регистру НачислениеАмортизацииОСБухгалтерскийУчет
		Движение = НачислениеАмортизацииБУ.Добавить();
		Движение.Период               = Дата;
		Движение.ОсновноеСредство     = ТекОС;
		Движение.Организация          = ТекОрганизация;
		Движение.НачислятьАмортизацию = Ложь;
		
		Если СтрокаТЧ.ПревышениеСуммДооценокНадСуммамиУценокБУ > 0 Тогда
			
			// ПереоценкаОСБухгалтерскийУчет
			СтрокаДвижений = ПереоценкаОСБУ.ДобавитьРасход();
			
			СтрокаДвижений.Период           = Дата;
			СтрокаДвижений.ОсновноеСредство = ТекОС;
			СтрокаДвижений.Организация      = ТекОрганизация;
			
			СтрокаДвижений.СуммаПереоценки 	= СтрокаТЧ.ПревышениеСуммДооценокНадСуммамиУценокБУ;
			
			// Хозрасчетный Дт 423 Кт 441
			ПроводкаБУ = ПроводкиБУ.Добавить();
			ПроводкаБУ.Период       = ДатаДока;
			ПроводкаБУ.Организация  = ТекОрганизация;
			
			ПроводкаБУ.СчетДт       = СтрокаТЧ.СчетУчетаДооценокОС;
			БухгалтерскийУчет.УстановитьСубконто(ПроводкаБУ.СчетДт, ПроводкаБУ.СубконтоДт, "ОсновныеСредства", ТекОС);

			ПроводкаБУ.СчетКт       = ПланыСчетов.Хозрасчетный.НераспределеннаяПрибыль;
			
			ПроводкаБУ.Сумма        = СтрокаТЧ.ПревышениеСуммДооценокНадСуммамиУценокБУ;
			ПроводкаБУ.Содержание   = НСтр("ru='Списана сумма дооценок ОС';uk='Списана сума дооцінок ОЗ'",Локализация.КодЯзыкаИнформационнойБазы());
			ПроводкаБУ.НомерЖурнала = НомерЖурнала;
			
		ИначеЕсли СтрокаТЧ.ПревышениеСуммДооценокНадСуммамиУценокБУ < 0 Тогда	
			
			// ПереоценкаОСБухгалтерскийУчет
			СтрокаДвижений = ПереоценкаОСБУ.ДобавитьПриход();
			
			СтрокаДвижений.Период           = ДатаДока;
			СтрокаДвижений.ОсновноеСредство = ТекОС;
			СтрокаДвижений.Организация      = ТекОрганизация;
			
			СтрокаДвижений.СуммаПереоценки 	= -СтрокаТЧ.ПревышениеСуммДооценокНадСуммамиУценокБУ;
			
		КонецЕсли;	
		
		// Движения по регистру НачислениеАмортизацииОСНалоговыйУчет
		Движение = НачислениеАмортизацииНУ.Добавить();
		Движение.Период               = Дата;
		Движение.ОсновноеСредство     = ТекОС;
		Движение.Организация          = ТекОрганизация;
		Движение.НачислятьАмортизацию = Ложь;
		
		// Движения по регистру СостоянияОСОрганизаций
		Движение = СостояниеОС.Добавить();
		
		Движение.ДатаСостояния    = ДатаДока;
		Движение.ОсновноеСредство = ТекОС;
		Движение.Организация      = ТекОрганизация;
		Движение.Состояние        = Перечисления.СостоянияОС.СнятоСУчета;
		Движение.НазваниеДокумента = Строка(СтруктураШапкиДокумента.Ссылка.Метаданные());
		Движение.НомерДокумента    = СтруктураШапкиДокумента.Номер;
		
		// Движения по регистру СобытияОСОрганизаций
		Движение = СобытияОС.Добавить();
		
		Движение.Период            = ДатаДока;
		Движение.ОсновноеСредство  = СтрокаТЧ.ОсновноеСредство;
		Движение.Организация       = СтруктураШапкиДокумента.Организация;
		Движение.Событие           = СтруктураШапкиДокумента.СобытиеОС;
		Движение.НазваниеДокумента = Строка(СтруктураШапкиДокумента.Ссылка.Метаданные());
		Движение.НомерДокумента    = СтруктураШапкиДокумента.Номер;

		// Движения по регистру ИНАГРО_РеализацияОрганизаций
		Если АмортизацияБА И ЗначениеЗаполнено(ЭтотОбъект.ИНАГРО_ДокументОперативногоУчета) Тогда  
			ИНАГРО_Общий.ИНАГРО_РегистрацияРеализации(СтрокаТЧ,ЭтотОбъект.ИНАГРО_Количество,СтрокаТЧ.СуммаСНДСРегл,ЭтотОбъект,1);	// ИН-АГРО
		КонецЕсли;	

		СчетУчетаБУ = СтрокаТЧ.СчетУчетаБУ;
		
		СчетПродажаОС = СтрокаТЧ.СчетПродажиОС;

		// Если ранее подготовка к передаче не была проведена
		Если НЕ ЗначениеЗаполнено(СтруктураШапкиДокумента.ДокПодготовкаКПередачеОС) Тогда
			
			// списание амортизации Д СчетНачисленияАмортизации К СчетУчетаБУ
			СуммаПроводки 	= СтрокаТЧ.АмортизацияБУ + СтрокаТЧ.АмортизацияЗаМесяцБУ;
			СуммаПроводкиНУ = СтрокаТЧ.АмортизацияНУ + СтрокаТЧ.АмортизацияЗаМесяцНУ;
			
			Если СуммаПроводки <> 0 ИЛИ (СуммаПроводкиНУ <> 0 И СтруктураШапкиДокумента.ЕстьНалогНаПрибыль) Тогда
				
				Проводка = ПроводкиБУ.Добавить();
				
				Проводка.Период       = ДатаДока;
				Проводка.Активность   = Истина;
				Проводка.Организация  = ТекОрганизация;
				Проводка.Содержание   = НСтр("ru='Списана амортизация';uk='Списано амортизацію'",Локализация.КодЯзыкаИнформационнойБазы());
				Проводка.НомерЖурнала = НомерЖурнала;
				Проводка.Сумма        = СуммаПроводки;
				
				Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыль ИЛИ СтруктураШапкиДокумента.ЕстьНДС Тогда
					
					Проводка.НалоговоеНазначениеДт = СтрокаТЧ.НалоговоеНазначение_ОС;
					
				КонецЕсли;	
				
				Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыль Тогда
					
					Проводка.СуммаНУДт = СуммаПроводкиНУ;
					
				КонецЕсли;
				
				Проводка.СчетДт = СтрокаТЧ.СчетНачисленияАмортизацииБУ;
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "ОсновныеСредства", ТекОС);
				
				Проводка.СчетКт = СчетУчетаБУ;
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ОсновныеСредства", ТекОС);
				
				Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыль ИЛИ СтруктураШапкиДокумента.ЕстьНДС Тогда
					
					Проводка.НалоговоеНазначениеКт = СтрокаТЧ.НалоговоеНазначение_ОС;
					
				КонецЕсли;	
				
				Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыль Тогда
					
					Проводка.СуммаНУКт = СуммаПроводкиНУ;
					
				КонецЕсли;
				
				Если НЕ СтруктураШапкиДокумента.ЕстьНДС Тогда
					// организация - не плательщик НДС. 
					Если НепроизводственныйОС Тогда
						// Непроизводственное
						Проводка.НалоговоеНазначениеДт = СтрокаТЧ.НалоговоеНазначение_ОС;
						Проводка.НалоговоеНазначениеКт = СтрокаТЧ.НалоговоеНазначение_ОС;
					Иначе	
						Проводка.НалоговоеНазначениеДт = НеОблНДСДеятельность;
						Проводка.НалоговоеНазначениеКт = НеОблНДСДеятельность;
					КонецЕсли;	
				КонецЕсли;	
				
			КонецЕсли;
				
			// списание остаточной стоимости Д ПродажаОС К СчетУчетаОС
			СуммаПроводки = СтрокаТЧ.СтоимостьБУ - СтрокаТЧ.АмортизацияБУ - СтрокаТЧ.АмортизацияЗаМесяцБУ;
			
			Если СуммаПроводки <> 0 ИЛИ (СтрокаТЧ.ОстСтоимостьНУ <> 0 И СтруктураШапкиДокумента.ЕстьНалогНаПрибыль) Тогда
				
				Проводка = ПроводкиБУ.Добавить();
				
				Проводка.Период       = ДатаДока;
				Проводка.Активность   = Истина;
				Проводка.Организация  = ТекОрганизация;
				Проводка.Содержание   = НСтр("ru='Списана ост. стоимость';uk='Списана зал. вартість'",Локализация.КодЯзыкаИнформационнойБазы());
				Проводка.НомерЖурнала = НомерЖурнала;
				Проводка.Сумма        = СуммаПроводки;
				
				Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыль ИЛИ СтруктураШапкиДокумента.ЕстьНДС Тогда
					
					Проводка.НалоговоеНазначениеДт = СтрокаТЧ.НалоговоеНазначение_ОС;
					
				КонецЕсли;	
				
				Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыль Тогда
					
					Проводка.СуммаНУДт = СтрокаТЧ.ОстСтоимостьНУ;
					
				КонецЕсли;
				
				Проводка.СчетДт = СчетПродажаОС;
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "ОсновныеСредства", ТекОС);
				
				Проводка.СчетКт = СчетУчетаБУ;
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ОсновныеСредства", ТекОС);
				
				Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыль ИЛИ СтруктураШапкиДокумента.ЕстьНДС Тогда
					
					Проводка.НалоговоеНазначениеКт = СтрокаТЧ.НалоговоеНазначение_ОС;
					
				КонецЕсли;	
				
				Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыль Тогда
					
					Если СтрокаТЧ.НалоговоеНазначение_ОС <> Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность Тогда 
						Проводка.СуммаНУКт = СтрокаТЧ.ОстСтоимостьНУ;
					КонецЕсли;	
					
				КонецЕсли;
				
				Если НЕ СтруктураШапкиДокумента.ЕстьНДС Тогда
					// организация - не плательщик НДС. 
					Если НепроизводственныйОС Тогда
						// Непроизводственное
						Проводка.НалоговоеНазначениеДт = СтрокаТЧ.НалоговоеНазначение_ОС;
						Проводка.НалоговоеНазначениеКт = СтрокаТЧ.НалоговоеНазначение_ОС;
					Иначе	
						Проводка.НалоговоеНазначениеДт = НеОблНДСДеятельность;
						Проводка.НалоговоеНазначениеКт = НеОблНДСДеятельность;
					КонецЕсли;	
				КонецЕсли;	
				
			КонецЕсли;
			
		КонецЕсли;

		// списание остаточной стоимости Д СчетРасходов К СчетУчета
		СуммаПроводки = СтрокаТЧ.СтоимостьБУ - СтрокаТЧ.АмортизацияБУ - СтрокаТЧ.АмортизацияЗаМесяцБУ;
		
		Если СуммаПроводки <> 0 ИЛИ (СтрокаТЧ.ОстСтоимостьНУ <> 0 И СтруктураШапкиДокумента.ЕстьНалогНаПрибыль) Тогда
			
			Проводка = ПроводкиБУ.Добавить();
			
			Проводка.Период       = ДатаДока;
			Проводка.Организация  = ТекОрганизация;
			Проводка.Содержание   = НСтр("ru='Списана ост. стоимость';uk='Списана зал. вартість'",Локализация.КодЯзыкаИнформационнойБазы());
			Проводка.НомерЖурнала = НомерЖурнала;
			Проводка.Сумма        = СуммаПроводки;
			
			Проводка.СчетДт = СтрокаТЧ.СчетРасходовБУ;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1, СтрокаТЧ.СубконтоРасходовБУ1);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 2, СтрокаТЧ.СубконтоРасходовБУ2);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3, СтрокаТЧ.СубконтоРасходовБУ3);
			
			Проводка.СчетКт = СчетПродажаОС;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ОсновныеСредства", ТекОС);
			
			Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыль ИЛИ СтруктураШапкиДокумента.ЕстьНДС Тогда
				
				Проводка.НалоговоеНазначениеКт = СтрокаТЧ.НалоговоеНазначение_ОС;
				
			КонецЕсли;	
			
			Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыль Тогда
				
				Проводка.СуммаНУКт = СтрокаТЧ.ОстСтоимостьНУ;
				
			КонецЕсли;
			
			СуммаНУРасход = Макс(СтрокаТЧ.ОстСтоимостьНУ - СтрокаТЧ.ПроводкиСуммаБезНДСРегл, 0);
			
			Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыльДо2015 И СуммаНУРасход > 0 Тогда
				
				Проводка.НалоговоеНазначениеДт  = СтрокаТЧ.НалоговоеНазначениеДоходовИЗатрат;
				
			КонецЕсли;	
			
			// если расход
			Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыльДо2015 И СуммаНУРасход > 0 Тогда
				
				Проводка.СуммаНУДт 				= СуммаНУРасход;
				
			КонецЕсли;
			
			Если НЕ СтруктураШапкиДокумента.ЕстьНДС Тогда
				// организация - не плательщик НДС. 
				Если НепроизводственныйОС Тогда
					// Непроизводственное
					Проводка.НалоговоеНазначениеКт = СтрокаТЧ.НалоговоеНазначение_ОС;
				Иначе	
					Проводка.НалоговоеНазначениеКт = НеОблНДСДеятельность;
				КонецЕсли;	
			КонецЕсли;	
			
		КонецЕсли;
		
	КонецЦикла;

	УправлениеНеоборотнымиАктивами.ПроверкаДублированияЗаписейСостоянийОС(СтруктураШапкиДокумента.Организация, Движения.СостоянияОСОрганизаций,Отказ,Заголовок);
	
	// ИНАГРО++   
	Если НЕ Отказ И АмортизацияБА И ЗначениеЗаполнено(ЭтотОбъект.ИНАГРО_ДокументОперативногоУчета) Тогда  
		
		ТаблицаОС = ОС.Выгрузить();
		
		// Очистим ТаблицаОС от возможных ОС, которые не относятся к БА
		//Выполнить "
		
		Если ИНАГРО_ОбщийВызовСервераПовтИсп.ЕстьБСПУ() Тогда
			
			МодульИНАГРО_БиологическиеАктивы = ОбщегоНазначения.ОбщийМодуль("ИНАГРО_БиологическиеАктивы");
			
			ТаблицаОС   = МодульИНАГРО_БиологическиеАктивы.ПолучитьИзОС_ОСБА(Дата, Организация, ТаблицаОС);			
			КодОперации = ?(МодульИНАГРО_БиологическиеАктивы.ЭтоЖивотное(ЭтотОбъект.ИНАГРО_БиологическийАктив), Перечисления.ИНАГРО_КодыОперацийУчетЖивотных.Продажа, Перечисления.ИНАГРО_КодыОперацийУчетРастений.Продажа);
			
			МодульИНАГРО_БиологическиеАктивы.ВыбытиеОСБА(ЭтотОбъект, СтруктураШапкиДокумента, ТаблицаОС, Движения, КодОперации);
			
		КонецЕсли;
		
	КонецЕсли;
	// ИНАГРО--   

КонецПроцедуры // ДвиженияПоРегистрамРегл()

Процедура ПроводкиПоНДС(ПроводкиБУ, СтруктураШапкиДокумента, ТаблицаПоТоварам, ТаблицаПоВторомуСобытиюНал, Отказ)
	
	Если СтруктураШапкиДокумента.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером Тогда
		// Это не наши ценности, следовательно НДС по ним учитывать не нужно
		Возврат;
	КонецЕсли;
	
	Если НЕ СтруктураШапкиДокумента.ЕстьНДС Тогда
		// Учет НДС не ведется
		Возврат;
	КОнецЕсли;

	// Получим таблицу движений по счетам НДС
	
	// ТОВАРЫ
	ТаблицаДвижений = ТаблицаПоТоварам.Скопировать();
	ТаблицаДвижений.Свернуть("СтавкаНДС,
							 |СчетДоходовБУ,
						     |СубконтоДоходовБУ1,
						     |СубконтоДоходовБУ2,
						     |СубконтоДоходовБУ3,
						     |СчетУчетаНДС,
						     |НалоговоеНазначениеДоходовИЗатрат",
						     "ПроводкиСуммаНДСРегл,
						     |ПроводкиСуммаНДСВал,
						     |ПроводкиСуммаБезНДСРегл,
						     |ОстСтоимостьНУ,
							 |ПроводкиСуммаНДСКурсНБУ");
	
	Для Каждого СтрокаТаблицы Из ТаблицаДвижений Цикл
		
		Если    СтрокаТаблицы.ПроводкиСуммаНДСРегл <> 0 
			ИЛИ СтрокаТаблицы.ПроводкиСуммаНДСВал  <> 0 Тогда
			
			Проводка = ПроводкиБУ.Добавить();

			Проводка.Период                     = СтруктураШапкиДокумента.Дата;
			Проводка.Активность                 = Истина;
			Проводка.Организация                = СтруктураШапкиДокумента.Организация;
			Проводка.Сумма                      = СтрокаТаблицы.ПроводкиСуммаНДСРегл;
			Проводка.Содержание                 = НСтр("ru='НДС: налоговые обязательства: отгрузка';uk=""ПДВ: податкові зобов'язання: відвантаження""",Локализация.КодЯзыкаИнформационнойБазы());
			Проводка.НомерЖурнала               = "";

			Проводка.СчетДт                     = СтрокаТаблицы.СчетДоходовБУ;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1, СтрокаТаблицы.СубконтоДоходовБУ1);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 2, СтрокаТаблицы.СубконтоДоходовБУ2);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3, СтрокаТаблицы.СубконтоДоходовБУ3);

			СуммаНУДоход = Макс(СтрокаТаблицы.ПроводкиСуммаБезНДСРегл - СтрокаТаблицы.ОстСтоимостьНУ, 0);
			
			Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыльДо2015 И СуммаНУДоход > 0 Тогда
				
				Проводка.НалоговоеНазначениеДт = СтрокаТаблицы.НалоговоеНазначениеДоходовИЗатрат;
				Проводка.СуммаНУДт = СтрокаТаблицы.ПроводкиСуммаНДСРегл;
				
			КонецЕсли;
			
			Проводка.СчетКт                     = СтрокаТаблицы.СчетУчетаНДС;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "Контрагенты", СтруктураШапкиДокумента.Контрагент);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "Договоры"   , СтруктураШапкиДокумента.ДоговорКонтрагента);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ДокументыРасчетовСКонтрагентами", НалоговыйУчет.ОпределитьСделкуНалоговыйУчет(СтруктураШапкиДокумента, Ссылка, Сделка));
			
			НалоговыйУчет.РазбитьПроводкуПоНДСНаПервоеВтороеСобытие(ТаблицаПоВторомуСобытиюНал, ПроводкиБУ, Проводка, 
													  "Кт", СтруктураШапкиДокумента.СчетУчетаНДСПодтвержденный, 
													  СтруктураШапкиДокумента.ДоговорКонтрагента, 
													  НалоговыйУчет.ОпределитьСделкуНалоговыйУчет(СтруктураШапкиДокумента, Ссылка, Сделка), Сделка,
													  Перечисления.СобытияОжидаемыйИПодтвержденныйНДСПродаж.Реализация,
													  СтрокаТаблицы.СтавкаНДС,
													  ,,,СтрокаТаблицы.ПроводкиСуммаНДСВал, СтрокаТаблицы.ПроводкиСуммаНДСКурсНБУ,КурсЗачетаАвансаРегл);
			
		КонецЕсли;

	КонецЦикла;
	
КонецПроцедуры

Функция ДвиженияПоРегистрамНалоговогоУчета(СтруктураШапкиДокумента, ТаблицаПоТоварам, Отказ)

	ТаблицаПоВторомуСобытиюНал = НалоговыйУчет.СоздатьСтруктуруТаблицыНалоговыхСумм();

	Если Не СтруктураШапкиДокумента.ЕстьНДС Тогда
		Возврат ТаблицаПоВторомуСобытиюНал;
	КонецЕсли;
	
	Если  СтруктураШапкиДокумента.ЕстьНалогНаПрибыль 
	  ИЛИ СтруктураШапкиДокумента.ЕстьНДС Тогда
		
		//Отразим Продажи в регистре ПродажиНалоговыйУчет
		НаборДвижений = Движения.ПродажиНалоговыйУчет;
		
		// Получим таблицу значений, совпадающую со струкутрой набора записей регистра.
		ТаблицаДвижений = НаборДвижений.ВыгрузитьКолонки();
		
		// ТОВАРЫ
		ТаблицаПродаж = ТаблицаПоТоварам.Скопировать();
		ТаблицаПродаж.Свернуть("СтавкаНДС","СуммаСНДСВал, СуммаНДСВал");
		
		ТаблицаПродаж.Колонки.СуммаСНДСВал.Имя = "СуммаВзаиморасчетов";
		ТаблицаПродаж.Колонки.СуммаНДСВал.Имя  = "СуммаНДС";
		ОбщегоНазначенияБПВызовСервера.ЗагрузитьВТаблицуЗначений(ТаблицаПродаж, ТаблицаДвижений);
		ТаблицаДвижений.ЗаполнитьЗначения(СтруктураШапкиДокумента.Организация       , "Организация");
		ТаблицаДвижений.ЗаполнитьЗначения(СтруктураШапкиДокумента.ДоговорКонтрагента, "ДоговорКонтрагента");
		ТаблицаДвижений.ЗаполнитьЗначения(НалоговыйУчет.ОпределитьСделкуНалоговыйУчет(СтруктураШапкиДокумента,
																		СтруктураШапкиДокумента.Ссылка, 
																		СтруктураШапкиДокумента.Сделка),
										  "Сделка");
		ТаблицаДвижений.ЗаполнитьЗначения(Перечисления.СобытияПродажиНалоговыйУчет.РеализацияПокупателю, "Событие");
		
		Если СтруктураШапкиДокумента.СложныйНалоговыйУчет Тогда
			
			// очистим налоговые реквизиты
			ТаблицаДвижений.ЗаполнитьЗначения(Перечисления.СтавкиНДС.ПустаяСсылка(), 			"СтавкаНДС");
			ТаблицаДвижений.ЗаполнитьЗначения(0, 												"СуммаНДС");
			
		Иначе		
			// упрощенный налоговый учет
			Если НЕ СтруктураШапкиДокумента.ЕстьНДС Тогда
				ТаблицаДвижений.ЗаполнитьЗначения(0, 												"СуммаНДС");	
				ТаблицаДвижений.ЗаполнитьЗначения(Перечисления.СтавкиНДС.ПустаяСсылка(), 			"СтавкаНДС");
			КонецЕсли;
			
			Если СтруктураШапкиДокумента.ВедениеВзаиморасчетовНУ = Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоРасчетнымДокументам Тогда
				// взаиморасчеты по договору по расчетным документам - необходимо заполнить в регистре реквизит РасчетныйДокумент
				ТаблицаДвижений.ЗаполнитьЗначения(СтруктураШапкиДокумента.Сделка, "РасчетныйДокумент");
			КонецЕсли;			
			
		КонецЕсли;	
		
		Если НЕ Отказ И ТаблицаДвижений.Количество() > 0 Тогда
				
			НаборДвижений.мПериод          = Дата;
			НаборДвижений.мТаблицаДвижений = ТаблицаДвижений;
				
			Движения.ПродажиНалоговыйУчет.ВыполнитьПриход();
			Движения.ПродажиНалоговыйУчет.Записать();
				
		КонецЕсли;
		
	КонецЕсли;	
	
	// ОжидаемыйИПодтвержденныйНДСПродаж
	Если НЕ СтруктураШапкиДокумента.СложныйНалоговыйУчет Тогда
		
		// Движения формируются по данным рассчета "первого события" 
	   НалоговыйУчет.ДвиженияПоРегистрамНалоговогоУчетаУпрощенныйНалоговыйУчет(ЭтотОбъект, ТаблицаПоВторомуСобытиюНал);
	
	ИначеЕсли СтруктураШапкиДокумента.ЕстьНДС Тогда

		НаборДвижений = Движения.ОжидаемыйИПодтвержденныйНДСПродаж;
		
		// Получим таблицу значений, совпадающую со струкутрой набора записей регистра.
		ТаблицаДвижений = НаборДвижений.ВыгрузитьКолонки();
		
		// ТОВАРЫ
		ТаблицаПродаж = ТаблицаПоТоварам.Скопировать();
		ТаблицаПродаж.Свернуть("СтавкаНДС","СуммаБезНДСВал, СуммаНДСВал");
		
		ТаблицаПродаж.Колонки.СуммаБезНДСВал.Имя = "БазаНДС";
		ТаблицаПродаж.Колонки.СуммаНДСВал   .Имя = "СуммаНДС";
		ОбщегоНазначенияБПВызовСервера.ЗагрузитьВТаблицуЗначений(ТаблицаПродаж, ТаблицаДвижений);
		ТаблицаДвижений.ЗаполнитьЗначения(СтруктураШапкиДокумента.Организация       , "Организация");
		ТаблицаДвижений.ЗаполнитьЗначения(СтруктураШапкиДокумента.ДоговорКонтрагента, "ДоговорКонтрагента");
		ТаблицаДвижений.ЗаполнитьЗначения(НалоговыйУчет.ОпределитьСделкуНалоговыйУчет(СтруктураШапкиДокумента,
																		СтруктураШапкиДокумента.Ссылка, 
																		СтруктураШапкиДокумента.Сделка),
											  "Сделка");
		ТаблицаДвижений.ЗаполнитьЗначения(Перечисления.СобытияОжидаемыйИПодтвержденныйНДСПродаж.Реализация       , "СобытиеНДС");
		ТаблицаДвижений.ЗаполнитьЗначения(Перечисления.КодыОперацийОжидаемыйИПодтвержденныйНДСПродаж.ОжидаемыйНДС, "КодОперации");

		Если НЕ Отказ И ТаблицаДвижений.Количество() > 0 Тогда
			
			НаборДвижений.мПериод          = Дата;
			НаборДвижений.мТаблицаДвижений = ТаблицаДвижений;
		
			Движения.ОжидаемыйИПодтвержденныйНДСПродаж.ВыполнитьПриход();
			Движения.ОжидаемыйИПодтвержденныйНДСПродаж.Записать();
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ТаблицаПоВторомуСобытиюНал;
	
КонецФункции


// Процедура формирует структуру шапки документа и дополнительных полей.
//
Процедура ПодготовитьСтруктуруШапкиДокумента(Заголовок, СтруктураШапкиДокумента) Экспорт

	// Дерево значений, содержащее имена необходимых полей в запросе по шапке.
	Перем ДеревоПолейЗапросаПоШапке;
	
	СтруктураШапкиДокумента = ОбщегоНазначенияРед12.СформироватьСтруктуруШапкиДокумента(ЭтотОбъект);
	СтруктураШапкиДокумента.Вставить("ВидСобытияОС",СтруктураШапкиДокумента.СобытиеОС.ВидСобытияОС);
	
	// Заполним по шапке документа дерево параметров, нужных при проведении.
	ДеревоПолейЗапросаПоШапке = ОбщегоНазначенияРед12.СформироватьДеревоПолейЗапросаПоШапке();
	ОбщегоНазначенияРед12.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "ДоговорыКонтрагентов", "Организация"          , "ДоговорОрганизация");
	ОбщегоНазначенияРед12.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "ДоговорыКонтрагентов", "ВидДоговора"          , "ВидДоговора");
	ОбщегоНазначенияРед12.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "Сделка"              , "ВидОперации"          , "СделкаВидОперации");
   	ОбщегоНазначенияРед12.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "ДоговорыКонтрагентов", "ВедениеВзаиморасчетов", "ВедениеВзаиморасчетов");
	ОбщегоНазначенияРед12.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "ДоговорыКонтрагентов", "ВалютаВзаиморасчетов" , "ВалютаВзаиморасчетов");
   	ОбщегоНазначенияРед12.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "ДоговорыКонтрагентов", "ВедениеВзаиморасчетовНУ", "ВедениеВзаиморасчетовНУ");
   	ОбщегоНазначенияРед12.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "ДоговорыКонтрагентов", "СложныйНалоговыйУчет", 	"СложныйНалоговыйУчет");

	// Сформируем запрос на дополнительные параметры, нужные при проведении, по данным шапки документа
	СтруктураШапкиДокумента = УправлениеЗапасами.СформироватьЗапросПоДеревуПолей(ЭтотОбъект, ДеревоПолейЗапросаПоШапке, СтруктураШапкиДокумента, мВалютаРегламентированногоУчета);
	
	
КонецПроцедуры // ПодготовитьСтруктуруШапкиДокумента()

// Процедура формирует таблицы документа.
//
Процедура ПодготовитьТаблицыДокумента(СтруктураШапкиДокумента, ТаблицаПоТоварам, Отказ, Заголовок) Экспорт
	
	ПогрешностиОкругления = Новый Соответствие;
	
	// Получим необходимые данные для проведения и проверки заполенения данные по табличной части "ОС".
	СтруктураПолей = Новый Структура();
	СтруктураПростыхПолей = Новый Структура;
	СтруктураСложныхПолей = Новый Структура;
	СтруктураПолей.Вставить("ОсновноеСредство"    , "ОсновноеСредство");
	СтруктураПолей.Вставить("Сумма"               , "Сумма");
	СтруктураПолей.Вставить("СтавкаНДС"           , "СтавкаНДС");
	СтруктураПолей.Вставить("НДС"                 , "СуммаНДС");
	СтруктураПолей.Вставить("НомерСтроки"         , "НомерСтроки");
	СтруктураПолей.Вставить("СтоимостьБУ"         , "СтоимостьБУ");
	СтруктураПолей.Вставить("АмортизацияБУ"       , "АмортизацияБУ");
	СтруктураПолей.Вставить("АмортизацияЗаМесяцБУ", "АмортизацияЗаМесяцБУ");
	СтруктураПолей.Вставить("СтоимостьНУ"         , "СтоимостьНУ");
	СтруктураПолей.Вставить("АмортизацияНУ"       , "АмортизацияНУ");
	СтруктураПолей.Вставить("АмортизацияЗаМесяцНУ", "АмортизацияЗаМесяцНУ");

	СтруктураПолей.Вставить("СхемаРеализации"    , "СхемаРеализации");
	СтруктураПолей.Вставить("СчетДоходовБУ"      , "СхемаРеализации.СчетДоходов");
	СтруктураПолей.Вставить("СубконтоДоходовБУ1" , "СхемаРеализации.СубконтоДоходов1");
	СтруктураПолей.Вставить("СубконтоДоходовБУ2" , "СхемаРеализации.СубконтоДоходов2");
	СтруктураПолей.Вставить("СубконтоДоходовБУ3" , "СхемаРеализации.СубконтоДоходов3");
	СтруктураПолей.Вставить("СчетРасходовБУ"     , "СхемаРеализации.СчетСебестоимости");
	СтруктураПолей.Вставить("СубконтоРасходовБУ1", "СхемаРеализации.СубконтоСебестоимости1");
	СтруктураПолей.Вставить("СубконтоРасходовБУ2", "СхемаРеализации.СубконтоСебестоимости2");
	СтруктураПолей.Вставить("СубконтоРасходовБУ3", "СхемаРеализации.СубконтоСебестоимости3");
	
	СтруктураПолей.Вставить("СчетПродажиОС", "СчетПродажиОС");
	
	СтруктураПолей.Вставить("ПревышениеСуммДооценокНадСуммамиУценокБУ",       "ПревышениеСуммДооценокНадСуммамиУценокБУ");
	
	СтруктураПолей.Вставить("НалоговоеНазначениеДоходовИЗатрат", "НалоговоеНазначениеДоходовИЗатрат");
	
	СтруктураПолей.Вставить("СчетУчетаНДС"    	, "Ссылка.СчетУчетаНДС");

	СтруктураСложныхПолей.Вставить("ОстСтоимостьНУ"    	, "СтоимостьНУ - АмортизацияНУ - АмортизацияЗаМесяцНУ");
	
 	РезультатЗапросаПоТоварам = ОбщегоНазначенияРед12.СформироватьЗапросПоТабличнойЧасти(ЭтотОбъект, "ОС", СтруктураПолей, СтруктураПростыхПолей, СтруктураСложныхПолей);

	// Подготовим таблицу товаров для проведения
	ТаблицаПоТоварам = ПодготовитьТаблицуТоваров(РезультатЗапросаПоТоварам, СтруктураШапкиДокумента, ПогрешностиОкругления);
	
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Процедура ЗаполнитьПоДокументуОснованию(Основание)

	// Заполним реквизиты из стандартного набора по документу основанию.
	ЗаполнениеДокументов.ЗаполнитьПоОснованию(ЭтотОбъект, Основание);

	Если ТипЗнч(Основание) = Тип("ДокументСсылка.ПодготовкаКПередачеОС") Тогда
		
		// Заполнение шапки
		Организация = Основание.Организация;
		ДокПодготовкаКПередачеОС = Основание.Ссылка;
		
		Для Каждого ТекСтрокаОС Из Основание.ОС Цикл
			
			НоваяСтрока = ОС.Добавить();
			НоваяСтрока.ОсновноеСредство    = ТекСтрокаОС.ОсновноеСредство;
			НоваяСтрока.СтоимостьБУ         = ТекСтрокаОС.СтоимостьБУ - ТекСтрокаОС.АмортизацияБУ - ТекСтрокаОС.АмортизацияЗаМесяцБУ;
			НоваяСтрока.СтоимостьНУ         = ТекСтрокаОС.СтоимостьНУ - ТекСтрокаОС.АмортизацияНУ - ТекСтрокаОС.АмортизацияЗаМесяцНУ;
			
			НоваяСтрока.СчетПродажиОС 		= ТекСтрокаОС.СчетПродажиОС;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьРеквизитыБА(Параметры) // ИНАГРО
	
	Дата 							 = Параметры.Дата;
	Организация  					 = Параметры.Организация;
	Контрагент  					 = Параметры.Контрагент;
	ДоговорКонтрагента  			 = Параметры.ДоговорКонтрагента;	
	Комментарий						 = Параметры.Комментарий; 
	ИНАГРО_ДокументОперативногоУчета = Параметры.ИНАГРО_ДокументОперативногоУчета;
	ИНАГРО_БиологическийАктив  		 = Параметры.ИНАГРО_БиологическийАктив; 
	ИНАГРО_Склад  					 = Параметры.ИНАГРО_Склад; 
	ИНАГРО_Количество 				 = Параметры.ИНАГРО_Количество;	
	СчетУчетаРасчетовСКонтрагентом 	 = Параметры.СчетУчетаРасчетовСКонтрагентом;
	СчетУчетаРасчетовПоАвансам 		 = Параметры.СчетУчетаРасчетовПоАвансам;
	СчетУчетаНДС 					 = Параметры.СчетУчетаНДС;
	СчетУчетаНДСПодтвержденный 		 = Параметры.СчетУчетаНДСПодтвержденный;
		
КонецПроцедуры 

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ


// Процедура вызывается перед записью документа 
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
 	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если АвторасчетНДС Тогда
		
		// соответствие для хранения погрешностей округлений
		ПогрешностиОкругления = Новый Соответствие();
		// пересчет сумм НДС с учетом ошибок округления
		УчетНДСКлиентСервер.ПересчитатьНДСсУчетомПогрешностиОкругления(ОС, Ссылка, СуммаВключаетНДС, ПогрешностиОкругления, "ОС", Строка(ВалютаДокумента));
		
	КонецЕсли;

	// Посчитать суммы документа и записать ее в соответствующий реквизит шапки для показа в журналах
	СуммаДокумента = УчетНДС.ПолучитьСуммуДокументаСНДС(ЭтотОбъект, "ОС");

КонецПроцедуры // ПередЗаписью

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	Перем Заголовок, СтруктураШапкиДокумента;
	Перем ТаблицаПоТоварам;

	// Заголовок для сообщений об ошибках проведения.
	Заголовок = НСтр("ru='Проведение документа ""';uk='Проведення документа ""'") + СокрЛП(Ссылка) + """: ";
	
	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ДОКУМЕНТА

	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	Если РучнаяКорректировка Тогда
		Возврат;
	КонецЕсли;

	ПодготовитьСтруктуруШапкиДокумента(Заголовок, СтруктураШапкиДокумента);
	
	// Получим данные учетной политики
	ПодготовитьПараметрыУчетнойПолитики(СтруктураШапкиДокумента, Отказ, Заголовок);

	ПодготовитьТаблицыДокумента(СтруктураШапкиДокумента, ТаблицаПоТоварам, Отказ, Заголовок);
	
	//Проверим на возможность проведения в БУ и НУ
	УправлениеВзаиморасчетами.ПроверкаВозможностиПроведенияВ_БУ_НУ(СтруктураШапкиДокумента, СтруктураШапкиДокумента.ДоговорКонтрагента, Отказ, Заголовок);
	
	//проверка, нет ли списанных ОС в табличной части
	УправлениеНеоборотнымиАктивами.ПроверитьНаСписанность(МоментВремени(), Организация, ТаблицаПоТоварам, Отказ, Заголовок);
	
	// Подготовим таблицу местонахождения для ТабАмортизации
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организация",  Организация);
	Запрос.УстановитьПараметр("Период",       Новый Граница(МоментВремени(), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("ВнешнийИсточник", ТаблицаПоТоварам);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
   	|	ВнешнийИсточник.ОсновноеСредство
	|ПОМЕСТИТЬ ОсновныеСредства
	|ИЗ &ВнешнийИсточник КАК ВнешнийИсточник
	|;
	|ВЫБРАТЬ
	|	ОсновныеСредства.ОсновноеСредство 									КАК ОсновноеСредство,
	|	МестонахождениеОСБухгалтерскийУчетСрезПоследних.ОсновноеСредство 	КАК ОС_БУ,
	|	МестонахождениеОСБухгалтерскийУчетСрезПоследних.Местонахождение 	КАК Местонахождение_БУ
	|ИЗ
	|	ОсновныеСредства
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОСБухгалтерскийУчет.СрезПоследних(&Период, ОсновноеСредство В (ВЫБРАТЬ ОсновноеСредство ИЗ ОсновныеСредства) И Организация = &Организация) КАК МестонахождениеОСБухгалтерскийУчетСрезПоследних
	|	ПО ОсновныеСредства.ОсновноеСредство = МестонахождениеОСБухгалтерскийУчетСрезПоследних.ОсновноеСредство";
	ТаблицаМестонахождений = Запрос.Выполнить().Выгрузить();
	
	// Движения по документу
	Если НЕ Отказ Тогда
		
		ДвиженияПоРегистрам(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоТоварам, ТаблицаМестонахождений, Отказ, Заголовок);
		
	КонецЕсли;
	
	Движения.Хозрасчетный.ВыполнитьДействияПередЗаписьюДвижений();
	
	// ИНАГРО++
	Движения.Хозрасчетный.Записать();
	ИНАГРО_Общий.ИНАГРО_ДвиженияЗатратыОрганизации_Приход(СтруктураШапкиДокумента, Движения);
	// ИНАГРО--

	ПроведениеСервер.ПодготовитьНаборыЗаписейКЗаписиДвижений(ЭтотОбъект);
	
КонецПроцедуры // ОбработкаПроведения()

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	Движения.Записать();
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	Если ДанныеЗаполнения <> Неопределено И ТипДанныхЗаполнения <> Тип("Структура")
		И Метаданные().ВводитсяНаОсновании.Содержит(ДанныеЗаполнения.Метаданные()) Тогда
		ЗаполнитьПоДокументуОснованию(ДанныеЗаполнения);
	КонецЕсли;
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);

	Если НЕ ЗначениеЗаполнено(СобытиеОС) Тогда
		СобытиеОС = УчетОС.ПолучитьСобытиеПоОСИзСправочника(Перечисления.ВидыСобытийОС.Передача);
	КонецЕсли;

	Если ЗначениеЗаполнено(Организация)
		И ЗначениеЗаполнено(Контрагент)
		И ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		Документы.ПередачаОС.ЗаполнитьСчетаУчетаРасчетов(ЭтотОбъект);
	КонецЕсли;
	
	// ИНАГРО++
	Если  ТипДанныхЗаполнения = Тип("Структура") И ДанныеЗаполнения.Свойство("ИНАГРО_ДокументОперативногоУчета") Тогда
		Если ДанныеЗаполнения.ИНАГРО_ДокументОперативногоУчета.Метаданные().Имя = "ИНАГРО_РеализацияБиологическихАктивов" Тогда
			ЗаполнитьРеквизитыБА(ДанныеЗаполнения);			
		КонецЕсли;
	КонецЕсли;
	// ИНАГРО--  

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	МассивНепроверяемыхРеквизитов = Новый Массив;

	ПлательщикНДС 	= УчетнаяПолитика.ПлательщикНДС(Организация, Дата);
	ПлательщикНП 	= УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Организация, Дата);
	ПлательщикНалогаНаПрибыльДо2015  = УчетнаяПолитика.ПлательщикНалогаНаПрибыльДо2015(Организация, Дата);

	РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДоговорКонтрагента, "ВидДоговора, ВалютаВзаиморасчетов, 
		|СложныйНалоговыйУчет, СхемаНалоговогоУчета");
	ЭтоКомиссия          = РеквизитыДоговора.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером;
	СложныйНалоговыйУчет = ЗначениеЗаполнено(ДоговорКонтрагента) И РеквизитыДоговора.СложныйНалоговыйУчет;
	
	Если ЭтоКомиссия Тогда

		МассивНепроверяемыхРеквизитов.Добавить("СчетУчетаРасчетовСКонтрагентом");

	КонецЕсли;
	
	Если Не РеализацияТоваровУслугФормыКлиентСервер.ИспользуетсяСчетУчетаНДС(ПлательщикНДС, ЭтоКомиссия, Дата) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СчетУчетаНДС");		
	КонецЕсли;
	Если Не РеализацияТоваровУслугФормыКлиентСервер.ИспользуетсяСчетУчетаНДСПодтвержденный(ПлательщикНДС, ЭтоКомиссия, Дата, СложныйНалоговыйУчет) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СчетУчетаНДСПодтвержденный");		
	КонецЕсли;
	
	Если Не ПлательщикНДС Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ОС.СтавкаНДС");
	КонецЕсли;
	
	Если Не ПлательщикНалогаНаПрибыльДо2015 Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ОС.НалоговоеНазначениеДоходовИЗатрат");
	КонецЕсли;
	
	Если НЕ ЭтоКомиссия Тогда
		// Схемы реализации должны быть заполнены правильно
		МассивРеквизитовДляПроверки = Новый Массив;
		МассивРеквизитовДляПроверки.Добавить("СчетДоходов");
		МассивРеквизитовДляПроверки.Добавить("СчетСебестоимости");
															 
		БухгалтерскийУчет.ПроверитьСхемыРеализацииТабличнойЧастиНаЗаполненость(
			ЭтотОбъект, 
			"ОС", НСтр("ru='ОС';uk='ОЗ'"), 
			"СхемаРеализации", НСтр("ru='Схема реализации';uk='Схема реалізації'") , 
			МассивРеквизитовДляПроверки, 
			Отказ
		);

    КонецЕсли;
	
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);

	УправлениеВнеоборотнымиАктивами.ПроверитьОтсутствиеДублейВТабличнойЧасти(ЭтотОбъект, "ОС", Новый Структура("ОсновноеСредство"), Отказ);

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)

	Дата = НачалоДня(ОбщегоНазначенияБП.ПолучитьРабочуюДату());
	Ответственный = Пользователи.ТекущийПользователь();

	СтруктураКурсаВзаиморасчетов = РаботаСКурсамиВалют.ПолучитьКурсВалюты(
	ВалютаДокумента, Дата);
	
	КурсВзаиморасчетов      = СтруктураКурсаВзаиморасчетов.Курс;
	КратностьВзаиморасчетов = СтруктураКурсаВзаиморасчетов.Кратность;
	
	// ИНАГРО++ 	
	ИНАГРО_ДокументОперативногоУчета = Неопределено;
	ИНАГРО_БиологическийАктив        = Справочники.БиологическиеАктивы.ПустаяСсылка();		
	ИНАГРО_Склад			         = Справочники.Склады.ПустаяСсылка();				
	Комментарий 			         = ""; 		
	// ИНАГРО-- 

КонецПроцедуры

мВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();

// ИНАГРО++
АмортизацияБА = Ложь;
Если ИНАГРО_ОбщийВызовСервераПовтИсп.ЕстьБСПУ() Тогда
	АмортизацияБА = Константы.ИНАГРО_НачислятьАмортизациюБА.Получить();
КонецЕсли;
// ИНАГРО--

#КонецЕсли

