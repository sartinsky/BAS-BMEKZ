#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Перем мВалютаРегламентированногоУчета;
Перем мУчетнаяПолитика;

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	Если ДанныеЗаполнения <> Неопределено И ТипДанныхЗаполнения <> Тип("Структура")
		И Метаданные().ВводитсяНаОсновании.Содержит(ДанныеЗаполнения.Метаданные()) Тогда
		ЗаполнитьПоДокументуОснованию(ДанныеЗаполнения);
	КонецЕсли;
	
	ИНАГРО_ЭлеваторЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ЭтотОбъект);

	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
		 
	Если  ТипДанныхЗаполнения = Тип("Структура")
		И ДанныеЗаполнения.Свойство("ВидОперации") Тогда
		ВидОперации = ДанныеЗаполнения.ВидОперации;
	КонецЕсли;
		
	Если ЗначениеЗаполнено(Организация)
		И ЗначениеЗаполнено(Контрагент)
		И ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		Документы.ИНАГРО_ОтражениеВвозаВывозаСобственногоЗернаВРегламентированномУчете.ЗаполнитьСчетаУчетаРасчетов(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив();		
		
	Если ВидОперации = Перечисления.ИНАГРО_ВидыОперацийВвозаВывозаСобственногоЗерна.РеализацияТоваровУслуг Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ДокументыЗакупки.Цена");
		МассивНепроверяемыхРеквизитов.Добавить("ДокументыЗакупки.Сумма");
		МассивНепроверяемыхРеквизитов.Добавить("ДокументыЗакупки.СтавкаНДС");
	КонецЕсли;	
		
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ВесДокумента = 0;
	
	Для Каждого СтрДокументыЗакупки Из ДокументыЗакупки Цикл
		ВесДокумента = ВесДокумента + СтрДокументыЗакупки.Количество * СтрДокументыЗакупки.Коэффициент;
	КонецЦикла;
	
	СуммаДокумента = УчетНДС.ПолучитьСуммуДокументаСНДС(ЭтотОбъект);	
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Перем Заголовок, СтруктураШапкиДокумента;
	Перем ТаблицаДокументыЗакупки, ТаблицаНакладных; 	
	
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначенияБПВызовСервера.ПредставлениеДокументаПриПроведении(Ссылка);
	
	ПодготовитьСтруктуруШапкиДокумента(Заголовок, СтруктураШапкиДокумента);	
	
	Если Отказ Тогда 
		Возврат;
	КонецЕсли;	
		
	ПодготовитьТаблицыДокумента(СтруктураШапкиДокумента, ТаблицаДокументыЗакупки);
	
	ПодготовитьТаблицуНакладных(СтруктураШапкиДокумента, ТаблицаНакладных);
	
	// Движения по документу
	Если НЕ Отказ Тогда
		ДвиженияПоРегистрам(РежимПроведения, СтруктураШапкиДокумента, ТаблицаНакладных, ДокументыЗакупки, Отказ, Заголовок, ТаблицаДокументыЗакупки);
	КонецЕсли;
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКЗаписиДвижений(ЭтотОбъект);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	Движения.Записать();

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Дата = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Ответственный = Пользователи.ТекущийПользователь(); 
		
КонецПроцедуры

#КонецОбласти

#Область Проведение

// Процедура формирует структуру шапки документа и дополнительных полей.
//
Процедура ПодготовитьСтруктуруШапкиДокумента(Заголовок, СтруктураШапкиДокумента) Экспорт

	// Дерево значений, содержащее имена необходимых полей в запросе по шапке.
	Перем ДеревоПолейЗапросаПоШапке;
	
	СтруктураШапкиДокумента = ОбщегоНазначенияРед12.СформироватьСтруктуруШапкиДокумента(ЭтотОбъект);
	
КонецПроцедуры

// Процедура формирует таблицы документа.
//
Процедура ПодготовитьТаблицыДокумента(СтруктураШапкиДокумента, ТаблицаДокументыЗакупки)
	
	СтруктураПолей = Новый Структура;
	СтруктураПростыхПолей = Новый Структура;
	СтруктураСложныхПолей = Новый Структура;
	
	СтруктураПолей.Вставить("НомерСтрокиТабличнойЧастиТиповогоДокумента", "НомерСтроки");
	СтруктураПолей.Вставить("ВидОперацииВвозаВывозаСобственногоЗерна",    "Ссылка.ВидОперации");
	СтруктураПолей.Вставить("Организация",                                "Ссылка.Организация");	
	СтруктураПолей.Вставить("Склад",                                      "Ссылка.Склад");
	СтруктураПолей.Вставить("ПоставщикПолучатель",                        "Ссылка.Контрагент");
	СтруктураПолей.Вставить("ДоговорКонтрагента",                         "Ссылка.ДоговорКонтрагента");
	СтруктураПолей.Вставить("ДокументОприходования",                      "ДокументОприходования");
	СтруктураПолей.Вставить("Номенклатура",                               "Номенклатура");
	СтруктураПолей.Вставить("ЕдиницаИзмерения",                           "ЕдиницаИзмерения");
	СтруктураПолей.Вставить("Коэффициент",                                "Коэффициент");
	СтруктураПолей.Вставить("Количество",                                 "Количество");
	СтруктураПолей.Вставить("Вес",                                        "Количество * Коэффициент");
	СтруктураПолей.Вставить("Цена",                                       "Цена");
	СтруктураПолей.Вставить("Сумма",                                      "Сумма");
	СтруктураПолей.Вставить("СуммаНДС",                                   "СуммаНДС");
	СтруктураПолей.Вставить("СтавкаНДС",                                  "СтавкаНДС");
	
	РезультатЗапросаПоТоварам = ОбщегоНазначенияРед12.СформироватьЗапросПоТабличнойЧасти(ЭтотОбъект, "ДокументыЗакупки", СтруктураПолей, ,СтруктураСложныхПолей);
	
	ТаблицаДокументыЗакупки = РезультатЗапросаПоТоварам.Выгрузить();
	
КонецПроцедуры

Процедура ПодготовитьТаблицуНакладных(СтруктураШапкиДокумента, ТаблицаНакладных)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДОК.ВидОперации КАК ВидОперацииВвозаВывозаСобственногоЗерна,
		|	ДОК.Организация КАК Организация,
		|	ДОК.Контрагент КАК ПоставщикПолучатель,
		|	ДОК.Склад КАК Склад,
		|	ИНАГРО_ОтражениеВвозаВывозаСобственногоЗернаВРегламентированномУчетеДокументыЗакупки.Номенклатура КАК Номенклатура,
		|	ВЫБОР
		|		КОГДА ИНАГРО_ОтражениеВвозаВывозаСобственногоЗернаВРегламентированномУчетеДокументыЗакупки.ДокументОприходования ССЫЛКА Документ.ИНАГРО_РеестрТТНВвоз
		|				И ДОК.ВидОперации <> &ВидыОперацийИсключение
		|			ТОГДА ИНАГРО_РеестрТТНВвозСписокТТН.ТТН
		|		КОГДА ИНАГРО_ОтражениеВвозаВывозаСобственногоЗернаВРегламентированномУчетеДокументыЗакупки.ДокументОприходования ССЫЛКА Документ.ИНАГРО_РеестрТТНВывоз
		|				И ДОК.ВидОперации <> &ВидыОперацийИсключение
		|			ТОГДА ИНАГРО_РеестрТТНВывозСписокТТН.ТТН
		|		КОГДА ИНАГРО_ОтражениеВвозаВывозаСобственногоЗернаВРегламентированномУчетеДокументыЗакупки.ДокументОприходования ССЫЛКА Документ.ИНАГРО_РеестрТТНВвозЖД
		|				И ДОК.ВидОперации <> &ВидыОперацийИсключение
		|			ТОГДА ИНАГРО_РеестрТТНВвозЖДСписокТТН.ТТН
		|		КОГДА ИНАГРО_ОтражениеВвозаВывозаСобственногоЗернаВРегламентированномУчетеДокументыЗакупки.ДокументОприходования ССЫЛКА Документ.ИНАГРО_РеестрТТНВывозЖД
		|				И ДОК.ВидОперации <> &ВидыОперацийИсключение
		|			ТОГДА ИНАГРО_РеестрТТНЖДВывозСписокТТН.ТТН
		|		ИНАЧЕ ИНАГРО_ОтражениеВвозаВывозаСобственногоЗернаВРегламентированномУчетеДокументыЗакупки.ДокументОприходования
		|	КОНЕЦ КАК ДокументОприходования,
		|	ВЫБОР
		|		КОГДА ИНАГРО_ОтражениеВвозаВывозаСобственногоЗернаВРегламентированномУчетеДокументыЗакупки.ДокументОприходования ССЫЛКА Документ.ИНАГРО_РеестрТТНВвоз
		|				И ДОК.ВидОперации <> &ВидыОперацийИсключение
		|			ТОГДА ИНАГРО_РеестрТТНВвозСписокТТН.Вес
		|		КОГДА ИНАГРО_ОтражениеВвозаВывозаСобственногоЗернаВРегламентированномУчетеДокументыЗакупки.ДокументОприходования ССЫЛКА Документ.ИНАГРО_РеестрТТНВывоз
		|				И ДОК.ВидОперации <> &ВидыОперацийИсключение
		|			ТОГДА ИНАГРО_РеестрТТНВывозСписокТТН.Вес
		|		КОГДА ИНАГРО_ОтражениеВвозаВывозаСобственногоЗернаВРегламентированномУчетеДокументыЗакупки.ДокументОприходования ССЫЛКА Документ.ИНАГРО_РеестрТТНВвозЖД
		|				И ДОК.ВидОперации <> &ВидыОперацийИсключение
		|			ТОГДА ИНАГРО_РеестрТТНВвозЖДСписокТТН.ФизическийВес
		|		КОГДА ИНАГРО_ОтражениеВвозаВывозаСобственногоЗернаВРегламентированномУчетеДокументыЗакупки.ДокументОприходования ССЫЛКА Документ.ИНАГРО_РеестрТТНВывозЖД
		|				И ДОК.ВидОперации <> &ВидыОперацийИсключение
		|			ТОГДА ИНАГРО_РеестрТТНЖДВывозСписокТТН.ФизическийВес
		|		ИНАЧЕ ИНАГРО_ОтражениеВвозаВывозаСобственногоЗернаВРегламентированномУчетеДокументыЗакупки.Количество * ИНАГРО_ОтражениеВвозаВывозаСобственногоЗернаВРегламентированномУчетеДокументыЗакупки.Коэффициент
		|	КОНЕЦ КАК Вес
		|ИЗ
		|	Документ.ИНАГРО_ОтражениеВвозаВывозаСобственногоЗернаВРегламентированномУчете.ДокументыЗакупки КАК ИНАГРО_ОтражениеВвозаВывозаСобственногоЗернаВРегламентированномУчетеДокументыЗакупки
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИНАГРО_РеестрТТНВвоз.СписокТТН КАК ИНАГРО_РеестрТТНВвозСписокТТН
		|		ПО ИНАГРО_ОтражениеВвозаВывозаСобственногоЗернаВРегламентированномУчетеДокументыЗакупки.ДокументОприходования = ИНАГРО_РеестрТТНВвозСписокТТН.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИНАГРО_РеестрТТНВвозЖД.СписокТТН КАК ИНАГРО_РеестрТТНВвозЖДСписокТТН
		|		ПО ИНАГРО_ОтражениеВвозаВывозаСобственногоЗернаВРегламентированномУчетеДокументыЗакупки.ДокументОприходования = ИНАГРО_РеестрТТНВвозЖДСписокТТН.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИНАГРО_РеестрТТНВывоз.СписокТТН КАК ИНАГРО_РеестрТТНВывозСписокТТН
		|		ПО ИНАГРО_ОтражениеВвозаВывозаСобственногоЗернаВРегламентированномУчетеДокументыЗакупки.ДокументОприходования = ИНАГРО_РеестрТТНВывозСписокТТН.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИНАГРО_РеестрТТНВывозЖД.СписокТТН КАК ИНАГРО_РеестрТТНЖДВывозСписокТТН
		|		ПО ИНАГРО_ОтражениеВвозаВывозаСобственногоЗернаВРегламентированномУчетеДокументыЗакупки.ДокументОприходования = ИНАГРО_РеестрТТНЖДВывозСписокТТН.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИНАГРО_ОтражениеВвозаВывозаСобственногоЗернаВРегламентированномУчете КАК ДОК
		|		ПО ИНАГРО_ОтражениеВвозаВывозаСобственногоЗернаВРегламентированномУчетеДокументыЗакупки.Ссылка = ДОК.Ссылка
		|ГДЕ
		|	ИНАГРО_ОтражениеВвозаВывозаСобственногоЗернаВРегламентированномУчетеДокументыЗакупки.Ссылка = &ДокументСсылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ДОК.ВидОперации,
		|	ДОК.Организация,
		|	ДОК.Контрагент,
		|	ДОК.Склад,
		|	ИНАГРО_ОтражениеВвозаВывозаСобственногоЗернаВРегламентированномУчетеДокументыЗакупки.Номенклатура,
		|	ВЫБОР
		|		КОГДА ИНАГРО_ОтражениеВвозаВывозаСобственногоЗернаВРегламентированномУчетеДокументыЗакупки.ДокументОприходования ССЫЛКА Документ.ИНАГРО_РеестрТТНВвоз
		|				И ДОК.ВидОперации <> &ВидыОперацийИсключение
		|			ТОГДА ИНАГРО_РеестрТТНВвозСписокТТН.ТТН
		|		КОГДА ИНАГРО_ОтражениеВвозаВывозаСобственногоЗернаВРегламентированномУчетеДокументыЗакупки.ДокументОприходования ССЫЛКА Документ.ИНАГРО_РеестрТТНВывоз
		|				И ДОК.ВидОперации <> &ВидыОперацийИсключение
		|			ТОГДА ИНАГРО_РеестрТТНВывозСписокТТН.ТТН
		|		КОГДА ИНАГРО_ОтражениеВвозаВывозаСобственногоЗернаВРегламентированномУчетеДокументыЗакупки.ДокументОприходования ССЫЛКА Документ.ИНАГРО_РеестрТТНВвозЖД
		|				И ДОК.ВидОперации <> &ВидыОперацийИсключение
		|			ТОГДА ИНАГРО_РеестрТТНВвозЖДСписокТТН.ТТН
		|		КОГДА ИНАГРО_ОтражениеВвозаВывозаСобственногоЗернаВРегламентированномУчетеДокументыЗакупки.ДокументОприходования ССЫЛКА Документ.ИНАГРО_РеестрТТНВывозЖД
		|				И ДОК.ВидОперации <> &ВидыОперацийИсключение
		|			ТОГДА ИНАГРО_РеестрТТНЖДВывозСписокТТН.ТТН
		|		ИНАЧЕ ИНАГРО_ОтражениеВвозаВывозаСобственногоЗернаВРегламентированномУчетеДокументыЗакупки.ДокументОприходования
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ИНАГРО_ОтражениеВвозаВывозаСобственногоЗернаВРегламентированномУчетеДокументыЗакупки.ДокументОприходования ССЫЛКА Документ.ИНАГРО_РеестрТТНВвоз
		|				И ДОК.ВидОперации <> &ВидыОперацийИсключение
		|			ТОГДА ИНАГРО_РеестрТТНВвозСписокТТН.Вес
		|		КОГДА ИНАГРО_ОтражениеВвозаВывозаСобственногоЗернаВРегламентированномУчетеДокументыЗакупки.ДокументОприходования ССЫЛКА Документ.ИНАГРО_РеестрТТНВывоз
		|				И ДОК.ВидОперации <> &ВидыОперацийИсключение
		|			ТОГДА ИНАГРО_РеестрТТНВывозСписокТТН.Вес
		|		КОГДА ИНАГРО_ОтражениеВвозаВывозаСобственногоЗернаВРегламентированномУчетеДокументыЗакупки.ДокументОприходования ССЫЛКА Документ.ИНАГРО_РеестрТТНВвозЖД
		|				И ДОК.ВидОперации <> &ВидыОперацийИсключение
		|			ТОГДА ИНАГРО_РеестрТТНВвозЖДСписокТТН.ФизическийВес
		|		КОГДА ИНАГРО_ОтражениеВвозаВывозаСобственногоЗернаВРегламентированномУчетеДокументыЗакупки.ДокументОприходования ССЫЛКА Документ.ИНАГРО_РеестрТТНВывозЖД
		|				И ДОК.ВидОперации <> &ВидыОперацийИсключение
		|			ТОГДА ИНАГРО_РеестрТТНЖДВывозСписокТТН.ФизическийВес
		|		ИНАЧЕ ИНАГРО_ОтражениеВвозаВывозаСобственногоЗернаВРегламентированномУчетеДокументыЗакупки.Количество * ИНАГРО_ОтражениеВвозаВывозаСобственногоЗернаВРегламентированномУчетеДокументыЗакупки.Коэффициент
		|	КОНЕЦ";
	
	Запрос.УстановитьПараметр("ДокументСсылка",         Ссылка);
	Запрос.УстановитьПараметр("ВидыОперацийИсключение", Перечисления.ИНАГРО_ВидыОперацийВвозаВывозаСобственногоЗерна.ПоступлениеТоваров);
	
	РезЗапроса = Запрос.Выполнить();
	
	ТаблицаНакладных = РезЗапроса.Выгрузить();
	
КонецПроцедуры

Процедура ДвиженияПоРегистрам(РежимПроведения, СтруктураШапкиДокумента, ТаблицаНакладных, ДокументыЗакупки, Отказ, Заголовок, ТаблицаДокументыЗакупки)
	
	ДвижениеПоРегиструКонтрольОтраженияВвозаВывозаСобственногоЗернаВРегламентированомУчете(РежимПроведения, СтруктураШапкиДокумента, ТаблицаНакладных, Отказ, Заголовок);
	
	ИнтерактивноеФормированиеТиповыхДокументов = ИНАГРО_ЭлеваторЗаполнениеДокументов.ПолучитьЗначениеПоУмолчанию("ИнтерактивноеФормированиеТиповыхДокументов");	
	Если ИнтерактивноеФормированиеТиповыхДокументов Тогда						
		ИНАГРО_Элеватор.СформироватьПодчиненныеДокументы(ЭтотОбъект, Отказ);
	КонецЕсли;
		
КонецПроцедуры

Процедура ДвижениеПоРегиструКонтрольОтраженияВвозаВывозаСобственногоЗернаВРегламентированомУчете(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоТоварам, Отказ, Заголовок)
	
	// Расход
	
	НаборДвижений = Движения.ИНАГРО_КонтрольОтраженияВвозаВывозаСобственногоЗернаВРегламентированномУчете;
	
	СтруктТаблицДокумента = Новый Структура;
	СтруктТаблицДокумента.Вставить("ТаблицаПоТоварам", ТаблицаПоТоварам);
	
	ТаблицыДанныхДокумента = ИНАГРО_Общий.ЗагрузитьТаблицыДокументаВСтруктуру(НаборДвижений, СтруктТаблицДокумента);
	
	ИНАГРО_Общий.ЗаписатьТаблицыДокументаВРегистр(НаборДвижений, ВидДвиженияНакопления.Расход, ТаблицыДанныхДокумента, Дата);		
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПоДокументуОснованию(Основание)
		
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.ИНАГРО_РеестрТТНВвоз") Тогда		
		
		Организация = Основание.Организация;
		Контрагент  = Основание.Поставщик;
		Склад       = Основание.Склад;
		
		ЗаполнитьПоОстаткамПокупка();
		
	КонецЕсли;
	
КонецПроцедуры		

Процедура ЗаполнитьПоОстаткамПокупка() Экспорт 
			
	ДокументыЗакупки.Очистить();
	
	Условие = " ИСТИНА ";
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИНАГРО_КонтрольОтраженияВвозаВывозаСобственногоЗернаВРегламентированномУчетеОстатки.ПоставщикПолучатель КАК Поставщик,
		|	ИНАГРО_КонтрольОтраженияВвозаВывозаСобственногоЗернаВРегламентированномУчетеОстатки.Организация КАК Организация,
		|	ИНАГРО_КонтрольОтраженияВвозаВывозаСобственногоЗернаВРегламентированномУчетеОстатки.Склад КАК Склад,
		|	ИНАГРО_КонтрольОтраженияВвозаВывозаСобственногоЗернаВРегламентированномУчетеОстатки.Номенклатура КАК Номенклатура,
		|	ИНАГРО_КонтрольОтраженияВвозаВывозаСобственногоЗернаВРегламентированномУчетеОстатки.ВесОстаток КАК ВесОстаток,
		|	ИНАГРО_КонтрольОтраженияВвозаВывозаСобственногоЗернаВРегламентированномУчетеОстатки.ДокументОприходования КАК ДокументОприходования,
		|	СПРНоменклатура.БазоваяЕдиницаИзмерения КАК БазоваяЕдиницаИзмерения
		|ИЗ
		|	РегистрНакопления.ИНАГРО_КонтрольОтраженияВвозаВывозаСобственногоЗернаВРегламентированномУчете.Остатки(
		|			&ДатаКон,
		|			Организация = &Организация
		|				И ПоставщикПолучатель = &ПоставщикПолучатель
		|				И Склад = &Склад
		|				И ВидОперацииВвозаВывозаСобственногоЗерна = &ВидОперации
		|				И &Условие) КАК ИНАГРО_КонтрольОтраженияВвозаВывозаСобственногоЗернаВРегламентированномУчетеОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СПРНоменклатура
		|		ПО ИНАГРО_КонтрольОтраженияВвозаВывозаСобственногоЗернаВРегламентированномУчетеОстатки.Номенклатура = СПРНоменклатура.Ссылка"; 	
	
	Запрос.УстановитьПараметр("ДатаКон",             Новый Граница(КонецДня(Дата), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация",         Организация);
	Запрос.УстановитьПараметр("ПоставщикПолучатель", Контрагент);
	Запрос.УстановитьПараметр("Склад",               Склад);	
	Запрос.УстановитьПараметр("ВидОперации",         ВидОперации);	
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие", Условие);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл 		
		
		НоваяСтрока = ДокументыЗакупки.Добавить();
		
		НоваяСтрока.ДокументОприходования = Выборка.ДокументОприходования;
		НоваяСтрока.Номенклатура          = Выборка.Номенклатура; 		
		НоваяСтрока.Количество            = Выборка.ВесОстаток		
			
	КонецЦикла;
	
	Документы.ИНАГРО_ОтражениеВвозаВывозаСобственногоЗернаВРегламентированномУчете.ЗаполнитьСведенияОНоменклатуреВТабличнойЧасти(ЭтотОбъект, "ДокументыЗакупки");
	
КонецПроцедуры

#КонецОбласти

мВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();

#КонецЕсли
