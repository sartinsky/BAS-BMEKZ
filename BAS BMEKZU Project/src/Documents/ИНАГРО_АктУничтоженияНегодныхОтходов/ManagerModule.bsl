#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПроцедурыИФункцииПечати

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	// Форма23
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПФ_MXL_Форма23";
	КомандаПечати.Представление = НСтр("ru='Акт уничтожения негодных отходов';uk='Акт знищення непридатних відходів'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокумента";

	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Реестр";
	КомандаПечати.Представление = НСтр("ru='Реестр документов';uk='Реєстр документів'");
	КомандаПечати.ЗаголовокФормы= НСтр("ru='Реестр документов ""Акт уничтожения негодных отходов""';uk='Реєстр документів ""Акт знищення непридатних відходів""'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм    = "ФормаСписка";
	КомандаПечати.Порядок       = 100;

       // Акт уничтожения 2021
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АктУничтожения_2021";
	КомандаПечати.Представление = НСтр("ru='Акт уничтожения негодных отходов (2021)';uk='Акт знищення непридатних відходів (2021)'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокумента";

КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм,ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Устанавливаем признак доступности печати покомплектно.
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
    
    Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_Форма23") Тогда
        УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ПФ_MXL_Форма23", НСтр("ru='Форма23';uk='Форма23'"), 
        ПечатьФорма23(МассивОбъектов, ОбъектыПечати, ПараметрыВывода), , "Документ.ИНАГРО_АктУничтоженияНегодныхОтходов.ПФ_MXL_Форма23", , Ложь);
    КонецЕсли;	
    // Формируем табличный документ и добавляем его в коллекцию печатных форм.
    Если  УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АктУничтожения_2021") Тогда	
        УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "АктУничтожения_2021", НСтр("ru='Акт уничтожения негодных отходов 2021';uk='Акт знищення непридатних відходів'"), 
        ПечатьАктУничтожения_2021(МассивОбъектов, ОбъектыПечати, ПараметрыВывода),, "Документ.ИНАГРО_АктРаспределения.ПФ_MXL_UK_АктУничтожения_2021", ,Истина);
    КонецЕсли; 
КонецПроцедуры

Функция ПечатьФорма23(МассивОбъектов, ОбъектыПечати, ПараметрыВывода)
	
	УстановитьПривилегированныйРежим(Истина);	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ИНАГРО_АктУничтоженияНегодныхОтходов_Форма23";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ИНАГРО_АктУничтоженияНегодныхОтходов.ПФ_MXL_Форма23");
	
	// печать производится на языке, указанном в настройках пользователя
	КодЯзыкаПечать = ПараметрыВывода.КодЯзыкаДляМногоязычныхПечатныхФорм;
	Макет.КодЯзыкаМакета = КодЯзыкаПечать;
	
	ПервыйДокумент = Истина;	
	
	Для каждого Ссылка Из МассивОбъектов Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;

		СрезЗавСкладом = РегистрыСведений.ОтветственныеЛица.СрезПоследних(Ссылка.Дата,Новый Структура("СтруктурнаяЕдиница",Ссылка.Склад));
		СрезНачЛаб = РегистрыСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(Ссылка.Дата,Новый Структура("СтруктурнаяЕдиница, ОтветственноеЛицо", Ссылка.Организация, Перечисления.ОтветственныеЛицаОрганизаций.ИНАГРО_НачальникВТЛ));
		СрезНачОхраны = РегистрыСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(Ссылка.Дата, Новый Структура("СтруктурнаяЕдиница, ОтветственноеЛицо", Ссылка.Организация, Перечисления.ОтветственныеЛицаОрганизаций.ИНАГРО_НачальникОхраны));
		ЗавСкладом = ?(СрезЗавСкладом.Количество()>0,СрезЗавСкладом[0].ФизическоеЛицо,"___________________");
		
		НачЛаб = ?(СрезНачЛаб.Количество()>0,СрезНачЛаб[0].ФизическоеЛицо,"___________________");
		НачОхраны = ?(СрезНачОхраны.Количество()>0,СрезНачОхраны[0].ФизическоеЛицо,"___________________");
		СрезЕДРПОУ = РегистрыСведений.КодыОрганизации.СрезПоследних(Ссылка.Дата,Новый Структура("Организация", Ссылка.Организация));
		ЕДРПОУ = ?(НЕ ПустаяСтрока(Ссылка.Организация.КодПоЕДРПОУ), Ссылка.Организация.КодПоЕДРПОУ,"___________");
		СрезДиректор = РегистрыСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(Ссылка.Дата,Новый Структура("СтруктурнаяЕдиница,ОтветственноеЛицо",Ссылка.Организация,Перечисления.ОтветственныеЛицаОрганизаций.Руководитель));
		ДолжностьДиректора = ?(СрезДиректор.Количество()>0, СрезДиректор[0].Должность,"__________");
		//Шапка
		ОблШапка = Макет.ПолучитьОбласть("Шапка");
		ОблШапка.Параметры.Заполнить(Ссылка);
		ОблШапка.Параметры.ЕДРПОУ 				= ЕДРПОУ; 
		ОблШапка.Параметры.Дата 				= Формат(Ссылка.Дата,"ДЛФ = Д");
		ОблШапка.Параметры.ДолжностьДиректора	= ДолжностьДиректора;
		ОблШапка.Параметры.НачальникЛаборатории = НачЛаб;
		ОблШапка.Параметры.НачальникОхраны		= НачОхраны;
		ОблШапка.Параметры.ЗавСкладом 			= ЗавСкладом;
		ОблШапка.Параметры.НомерРаспоряжения	= Ссылка.НомерРаспоряжения;
		ОблШапка.Параметры.ДатаРаспоряжения		= Формат(Ссылка.ДатаРаспоряжения, "ДЛФ=D");
		ОблШапка.Параметры.Примечание			= Ссылка.Комментарий;
		ОблШапка.Параметры.НомерАкта			= Ссылка.НомерАкта;
		ОблШапка.Параметры.ДатаАкта				= Формат(Ссылка.ДатаАкта, "ДЛФ=D");
		ТабДокумент.Вывести(ОблШапка);
		
		//Строка
		ОблСтрока = Макет.ПолучитьОбласть("Строка");
		Для Каждого СтрСписокКультур Из Ссылка.СписокНоменклатуры Цикл
			ОблСтрока.Параметры.Заполнить(СтрСписокКультур);
			ОблСтрока.Параметры.ЛабораторныйАнализ = СтрСписокКультур.НомерАнализа + " від " + Формат(СтрСписокКультур.ЛабораторныйАнализ.Дата,"ДЛФ = Д");
			ОблСтрока.Параметры.ПроцентЗерна = ?(СтрСписокКультур.ЗерноваяПримесь = 0,0,100 - СтрСписокКультур.ЗерноваяПримесь);
			ТабДокумент.Вывести(ОблСтрока);
		КонецЦикла;
		
		//Дно
		ОблДно = Макет.ПолучитьОбласть("Дно");
		ОблДно.Параметры.ИтогоВес = Ссылка.СписокНоменклатуры.Итог("Вес");
		ОблДно.Параметры.НомерПропуска 	= Ссылка.НомерПропуска;
		ОблДно.Параметры.ДатаПропуска 	= Формат(Ссылка.ДатаПропуска, "ДЛФ=D");
		ОблДно.Параметры.НачальникЛаборатории = НачЛаб;
		ОблДно.Параметры.НачальникОхраны		= НачОхраны;
		ОблДно.Параметры.ЗавСкладом = ЗавСкладом;
		
		ТабДокумент.Вывести(ОблДно);
			
	КонецЦикла;
	
	Возврат ТабДокумент;	
	
КонецФункции 

Функция ПечатьАктУничтожения_2021(МассивОбъектов, ОбъектыПечати, ПараметрыВывода)
    
    УстановитьПривилегированныйРежим(Истина);
    ТабДокумент = Новый ТабличныйДокумент;
    ТабДокумент.ИмяПараметровПечати =  "ПАРАМЕТРЫ_ПЕЧАТИ_ИНАГРО_АктУничтоженияНегодныхОтходов_АктУничтожения_2021";
    
    Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ИНАГРО_АктУничтоженияНегодныхОтходов.ПФ_MXL_UK_АктУничтожения_2021");
    
    // печать производится на языке, указанном в настройках пользователя
    КодЯзыкаПечать = ПараметрыВывода.КодЯзыкаДляМногоязычныхПечатныхФорм;
    Макет.КодЯзыкаМакета = КодЯзыкаПечать;
    
    Запрос = Новый Запрос;
    Запрос.Текст = 
    "ВЫБРАТЬ
    |   ИНАГРО_АктУничтоженияНегодныхОтходов.Организация.Ссылка КАК ОрганизацияСсылка,
    |   ИНАГРО_АктУничтоженияНегодныхОтходов.Организация.НаименованиеПолное КАК Организация,
    |   ИНАГРО_АктУничтоженияНегодныхОтходов.Ссылка КАК Ссылка,
    |   ИНАГРО_АктУничтоженияНегодныхОтходов.Дата КАК Дата,
    |   ИНАГРО_АктУничтоженияНегодныхОтходов.Склад КАК Склад,
    |   ИНАГРО_АктУничтоженияНегодныхОтходов.НомерПропуска КАК Пропуск,
    |   ИНАГРО_АктУничтоженияНегодныхОтходов.ДатаПропуска КАК ДатаПропуска,
    |   ИНАГРО_АктУничтоженияНегодныхОтходов.Организация.КодПоЕДРПОУ КАК ЄДРПОУ,
    |   ИНАГРО_АктУничтоженияНегодныхОтходов.Номер КАК Номер,
    |   ИНАГРО_АктУничтоженияНегодныхОтходов.НомерРаспоряжения КАК НомерРаспоряжения,
    |   ИНАГРО_АктУничтоженияНегодныхОтходовСписокНоменклатуры.ЛабораторныйАнализ КАК Анализ,
    |   ЕСТЬNULL(ИНАГРО_АктУничтоженияНегодныхОтходовСписокНоменклатуры.ЛабораторныйАнализ.СорнаяПримесь, 0) КАК сор,
    |   ИНАГРО_АктУничтоженияНегодныхОтходовСписокНоменклатуры.ЗерноваяПримесь КАК ЗерноваяПримесь,
    |   ИНАГРО_АктУничтоженияНегодныхОтходовСписокНоменклатуры.Вес КАК Вес,
    |   ИНАГРО_АктУничтоженияНегодныхОтходов.ДатаАкта КАК ДатаАкта,
    |   ИНАГРО_АктУничтоженияНегодныхОтходов.НомерАкта КАК НомерАкта,
    |   ИНАГРО_АктУничтоженияНегодныхОтходов.ДатаРаспоряжения КАК ДатаРаспоряжения
    |ИЗ
    |   Документ.ИНАГРО_АктУничтоженияНегодныхОтходов.СписокНоменклатуры КАК ИНАГРО_АктУничтоженияНегодныхОтходовСписокНоменклатуры
    |       ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИНАГРО_АктУничтоженияНегодныхОтходов КАК ИНАГРО_АктУничтоженияНегодныхОтходов
    |       ПО ИНАГРО_АктУничтоженияНегодныхОтходовСписокНоменклатуры.Ссылка = ИНАГРО_АктУничтоженияНегодныхОтходов.Ссылка
    |ГДЕ
    |   ИНАГРО_АктУничтоженияНегодныхОтходов.Ссылка В(&МассивОбъектов)";
    Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
    Выборка = Запрос.Выполнить().Выбрать();
    
    Пока Выборка.Следующий() Цикл
        
        Руководители = ИНАГРО_Элеватор.ОтветственныеЛицаОрганизацииИмяФамилия(Выборка.ОрганизацияСсылка, Выборка.Дата);	 
        
        //Шапка
        ОблШапка = Макет.ПолучитьОбласть("Шапка");
        ОблШапка.Параметры.Заполнить(Выборка);
        
        ОблШапка.Параметры.Дата		 			= Формат(Выборка.Дата,"ДЛФ=DD");
        ОблШапка.Параметры.День                 = Формат(Выборка.Дата, "ДФ=дд");
        ОблШапка.Параметры.Месяц                = МесяцПрописью(Выборка.Дата, ПараметрыВывода);;
        ОблШапка.Параметры.Год                  = Формат(Выборка.Дата, "ДФ=гг");
        ОблШапка.Параметры.Директор				= Руководители.Руководитель;
        ОблШапка.Параметры.НачальникВТЛ 		= Руководители.НачальникВТЛ;
        ОблШапка.Параметры.ЗавСкладом 			= ИНАГРО_Элеватор.ОтветственныеЛицаИмяФамилия(Выборка.Организация, Выборка.Дата, Выборка.Склад);
        ОблШапка.Параметры.Сор					= 100 - Выборка.сор;
        ОблШапка.Параметры.ДатаПропуска			= Формат(Выборка.ДатаПропуска, "ДЛФ=DD");
        ОблШапка.Параметры.ДатаАкта             = Формат(Выборка.ДатаАкта, "ДЛФ=DD");
        ОблШапка.Параметры.НачальникОхраны      = Руководители.НачальникОхраны;
        ОблШапка.Параметры.Приказ               = СтрШаблон(НСтр("ru = '№%1 от %2'; uk = '№%1 від %2'"), Выборка.НомерРаспоряжения, Формат(Выборка.ДатаРаспоряжения, "ДЛФ=DD")); 
        ТабДокумент.Вывести(ОблШапка);
        
        ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
        
    КонецЦикла;
    Возврат ТабДокумент;

КонецФункции

Функция МесяцПрописью(ТекДата, ПараметрыВывода)
	
	КодЯзыкаПечать = ПараметрыВывода.КодЯзыкаДляМногоязычныхПечатныхФорм;
	ФорматДаты = Формат(ТекДата, "ДЛФ=DD;Л=" + Локализация.ОпределитьКодЯзыкаДляФормат(КодЯзыкаПечать));
	аа = Лев(ФорматДаты, СтрДлина(ФорматДаты)-7);
	Месяц = СокрЛП(Прав(аа, СтрДлина(аа)-2));
	
	Возврат Месяц;
	
КонецФункции

Функция ПолучитьДополнительныеРеквизитыДляРеестра() Экспорт
	
	Результат = Новый Структура;	
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли