
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АдресХранилищаТаблицыДоговоровСделок") Тогда
		ТаблицаДоговоровСделок.Загрузить(ПолучитьИзВременногоХранилища(Параметры.АдресХранилищаТаблицыДоговоровСделок));
	КонецЕсли;
	
	Если Параметры.Свойство("Основание") Тогда
		Основание = Параметры.Основание;	
	КонецЕсли;
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)

	Если ЗавершениеРаботы И Модифицированность Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Не ЗавершениеРаботы И Не ВыбранДоговорСделка Тогда
		
		Отказ = Истина;
		
		ТекстВопроса = НСтр("ru='Не выбран договор, документ ""Регистрация входящего налогового документа"" создан не будет. Продолжить?';uk= 'Не вибраний договір, документ ""Реєстрація вхідного податкового документа"" створений не буде. Продовжити?'");	
			
		ОписаниеОповещения = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТабличныхЧастей

&НаКлиенте
Процедура ТаблицаДоговоровСделокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	МассивСтрок = Новый Массив;
	МассивСтрок.Добавить(ВыбраннаяСтрока);
	ВыбратьЗавершение(-1, МассивСтрок);
	
КонецПроцедуры

#КонецОбласти

#Область КомандыФормы

&НаКлиенте
Процедура Выбрать(Команда)

	МассивИдентификаторовСтрок = Элементы.ТаблицаДоговоровСделок.ВыделенныеСтроки;
	
	Если МассивИдентификаторовСтрок.Количество() = 0 Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьЗавершение", ЭтотОбъект, МассивИдентификаторовСтрок);
		ТекстВопроса = НСтр("ru='Строка не выбрана! Документ не будет заполнен корректно. Продолжить?';uk= 'Строка не выбрана! Документ не будет заполнен корректно. Продолжить?'");
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
		Возврат;	
	КонецЕсли;
	
	ВыбратьЗавершение(-1, МассивИдентификаторовСтрок);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеФормой()
	
	КоличествоСтрокСПустымКонтрагентом = ТаблицаДоговоровСделок.НайтиСтроки(
		Новый Структура("Контрагент", ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка"))).Количество();
		
	КоличествоСтрокТаблицыВсего = ТаблицаДоговоровСделок.Количество();
	
	Элементы.ТаблицаДоговоровСделокКонтрагент.Видимость = НЕ(КоличествоСтрокСПустымКонтрагентом = КоличествоСтрокТаблицыВсего);

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьЗавершение(Ответ, ВыделенныеСтроки) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;	
	КонецЕсли;
	
	ПараметрыФормыВыбора = Новый Структура("Основание,ДоговорСделка");
	ПараметрыФормыВыбора.Вставить("Основание", Основание);
	
	Если ВыделенныеСтроки.Количество() > 0 Тогда
		Идентификатор = ВыделенныеСтроки[0];
		СтрокаДоговораСделки = ТаблицаДоговоровСделок.НайтиПоИдентификатору(Идентификатор);
		Если СтрокаДоговораСделки <> Неопределено Тогда
			ПараметрыФормыВыбора.ДоговорСделка = Новый Структура("ДоговорКонтрагента,Сделка", 	
				СтрокаДоговораСделки.ДоговорКонтрагента, СтрокаДоговораСделки.Сделка);
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура("Основание", ПараметрыФормыВыбора);
	
	ОткрытьФорму("Документ.РегистрацияВходящегоНалоговогоДокумента.Форма.ФормаДокумента", ПараметрыОткрытия);
	
	ВыбранДоговорСделка = Истина;
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если НЕ Ответ = КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ВыбранДоговорСделка = Истина;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти