#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды 
	
	ИНАГРО_КонтрольПроцессов.ПроверкаРазрешенийПередОткрытием(ЭтотОбъект, Отказ);
	
	Если Параметры.Ключ.Пустая() Тогда
		ПодготовитьФормуНаСервере();		
	КонецЕсли;
	
	// Уведомим о появлении функционала рабочей даты
	ЭтаФорма.НастройкиПредупреждений = ОбщегоНазначенияБП.НастройкиПредупрежденийОбИзменениях("РабочаяДатаИзДокумента");
	 
	// Показываем, если это новый документ и сама рабочая дата еще не установлена.
	ЭтаФорма.НастройкиПредупреждений.РабочаяДатаИзДокумента = ЭтаФорма.НастройкиПредупреждений.РабочаяДатаИзДокумента
	 	И ЭтаФорма.Параметры.Ключ.Пустая()
	  	И НЕ ЗначениеЗаполнено(БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("РабочаяДата"));
		
	УстановитьПараметрыВыбораВидОперации();
	
	// Если форма вызвана в режиме смена вида операции - модифицируем сразу при открытии, 
	// чтобы можно было подтвердить или отказаться от изменения путем сохранения или 
	// отказа от сохранения документа.
	Если Параметры.Свойство("ИзменитьВидОперации")
		И Параметры.ИзменитьВидОперации Тогда
		
		// Не кэшируем переменную Объект, т.к. может вызываться Форма.ИзменитьРеквизиты(),
		// которая меняет Объект.
		Объект.ВидОперации = Параметры.ЗначенияЗаполнения.ВидОперации;
		
		УстановитьЗаголовокФормы();		
		УправлениеФормойНаСервере();
		
		Модифицированность = Истина;
		
	КонецЕсли;	
	
	// ПодключаемоеОборудование
	ИНАГРО_ПодключаемоеОборудованиеОбщегоНазначения.НастроитьПодключаемоеОборудование(ЭтаФорма);
	// ПодключаемоеОборудование	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если  ТипЗнч(Параметр) = Тип("Структура") 
		И Параметр.Свойство("ИдентификаторВызывающейФормы")
		И Параметр.ИдентификаторВызывающейФормы = УникальныйИдентификатор Тогда
		Если ИмяСобытия = "СозданЛабораторныйАнализ" ИЛИ ИмяСобытия = "ИзмененЛабораторныйАнализ" Тогда
			Если ИНАГРО_Элеватор.ЛабораторныйАнализПроверкаЗаполнения(Параметр.ДокументСсылка, ПолучитьПараметрыДляПроверкиЗаполнения()) Тогда
				Объект.ЛабораторныйАнализ = Параметр.ДокументСсылка;
				ЛабораторныйАнализПриИзмененииНаСервере();
				Модифицированность = Истина;
			КонецЕсли;
		КонецЕсли;		
	Иначе
		ОбщегоНазначенияБПКлиент.ОбработкаОповещенияФормыДокумента(ЭтаФорма, Объект.Ссылка, ИмяСобытия, Параметр, Источник);
	КонецЕсли;	
	
	Если Источник = "ПодключаемоеОборудование" Тогда
		Если ИмяСобытия = "WP89" Тогда			
			ЗавершениеПолученияВеса(Параметр, Неопределено);			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	ПодготовитьФормуНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	РасчетВеса();		

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
		
	// проверяем нужно ли формировать ттн...
	
	Ссылка = Объект.Ссылка; 	
	
	НашаМодифицированность = Модифицированность;
	
	Если Модифицированность Тогда
		
		Если Объект.Дата                    <> Ссылка.Дата Тогда
		ИначеЕсли Объект.Организация        <> Ссылка.Организация Тогда	
		ИначеЕсли Объект.Владелец           <> Ссылка.Владелец Тогда	
		ИначеЕсли Объект.ДоговорКонтрагента <> Ссылка.ДоговорКонтрагента Тогда			
		ИначеЕсли Объект.ВидХранения        <> Ссылка.ВидХранения Тогда	
		ИначеЕсли Объект.Склад              <> Ссылка.Склад Тогда	
		ИначеЕсли Объект.Номенклатура       <> Ссылка.Номенклатура Тогда	
		ИначеЕсли Объект.Урожай             <> Ссылка.Урожай Тогда	
		ИначеЕсли Объект.Влажность          <> Ссылка.Влажность Тогда	
		ИначеЕсли Объект.СорнаяПримесь      <> Ссылка.СорнаяПримесь Тогда	
		ИначеЕсли Объект.ЗерноваяПримесь    <> Ссылка.ЗерноваяПримесь Тогда	
		ИначеЕсли Объект.ВесВывоз           <> Ссылка.ВесВывоз Тогда							
		Иначе
			НашаМодифицированность = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ИнтерактивноеФормированиеРеестровТТН И НашаМодифицированность Тогда
		
		// Если это не новый документ или присутсвует в каком-то реестре
		// Нужно переформировывать реестр по старым параметрам
		
		Если ЗначениеЗаполнено(Объект.Ссылка) И ВключенаВРеестр() Тогда
			
			ПараметрыСтарые = Новый Структура;
			ПараметрыСтарые.Вставить("ПериодС",           НачалоДня(Ссылка.Дата));
			ПараметрыСтарые.Вставить("ПериодПо",          КонецДня(Ссылка.Дата));
			ПараметрыСтарые.Вставить("Организация",       Ссылка.Организация);			
			ПараметрыСтарые.Вставить("ВидХранения",       Ссылка.ВидХранения);
			ПараметрыСтарые.Вставить("Склад",             Ссылка.Склад);
			ПараметрыСтарые.Вставить("НовыйСклад",        Ссылка.НовыйСклад);	
			ПараметрыСтарые.Вставить("Номенклатура",      Ссылка.Номенклатура);
			ПараметрыСтарые.Вставить("НоваяНоменклатура", Ссылка.НоваяНоменклатура);
			ПараметрыСтарые.Вставить("Урожай",            Ссылка.Урожай);
			ПараметрыСтарые.Вставить("НовыйУрожай",       Ссылка.НовыйУрожай);					
			ПараметрыСтарые.Вставить("Влажность",         Ссылка.Влажность);
			ПараметрыСтарые.Вставить("СорнаяПримесь",     Ссылка.СорнаяПримесь);
			ПараметрыСтарые.Вставить("ЗерноваяПримесь",   Ссылка.ЗерноваяПримесь);
			
			ТекущийОбъект.ДополнительныеСвойства.Вставить("ПараметрыСтарые", ПараметрыСтарые);
			
		КонецЕсли;
		
		ПараметрыТекущие = Новый Структура;
		ПараметрыТекущие.Вставить("ПериодС",           НачалоДня(Объект.Дата));
		ПараметрыТекущие.Вставить("ПериодПо",          КонецДня(Объект.Дата));
		ПараметрыТекущие.Вставить("Организация",       Объект.Организация);
		ПараметрыТекущие.Вставить("ВидХранения",       Объект.ВидХранения);
		ПараметрыТекущие.Вставить("Склад",             Объект.Склад);
		ПараметрыТекущие.Вставить("НовыйСклад",        Объект.НовыйСклад);
		ПараметрыТекущие.Вставить("Номенклатура",      Объект.Номенклатура);
		ПараметрыТекущие.Вставить("НоваяНоменклатура", Объект.НоваяНоменклатура);
		ПараметрыТекущие.Вставить("Урожай",            Объект.Урожай);
		ПараметрыТекущие.Вставить("НовыйУрожай",       Объект.НовыйУрожай);
		ПараметрыТекущие.Вставить("Влажность",         Объект.Влажность);
		ПараметрыТекущие.Вставить("Сорнаяпримесь",     Объект.СорнаяПримесь);
		ПараметрыТекущие.Вставить("ЗерноваяПримесь",   Объект.ЗерноваяПримесь);
		
		ТекущийОбъект.ДополнительныеСвойства.Вставить("ПараметрыТекущие", ПараметрыТекущие);

	КонецЕсли;
	
	//РасчетВеса(); 
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ИНАГРО_КонтрольПроцессов.СохранитьЗначенияВесаТары(ТекущийОбъект, Отказ) ;
	
	Если ТекущийОбъект.ПометкаУдаления Тогда
		ИНАГРО_КонтрольПроцессов.ОчиститьЗначенияВесаТары(ТекущийОбъект);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ТекущийОбъект.ДополнительныеСвойства.Свойство("ПараметрыСтарые") Тогда
		ИНАГРО_Элеватор.СформироватьРеестрТТН_Внутр(ТекущийОбъект.ДополнительныеСвойства.ПараметрыСтарые);
	КонецЕсли;
	
	Если ТекущийОбъект.ДополнительныеСвойства.Свойство("ПараметрыТекущие") Тогда
		ИНАГРО_Элеватор.СформироватьРеестрТТН_Внутр(ТекущийОбъект.ДополнительныеСвойства.ПараметрыТекущие);
	КонецЕсли;

	УстановитьЗаголовокФормы();		
		
	УстановитьСостояниеДокумента();
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	Если НачалоДня(Объект.Дата) = НачалоДня(ТекущаяДатаДокумента) Тогда
		// Изменение времени не влияет на поведение документа.
		ТекущаяДатаДокумента = Объект.Дата;
		Возврат;
	КонецЕсли;		
		
	// Общие проверки условий по датам.
	ТребуетсяВызовСервера = ИНАГРО_ЭлеваторКлиент.ТребуетсяВызовСервераПриИзмененииДатыДокумента(Объект.Дата, ТекущаяДатаДокумента);
		
	// Если определили, что изменение даты может повлиять на какие-либо параметры, 
	// то передаем обработку на сервер.
	Если ТребуетсяВызовСервера Тогда
		ДатаПриИзмененииНаСервере();
	КонецЕсли;
	
	// Запомним новую дату документа.
	ТекущаяДатаДокумента = Объект.Дата;	
	
КонецПроцедуры

&НаСервере
Процедура ДатаПриИзмененииНаСервере()

	ИНАГРО_ЭлеваторЗаполнениеДокументов.ЗаполнитьТипНомерЖурналаВесовой(Объект);	

	УстановитьФункциональныеОпцииФормы();
	
	УправлениеФормойНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура ВидОперацииПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ВидОперации) Тогда	
		ВидОперацииПриИзмененииНаСервере();	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВидОперацииПриИзмененииНаСервере()
	
	ПерезаполнитьРеквизиты();

	Если Объект.ВидОперации = Перечисления.ИНАГРО_ВидыОперацийТТНВнутр.ПеремещениеФасованного Тогда		
		
		Объект.СписокНоменклатуры.Очистить();
		
		НоваяСтрока = Объект.СписокНоменклатуры.Добавить();
		НоваяСтрока.Номенклатура      = Объект.Номенклатура;
		НоваяСтрока.НоваяНоменклатура = Объект.НоваяНоменклатура;
		НоваяСтрока.Урожай            = Объект.Урожай;
		НоваяСтрока.НовыйУрожай       = Объект.НовыйУрожай;
		НоваяСтрока.Вес               = Объект.ВесВывоз;			
				
	КонецЕсли;
	
	ЗаполнитьСчетаУчетаВТабличнойЧасти();
	
	УстановитьЗаголовокФормы(); 	
	
	УправлениеФормойНаСервере();	
	
	Модифицированность = Истина;
	
КонецПроцедуры 

&НаКлиенте
Процедура ОтражатьВБухгалтерскомУчетеПриИзменении(Элемент)
	
	Если Объект.СписокНоменклатуры.Количество() > 0 Тогда
		
		Если Объект.ОтражатьВБухгалтерскомУчете Тогда		
			ТекстВопроса = НСтр("ru='Установить счета бухгалтерского учета в соответствии со значениями по умолчанию?';uk='Встановити рахунки бухгалтерського обліку відповідно до значень за замовчуванням?'");
		Иначе
			ТекстВопроса = НСтр("ru='Очистить счета бухгалтерского учета?';uk='Очистити рахунки бухгалтерського обліку?'");
		КонецЕсли;
		
		Оповещение = Новый ОписаниеОповещения("ОтражатьВБухгалтерскомУчетеПриИзмененииЗавершение", ЭтотОбъект, Параметры);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);	
		
		Возврат;
		
	КонецЕсли;	
	
	УправлениеФормойНаСервере(); 
	
КонецПроцедуры

&НаКлиенте
Процедура ОтражатьВБухгалтерскомУчетеПриИзмененииЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		УправлениеФормойНаСервере();
		Возврат;
	КонецЕсли; 	
	
	ОтражатьВБухгалтерскомУчетеПриИзмененииЗавершениеНаСервере();	
   
КонецПроцедуры

&НаСервере
Процедура ОтражатьВБухгалтерскомУчетеПриИзмененииЗавершениеНаСервере()
	
	Если Объект.ВидОперации = Перечисления.ИНАГРО_ВидыОперацийТТНВнутр.ПеремещениеФасованного Тогда
		ЗаполнитьСчетаУчетаВТабличнойЧасти();
	КонецЕсли;
	
	УправлениеФормойНаСервере();
   
КонецПроцедуры

&НаКлиенте
Процедура ПриказНаПеремещениеПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ПриказНаПеремещениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Организация", Объект.Организация);
	
	Если ЗначениеЗаполнено(Объект.Владелец) Тогда
		ПараметрыОтбора.Вставить("Владелец", Объект.Владелец);			
	КонецЕсли;	
	Если ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
		ПараметрыОтбора.Вставить("ДоговорКонтрагента", Объект.ДоговорКонтрагента);
	КонецЕсли;
	Если ЗначениеЗаполнено(Объект.Склад) Тогда
		ПараметрыОтбора.Вставить("Склад", Объект.Склад);
	КонецЕсли;
	Если ЗначениеЗаполнено(Объект.НовыйСклад) Тогда
		ПараметрыОтбора.Вставить("НовыйСклад", Объект.НовыйСклад);
	КонецЕсли;
	Если ЗначениеЗаполнено(Объект.Силос) Тогда
		ПараметрыОтбора.Вставить("МестоХранения", Объект.Силос);
	КонецЕсли;
	Если ЗначениеЗаполнено(Объект.НовыйСилос) Тогда
		ПараметрыОтбора.Вставить("НовоеМестоХранения", Объект.НовыйСилос);
	КонецЕсли;	
	Если ЗначениеЗаполнено(Объект.Урожай) Тогда
		ПараметрыОтбора.Вставить("Урожай", Объект.Урожай);
	КонецЕсли;
	Если ЗначениеЗаполнено(Объект.ВидХранения) Тогда
		ПараметрыОтбора.Вставить("ВидХранения", Объект.ВидХранения);
	КонецЕсли;	
		 		
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", 	   ПараметрыОтбора);	
	ПараметрыФормы.Вставить("Ссылка",      Объект.Ссылка);
	ПараметрыФормы.Вставить("Дата",        Объект.Дата);
	ПараметрыФормы.Вставить("РежимВыбора", Истина); 
				
	ОткрытьФорму("Документ.ИНАГРО_ПриказНаПеремещение.Форма.ФормаВыбораПриказа", ПараметрыФормы, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриказНаПеремещениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Объект.ПриказНаПеремещение = ВыбранноеЗначение;
	
	ПолучитьНоменклатуруИзПриказа(ВыбранноеЗначение);
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)

	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		ОрганизацияПриИзмененииНаСервере();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	Если Объект.ВидОперации = Перечисления.ИНАГРО_ВидыОперацийТТНВнутр.ПеремещениеФасованного Тогда
		ЗаполнитьСчетаУчетаВТабличнойЧасти();
	КонецЕсли;
		
	УстановитьФункциональныеОпцииФормы();	
	
	УправлениеФормойНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура ВладелецПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Владелец) Тогда
		
		ПараметрыОтбораДоговора = ПолучитьПараметрыДляДоговоров();
		
		ИНАГРО_ЭлеваторЗаполнениеДокументов.ПриИзмененииЗначенияКонтрагента(Объект.Дата, Объект.Организация, Объект.Владелец, Объект.ДоговорКонтрагента, ПараметрыОтбораДоговора);
		
		ВладелецПриИзмененииНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВладелецПриИзмененииНаСервере()
	
	ПерезаполнитьРеквизиты();
	
	Если Объект.ВидОперации = Перечисления.ИНАГРО_ВидыОперацийТТНВнутр.ПеремещениеФасованного Тогда
		ЗаполнитьСчетаУчетаВТабличнойЧасти();
	КонецЕсли;
	
	УстановитьФункциональныеОпцииФормы();
	
	УправлениеФормойНаСервере();	

КонецПроцедуры

&НаКлиенте
Процедура ВладелецНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		
	Если ДополнительныйКонтрольДоговоров Тогда 
		СтандартнаяОбработка = Ложь;
		ИНАГРО_ЭлеваторКлиент.ВладелецНачалоВыбораЗавершение();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СкладПриИзменении(Элемент)

	Если ЗначениеЗаполнено(Объект.Склад) Тогда
		СкладПриИзмененииНаСервере();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура СкладПриИзмененииНаСервере()	
	
	Объект.МестоХранения = Справочники.ИНАГРО_МестаХранения.ПустаяСсылка();

	УстановитьФункциональныеОпцииФормы(); 	
	
	УправлениеФормойНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура НовыйСкладПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.НовыйСклад) Тогда
		НовыйСкладПриИзмененииНаСервере();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура НовыйСкладПриИзмененииНаСервере()	
	
	Объект.НовоеМестоХранения = Справочники.ИНАГРО_МестаХранения.ПустаяСсылка();

	УстановитьФункциональныеОпцииФормы(); 	
	
	УправлениеФормойНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура ВидХраненияПриИзменении(Элемент)
	
	ПараметрыОтбораДоговора = ПолучитьПараметрыДляДоговоров();
	
	ИНАГРО_ЭлеваторЗаполнениеДокументов.ПриИзмененииЗначенияКонтрагента(Объект.Дата, Объект.Организация, Объект.Владелец, Объект.ДоговорКонтрагента, ПараметрыОтбораДоговора);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
		ДоговорКонтрагентаПриИзмененииНаСервере();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ДоговорКонтрагентаПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.ДоговорКонтрагента.ВидВзаиморасчетов) Тогда
		НайденВидХранения = Справочники.ИНАГРО_ВидыХранения.НайтиПоРеквизиту("ТипДоговора", Объект.ДоговорКонтрагента.ВидВзаиморасчетов); 
		Если НайденВидХранения <> Неопределено Тогда
			Объект.ВидХранения = НайденВидХранения.Ссылка;
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

#Область ОбработчикиСобытийЭлементовПанелиТранспортКультура

&НаКлиенте
Процедура ВодительПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Водитель) Тогда
		
		ДанныеОбъекта = Новый Структура("Водитель, НомерТранспорта, НомерПрицепа");
		ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);
		
		ДанныеОбъекта.Вставить("ВестиСправочникВодителей",   ВестиСправочникВодителей);
		ДанныеОбъекта.Вставить("ВестиСправочникАвтомобилей", ВестиСправочникАвтомобилей);
		
		ВодительПриИзмененииНаСервере(ДанныеОбъекта);
		
		ЗаполнитьЗначенияСвойств(Объект, ДанныеОбъекта);
		
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВодительПриИзмененииНаСервере(ДанныеОбъекта)
	
	Водитель = ДанныеОбъекта.Водитель;
	
	Если ДанныеОбъекта.ВестиСправочникВодителей Тогда
		
		Если ДанныеОбъекта.ВестиСправочникАвтомобилей Тогда
			
			Если ЗначениеЗаполнено(Водитель.НомерТранспорта) Тогда
				ДанныеОбъекта.НомерТранспорта = Водитель.НомерТранспорта;
			КонецЕсли;
			Если ЗначениеЗаполнено(Водитель.НомерПрицепа) Тогда
				ДанныеОбъекта.НомерПрицепа	  = Водитель.НомерПрицепа;
			КонецЕсли;
						
		Иначе
			
			ДанныеОбъекта.НомерТранспорта = Строка(Водитель.НомерТранспорта);
			ДанныеОбъекта.НомерПрицепа	  = Строка(Водитель.НомерПрицепа);
			
		КонецЕсли;		
		
	КонецЕсли; 		
		
КонецПроцедуры 

&НаКлиенте
Процедура НоменклатураПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Номенклатура) Тогда				
		ПерезаполнитьРеквизиты();		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УрожайПриИзменении(Элемент)
	
	Если    Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ИНАГРО_ВидыОперацийТТНВнутр.Перемещение")
		ИЛИ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ИНАГРО_ВидыОперацийТТНВнутр.Перевзвешивание") Тогда
		Объект.НовыйУрожай = Объект.Урожай;
	КонецЕсли;

	ПараметрыОтбораДоговора = ПолучитьПараметрыДляДоговоров();
	
	ИНАГРО_ЭлеваторЗаполнениеДокументов.ПриИзмененииЗначенияКонтрагента(Объект.Дата, Объект.Организация, Объект.Владелец, Объект.ДоговорКонтрагента, ПараметрыОтбораДоговора);
	
КонецПроцедуры

&НаКлиенте
Процедура ЛабораторныйАнализПриИзменении(Элемент)
		
	ЛабораторныйАнализПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЛабораторныйАнализПриИзмененииНаСервере()
	
	ДанныеОбъекта = Новый Структура(
		"ЛабораторныйАнализ, НомерАнализа, Влажность, 
		|СорнаяПримесь, ЗерноваяПримесь, ЗачетныйВес
		|");
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект); 

	ПараметрыДляРасчетаЗачетногоВеса = ПолучитьПараметрыДляРасчетаЗачетногоВеса();

	ИНАГРО_Элеватор.ЛабораторныйАнализПриИзмененииНаСервере(ДанныеОбъекта, ПараметрыДляРасчетаЗачетногоВеса);
	
	ЗаполнитьЗначенияСвойств(Объект, ДанныеОбъекта); 
	
	// !!!!!
	РасчетВеса();
	
	УправлениеФормойНаСервере();
	
	//Объект.НомерАнализа    = ИНАГРО_Элеватор.ПолучитьНомерЛабАнализаИзЛабАнализа(Объект.ЛабораторныйАнализ);
	//
	//Объект.Влажность       = Объект.ЛабораторныйАнализ.Влажность;
	//Объект.СорнаяПримесь   = Объект.ЛабораторныйАнализ.СорнаяПримесь;
	//Объект.ЗерноваяПримесь = Объект.ЛабораторныйАнализ.ЗерноваяПримесь;
	//
	//ПараметрыДляРасчетаЗачетногоВеса = ПолучитьПараметрыДляРасчетаЗачетногоВеса();

	//Объект.ЗачетныйВес     = ИНАГРО_Элеватор.РассчитатьЗачетныйВес(ПараметрыДляРасчетаЗачетногоВеса);

	//УправлениеФормойНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура ЛабораторныйАнализНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОтбора = Новый Структура;
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		ПараметрыОтбора.Вставить("Организация",        Объект.Организация);		
	КонецЕсли;	
	Если ЗначениеЗаполнено(Объект.Владелец) Тогда
		ПараметрыОтбора.Вставить("Владелец",           Объект.Владелец);
	КонецЕсли;	
	Если ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
		ПараметрыОтбора.Вставить("ДоговорКонтрагента", Объект.ДоговорКонтрагента);		
	КонецЕсли;	
	Если ЗначениеЗаполнено(Объект.Склад) Тогда
		ПараметрыОтбора.Вставить("Склад",              Объект.Склад);
	КонецЕсли;	
	Если ЗначениеЗаполнено(Объект.ВидХранения) Тогда
		ПараметрыОтбора.Вставить("ВидХранения",        Объект.ВидХранения);
	КонецЕсли;	
	Если ЗначениеЗаполнено(Объект.Номенклатура) Тогда
		ПараметрыОтбора.Вставить("Номенклатура",       Объект.Номенклатура);
	КонецЕсли;	
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор",  ПараметрыОтбора);
	ПараметрыФормы.Вставить("Ссылка", Объект.Ссылка);
	
	ОткрытьФорму("Документ.ИНАГРО_ЛабораторныйАнализ.Форма.ФормаВыбора", ПараметрыФормы, Элемент, УникальныйИдентификатор, , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ВлажностьПриИзменении(Элемент)
	
	РасчетЗачетногоВеса();
	
КонецПроцедуры

&НаКлиенте
Процедура СорнаяПримесьПриИзменении(Элемент)
	
	РасчетЗачетногоВеса();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовПанелиВесовая

&НаКлиенте
Процедура ВесБруттоВывозПриИзменении(Элемент)
	
	ВесБруттоВывозПриИзмененииНаСервере();

КонецПроцедуры

&НаСервере
Процедура ВесБруттоВывозПриИзмененииНаСервере()
	
	Если НЕ ИНАГРО_КонтрольПроцессов.ПроверитьМаксимальнуюГрузоподъемностьВесов(Объект.ВесБруттоВывоз) Тогда 
		Объект.ВесБруттоВывоз = 0;
		Возврат;
	КонецЕсли;
	
	Если НЕ ИНАГРО_КонтрольПроцессов.ПроверитьОтрицательноеНетто(Объект.ВесБруттоВывоз, Объект.ВесТарыВывоз) Тогда 
		Объект.ВесБруттоВывоз = 0;
		Возврат;
	КонецЕсли;
	
	Если Объект.ВесБруттоВывозКонтроль = 0 Тогда 
		Объект.ВесБруттоВывозКонтроль = Объект.ВесБруттоВывоз;
	КонецЕсли;

	ИНАГРО_КонтрольПроцессов.КонтрольИзмененияВесаТТН(Объект, Объект.ВесБруттоВывозКонтроль, Объект.ВесБруттоВывоз);

	РасчетВеса();
	
	УправлениеПоследовательностьюВводаВеса();
	
	Объект.РедактировалсяВручнуюВесБруттоВывоз = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ВесБрутто1ВывозПриИзменении(Элемент)
	
	ВесБрутто1ВывозПриИзмененииНаСервере();

КонецПроцедуры

&НаСервере
Процедура ВесБрутто1ВывозПриИзмененииНаСервере()
	
	Если НЕ ИНАГРО_КонтрольПроцессов.ПроверитьМаксимальнуюГрузоподъемностьВесов(Объект.ВесБрутто1Вывоз) Тогда 
		Объект.ВесБрутто1Вывоз = 0;
		Возврат;
	КонецЕсли;
	
	Если НЕ ИНАГРО_КонтрольПроцессов.ПроверитьОтрицательноеНетто(Объект.ВесБрутто1Вывоз, Объект.ВесТары1Вывоз) Тогда 
		Объект.ВесБрутто1Вывоз = 0;
		Возврат;
	КонецЕсли;
	
	Если Объект.ВесБрутто1ВывозКонтроль = 0 Тогда 
		Объект.ВесБрутто1ВывозКонтроль = Объект.ВесБрутто1Вывоз;
	КонецЕсли;

	ИНАГРО_КонтрольПроцессов.КонтрольИзмененияВесаТТН(Объект, Объект.ВесБрутто1Контроль, Объект.ВесБрутто1);
	
	РасчетВеса();
	
	УправлениеПоследовательностьюВводаВеса();	
	
	Объект.РедактировалсяВручнуюВесБрутто1Вывоз = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ВесТарыВывозПриИзменении(Элемент)
	
	ВесТарыВывозПриИзмененииНаСервере();

КонецПроцедуры

&НаСервере
Процедура ВесТарыВывозПриИзмененииНаСервере()
	
	Если НЕ ИНАГРО_КонтрольПроцессов.ПроверитьМаксимальнуюГрузоподъемностьВесов(Объект.ВесТарыВывоз) Тогда 
		Объект.ВесТарыВывоз = 0;
		Возврат;
	КонецЕсли;
	
	Если Объект.ВесТарыВывозКонтроль = 0 тогда 
		Объект.ВесТарыВывозКонтроль = Объект.ВесТарыВывоз;
	КонецЕсли;

	ИНАГРО_КонтрольПроцессов.КонтрольИзмененияВесаТТН(Объект, Объект.ВесТарыВывозКонтроль, Объект.ВесТарыВывоз);

	РасчетВеса();
	
	УправлениеПоследовательностьюВводаВеса();
	
	Объект.РедактировалсяВручнуюВесТарыВывоз = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ВесТары1ВывозПриИзменении(Элемент)
	
	ВесТары1ВывозПриИзмененииНаСервере();

КонецПроцедуры

&НаСервере
Процедура ВесТары1ВывозПриИзмененииНаСервере()
	
	Если НЕ ИНАГРО_КонтрольПроцессов.ПроверитьМаксимальнуюГрузоподъемностьВесов(Объект.ВесТары1Вывоз) Тогда 
		Объект.ВесТары1Вывоз = 0;
		Возврат;
	КонецЕсли;
	
	Если Объект.ВесТары1ВывозКонтроль = 0 тогда 
		Объект.ВесТары1ВывозКонтроль = Объект.ВесТары1Вывоз;
	КонецЕсли;

	ИНАГРО_КонтрольПроцессов.контрольИзмененияВесаТТН(Объект, Объект.ВесТары1ВывозКонтроль, Объект.ВесТары1Вывоз);

	РасчетВеса();
	
	УправлениеПоследовательностьюВводаВеса();	
	
	Объект.РедактировалсяВручнуюВесТары1Вывоз = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ВесБруттоПриИзменении(Элемент)
	
	ВесБруттоПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ВесБруттоПриИзмененииНаСервере()
	
	Если НЕ ИНАГРО_КонтрольПроцессов.ПроверитьМаксимальнуюГрузоподъемностьВесов(Объект.ВесБрутто) Тогда 
		Объект.ВесБрутто = 0;
		Возврат;
	КонецЕсли;	
	
	Если Объект.ВесБруттоКонтроль = 0 Тогда 
		Объект.ВесБруттоКонтроль = Объект.ВесБрутто;
	КонецЕсли;
	
	ИНАГРО_КонтрольПроцессов.КонтрольИзмененияВесаТТН(Объект, Объект.ВесБруттоКонтроль, Объект.ВесБрутто);

	РасчетВеса();
	
	УправлениеПоследовательностьюВводаВеса();	
	
	Объект.РедактировалсяВручнуюВесБрутто = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ВесБрутто1ПриИзменении(Элемент)
	
	ВесБрутто1ПриИзмененииНаСервере();

КонецПроцедуры

&НаСервере
Процедура ВесБрутто1ПриИзмененииНаСервере()
	
	Если НЕ ИНАГРО_КонтрольПроцессов.ПроверитьМаксимальнуюГрузоподъемностьВесов(Объект.ВесБрутто1) Тогда 
		Объект.ВесБрутто1 = 0;
		Возврат;
	КонецЕсли;	
	
	Если Объект.ВесБрутто1Контроль = 0 Тогда 
		Объект.ВесБрутто1Контроль = Объект.ВесБрутто1;
	КонецЕсли;
	
	ИНАГРО_КонтрольПроцессов.КонтрольИзмененияВесаТТН(Объект, Объект.ВесБрутто1Контроль, Объект.ВесБрутто1);

	РасчетВеса();
	
	УправлениеПоследовательностьюВводаВеса();	
	  	
	Объект.РедактировалсяВручнуюВесБрутто1 = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ВесТарыПриИзменении(Элемент)
	
	ВесТарыПриИзмененииНаСервере();

КонецПроцедуры

&НаСервере
Процедура ВесТарыПриИзмененииНаСервере()
	
	Если НЕ ИНАГРО_КонтрольПроцессов.ПроверитьМаксимальнуюГрузоподъемностьВесов(Объект.ВесТары) Тогда 
		Объект.ВесТары = 0;
		Возврат;
	КонецЕсли;
	
	Если НЕ ИНАГРО_КонтрольПроцессов.ПроверитьОтрицательноеНетто(Объект.ВесБрутто, Объект.ВесТары) Тогда 
		Объект.ВесТары = 0;
		Возврат;
	КонецЕсли;
	
	Если Объект.ВесТарыКонтроль = 0 тогда 
		Объект.ВесТарыКонтроль = Объект.ВесТары;
	КонецЕсли;

	ИНАГРО_КонтрольПроцессов.КонтрольИзмененияВесаТТН(Объект, Объект.ВесТарыКонтроль, Объект.ВесТары);

	РасчетВеса();
	
	УправлениеПоследовательностьюВводаВеса();
	
	Объект.РедактировалсяВручнуюВесТары = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ВесТары1ПриИзменении(Элемент)
	
	ВесТары1ПриИзмененииНаСервере();

КонецПроцедуры

&НаСервере
Процедура ВесТары1ПриИзмененииНаСервере()
	
	Если НЕ ИНАГРО_КонтрольПроцессов.ПроверитьМаксимальнуюГрузоподъемностьВесов(Объект.ВесТары1) Тогда 
		Объект.ВесТары1 = 0;
		Возврат;
	КонецЕсли;
	
	Если НЕ ИНАГРО_КонтрольПроцессов.ПроверитьОтрицательноеНетто(Объект.ВесБрутто1, Объект.ВесТары1) Тогда 
		Объект.ВесТары1 = 0;
		Возврат;
	КонецЕсли;	
	
	Если Объект.ВесТары1Контроль = 0 тогда 
		Объект.ВесТары1Контроль = Объект.ВесТары1;
	КонецЕсли;

	ИНАГРО_КонтрольПроцессов.КонтрольИзмененияВесаТТН(Объект, Объект.ВесТары1Контроль, Объект.ВесТары1);
	
	РасчетВеса();	
	
	УправлениеПоследовательностьюВводаВеса();	
	
	Объект.РедактировалсяВручнуюВесТары = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ТипЖурналаВывозПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ТипЖурналаВывоз) Тогда
		
		ДанныеОбъекта = Новый Структура("ВидЖурнала, ТипЖурналаВывоз, НомерЖурналаВывоз");		
		ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);		
	
		ТипЖурналаВывозПриИзмененииНаСервере(ДанныеОбъекта);
		
		Объект.НомерЖурналаВывоз = ДанныеОбъекта.НомерЖурналаВывоз;
		
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ТипЖурналаВывозПриИзмененииНаСервере(ДанныеОбъекта)
	
	Отбор = Новый Структура("ВидЖурнала, ТипЖурнала", ДанныеОбъекта.ВидЖурнала, ДанныеОбъекта.ТипЖурналаВывоз);
	
	ДанныеОбъекта.НомерЖурналаВывоз = РегистрыСведений.ИНАГРО_НумерацияЖурналовЭлеватора.Получить(Отбор).ТекущийНомерЖурнала + 1;

КонецПроцедуры

&НаКлиенте
Процедура ТипЖурналаВвозПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ТипЖурналаВвоз) Тогда
		
		ДанныеОбъекта = Новый Структура("ВидЖурнала, ТипЖурналаВвоз, НомерЖурналаВвоз");		
		ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);		
	
		ТипЖурналаВвозПриИзмененииНаСервере(ДанныеОбъекта);
		
		Объект.НомерЖурналаВвоз = ДанныеОбъекта.НомерЖурналаВвоз;
		
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ТипЖурналаВвозПриИзмененииНаСервере(ДанныеОбъекта)
	
	Отбор = Новый Структура("ВидЖурнала, ТипЖурнала", ДанныеОбъекта.ВидЖурнала, ДанныеОбъекта.ТипЖурналаВвоз);
		
	ДанныеОбъекта.НомерЖурналаВвоз = РегистрыСведений.ИНАГРО_НумерацияЖурналовЭлеватора.Получить(Отбор).ТекущийНомерЖурнала + 1;

КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ОписаниеДвиженияФормы36НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования,
		ЭтотОбъект,
		"Объект.ОписаниеДвиженияФормы36",
		НСтр("ru='Описание движения формы 36';uk='Опис руху форми 36'"));

КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования,
		ЭтотОбъект,
		"Объект.Комментарий");

КонецПроцедуры

&НаКлиенте
Процедура КомментарийВывозНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования,
		ЭтотОбъект,
		"Объект.КомментарийВывоз");

КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокНоменклатуры

&НаКлиенте
Процедура СписокНоменклатурыНоменклатураПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.СписокНоменклатуры.ТекущиеДанные;
	
	Если    Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ИНАГРО_ВидыОперацийТТНВнутр.Перемещение")
		ИЛИ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ИНАГРО_ВидыОперацийТТНВнутр.Перевзвешивание") Тогда
		ТекущиеДанные.НоваяНоменклатура = ТекущиеДанные.Номенклатура;
	КонецЕсли;
		
	ДанныеСтрокиТаблицы = Новый Структура("Номенклатура, СчетУчетаБУ, НовыйСчетУчетаБУ");
	ЗаполнитьЗначенияСвойств(ДанныеСтрокиТаблицы, ТекущиеДанные);
	
	ДанныеОбъекта = Новый Структура(
		"Дата, Организация, Владелец,
		|Склад, ОтражатьВБухгалтерскомУчете"); 
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);
	ДанныеОбъекта.Вставить("СобственноеПодразделение", СобственноеПодразделение);
	
	ТекущиеДанные.СчетУчетаБУ = ДанныеСтрокиТаблицы.СчетУчетаБУ;

КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыУрожайПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.СписокНоменклатуры.ТекущиеДанные;
	
	Если    Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ИНАГРО_ВидыОперацийТТНВнутр.Перемещение")
		ИЛИ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ИНАГРО_ВидыОперацийТТНВнутр.Перевзвешивание") Тогда
		ТекущиеДанные.НовыйУрожай = ТекущиеДанные.Урожай;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыВидФасовкиПриИзменении(Элемент)
	
	РасчетВесаДляСтрокиТабличнойЧасти();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыКоличествоПриИзменении(Элемент)
	
	РасчетВесаДляСтрокиТабличнойЧасти();

КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыВесПриИзменении(Элемент)
	
	РасчетВесаДляСтрокиТабличнойЧасти();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьЛабораторныйАнализ(Команда)
			
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Дата",               Объект.Дата);
	ЗначенияЗаполнения.Вставить("Ссылка",             ПредопределенноеЗначение("Документ.ИНАГРО_ЛабораторныйАнализ.ПустаяСсылка"));
	ЗначенияЗаполнения.Вставить("Организация",        Объект.Организация);
	ЗначенияЗаполнения.Вставить("Владелец",           Объект.Владелец);
	ЗначенияЗаполнения.Вставить("ДоговорКонтрагента", Объект.ДоговорКонтрагента);
	ЗначенияЗаполнения.Вставить("Склад",        	  Объект.Склад);
	ЗначенияЗаполнения.Вставить("ВидХранения",        Объект.ВидХранения);
	ЗначенияЗаполнения.Вставить("Урожай",             Объект.Урожай);
	ЗначенияЗаполнения.Вставить("Номенклатура",       Объект.Номенклатура);
	ЗначенияЗаполнения.Вставить("Влажность",          Объект.Влажность);
	ЗначенияЗаполнения.Вставить("СорнаяПримесь",      Объект.СорнаяПримесь);
	ЗначенияЗаполнения.Вставить("ЗерноваяПримесь",    Объект.ЗерноваяПримесь);	
	
	ЗначенияЗаполнения.Вставить("МассаПартии",        Объект.Вес);
	
	ЗначенияЗаполнения.Вставить("НомерТранспорта",    Объект.НомерТранспорта);
	
	ЗначенияЗаполнения.Вставить("Силос",              Объект.Силос);	
	
	ИНАГРО_ЭлеваторКлиент.СоздатьДокументНаОсновании(ЭтаФорма, ЗначенияЗаполнения);

КонецПроцедуры

&НаКлиенте
Процедура КнопкаF5(Команда)
	
	Если (НЕ ИНАГРО_ЭлеваторУправлениеПользователями.ЗапретитьАвтоматическиИзменятьВесТТН(ТекущийПользователь, Объект.Дата))
		ИЛИ (ИНАГРО_ЭлеваторУправлениеПользователями.ЗапретитьАвтоматическиИзменятьВесТТН(ТекущийПользователь, Объект.Дата) И Объект.ВесБруттоВывоз = 0) Тогда 	
	
	    ИмяНажатойКомандыПолученияВеса = Команда.Имя;
		ОповещениеПолученияВеса = Новый ОписаниеОповещения("ЗавершениеПолученияВеса", ЭтаФорма);
		
		ИНАГРО_МенеджерОборудованияКлиент.НачатьПолученияВесаСЭлектронныхВесов(ОповещениеПолученияВеса, ЭтаФорма.УникальныйИдентификатор);		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура КнопкаF6(Команда)
	
	Если (НЕ ИНАГРО_ЭлеваторУправлениеПользователями.ЗапретитьАвтоматическиИзменятьВесТТН(ТекущийПользователь, Объект.Дата))
		ИЛИ (ИНАГРО_ЭлеваторУправлениеПользователями.ЗапретитьАвтоматическиИзменятьВесТТН(ТекущийПользователь, Объект.Дата) И Объект.ВесТарыВывоз = 0) Тогда 	
	
	    ИмяНажатойКомандыПолученияВеса = Команда.Имя;
		ОповещениеПолученияВеса = Новый ОписаниеОповещения("ЗавершениеПолученияВеса", ЭтаФорма);
		
		ИНАГРО_МенеджерОборудованияКлиент.НачатьПолученияВесаСЭлектронныхВесов(ОповещениеПолученияВеса, ЭтаФорма.УникальныйИдентификатор);		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаF7(Команда)
	
	Если (НЕ ИНАГРО_ЭлеваторУправлениеПользователями.ЗапретитьАвтоматическиИзменятьВесТТН(ТекущийПользователь, Объект.Дата))
		ИЛИ (ИНАГРО_ЭлеваторУправлениеПользователями.ЗапретитьАвтоматическиИзменятьВесТТН(ТекущийПользователь, Объект.Дата) И Объект.ВесБрутто1Вывоз = 0) Тогда 	
	
	    ИмяНажатойКомандыПолученияВеса = Команда.Имя;
		ОповещениеПолученияВеса = Новый ОписаниеОповещения("ЗавершениеПолученияВеса", ЭтаФорма);
		
		ИНАГРО_МенеджерОборудованияКлиент.НачатьПолученияВесаСЭлектронныхВесов(ОповещениеПолученияВеса, ЭтаФорма.УникальныйИдентификатор);		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаF8(Команда)
	
	Если (НЕ ИНАГРО_ЭлеваторУправлениеПользователями.ЗапретитьАвтоматическиИзменятьВесТТН(ТекущийПользователь, Объект.Дата))
		ИЛИ (ИНАГРО_ЭлеваторУправлениеПользователями.ЗапретитьАвтоматическиИзменятьВесТТН(ТекущийПользователь, Объект.Дата) И Объект.ВесТары1Вывоз = 0) Тогда 	
	
	    ИмяНажатойКомандыПолученияВеса = Команда.Имя;
		ОповещениеПолученияВеса = Новый ОписаниеОповещения("ЗавершениеПолученияВеса", ЭтаФорма);
		
		ИНАГРО_МенеджерОборудованияКлиент.НачатьПолученияВесаСЭлектронныхВесов(ОповещениеПолученияВеса, ЭтаФорма.УникальныйИдентификатор);		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаF9(Команда)
	
	Если (НЕ ИНАГРО_ЭлеваторУправлениеПользователями.ЗапретитьАвтоматическиИзменятьВесТТН(ТекущийПользователь, Объект.Дата))
		ИЛИ (ИНАГРО_ЭлеваторУправлениеПользователями.ЗапретитьАвтоматическиИзменятьВесТТН(ТекущийПользователь, Объект.Дата) И Объект.ВесБрутто = 0) Тогда 	
	
	    ИмяНажатойКомандыПолученияВеса = Команда.Имя;
		ОповещениеПолученияВеса = Новый ОписаниеОповещения("ЗавершениеПолученияВеса", ЭтаФорма);
		
		ИНАГРО_МенеджерОборудованияКлиент.НачатьПолученияВесаСЭлектронныхВесов(ОповещениеПолученияВеса, ЭтаФорма.УникальныйИдентификатор);		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура КнопкаF10(Команда)
	
	Если (НЕ ИНАГРО_ЭлеваторУправлениеПользователями.ЗапретитьАвтоматическиИзменятьВесТТН(ТекущийПользователь, Объект.Дата))
		ИЛИ (ИНАГРО_ЭлеваторУправлениеПользователями.ЗапретитьАвтоматическиИзменятьВесТТН(ТекущийПользователь, Объект.Дата) И Объект.ВесТары = 0) Тогда 	
	
	    ИмяНажатойКомандыПолученияВеса = Команда.Имя;
		ОповещениеПолученияВеса = Новый ОписаниеОповещения("ЗавершениеПолученияВеса", ЭтаФорма);
		
		ИНАГРО_МенеджерОборудованияКлиент.НачатьПолученияВесаСЭлектронныхВесов(ОповещениеПолученияВеса, ЭтаФорма.УникальныйИдентификатор);		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаF11(Команда)
	
	Если (НЕ ИНАГРО_ЭлеваторУправлениеПользователями.ЗапретитьАвтоматическиИзменятьВесТТН(ТекущийПользователь, Объект.Дата))
		ИЛИ (ИНАГРО_ЭлеваторУправлениеПользователями.ЗапретитьАвтоматическиИзменятьВесТТН(ТекущийПользователь, Объект.Дата) И Объект.ВесБрутто1 = 0) Тогда 	
	
	    ИмяНажатойКомандыПолученияВеса = Команда.Имя;
		ОповещениеПолученияВеса = Новый ОписаниеОповещения("ЗавершениеПолученияВеса", ЭтаФорма);
		
		ИНАГРО_МенеджерОборудованияКлиент.НачатьПолученияВесаСЭлектронныхВесов(ОповещениеПолученияВеса, ЭтаФорма.УникальныйИдентификатор);		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаF12(Команда)
	
	Если (НЕ ИНАГРО_ЭлеваторУправлениеПользователями.ЗапретитьАвтоматическиИзменятьВесТТН(ТекущийПользователь, Объект.Дата))
		ИЛИ (ИНАГРО_ЭлеваторУправлениеПользователями.ЗапретитьАвтоматическиИзменятьВесТТН(ТекущийПользователь, Объект.Дата) И Объект.ВесТары1 = 0) Тогда 	
	
	    ИмяНажатойКомандыПолученияВеса = Команда.Имя;
		ОповещениеПолученияВеса = Новый ОписаниеОповещения("ЗавершениеПолученияВеса", ЭтаФорма);
		
		ИНАГРО_МенеджерОборудованияКлиент.НачатьПолученияВесаСЭлектронныхВесов(ОповещениеПолученияВеса, ЭтаФорма.УникальныйИдентификатор);		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВъездВывоз(Команда)
	
	Объект.ВремяПрибытияВывоз = ТекущаяДата();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыездВывоз(Команда)
	
	Объект.ВремяВыбытияВывоз = ТекущаяДата();	
	
КонецПроцедуры

&НаКлиенте
Процедура Въезд(Команда)
	
	Объект.ВремяПрибытия = ТекущаяДата();

КонецПроцедуры

&НаКлиенте
Процедура Выезд(Команда)
	
	Объект.ВремяВыбытия = ТекущаяДата();	

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьФормуНаСервере()

	ТекущаяДатаДокумента = Объект.Дата;	

	УстановитьФункциональныеОпцииФормы();
	
	Если ДополнительныйКонтрольДоговоров Тогда
		Элементы.Владелец.КнопкаСоздания        = Ложь;
		Элементы.Владелец.ИсторияВыбораПриВводе = ИсторияВыбораПриВводе.НеИспользовать;
	КонецЕсли;

	УстановитьЗаголовокФормы();
	
	УстановитьТипыДанныхРеквизитов();

	УправлениеФормойНаСервере();
	
	УстановитьСостояниеДокумента();
	
КонецПроцедуры 

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы()

	ОбщегоНазначенияБПКлиентСервер.УстановитьПараметрыФункциональныхОпцийФормыДокумента(ЭтаФорма);	
	
	ТекущийПользователь                         = Пользователи.ТекущийПользователь();	
	
	ПараметрыУчетаЭлеватора                     = ИНАГРО_Элеватор.ПолучитьПараметрыУчетаЭлеватора(Объект.Дата);	
	ВестиСправочникАвтомобилей                  = ПараметрыУчетаЭлеватора.ВестиСправочникАвтомобилей; 
	ВестиСправочникВодителей                    = ПараметрыУчетаЭлеватора.ВестиСправочникВодителей;
	ИнтерактивноеФормированиеРеестровТТН        = ПараметрыУчетаЭлеватора.ИнтерактивноеФормированиеРеестровТТН;	
	ДополнительныйКонтрольДоговоров             = ПараметрыУчетаЭлеватора.ДополнительныйКонтрольДоговоров;
	ЛабораторияРаботаетВСистеме                 = ПараметрыУчетаЭлеватора.ЛабораторияРаботаетВСистеме;
	КонтролироватьПоследовательностьВводаВеса   = ПараметрыУчетаЭлеватора.КонтролироватьПоследовательностьВводаВеса;	
	РучноеУправлениеЗачетнымВесом               = ПараметрыУчетаЭлеватора.РучноеУправлениеЗачетнымВесом;
	
	СобственноеПодразделение                    = ИНАГРО_Элеватор.ПолучитьДополнительныйРеквизитКонтрагента(Объект.Владелец, "СобственноеПодразделение");	
	ВидимостьМестаХранения                      = ИНАГРО_Элеватор.ПолучитьДополнительныйРеквизитСклада(Объект.Склад,      "ВестиУчетПоМестамХранения");
	ВидимостьНовогоМестаХранения                = ИНАГРО_Элеватор.ПолучитьДополнительныйРеквизитСклада(Объект.НовыйСклад, "ВестиУчетПоМестамХранения");
	
КонецПроцедуры 

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)

	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;	
	
	Если  Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ИНАГРО_ВидыОперацийТТНВнутр.ПеремещениеФасованного")
		И НЕ Форма.СобственноеПодразделение Тогда
		Элементы.ОтражатьВБухгалтерскомУчете.Доступность = Истина;
	Иначе	
		Элементы.ОтражатьВБухгалтерскомУчете.Доступность = Ложь;
	КонецЕсли; 	
	
	Элементы.ГруппаСписокКультур.Видимость = Ложь;
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ИНАГРО_ВидыОперацийТТНВнутр.Перемещение") Тогда		
		
		Элементы.Номенклатура.Доступность      = Истина;		
		Элементы.НоваяНоменклатура.Доступность = Ложь;
		Элементы.Урожай.Доступность            = Истина;
		Элементы.НовыйУрожай.Доступность       = Ложь;		
		
		УстановитьДоступностьКачества(Элементы, Истина);
		
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ИНАГРО_ВидыОперацийТТНВнутр.ПеремещениеФасованного") Тогда	
		
		Элементы.Номенклатура.Доступность      = Ложь;		
		Элементы.НоваяНоменклатура.Доступность = Ложь;
		Элементы.Урожай.Доступность            = Ложь;		
		Элементы.НовыйУрожай.Доступность       = Ложь;
		
		Элементы.ГруппаСписокКультур.Видимость = Истина;					

		УстановитьДоступностьКачества(Элементы, Ложь);	
		
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ИНАГРО_ВидыОперацийТТНВнутр.Пересортица") Тогда
		
		Элементы.Номенклатура.Доступность      = Истина;		
		Элементы.НоваяНоменклатура.Доступность = Истина;		
		Элементы.Урожай.Доступность            = Истина;		
		Элементы.НовыйУрожай.Доступность       = Истина;		
		
		УстановитьДоступностьКачества(Элементы, Истина);
		
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ИНАГРО_ВидыОперацийТТНВнутр.Перевзвешивание") Тогда
		
		Элементы.Номенклатура.Доступность      = Истина;
		Элементы.НоваяНоменклатура.Доступность = Ложь;		
		Элементы.Урожай.Доступность            = Истина;
		Элементы.НовыйУрожай.Доступность       = Ложь;	
		
		УстановитьДоступностьКачества(Элементы, Истина); 		
		
	КонецЕсли;
	
	Элементы.СписокНоменклатурыСчетУчетаБУ.Видимость      = Объект.ОтражатьВБухгалтерскомУчете;
	Элементы.СписокНоменклатурыНовыйСчетУчетаБУ.Видимость = Объект.ОтражатьВБухгалтерскомУчете;

	Элементы.ЛабораторныйАнализ.Видимость   = Форма.ЛабораторияРаботаетВСистеме;	
	
	УстановитьДоступностьПоказателейКачества(Форма);
	
	Элементы.ДоговорКонтрагента.Доступность = НЕ Форма.ДополнительныйКонтрольДоговоров;	
	Элементы.МестоХранения.Видимость	    = Форма.ВидимостьМестаХранения;
	Элементы.НовоеМестоХранения.Видимость   = Форма.ВидимостьНовогоМестаХранения;	
	Элементы.ЗачетныйВес.Доступность        = Форма.РучноеУправлениеЗачетнымВесом;
	Элементы.ЗачетныйВесВывоз.Доступность   = Форма.РучноеУправлениеЗачетнымВесом;
	
	ЕстьПриказНаВывоз = ЗначениеЗаполнено(Объект.ПриказНаПеремещение);
	
	Элементы.Владелец.Доступность           = НЕ ЕстьПриказНаВывоз;	
	Элементы.ДоговорКонтрагента.Доступность	= НЕ ЕстьПриказНаВывоз;
	Элементы.Склад.Доступность              = НЕ ЕстьПриказНаВывоз;	
	Элементы.МестоХранения.Доступность      = НЕ ЕстьПриказНаВывоз;
	Элементы.Урожай.Доступность             = НЕ ЕстьПриказНаВывоз;
				
КонецПроцедуры

&НаСервере
Процедура УправлениеФормойНаСервере()
	
	ИНАГРО_ЭлеваторЗаполнениеДокументов.ЗаполнитьДополнительныеРеквизиты(ЭтаФорма);	
	
	УправлениеФормой(ЭтаФорма);
	
	УправлениеПоследовательностьюВводаВеса();
				
КонецПроцедуры 

&НаСервере
Процедура УстановитьПараметрыВыбораВидОперации()
	
	МассивВидовОпераций = Документы.ИНАГРО_ТТНВнутр.ПолучитьФиксированныйМассивВидовОпераций();
	
	ПараметрМассивВидовОпераций = Новый ПараметрВыбора("Отбор.Ссылка", МассивВидовОпераций);
	
	НовыйМассив = Новый Массив();
	НовыйМассив.Добавить(ПараметрМассивВидовОпераций);
	НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);
	
	Элементы.ВидОперации.ПараметрыВыбора = НовыеПараметры;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовокФормы()
	
	ОбъектФормы = ЭтаФорма.Объект;

	ТекстЗаголовка = НСтр("ru='Товарно-транспортная накладная (внутренняя)';uk='Товарно-транспортна накладна (внутрішня)'");
	
	Если ЗначениеЗаполнено(ОбъектФормы.Ссылка) Тогда
		ТекстЗаголовка = ТекстЗаголовка + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru=' %1 от %2';uk=' %1 від %2'"), ОбъектФормы.Номер, ОбъектФормы.Дата);
	Иначе
		ТекстЗаголовка = ТекстЗаголовка + НСтр("ru=' (создание)';uk=' (створення)'");
	КонецЕсли;
	
	ЭтаФорма.Заголовок = ТекстЗаголовка + " (" + Строка(ОбъектФормы.ВидОперации) + ")";

КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияБП.СостояниеДокумента(Объект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТипыДанныхРеквизитов()
	
	// Водитель 
	
	Если ВестиСправочникВодителей Тогда
		
		ТипДанных = "СправочникСсылка.ИНАГРО_Водители";

		Элементы.Водитель.КнопкаВыпадающегоСписка        = Неопределено;
		Элементы.Водитель.КнопкаВыбора                   = Неопределено;
		Элементы.Водитель.КнопкаОчистки                  = Неопределено;
		Элементы.Водитель.КнопкаСоздания                 = Неопределено;
		Элементы.Водитель.ИсторияВыбораПриВводе          = ИсторияВыбораПриВводе.Авто;

						
	Иначе
		
		ТипДанных = "Строка";

		Элементы.Водитель.КнопкаВыпадающегоСписка        = Ложь;
		Элементы.Водитель.КнопкаВыбора                   = Ложь;
		Элементы.Водитель.КнопкаОчистки                  = Истина;
		Элементы.Водитель.КнопкаСоздания                 = Ложь;
		Элементы.Водитель.ИсторияВыбораПриВводе          = ИсторияВыбораПриВводе.НеИспользовать;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.Водитель) Тогда
		Элементы.Водитель.ОграничениеТипа        = Новый ОписаниеТипов(ТипДанных);					
		Элементы.Водитель.ВыбиратьТип            = Ложь;
	КонецЕсли;

	// Автомобиль
	
	Если ВестиСправочникАвтомобилей Тогда
		
		ТипДанных = "СправочникСсылка.ИНАГРО_ТранспортныеСредства";
		
		Элементы.НомерТранспорта.КнопкаВыпадающегоСписка = Неопределено;
		Элементы.НомерТранспорта.КнопкаВыбора            = Неопределено;		
		Элементы.НомерТранспорта.КнопкаОчистки           = Неопределено;
		Элементы.НомерТранспорта.КнопкаСоздания          = Неопределено;
		Элементы.НомерТранспорта.ИсторияВыбораПриВводе   = ИсторияВыбораПриВводе.Авто;
		
		Элементы.НомерПрицепа.КнопкаВыпадающегоСписка    = Неопределено;
		Элементы.НомерПрицепа.КнопкаВыбора               = Неопределено;
		Элементы.НомерПрицепа.КнопкаОчистки              = Неопределено;						
		Элементы.НомерПрицепа.КнопкаСоздания             = Неопределено;
		Элементы.НомерПрицепа.ИсторияВыбораПриВводе      = ИсторияВыбораПриВводе.Авто;
		
	Иначе
		
		ТипДанных = "Строка";
		
		Элементы.НомерТранспорта.КнопкаВыпадающегоСписка = Ложь;
		Элементы.НомерТранспорта.КнопкаВыбора            = Ложь;		
		Элементы.НомерТранспорта.КнопкаОчистки           = Истина;
		Элементы.НомерТранспорта.КнопкаСоздания          = Ложь;
		Элементы.НомерТранспорта.ИсторияВыбораПриВводе   = ИсторияВыбораПриВводе.НеИспользовать;
		
		Элементы.НомерПрицепа.КнопкаВыпадающегоСписка    = Ложь;
		Элементы.НомерПрицепа.КнопкаВыбора               = Ложь;
		Элементы.НомерПрицепа.КнопкаОчистки              = Истина;						
		Элементы.НомерПрицепа.КнопкаСоздания             = Ложь;
		Элементы.НомерПрицепа.ИсторияВыбораПриВводе      = ИсторияВыбораПриВводе.НеИспользовать; 		
				
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(Объект.НомерТранспорта) Тогда
		Элементы.НомерТранспорта.ОграничениеТипа = Новый ОписаниеТипов(ТипДанных);				
		Элементы.НомерТранспорта.ВыбиратьТип     = Ложь;			 				
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.НомерПрицепа) Тогда				
		Элементы.НомерПрицепа.ОграничениеТипа    = Новый ОписаниеТипов(ТипДанных);
		Элементы.НомерПрицепа.ВыбиратьТип        = Ложь					
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьКачества(Элементы, Доступность)
	
	Элементы.ЛабораторныйАнализ.Доступность = Доступность;
	Элементы.НомерАнализа.Доступность       = Доступность;
	Элементы.Влажность.Доступность          = Доступность;
	Элементы.СорнаяПримесь.Доступность      = Доступность;
	Элементы.ЗерноваяПримесь.Доступность    = Доступность;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьПоказателейКачества(Форма)
	
	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;	
	
	СписокРеквизитов = Новый Структура("Влажность, СорнаяПримесь, ЗерноваяПримесь");	
	ИНАГРО_ЭлеваторКлиентСервер.УстановитьДоступностьРеквизитовПоНастройке(Форма, СписокРеквизитов, Форма.ЛабораторияРаботаетВСистеме, ЗначениеЗаполнено(Объект.ЛабораторныйАнализ));

КонецПроцедуры

&НаСервере
Процедура УправлениеПоследовательностьюВводаВеса()	
		
	Если НЕ Объект.Проведен Тогда
		
		Если КонтролироватьПоследовательностьВводаВеса Тогда
			
			// Вывоз
			
			////// Авто
			Если ЗначениеЗаполнено(Объект.НомерПрицепа) Тогда
				ДоступностьКнопкиF5     = Объект.ВесТарыВывоз <> 0 И Объект.ВесБруттоВывоз = 0 И Объект.ВесБрутто1Вывоз = 0 ;
				ДоступностьКнопкиF6     = Объект.ВесТарыВывоз = 0;
			Иначе
				ДоступностьКнопкиF5     = Объект.ВесТарыВывоз <> 0 И Объект.ВесБруттоВывоз = 0; 
				ДоступностьКнопкиF6     = Объект.ВесТарыВывоз = 0;
			КонецЕсли;	  
			
			////// Прицеп						  
			Если ЗначениеЗаполнено(Объект.НомерПрицепа) Тогда
				ДоступностьКнопкиF7     =  Объект.ВесТарыВывоз <> 0 И Объект.ВесТары1Вывоз <> 0 И Объект.ВесБруттоВывоз <> 0 И Объект.ВесБрутто1Вывоз = 0;
				ДоступностьКнопкиF8     =  Объект.ВесБруттоВывоз = 0 И Объект.ВесБрутто1Вывоз = 0 И Объект.ВесТарыВывоз <> 0 И Объект.ВесТары1Вывоз = 0;
			Иначе
				ДоступностьКнопкиF7     =  Ложь;
				ДоступностьКнопкиF8     =  Ложь;
			КонецЕсли;	  
			
			НедоступностьВсехКнопокВывоз = (Объект.ВесБруттоВывоз <> 0 И Объект.ВесТарыВывоз <> 0   И Объект.ВесБрутто1Вывоз <> 0 И Объект.ВесТары1Вывоз <> 0)
									   ИЛИ (Объект.ВесБруттоВывоз <> 0 И Объект.ВесТарыВывоз <> 0   И Объект.ВесТары1Вывоз = 0);
			Элементы.КнопкаF5.Доступность  = ДоступностьКнопкиF5 И НЕ НедоступностьВсехКнопокВывоз;
			Элементы.КнопкаF6.Доступность  = ДоступностьКнопкиF6 И НЕ ДоступностьКнопкиF5 И НЕ НедоступностьВсехКнопокВывоз;
			Элементы.КнопкаF7.Доступность  = ДоступностьКнопкиF7 И НЕ НедоступностьВсехКнопокВывоз;
			Элементы.КнопкаF8.Доступность  = ДоступностьКнопкиF8 И НЕ НедоступностьВсехКнопокВывоз;
			
			Если ИНАГРО_ЭлеваторУправлениеПользователями.ЗапретитьВручнуюИзменятьВесТТН(ТекущийПользователь) Тогда
				Элементы.ВесБруттоВывоз.Доступность  = Ложь;
				Элементы.ВесБрутто1Вывоз.Доступность = Ложь;
				Элементы.ВесТарыВывоз.Доступность    = Ложь;
				Элементы.ВесТары1Вывоз.Доступность   = Ложь;
			Иначе
				Элементы.ВесБруттоВывоз.Доступность  = Элементы.КнопкаF5.Доступность ИЛИ Элементы.КнопкаF8.Доступность;
				Элементы.ВесБрутто1Вывоз.Доступность = Элементы.КнопкаF7.Доступность;
				Элементы.ВесТарыВывоз.Доступность    = Элементы.КнопкаF6.Доступность;
				Элементы.ВесТары1Вывоз.Доступность   = Элементы.КнопкаF8.Доступность;
			КонецЕсли;
			
			// Ввоз
			
			////// Авто
			Если ЗначениеЗаполнено(Объект.НомерПрицепа) Тогда
				ДоступностьКнопкиF9     =  Объект.ВесБрутто = 0 И НедоступностьВсехКнопокВывоз;
				ДоступностьКнопкиF10     = Объект.ВесБрутто <> 0 И Объект.ВесТары = 0 И Объект.ВесТары1 = 0;
			Иначе
				ДоступностьКнопкиF9     =  Объект.ВесБрутто = 0;
				ДоступностьКнопкиF10     = Объект.ВесБрутто <> 0 И Объект.ВесТары = 0;
			КонецЕсли;	  
			
			////// Прицеп						  
			Если ЗначениеЗаполнено(Объект.НомерПрицепа) Тогда
				ДоступностьКнопкиF11     =  Объект.ВесБрутто <> 0 И Объект.ВесБрутто1 = 0 И Объект.ВесТары = 0 И Объект.ВесТары1 = 0;
				ДоступностьКнопкиF12     =  Объект.ВесБрутто <> 0 И Объект.ВесТары <> 0 И Объект.ВесБрутто1 <> 0;
			Иначе
				ДоступностьКнопкиF11     =  Ложь;
				ДоступностьКнопкиF12     =  Ложь;
			КонецЕсли;	  
			
			НедоступностьВсехКнопокВвоз  = (Объект.ВесБрутто <> 0 И Объект.ВесТары <> 0 И Объект.ВесБрутто1 <> 0 И Объект.ВесТары1 <> 0)
			ИЛИ (Объект.ВесБрутто <> 0 И Объект.ВесТары <> 0 И Объект.ВесТары1 = 0 И НЕ ЗначениеЗаполнено(Объект.НомерПрицепа));
			
			Элементы.КнопкаF9.Доступность  = ДоступностьКнопкиF9  И НЕ НедоступностьВсехКнопокВвоз;
			Элементы.КнопкаF10.Доступность = ДоступностьКнопкиF10 И НЕ НедоступностьВсехКнопокВвоз;
			Элементы.КнопкаF11.Доступность = ДоступностьКнопкиF11 И НЕ НедоступностьВсехКнопокВвоз;
			Элементы.КнопкаF12.Доступность = ДоступностьКнопкиF12 И НЕ НедоступностьВсехКнопокВвоз;
			
			Если ИНАГРО_ЭлеваторУправлениеПользователями.ЗапретитьВручнуюИзменятьВесТТН(ТекущийПользователь) Тогда
				Элементы.ВесБрутто.Доступность       = Ложь;
				Элементы.ВесБрутто1.Доступность      = Ложь;
				Элементы.ВесТары.Доступность         = Ложь;
				Элементы.ВесТары1.Доступность        = Ложь;
			Иначе
				Элементы.ВесБрутто.Доступность       = Элементы.КнопкаF9.Доступность;
				Элементы.ВесБрутто1.Доступность      = Элементы.КнопкаF11.Доступность;
				Элементы.ВесТары.Доступность         = Элементы.КнопкаF10.Доступность;
				Элементы.ВесТары1.Доступность        = Элементы.КнопкаF12.Доступность;
			КонецЕсли;
			
			Если Объект.ВесБруттоВывоз = 0 Тогда 
				Элементы.ГруппаВесоваяВвоз.Видимость = Ложь;
			Иначе
				Элементы.ГруппаВесоваяВвоз.Видимость = Истина;
			КонецЕсли;
			
		КонецЕсли;
	Иначе	
		Элементы.ВесБруттоВывоз.Доступность  = Объект.Проведен;
		Элементы.ВесБрутто1Вывоз.Доступность = Объект.Проведен;
		Элементы.ВесТарыВывоз.Доступность    = Объект.Проведен;
		Элементы.ВесТары1Вывоз.Доступность   = Объект.Проведен;
		
		Элементы.ВесБрутто.Доступность       = Объект.Проведен;
		Элементы.ВесБрутто1.Доступность      = Объект.Проведен;
		Элементы.ВесТары.Доступность         = Объект.Проведен;
		Элементы.ВесТары1.Доступность        = Объект.Проведен;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеПолученияВеса(Результат, Параметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Если ИмяНажатойКомандыПолученияВеса = "КнопкаF5" Тогда			
			Объект.ВесБруттоВывоз = Результат;
			ЗавершениеПолученияВесаНаСервере();			
		ИначеЕсли ИмяНажатойКомандыПолученияВеса = "КнопкаF6" Тогда			
			Объект.ВесТарыВывоз = Результат;
			ЗавершениеПолученияВесаНаСервере();			
		ИначеЕсли ИмяНажатойКомандыПолученияВеса = "КнопкаF7" Тогда			
			Объект.ВесБрутто1Вывоз = Результат;
			ЗавершениеПолученияВесаНаСервере();			
		ИначеЕсли ИмяНажатойКомандыПолученияВеса = "КнопкаF8" Тогда			
			Объект.ВесТары1Вывоз = Результат;
			ЗавершениеПолученияВесаНаСервере();			
		ИначеЕсли ИмяНажатойКомандыПолученияВеса = "КнопкаF9" Тогда			
			Объект.ВесБрутто = Результат;
			ЗавершениеПолученияВесаНаСервере();			
		ИначеЕсли ИмяНажатойКомандыПолученияВеса = "КнопкаF10" Тогда			
			Объект.ВесТары = Результат;
			ЗавершениеПолученияВесаНаСервере();			
		ИначеЕсли ИмяНажатойКомандыПолученияВеса = "КнопкаF11" Тогда			
			Объект.ВесБрутто1 = Результат;
			ЗавершениеПолученияВесаНаСервере();			
		ИначеЕсли ИмяНажатойКомандыПолученияВеса = "КнопкаF12" Тогда			
			Объект.ВесТары1 = Результат;
			ЗавершениеПолученияВесаНаСервере();			
		КонецЕсли;	
	КонецЕсли;
	
	РасчетВеса();
	УправлениеПоследовательностьюВводаВеса();
	
	ИмяНажатойКомандыПолученияВеса = Неопределено;
	
КонецПроцедуры

&НаСервере
Процедура ЗавершениеПолученияВесаНаСервере()
	
	Если ИмяНажатойКомандыПолученияВеса = "КнопкаF5" Тогда
		
		Если НЕ ИНАГРО_КонтрольПроцессов.ПроверитьОтрицательноеНетто(Объект.ВесБруттоВывоз, Объект.ВесТарыВывоз) Тогда 
			Объект.ВесБруттоВывоз = 0;
			Возврат;
		КонецЕсли;
		
		Если Объект.ВесБруттоВывозКонтроль = 0 Тогда 
			Объект.ВесБруттоВывозКонтроль = Объект.ВесБруттоВывоз;
		КонецЕсли;
		
		ИНАГРО_КонтрольПроцессов.КонтрольИзмененияВесаТТН(Объект, Объект.ВесБруттоВывозКонтроль, Объект.ВесБруттоВывоз);
	
	ИначеЕсли ИмяНажатойКомандыПолученияВеса = "КнопкаF6" Тогда
		
		Если Объект.ВесТарыВывозКонтроль = 0 Тогда 
			Объект.ВесТарыВывозКонтроль = Объект.ВесТарыВывоз;
		КонецЕсли;
		
		ИНАГРО_КонтрольПроцессов.КонтрольИзмененияВесаТТН(Объект, Объект.ВесТарыВывозКонтроль, Объект.ВесТарыВывоз);
		
	ИначеЕсли ИмяНажатойКомандыПолученияВеса = "КнопкаF7" Тогда
		
		Если НЕ ИНАГРО_КонтрольПроцессов.ПроверитьОтрицательноеНетто(Объект.ВесБрутто1Вывоз, Объект.ВесТары1Вывоз) Тогда 
			Объект.ВесБрутто1Вывоз = 0;
			Возврат;
		КонецЕсли;
		
		Если Объект.ВесБрутто1ВывозКонтроль = 0 тогда 
			Объект.ВесБрутто1ВывозКонтроль = Объект.ВесБрутто1Вывоз;
		КонецЕсли;
		
		ИНАГРО_КонтрольПроцессов.КонтрольИзмененияВесаТТН(Объект, Объект.ВесБрутто1ВывозКонтроль, Объект.ВесБрутто1Вывоз);

	ИначеЕсли ИмяНажатойКомандыПолученияВеса = "КнопкаF8" Тогда
		
		Если Объект.ВесТары1ВывозКонтроль = 0 тогда 
			Объект.ВесТары1ВывозКонтроль = Объект.ВесТары1Вывоз;
		КонецЕсли;
		
		ИНАГРО_КонтрольПроцессов.КонтрольИзмененияВесаТТН(Объект, Объект.ВесТары1ВывозКонтроль, Объект.ВесТары1Вывоз);
		
	ИначеЕсли ИмяНажатойКомандыПолученияВеса = "КнопкаF9" Тогда
		
		Если Объект.ВесБруттоКонтроль = 0 Тогда 
			Объект.ВесБруттоКонтроль = Объект.ВесБрутто;
		КонецЕсли;
		
		ИНАГРО_КонтрольПроцессов.КонтрольИзмененияВесаТТН(Объект, Объект.ВесБруттоКонтроль, Объект.ВесБрутто);	
		
	ИначеЕсли ИмяНажатойКомандыПолученияВеса = "КнопкаF10" Тогда
		
		Если НЕ ИНАГРО_КонтрольПроцессов.ПроверитьОтрицательноеНетто(Объект.ВесБрутто, Объект.ВесТары) Тогда 
			Объект.ВесТары = 0;
			Возврат;
		КонецЕсли;
		
		Если Объект.ВесТарыКонтроль = 0 Тогда 
			Объект.ВесТарыКонтроль = Объект.ВесТары;
		КонецЕсли;
		
		ИНАГРО_КонтрольПроцессов.КонтрольИзмененияВесаТТН(Объект, Объект.ВесТарыКонтроль, Объект.ВесТары);	
		
	ИначеЕсли ИмяНажатойКомандыПолученияВеса = "КнопкаF11" Тогда
		
		Если Объект.ВесБрутто1Контроль = 0 тогда 
			Объект.ВесБрутто1Контроль = Объект.ВесБрутто1;
		КонецЕсли;
		
		ИНАГРО_КонтрольПроцессов.КонтрольИзмененияВесаТТН(Объект, Объект.ВесБрутто1Контроль, Объект.ВесБрутто1);	
		
	ИначеЕсли ИмяНажатойКомандыПолученияВеса = "КнопкаF12" Тогда	
		
		Если НЕ ИНАГРО_КонтрольПроцессов.ПроверитьОтрицательноеНетто(Объект.ВесБрутто1, Объект.ВесТары1) Тогда 
			Объект.ВесТары1 = 0;
			Возврат;
		КонецЕсли;
		
		Если Объект.ВесТары1Контроль = 0 Тогда 
			Объект.ВесТары1Контроль = Объект.ВесТары1;
		КонецЕсли;
		
		ИНАГРО_КонтрольПроцессов.КонтрольИзмененияВесаТТН(Объект, Объект.ВесТары1Контроль, Объект.ВесТары1);	
		
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура РасчетВеса(СтрокаТабличнойЧасти = Неопределено)	
	
	Объект.ВесНетто       = Объект.ВесБрутто - Объект.ВесТары;
	Объект.ВесНетто1      = Объект.ВесБрутто1 - Объект.ВесТары1;
	
	Объект.ВесНеттоВывоз  = Объект.ВесБруттоВывоз - Объект.ВесТарыВывоз;
	Объект.ВесНетто1Вывоз = Объект.ВесБрутто1Вывоз - Объект.ВесТары1Вывоз;
	
	Если Объект.ВидОперации = Перечисления.ИНАГРО_ВидыОперацийТТНВнутр.ПеремещениеФасованного Тогда
		
		Если СтрокаТабличнойЧасти <> Неопределено И ЗначениеЗаполнено(СтрокаТабличнойЧасти.ВидФасовки) Тогда			 	
			СтрокаТабличнойЧасти.Вес = СтрокаТабличнойЧасти.Количество * СтрокаТабличнойЧасти.ВидФасовки.Вес;		
		КонецЕсли;
		
		ПересчитатьИтоги();	
		
	Иначе
		
		Объект.Вес      = Объект.ВесНетто + Объект.ВесНетто1;
		Объект.ВесВывоз = Объект.ВесНеттоВывоз + Объект.ВесНетто1Вывоз;
				
		РасчетЗачетногоВеса();
		
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура РасчетЗачетногоВеса()
	
	ПараметрыДляРасчетаЗачетногоВеса      = ПолучитьПараметрыДляРасчетаЗачетногоВеса();
	
	ПараметрыДляРасчетаЗачетногоВесаВывоз = ПолучитьПараметрыДляРасчетаЗачетногоВеса();	
	ПараметрыДляРасчетаЗачетногоВесаВывоз.Вставить("ФизическийВес", Объект.ВесВывоз);
	
	Объект.ЗачетныйВес      = ИНАГРО_Элеватор.ЗачетныйВесПриПроведении(ПараметрыДляРасчетаЗачетногоВеса);
	Объект.ЗачетныйВесВывоз = ИНАГРО_Элеватор.ЗачетныйВесПриПроведении(ПараметрыДляРасчетаЗачетногоВесаВывоз);
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетВесаДляСтрокиТабличнойЧасти()
		
	ТекущиеДанные = Элементы.СписокНоменклатуры.ТекущиеДанные;
	
	ДанныеСтрокиТаблицы = Новый Структура("ВидФасовки, Количество, Вес");	
	ЗаполнитьЗначенияСвойств(ДанныеСтрокиТаблицы, ТекущиеДанные);
	
	РасчетВеса(ДанныеСтрокиТаблицы);
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ДанныеСтрокиТаблицы);

	ПересчитатьИтоги();
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьИтоги()	
	
	ИтогВес = Объект.СписокНоменклатуры.Итог("Вес");
	
	Объект.Вес              = ИтогВес;
	Объект.ЗачетныйВес      = Объект.Вес;				
	Объект.ВесВывоз         = ИтогВес;
	Объект.ЗачетныйВесВывоз = Объект.ВесВывоз;	

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСчетаУчетаВТабличнойЧасти()
	
	Если Объект.СписокНоменклатуры.Количество() > 0 Тогда
		Документы.ИНАГРО_ТТНВнутр.ЗаполнитьСчетаУчетаВТабличнойЧасти(Объект, "СписокНоменклатуры", СобственноеПодразделение);
	КонецЕсли; 
		
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьРеквизиты()
	
	Перевес = Ложь;
	
	Если Объект.ВидОперации = Перечисления.ИНАГРО_ВидыОперацийТТНВнутр.Перемещение  Тогда		
		Объект.НоваяНоменклатура = Объект.Номенклатура;		
		Объект.НовыйУрожай       = Объект.Урожай;			
	ИначеЕсли Объект.ВидОперации = Перечисления.ИНАГРО_ВидыОперацийТТНВнутр.Перевзвешивание Тогда
		Объект.Перевес           = Истина;
		Объект.НоваяНоменклатура = Объект.Номенклатура;
		Объект.НовыйУрожай       = Объект.Урожай;		
	ИначеЕсли Объект.ВидОперации = Перечисления.ИНАГРО_ВидыОперацийТТНВнутр.ПеремещениеФасованного Тогда
		Если СобственноеПодразделение Тогда			
			Объект.ОтражатьВБухгалтерскомУчете = Ложь;
		КонецЕсли;
		Объект.Влажность         = 0;
		Объект.СорнаяПримесь     = 0;
		Объект.ЗерноваяПримесь   = 0;
	КонецЕсли;	
	
	РасчетВеса();
		
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьНоменклатуруИзПриказа(ВыбранноеЗначение)
	
	СписокВыбора = Новый СписокЗначений;

	СписокВыбора = ПолучитьНоменклатуруИзПриказаНаСервере(СписокВыбора, ВыбранноеЗначение);
		
	Если СписокВыбора.Количество() > 1 Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("СписокВыбора", СписокВыбора); 
		
		ОповещениеОЗакрытии = Новый ОписаниеОповещения("ПолучитьНоменклатуруИзПриказаЗавершение", ЭтотОбъект);
		
		ОткрытьФорму("ОбщаяФорма.ИНАГРО_ФормаВыбораКультуры", ПараметрыФормы, , Новый УникальныйИдентификатор(), , , ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	ИначеЕсли СписокВыбора.Количество() = 1 Тогда		
				
		ПолучитьНоменклатуруИзПриказаЗавершение(Новый Структура("Номенклатура", СписокВыбора.Получить(0).Значение), );
		
	КонецЕсли;	
	              	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьНоменклатуруИзПриказаЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия <> Неопределено Тогда
		
		Объект.Номенклатура = РезультатЗакрытия.Номенклатура;
		
		ПолучитьНоменклатуруИзПриказаЗавершениеНаСервере();

	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПолучитьНоменклатуруИзПриказаЗавершениеНаСервере()
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");	
	ДокументОбъект.ЗаполнитьПоДокументуОснованию(Объект.ПриказНаПеремещение, Объект.Номенклатура); 	
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
	
	УстановитьФункциональныеОпцииФормы();
		
	УправлениеФормойНаСервере();
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Функция ВключенаВРеестр()
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	РеестрТТН.Ссылка
		|ИЗ
		|	Документ.ИНАГРО_РеестрТТНВнутр КАК РеестрТТН
		|ГДЕ
		|	РеестрТТН.СписокТТН.ТТН.Дата МЕЖДУ &ДатаС И &ДатаПо
		|	И РеестрТТН.СписокТТН.ТТН = &ТекДок";
	
	Запрос.УстановитьПараметр("ДатаС",  НачалоДня(Объект.Дата));
	Запрос.УстановитьПараметр("ДатаПо", КонецДня(Объект.Дата));
	Запрос.УстановитьПараметр("ТекДок", Объект.Ссылка);
	
	Выб = Запрос.Выполнить().Выбрать();
	
	Если Выб.Количество() > 0 Тогда
		Возврат Истина;
	Иначе 
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьНоменклатуруИзПриказаНаСервере(СписокВыбора, Ссылка)
			
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИНАГРО_ПриказНаПеремещениеКультуры.Номенклатура
		|ИЗ
		|	Документ.ИНАГРО_ПриказНаПеремещение.Культуры КАК ИНАГРО_ПриказНаПеремещениеКультуры
		|ГДЕ
		|	ИНАГРО_ПриказНаПеремещениеКультуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ИНАГРО_ПриказНаПеремещениеКультуры.Номенклатура";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		СписокВыбора.Добавить(ВыборкаДетальныеЗаписи.Номенклатура);
	КонецЦикла;	

	Возврат СписокВыбора;

КонецФункции

&НаСервере
Функция ПолучитьПараметрыДляДоговоров()

	ПараметрыОтбора = Новый Структура("ВидХранения, Урожай");
	ЗаполнитьЗначенияСвойств(ПараметрыОтбора, Объект);

	Возврат ПараметрыОтбора;

КонецФункции

&НаКлиенте
Функция ПолучитьПараметрыДляПроверкиЗаполнения()
	
	ПараметрыДляПроверкиЗаполнения = Новый Структура(
		"Склад, ВидХранения, Урожай, 
		|Номенклатура");
	ЗаполнитьЗначенияСвойств(ПараметрыДляПроверкиЗаполнения, Объект);

	Возврат ПараметрыДляПроверкиЗаполнения; 

КонецФункции

&НаСервере
Функция ПолучитьПараметрыДляРасчетаЗачетногоВеса()
	
	ПараметрыДляРасчетаЗачетногоВеса = Новый Структура(
		"Ссылка, Дата, Организация,
		|Владелец, ДоговорКонтрагента, Номенклатура,
		|Склад, Влажность, СорнаяПримесь,
		|ФизическийВес, ЗачетныйВес     
		|");
	ЗаполнитьЗначенияСвойств(ПараметрыДляРасчетаЗачетногоВеса, Объект);
	ПараметрыДляРасчетаЗачетногоВеса.Вставить("ФизическийВес", Объект.Вес); 

	Возврат ПараметрыДляРасчетаЗачетногоВеса;

КонецФункции

#КонецОбласти  

#Область СлужебныеПроцедурыИФункцииБСП

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	
	Если ТипЗнч(Команда) = Тип("КомандаФормы") Тогда
		
		ИмяКоманды      = Команда.Имя;
		АдресНастроек   = ЭтотОбъект.ПараметрыПодключаемыхКоманд.АдресТаблицыКоманд;
		ОписаниеКоманды = ПодключаемыеКомандыКлиентПовтИсп.ОписаниеКоманды(ИмяКоманды, АдресНастроек);
		
		Если    ОписаниеКоманды.Идентификатор = "Краткая"
			ИЛИ ОписаниеКоманды.Идентификатор = "Ф1_СГ"
			ИЛИ ОписаниеКоманды.Идентификатор = "Пропуск"
            ИЛИ ОписаниеКоманды.Идентификатор = "Пропуск_2021" Тогда
			
			ДополнительныеПараметры = Новый Структура;
			ДополнительныеПараметры.Вставить("Команда",              Команда);
			ДополнительныеПараметры.Вставить("ИдентификаторКоманды", ОписаниеКоманды.Идентификатор);
			
			Оповещение = Новый ОписаниеОповещения("ВводЧислаЗавершение", ЭтотОбъект, ДополнительныеПараметры); 
			ПоказатьВводЧисла(Оповещение, 0, Нстр("ru='Введите количество экземпляров';uk='Введіть кількість екземплярів'"), 15, 0);
			
		Иначе
			ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
		КонецЕсли; 
		
	Иначе
		ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте 
Процедура ВводЧислаЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт 
	
	Если РезультатЗакрытия <> Неопределено Тогда
		ЗаписатьВыбранноеКоличество(РезультатЗакрытия, ДополнительныеПараметры.ИдентификаторКоманды);		
		ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, ДополнительныеПараметры.Команда, Объект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаписатьВыбранноеКоличество(РезультатЗакрытия, ИдентификаторКоманды) 
	
	Документы.ИНАГРО_ТТНВнутр.ЗаписатьВыбранноеКоличество(РезультатЗакрытия, ИдентификаторКоманды);
	
КонецПроцедуры

#КонецОбласти 