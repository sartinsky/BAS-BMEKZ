////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоПлательщик = Параметры.ЭтоПлательщик;
	ЭтоБанк 	  = Параметры.ЭтоБанк;
	
	АвтоЗначенияРеквизитов = УчетДенежныхСредствБП.СформироватьАвтоЗначенияРеквизитовПлательщикаПолучателя(
		Параметры.Объект.Организация,
		Параметры.Объект.СчетОрганизации,
		Параметры.Объект.Контрагент,
		Параметры.Объект.СчетКонтрагента);
	
	Если ЭтоПлательщик И ЭтоБанк Тогда
		
		ТекстНаименования  = Параметры.Объект.НаименованиеБанкаПлательщика;
		МФО 			   = СокрЛП(АвтоЗначенияРеквизитов.МФОБанкаПлательщика);
		МФООбъекта 		   = СокрЛП(АвтоЗначенияРеквизитов.МФОБанкаПлательщика);
		
		Элементы.КодПоЕДРПОУ.Видимость = Ложь;
		Элементы.МФО.Видимость = Истина;
		Элементы.МФО.Заголовок = НСтр("ru='МФО банка плательщика';uk='МФО банку платника'");
		Элементы.ТекстНаименования.Заголовок = НСтр("ru='Наименование банка плательщика';uk='Найменування банку платника'");
		
		Заголовок = НСтр("ru='Реквизиты банка плательщика';uk='Реквізити банку платника'") 
		
	ИначеЕсли НЕ ЭтоПлательщик И ЭтоБанк Тогда
		
		ТекстНаименования  = Параметры.Объект.НаименованиеБанкаПолучателя;
		МФО 			   = СокрЛП(АвтоЗначенияРеквизитов.МФОБанкаПолучателя);
		МФООбъекта 		   = СокрЛП(АвтоЗначенияРеквизитов.МФОБанкаПолучателя);
		
		Элементы.КодПоЕДРПОУ.Видимость = Ложь;
		Элементы.МФО.Видимость = Истина;
		Элементы.МФО.Заголовок = НСтр("ru='МФО банка получателя';uk='МФО банку отримувача'");
		Элементы.ТекстНаименования.Заголовок = НСтр("ru='Наименование банка получателя';uk='Найменування банку отримувача'");
		
		Заголовок = НСтр("ru='Реквизиты банка получателя';uk='Реквізити банку отримувача'") 
		
	ИначеЕсли ЭтоПлательщик Тогда
		
		ТекстНаименования  = Параметры.Объект.ТекстПлательщика;
		КодПоЕДРПОУ 	   = Параметры.Объект.КодПоЕДРПОУОрганизации;
		КодПоЕДРПОУОбъекта = АвтоЗначенияРеквизитов.КодПоЕДРПОУОрганизации;
		
		Элементы.КодПоЕДРПОУ.Видимость = Истина;
		Элементы.МФО.Видимость = Ложь;
		Элементы.КодПоЕДРПОУ.Заголовок = НСтр("ru='Код по ЕДРПОУ плательщика';uk='Код за ЄДРПОУ платника'");
		Элементы.ТекстНаименования.Заголовок = НСтр("ru='Наименование плательщика';uk='Найменування платника'");
		
		Заголовок = НСтр("ru='Реквизиты плательщика';uk='Реквізити платника'") 
		
	Иначе
		
		ТекстНаименования  = Параметры.Объект.ТекстПолучателя;
		КодПоЕДРПОУ 	   = Параметры.Объект.КодПоЕДРПОУКонтрагента;
		КодПоЕДРПОУОбъекта = АвтоЗначенияРеквизитов.КодПоЕДРПОУКонтрагента;
		
		Элементы.КодПоЕДРПОУ.Видимость = Истина;
		Элементы.МФО.Видимость = Ложь;
		Элементы.КодПоЕДРПОУ.Заголовок = НСтр("ru='Код по ЕДРПОУ получателя';uk='Код за ЄДРПОУ одержувача'");
		Элементы.ТекстНаименования.Заголовок = НСтр("ru='Наименование получателя';uk='Найменування одержувача'");
		
		Заголовок = НСтр("ru='Реквизиты получателя';uk='Реквізити одержувача'") 
		
	КонецЕсли;

	ПлательщикаПолучателя = ?(ЭтоПлательщик, НСтр("ru='плательщика';uk='платника'"), НСтр("ru='получателя';uk='одержувача'"));
	
	ТекстКодПоЕДРПОУВладельца = НСтр("ru='%1 - Код по ЕДРПОУ, указанный для %2';uk='%1 - Код за ЄДРПОУ, вказаний для %2'");
	ТекстКодПоЕДРПОУВладельца = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ТекстКодПоЕДРПОУВладельца, КодПоЕДРПОУОбъекта, ПлательщикаПолучателя);
	Элементы.КодПоЕДРПОУ.СписокВыбора.Добавить(КодПоЕДРПОУОбъекта, ТекстКодПоЕДРПОУВладельца);
	
	УстановитьДоступность();
	
	ЭтаФорма.Заголовок = Заголовок;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)

	Если ЗавершениеРаботы И Модифицированность Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		ТекстВопроса = НСтр("ru='Изменить значения реквизитов в документе?';uk='Змінити значення реквізитів у документі?'");
		Оповещение   = Новый ОписаниеОповещения("ВопросПередЗакрытиемЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена,, КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ТАБЛИЧНОЙ ЧАСТИ <ИмяТабличнойЧасти>


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ОК(Команда)
	
	Если ЕстьОшибкиЗаполненияРеквизитов() Тогда
		Возврат;
	КонецЕсли;
	
	ИзменитьРеквизитыДокумента();
	
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура УстановитьДоступность()
	
	
КонецПроцедуры

&НаКлиенте
Функция ЕстьОшибкиЗаполненияРеквизитов()
	
	ОчиститьСообщения();
	ЕстьОшибки = Ложь;
	
	Если Не ЭтоБанк Тогда
		Если ПустаяСтрока(КодПоЕДРПОУ) Тогда
			Если ЭтоПлательщик Тогда
				ТекстСообщения = НСтр("ru='Поле ""Код по ЕДРПОУ плательщика"" не заполнено';uk='Поле ""Код за ЄДРПОУ платника"" не заповнене'");
			Иначе
				ТекстСообщения = НСтр("ru='Поле ""Код по ЕДРПОУ получателя"" не заполнено';uk='Поле ""Код за ЄДРПОУ одержувача"" не заповнене'");
			КонецЕсли;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "КодПоЕДРПОУ",, ЕстьОшибки);
		КонецЕсли;
	КонецЕсли;

	Если ПустаяСтрока(ТекстНаименования) Тогда
		Если ЭтоПлательщик И ЭтоБанк Тогда
			ТекстСообщения = НСтр("ru='Поле ""Наименование банка плательщика"" не заполнено';uk='Поле ""Найменування банку платника"" не заповнене'");
		ИначеЕсли Не ЭтоПлательщик И ЭтоБанк Тогда
			ТекстСообщения = НСтр("ru='Поле ""Наименование банка получателя"" не заполнено';uk='Поле ""Найменування банку одержувача"" не заповнене'");
		ИначеЕсли ЭтоПлательщик Тогда
			ТекстСообщения = НСтр("ru='Поле ""Наименование плательщика"" не заполнено';uk='Поле ""Найменування платника"" не заповнене'");
		Иначе
			ТекстСообщения = НСтр("ru='Поле ""Наименование получателя"" не заполнено';uk='Поле ""Найменування одержувача"" не заповнене'");
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "ТекстНаименования",, ЕстьОшибки);
	КонецЕсли;
	
	Возврат ЕстьОшибки;
	
КонецФункции

&НаКлиенте
Процедура ИзменитьРеквизитыДокумента()
	
	СтруктураРезультата = Новый Структура;
	
	Если ЭтоПлательщик И ЭтоБанк Тогда
		СтруктураРезультата.Вставить("НаименованиеБанкаПлательщика", ТекстНаименования);
		Оповестить("ВыборРеквизитовБанкаПлательщика", СтруктураРезультата);		
	ИначеЕсли Не ЭтоПлательщик И ЭтоБанк Тогда
		СтруктураРезультата.Вставить("НаименованиеБанкаПолучателя", ТекстНаименования);
		Оповестить("ВыборРеквизитовБанкаПолучателя", СтруктураРезультата);		
	ИначеЕсли ЭтоПлательщик Тогда
		СтруктураРезультата.Вставить("КодПоЕДРПОУОрганизации",   КодПоЕДРПОУ);
		СтруктураРезультата.Вставить("ТекстПлательщика", ТекстНаименования);
		Оповестить("ВыборРеквизитовПлательщика", СтруктураРезультата);		
	Иначе		
		СтруктураРезультата.Вставить("КодПоЕДРПОУКонтрагента",   КодПоЕДРПОУ);
		СтруктураРезультата.Вставить("ТекстПолучателя", ТекстНаименования);
		Оповестить("ВыборРеквизитовПолучателя", СтруктураРезультата);		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ЗАВЕРШЕНИЕ НЕМОДАЛЬНЫХ ВЫЗОВОВ

&НаКлиенте
Процедура ВопросПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Если ЕстьОшибкиЗаполненияРеквизитов() Тогда
			Возврат;
		КонецЕсли;
		
		ИзменитьРеквизитыДокумента();
		Модифицированность = Ложь;
	ИначеЕсли Результат <> КодВозвратаДиалога.Нет Тогда
		Возврат;
	Иначе
		Модифицированность = Ложь;
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры
