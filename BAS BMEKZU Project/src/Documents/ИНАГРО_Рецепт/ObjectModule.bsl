#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда 

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	Если ДанныеЗаполнения <> Неопределено И ТипДанныхЗаполнения <> Тип("Структура")
		И Метаданные().ВводитсяНаОсновании.Содержит(ДанныеЗаполнения.Метаданные()) Тогда
		ЗаполнитьПоДокументуОснованию(ДанныеЗаполнения);
	КонецЕсли;
	
	ИНАГРО_ЭлеваторЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ЭтотОбъект);		

	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	
	Если  ТипДанныхЗаполнения = Тип("Структура")
		И ДанныеЗаполнения.Свойство("ВидОперации") Тогда
		ВидОперации = ДанныеЗаполнения.ВидОперации;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда	
		Возврат;	
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)

	Дата = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Ответственный = Пользователи.ТекущийПользователь();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПоДокументуОснованию(Основание)
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.ИНАГРО_Форма117") Тогда
				
		Дата           = Основание.Дата;
		ВидОперации    = Перечисления.ИНАГРО_ВидыОперацийРецепт.РецептПоНесколькимГотовымПродукциям;
		Организация    = Основание.Организация;
		Контрагент     = Основание.Владелец;
		Владелец       = Основание.Владелец;
		
		ВсегоВесПартии = 0;
		
		Для Каждого ТекСтрокаСырье Из Основание.Сырье Цикл
			НоваяСтрока = СписокНоменклатуры.Добавить();
			НоваяСтрока.Номенклатура     = ТекСтрокаСырье.Номенклатура;
			НоваяСтрока.КоличествоПартии = ТекСтрокаСырье.ФизическийВес;
			ВсегоВесПартии               = ВсегоВесПартии + ТекСтрокаСырье.ФизическийВес;
		КонецЦикла;
		
		ВесПартии      = ВсегоВесПартии;
		
		Для Каждого ТекСтрокаПродукция Из Основание.Продукция Цикл
			НоваяСтрока = СписокПродукции.Добавить();
			НоваяСтрока.Номенклатура     = ТекСтрокаПродукция.Номенклатура;
			НоваяСтрока.КоличествоПартии = ТекСтрокаПродукция.РасчетныйВесВыходаПродукции;
			НоваяСтрока.Процент          = ТекСтрокаПродукция.РасчетныйПроцентВыходаПродукции;
		КонецЦикла;
		
	КонецЕсли;
			
КонецПроцедуры

#КонецОбласти

#КонецЕсли