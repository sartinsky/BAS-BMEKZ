
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ДляВсехСтрок( ЗначениеРазрешено(Работники.ФизическоеЛицо, NULL КАК ИСТИНА)
	|	) И ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область ПроцедурыИФункцииПечати

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Список перечислений
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ЗаявлениеНаПрименениеЛьготы";
	КомандаПечати.Представление = НСтр("ru='Заявление на применение льготы';uk='Заява на застосування пільги'");
	
	// Список перечислений в Microsoft Word
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ЗаявлениеНаПрименениеЛьготы";
	КомандаПечати.Представление = НСтр("ru='Заявление на применение льготы в Microsoft Word';uk='Заява на застосування пільги у Microsoft Word'");
	КомандаПечати.ФорматСохранения = ТипФайлаТабличногоДокумента.DOCX;
	
	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор  = "Реестр";
	КомандаПечати.Представление  = НСтр("ru='Реестр документов';uk='Реєстр документів'");
	КомандаПечати.ЗаголовокФормы = НСтр("ru='Реестр документов ""Заявление на применение льготы НДФЛ""';uk='Реєстр документів ""Заява на застосування пільги ПДФО""'");
	КомандаПечати.Обработчик     = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм     = "ФормаСписка";
	КомандаПечати.Порядок        = 100;
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЗаявлениеНаПрименениеЛьготы") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ЗаявлениеНаПрименениеЛьготы", НСтр("ru='Заявление на применение льготы';uk='Заява на застосування пільги'"), ПечатьЗаявление(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	
КонецПроцедуры

Функция ПечатьЗаявление(МассивОбъектов, ОбъектыПечати)
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ЗаявлениеНаПрименениеЛьготы_ЗаявлениеНаПрименениеЛьготы";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ЗаявлениеНаПрименениеЛьготы.ПФ_MXL_UK_ЗаявлениеНаПрименениеЛьготы");
	
	// получаем данные для печати
	Для Каждого Ссылка Из МассивОбъектов Цикл
		Выборка = ВыборкаДляПечати(Ссылка);
		ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Пока Выборка.Следующий() Цикл
			
			//ДанныеФизЛица = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(Выборка.Организация, Выборка.Работник, Выборка.ДатаДок);
			ДанныеФизЛица = ИНАГРО_ЗарплатаКадрыРасширенный.ДанныеФизЛица(Выборка.Организация, Выборка.Работник, Выборка.ДатаДок);

			ОбластьШапка1 = Макет.ПолучитьОбласть("Шапка1");
			ОбластьШапка1.Параметры.Организация = Выборка.Организация.НаименованиеПолное;
			ОбластьШапка1.Параметры.ФИО = Выборка.ФИО;
			ТабличныйДокумент.Присоединить(ОбластьШапка1);
			
			ОбластьШапка2 = Макет.ПолучитьОбласть("Шапка2");
			ОбластьШапка2.Параметры.Должность = ДанныеФизЛица.Должность;
			ТабличныйДокумент.Присоединить(ОбластьШапка2);
			
			ОбластьДРФО = Макет.ПолучитьОбласть("ДРФО");
			ТабличныйДокумент.Присоединить(ОбластьДРФО);
			
			Счетчик = 1; 
			Пока Счетчик < 11 Цикл
				ОбластьДРФО = Макет.ПолучитьОбласть("КодПоДРФО" + Строка(Счетчик));	
				КодПоДРФО = Сред(Выборка.Работник.КодПоДРФО, Счетчик, 1);
				Если Ссылка.Номер <> Неопределено Тогда
					ОбластьДРФО.Параметры.Номер = КодПоДРФО;
				КонецЕсли;
				ТабличныйДокумент.Присоединить(ОбластьДРФО);
				Счетчик = Счетчик + 1;
			КонецЦикла;
			
			
			Если Выборка.Актуальность Тогда
				ОбластьЗаявление = Макет.ПолучитьОбласть("ПрименениеЛьготы");
				КодЛьготы = СтрЗаменить(Выборка.Льгота.Код,"ВР","");
				ОбластьЗаявление.Параметры.Норма = КодЛьготы;
				
				ОбластьЗаявление.Параметры.Дата = Формат(Ссылка.Дата,"Л=uk; ДФ='дд ММММ гггг'");
				ТабличныйДокумент.Присоединить(ОбластьЗаявление);
			Иначе
				ОбластьЗаявление = Макет.ПолучитьОбласть("ОтказОтЛьготы");
				ОбластьЗаявление.Параметры.Месяц =  Формат(Выборка.ДатаИзменения,"Л=uk; ДФ=ММММ");
				ОбластьЗаявление.Параметры.Год2 = Прав(Формат(Выборка.ДатаИзменения,"ДФ=гггг"), 1);
				ОбластьЗаявление.Параметры.Год1 = Сред(Формат(Выборка.ДатаИзменения,"ДФ=гггг"), 3,1);
				ОбластьЗаявление.Параметры.Дата = Формат(Ссылка.Дата,"Л=uk; ДФ='дд ММММ гггг'");
				ТабличныйДокумент.Присоединить(ОбластьЗаявление);
			КонецЕсли;
		
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЦикла;
		
		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, Ссылка);
		
	КонецЦикла;
	

	Возврат ТабличныйДокумент;
	
КонецФункции

#КонецОбласти

// Формирует запрос по документу
//
// Параметры: 
//  Ведомости - массив ДокументСсылка.ЗаявлениеНаПрименениеЛьготы
//
// Возвращаемое значение:
//  Результат запроса
//
Функция ВыборкаДляПечати(Ссылка)
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	
	Запрос.УстановитьПараметр("МоментВремени", Ссылка.МоментВремени());
	Запрос.УстановитьПараметр("Организация", Ссылка.Организация);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ЗаявлениеНаПрименениеЛьготыРаботники.Ссылка.Организация КАК Организация,
	|	ЗаявлениеНаПрименениеЛьготыРаботники.Ссылка,
	|	ЗаявлениеНаПрименениеЛьготыРаботники.Ссылка.Дата КАК ДатаДок,
	|	ЗаявлениеНаПрименениеЛьготыРаботники.НомерСтроки,
	|	ЗаявлениеНаПрименениеЛьготыРаботники.Льгота,
	|	ЗаявлениеНаПрименениеЛьготыРаботники.ДатаИзменения,
	|	ЗаявлениеНаПрименениеЛьготыРаботники.Актуальность,
	|	ЗаявлениеНаПрименениеЛьготыРаботники.ИНАГРО_Сотрудник.ФизическоеЛицо КАК Работник,
	|	ЗаявлениеНаПрименениеЛьготыРаботники.ИНАГРО_Сотрудник.ФизическоеЛицо.Наименование КАК ФИО
	|ИЗ
	|	Документ.ЗаявлениеНаПрименениеЛьготы.Работники КАК ЗаявлениеНаПрименениеЛьготыРаботники
	|ГДЕ
	|	ЗаявлениеНаПрименениеЛьготыРаботники.Ссылка = &Ссылка";
	
	Возврат Запрос.Выполнить().Выбрать();
	
	
КонецФункции

#КонецЕсли