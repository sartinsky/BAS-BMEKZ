#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Формирует проводку по расходам на сумму текущего налога на прибыль
Процедура ПровестиРасходыПоТекущемуНалогуНаПрибыль()

	Проводка = Движения.Хозрасчетный.Добавить();
	
	Проводка.Период                     = Дата;
	Проводка.Организация                = Организация;
	Проводка.Сумма                      = ТекущийНалогНаПрибыль;
	
	Проводка.СчетДт						= СчетРасходовПоНалогуНаПрибыль;
	БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1, СубконтоРасходов1);
	БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 2, СубконтоРасходов2);
	БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3, СубконтоРасходов3);
	
	Проводка.СчетКт						= ПланыСчетов.Хозрасчетный.РасчетыПоНалогуНаПрибыль;
	
	Проводка.Содержание					= НСтр("ru='Расход по налогу на прибыль';uk='Витрата по податку на прибуток'",Локализация.КодЯзыкаИнформационнойБазы());

КонецПроцедуры // ПровестиРасходыПоТекущемуНалогуНаПрибыль()
 
// Выполняет движения по регистрам.
//
// Параметры
//  Отказ                   - флаг отказа в проведении,
//  Заголовок               - строка, заголовок сообщения об ошибке проведения.
//
Процедура ДвиженияПоРегистрам(Отказ, Заголовок)
	
	ПроводкиБУ = Движения.Хозрасчетный;
	
	Если НЕ РассчитыватьОНАиОНО Тогда
	
		// Просто показываем расходы в размере текущего налога на прибыль
		ПровестиРасходыПоТекущемуНалогуНаПрибыль();
		
		// И на этом прекращаем формирование проводок
		Возврат;
	
	КонецЕсли; 

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВложенныйЗапрос.Статья,
	               |	ВложенныйЗапрос.ВидНалоговойДеятельности,
	               |	СУММА(ВложенныйЗапрос.СуммаОНА) КАК СуммаОНА,
	               |	СУММА(ВложенныйЗапрос.СуммаОНО) КАК СуммаОНО,
	               |	СУММА(ВложенныйЗапрос.ОстатокОНА) КАК ОстатокОНА,
	               |	СУММА(ВложенныйЗапрос.ОстатокОНО) КАК ОстатокОНО
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		РасчетыПоНалогуНаПрибыльСтатьи.Статья КАК Статья,
	               |		РасчетыПоНалогуНаПрибыльСтатьи.ВидНалоговойДеятельности КАК ВидНалоговойДеятельности,
	               |		РасчетыПоНалогуНаПрибыльСтатьи.СуммаОНА КАК СуммаОНА,
	               |		-(РасчетыПоНалогуНаПрибыльСтатьи.СуммаОНО) КАК СуммаОНО,
	               |		0 КАК ОстатокОНА,
	               |		0 КАК ОстатокОНО
	               |	ИЗ
	               |		Документ.РасчетыПоНалогуНаПрибыль.Статьи КАК РасчетыПоНалогуНаПрибыльСтатьи
	               |	
	               |	ГДЕ
	               |		РасчетыПоНалогуНаПрибыльСтатьи.Ссылка = &Ссылка
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		ХозрасчетныйОстатки.Субконто1,
	               |		ХозрасчетныйОстатки.Субконто2,
	               |		0,
	               |		0,
	               |		ХозрасчетныйОстатки.СуммаОстаток,
	               |		0
	               |	ИЗ
	               |		РегистрБухгалтерии.Хозрасчетный.Остатки(&Дата, Счет = &СчетОНА, &МассивСубконто, Организация = &Организация) КАК ХозрасчетныйОстатки
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		ХозрасчетныйОстатки.Субконто1,
	               |		ХозрасчетныйОстатки.Субконто2,
	               |		0,
	               |		0,
	               |		0,
	               |		ХозрасчетныйОстатки.СуммаОстаток
	               |	ИЗ
	               |		РегистрБухгалтерии.Хозрасчетный.Остатки(&Дата, Счет = &СчетОНО, &МассивСубконто, Организация = &Организация) КАК ХозрасчетныйОстатки) КАК ВложенныйЗапрос
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВложенныйЗапрос.Статья,
	               |	ВложенныйЗапрос.ВидНалоговойДеятельности
	               |
	               |ИТОГИ СУММА(СуммаОНА), СУММА(СуммаОНО), СУММА(ОстатокОНА), СУММА(ОстатокОНО) ПО
	               |	ОБЩИЕ";
				   
	//Знаки сумм в запросе:
	// 	сумма ОНА и остаток ОНА - всегда положительные числа (как остаток по активному счету)
	// 	сумма ОНО и остаток ОНО - всегда отрицательные числа (как остаток по пассивному счету)
				   
	Запрос.УстановитьПараметр("Дата", МоментВремени());
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СчетОНА", ПланыСчетов.Хозрасчетный.ОтсроченныеНалоговыеАктивы);
	Запрос.УстановитьПараметр("СчетОНО", ПланыСчетов.Хозрасчетный.ОтсроченныеНалоговыеОбязательства);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	МассивСубконто = Новый Массив(2);
	МассивСубконто[0] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиОтсроченныхНалоговыхАктивовИОбязательств;
	МассивСубконто[1] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ВидыНалоговойДеятельности;
	Запрос.УстановитьПараметр("МассивСубконто", МассивСубконто);
	
	ИтоговаяВыборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ВыборкаПустая = Истина;
	Пока ИтоговаяВыборка.Следующий() Цикл
		
		ВыборкаПустая = Ложь;
		
		// Общий итог по запросу
	
		// Покажем финансовый результат (доход или расход) по налогу на прибыль
		РазницаОНАиОНО 		= (ИтоговаяВыборка.СуммаОНА + ИтоговаяВыборка.СуммаОНО) - 
								(ИтоговаяВыборка.ОстатокОНА + ИтоговаяВыборка.ОстатокОНО);
								
		ФинРезультатПоНалогуНаПрибыль = ТекущийНалогНаПрибыль - РазницаОНАиОНО;
						
		Если ФинРезультатПоНалогуНаПрибыль > 0 Тогда
		
			// Расход
			Проводка = ПроводкиБУ.Добавить();
			
			Проводка.Период                     = Дата;
			Проводка.Организация                = Организация;
			Проводка.Сумма                      = ФинРезультатПоНалогуНаПрибыль;
			
			Проводка.СчетДт						= СчетРасходовПоНалогуНаПрибыль;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1, СубконтоРасходов1);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 2, СубконтоРасходов2);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3, СубконтоРасходов3);
			
			Проводка.СчетКт						= ПланыСчетов.Хозрасчетный.РасчетыПоНалогуНаПрибыль;
			
			Проводка.Содержание					= НСтр("ru='Расход по налогу на прибыль';uk='Витрата по податку на прибуток'",Локализация.КодЯзыкаИнформационнойБазы());
		
		ИначеЕсли ФинРезультатПоНалогуНаПрибыль < 0 Тогда
		
			// Доход
			
			Проводка = ПроводкиБУ.Добавить();
			
			Проводка.Период                     = Дата;
			Проводка.Организация                = Организация;
			Проводка.Сумма                      = -ФинРезультатПоНалогуНаПрибыль;
			
			Проводка.СчетДт						= ПланыСчетов.Хозрасчетный.РасчетыПоНалогуНаПрибыль;
			
			Проводка.СчетКт						= СчетДоходовПоНалогуНаПрибыль;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 1, СубконтоДоходов1);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 2, СубконтоДоходов2);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 3, СубконтоДоходов3);
			
			Проводка.Содержание					= НСтр("ru='Доход от налога на прибыль';uk='Дохід від податку на прибуток'",Локализация.КодЯзыкаИнформационнойБазы());
		
		КонецЕсли; 
		
		Выборка = ИтоговаяВыборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока Выборка.Следующий() Цикл
			
			// Цикл по конкретным статьям и видам налоговой деятельности
			
			// Приводим остатки на счетах ОНА и ОНО в соответствие значениям, указанным в документе
			
			РазницаОНАиОНО 		= (Выборка.СуммаОНА + Выборка.СуммаОНО) - 
									(Выборка.ОстатокОНА + Выборка.ОстатокОНО);
									
			Если РазницаОНАиОНО > 0 Тогда
			
				// Прирост ОНА (уменьшение ОНО)
				
				СуммаПогашенияОНО = 0;
				
				Если Выборка.ОстатокОНО <> 0 Тогда
				
					// Имеется остаток ОНО - погасим его, сколько позволит сумма
					СуммаПогашенияОНО = Мин(РазницаОНАиОНО, -Выборка.ОстатокОНО);
					
					Проводка = ПроводкиБУ.Добавить();
					Проводка.Период                     = Дата;
					Проводка.Организация                = Организация;
					Проводка.Сумма                      = СуммаПогашенияОНО;
					
					Проводка.СчетДт						= ПланыСчетов.Хозрасчетный.ОтсроченныеНалоговыеОбязательства;
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "СтатьиОтсроченныхНалоговыхАктивовИОбязательств", Выборка.Статья);
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "ВидыНалоговойДеятельности", Выборка.ВидНалоговойДеятельности);
					
					Проводка.СчетКт						= ПланыСчетов.Хозрасчетный.РасчетыПоНалогуНаПрибыль;
					
					Проводка.Содержание					= НСтр("ru='Уменьшение отсроченных налоговых обязательств';uk=""Зменшення відстрочених податкових зобов'язань""",Локализация.КодЯзыкаИнформационнойБазы());
				
				КонецЕсли; 
				
				СуммаВозникновенияОНА = РазницаОНАиОНО - СуммаПогашенияОНО;
				
				Если СуммаВозникновенияОНА <> 0 Тогда
				
					// На оставшуюся сумму покажем увеличение ОНА
					
					Проводка = ПроводкиБУ.Добавить();
					Проводка.Период                     = Дата;
					Проводка.Организация                = Организация;
					Проводка.Сумма                      = СуммаВозникновенияОНА;
					
					Проводка.СчетДт						= ПланыСчетов.Хозрасчетный.ОтсроченныеНалоговыеАктивы;
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "СтатьиОтсроченныхНалоговыхАктивовИОбязательств", Выборка.Статья);
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "ВидыНалоговойДеятельности", Выборка.ВидНалоговойДеятельности);
					
					Проводка.СчетКт						= ПланыСчетов.Хозрасчетный.РасчетыПоНалогуНаПрибыль;
					
					Проводка.Содержание					= НСтр("ru='Увеличение отсроченных налоговых активов';uk='Збільшення відстрочених податкових активів'",Локализация.КодЯзыкаИнформационнойБазы());
				
				КонецЕсли; 
			
			ИначеЕсли РазницаОНАиОНО < 0 Тогда
			
				// Прирост ОНО (уменьшение ОНА)
				
				СуммаПогашенияОНА = 0;
				
				Если Выборка.ОстатокОНА <> 0 Тогда
				
					// Имеется остаток ОНА - погасим его, сколько позволит сумма
					СуммаПогашенияОНА = Мин(-РазницаОНАиОНО, Выборка.ОстатокОНА);
					
					Проводка = ПроводкиБУ.Добавить();
					Проводка.Период                     = Дата;
					Проводка.Организация                = Организация;
					Проводка.Сумма                      = СуммаПогашенияОНА;
					
					Проводка.СчетДт						= ПланыСчетов.Хозрасчетный.РасчетыПоНалогуНаПрибыль;
					
					Проводка.СчетКт						= ПланыСчетов.Хозрасчетный.ОтсроченныеНалоговыеАктивы;
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "СтатьиОтсроченныхНалоговыхАктивовИОбязательств", Выборка.Статья);
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ВидыНалоговойДеятельности", Выборка.ВидНалоговойДеятельности);
					
					Проводка.Содержание					= НСтр("ru='Уменьшение отсроченных налоговых активов';uk='Зменшення відстрочених податкових активів'",Локализация.КодЯзыкаИнформационнойБазы());
				
				КонецЕсли; 
				
				СуммаВозникновенияОНО = - РазницаОНАиОНО - СуммаПогашенияОНА;
				
				Если СуммаВозникновенияОНО <> 0 Тогда
				
					// На оставшуюся сумму покажем увеличение ОНО
					
					Проводка = ПроводкиБУ.Добавить();
					Проводка.Период                     = Дата;
					Проводка.Организация                = Организация;
					Проводка.Сумма                      = СуммаВозникновенияОНО;
					
					Проводка.СчетДт						= ПланыСчетов.Хозрасчетный.РасчетыПоНалогуНаПрибыль;
					
					Проводка.СчетКт						= ПланыСчетов.Хозрасчетный.ОтсроченныеНалоговыеОбязательства;
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "СтатьиОтсроченныхНалоговыхАктивовИОбязательств", Выборка.Статья);
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ВидыНалоговойДеятельности", Выборка.ВидНалоговойДеятельности);
					
					Проводка.Содержание					= НСтр("ru='Увеличение отсроченных налоговых обязательств';uk=""Збільшення відстрочених податкових зобов'язань""",Локализация.КодЯзыкаИнформационнойБазы());
				
				КонецЕсли; 
			
			КонецЕсли; 
		
		КонецЦикла; 
	
	КонецЦикла;
	
	Если ВыборкаПустая Тогда
		
		ПровестиРасходыПоТекущемуНалогуНаПрибыль();
	
	КонецЕсли; 
	
КонецПроцедуры // ДвиженияПоРегистрам()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Процедура - обработчик события "ОбработкаПроведения"
//
Процедура ОбработкаПроведения(Отказ)
	
	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ДОКУМЕНТА
	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	Если РучнаяКорректировка Тогда
		Возврат;
	КонецЕсли;
	
	Заголовок = ОбщегоНазначенияБПВызовСервера.ПредставлениеДокументаПриПроведении(Ссылка);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// Движения документа по регистрам
	Если НЕ Отказ Тогда
		
		ДвиженияПоРегистрам(Отказ, Заголовок);
	
	КонецЕсли; 
	
	Движения.Хозрасчетный.ВыполнитьДействияПередЗаписьюДвижений();

	ПроведениеСервер.ПодготовитьНаборыЗаписейКЗаписиДвижений(ЭтотОбъект);
	
КонецПроцедуры // ОбработкаПроведения()

Процедура ОбработкаУдаленияПроведения(Отказ)

	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	Движения.Записать();

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ОбщегоНазначенияРед12.ДокументыВПериоде(Ссылка, "Квартал", Новый Структура("Организация", Организация)).Количество() > 0 Тогда
		ТекстСообщения = НСтр("ru='За указанный период уже существует документ ""';uk='За вказаний період вже існує документ ""'") + Ссылка.Метаданные().Представление() + """";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Дата", "Объект", Отказ);
	КонецЕсли;
	
	// Исключаем из проверки реквизиты, заполнение которых стало необязательным:
	МассивНепроверяемыхРеквизитов = Новый Массив();
	ДатаНКУ2015 = '2015 01 01';
	Если (Дата >= ДатаНКУ2015) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Статьи.ВидНалоговойДеятельности");
	КонецЕсли;

	// Удаляем из проверяемых реквизитов все, по которым автоматическая проверка не нужна:
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)

	Дата = НачалоДня(ОбщегоНазначенияБП.ПолучитьРабочуюДату());
	Ответственный = Пользователи.ТекущийПользователь();

КонецПроцедуры

#КонецЕсли
