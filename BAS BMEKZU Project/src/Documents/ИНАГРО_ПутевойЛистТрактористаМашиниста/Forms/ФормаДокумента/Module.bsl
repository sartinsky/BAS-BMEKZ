#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
			
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды 
	
	Если Параметры.Ключ.Пустая() Тогда
		ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДате(ЭтаФорма, "Объект.ПериодРегистрации", "МесяцСтрока");
		ПодготовитьФормуНаСервере();
	КонецЕсли;
	
	// Уведомим о появлении функционала рабочей даты
	ЭтаФорма.НастройкиПредупреждений = ОбщегоНазначенияБП.НастройкиПредупрежденийОбИзменениях("РабочаяДатаИзДокумента");
	
	// Показываем, если это новый документ и сама рабочая дата еще не установлена.
	ЭтаФорма.НастройкиПредупреждений.РабочаяДатаИзДокумента = ЭтаФорма.НастройкиПредупреждений.РабочаяДатаИзДокумента
	И ЭтаФорма.Параметры.Ключ.Пустая()
	И НЕ ЗначениеЗаполнено(БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("РабочаяДата"));
	 	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
			
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ИсточникВыбора.ИмяФормы = "РегистрСведений.ИНАГРО_ВидыРаботСельхозтехники.Форма.ФормаПодбора" Тогда 		
		ОбработкаВыбораПодборСервер(ВыбранноеЗначение);				         
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	ОбщегоНазначенияБПКлиент.ОбработкаОповещенияФормыДокумента(ЭтаФорма, Объект.Ссылка, ИмяСобытия, Параметр, Источник);

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДате(ЭтаФорма, "Объект.ПериодРегистрации", "МесяцСтрока");
	
	ПодготовитьФормуНаСервере(); 	
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаполнитьДобавленныеКолонкиТаблицыПутевыеЛисты();

	УстановитьСостояниеДокумента();

КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)

	Если НачалоДня(Объект.Дата) = НачалоДня(ТекущаяДатаДокумента) Тогда
		// Изменение времени не влияет на поведение документа.
		ТекущаяДатаДокумента = Объект.Дата;
		Возврат;
	КонецЕсли;

	// Общие проверки условий по датам.
	ТребуетсяВызовСервера = ОбщегоНазначенияБПКлиент.ТребуетсяВызовСервераПриИзмененииДатыДокумента(Объект.Дата, 
		ТекущаяДатаДокумента);
		
	// Если определили, что изменение даты может повлиять на какие-либо параметры, 
	// то передаем обработку на сервер.
	Если ТребуетсяВызовСервера Тогда
		ДатаПриИзмененииНаСервере();
	КонецЕсли;
	
	// Запомним новую дату документа.
	ТекущаяДатаДокумента = Объект.Дата;	
		
	Объект.ПериодРегистрации = НачалоМесяца(ТекущаяДатаДокумента);
		
	УстановкаПоказанийБака();  	
	
	Для Каждого СтрокаТабличнойЧасти Из Объект.ПутевыеЛисты Цикл
		РассчитатьЗатратыПоНорме(СтрокаТабличнойЧасти);
	КонецЦикла; 

КонецПроцедуры

&НаСервере
Процедура ДатаПриИзмененииНаСервере()

	УстановитьФункциональныеОпцииФормы();

	УправлениеФормой(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)

	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		ОрганизацияПриИзмененииНаСервере();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()

	УстановитьФункциональныеОпцииФормы();
	 		
	УправлениеФормой(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура АвтомобильПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(ПолучитьЗначениеРеквизита(Объект.Автомобиль, "Модель")) Тогда
		ТекстСообщения = Нстр("ru='Не заполнена модель автомобиля!';uk='Не заповнена модель автомобиля!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	АвтомобильПриИзмененииНаСервере(Объект.Автомобиль);
		
	ЗаполнитьСведенияОТС();
	
	Объект.Водитель = СвойстваТС.Водитель;
	
	УстановкаПоказанийБака(); 	
	
	УстановитьФункциональныеОпцииФормы();
	
	Для Каждого СтрокаТабличнойЧасти Из Объект.ПутевыеЛисты Цикл
		
		СтрокаТабличнойЧасти.СменнаяЭталоннаяВыработка = СвойстваТС.КоэфУЕГА;
		
		РассчитатьЗатратыПоНорме(СтрокаТабличнойЧасти);
		
	КонецЦикла;  
	
	УправлениеФормой(ЭтаФорма);  	
	
	Если Объект.ПутевыеЛисты.Количество() > 0 Тогда
		ТекстВопроса = Нстр("ru='Выполнить перерасчет затрат топлива по норме?';uk='Виконати розрахунок витрат палива за нормою?'");
		Оповещение = Новый ОписаниеОповещения("АвтомобильПриИзмененииЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да); 
	КонецЕсли;  
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура АвтомобильПриИзмененииНаСервере(Автомобиль)
			
	Если Автомобиль.Модель.ВидТехники <> Перечисления.ИНАГРО_ВидыТехники.Сельхозтехника Тогда 
		ТекстСообщения = Нстр("ru='Выбран автомобиль, модель которого имеет Вид техники не Сельхозтехника!';uk='Обрано автомобіль, модель якого має Вид техніки не Сільгосптехніка!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Автомобиль = Справочники.ОсновныеСредства.ПустаяСсылка();
		Возврат;
	КонецЕсли;  	
		
КонецПроцедуры

&НаКлиенте
Процедура АвтомобильПриИзмененииЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
   	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		РассчитатьРасходТопливаПоНорме();
		РассчитатьНормуРасходаТоплива();   
	КонецЕсли; 	
	
КонецПроцедуры

&НаКлиенте
Процедура АвтомобильОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если НЕ ПолучитьЗначениеРеквизита(ВыбранноеЗначение, "Автотранспорт") Тогда
		ТекстСообщения = Нстр("ru='Выбран не Автотранспорт!';uk='Обрано не Автотранспорт!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;  
	
КонецПроцедуры

&НаКлиенте
Процедура ВодительПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(Объект.Водитель) Тогда
		Возврат;
	КонецЕсли;  
	
	Объект.ДокументОснование = ПредопределенноеЗначение("Документ.ИНАГРО_ДоговорНаВыполнениеРаботСФизЛицом.ПустаяСсылка");
	
	Если ПолучитьЗначениеРеквизита(Объект.Водитель, "ИНАГРО_ДоговорПодряда") Тогда 		
		Объект.ДокументОснование = ВодительПриИзмененииНаСервере(Объект.Водитель, Объект.Дата); 		
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);
	
	Если НЕ ПараметрыТехники.ДоплачиватьВодителямЗаКлассность Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.ПутевыеЛисты.Количество() > 0 Тогда
		ТекстВопроса = Нстр("ru='Пересчитать з/п за классность?';uk='Перерахувати з/п за класність?'");
		Оповещение = Новый ОписаниеОповещения("ВодительПриИзмененииЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да); 
	КонецЕсли; 	

КонецПроцедуры

&НаСервереБезКонтекста
Функция ВодительПриИзмененииНаСервере(Водитель, Дата)
	
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|			Договор.Ссылка КАК ДокументОснование
		|		ИЗ
		|			Документ.ИНАГРО_ДоговорНаВыполнениеРаботСФизЛицом КАК Договор
		|		ГДЕ
		|			&ДатаАктуальности МЕЖДУ Договор.ДатаНачала И Договор.ДатаОкончания
		|		    И Договор.Сотрудник = &Сотрудник";		
		Запрос.УстановитьПараметр("Сотрудник",        Водитель);
		Запрос.УстановитьПараметр("ДатаАктуальности", Дата);
		
		ТаблДоговорОснование = Запрос.Выполнить().Выгрузить();
		
		Если ТаблДоговорОснование.Количество() > 0 Тогда
			ТекДокументОснование = ТаблДоговорОснование[0].ДокументОснование;
		КонецЕсли; 
		
		Возврат ТекДокументОснование;

КонецФункции   

&НаКлиенте
Процедура ВодительПриИзмененииЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
   	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли; 		
	
	Для Каждого СтрокаТабличнойЧасти Из Объект.ПутевыеЛисты Цикл 		
		РассчитатьЗатратыПоНорме(СтрокаТабличнойЧасти); 	
	КонецЦикла;   
	
КонецПроцедуры 

&НаКлиенте
Процедура СводныйПутевойЛистПриИзменении(Элемент)
	
	Если НЕ Объект.СводныйПутевойЛист Тогда
		ОдинДень = 1;
		Для Каждого СтрокаТабличнойЧасти Из Объект.ПутевыеЛисты Цикл
			СтрокаТабличнойЧасти.ОтработаноДней = ОдинДень;
			ОдинДень = 0;
		КонецЦикла;	
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования,
		ЭтотОбъект,
		"Объект.Комментарий");

КонецПроцедуры

&НаКлиенте
Процедура МесяцСтрокаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("МесяцНачисленияСтрокойНачалоВыбораЗавершение", ЭтотОбъект);
	ИНАГРО_ЗарплатаКадрыРасширенныйКлиент.ВводМесяцаНачалоВыбора(ЭтаФорма, ЭтаФорма, "Объект.ПериодРегистрации", "МесяцСтрока", , Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцСтрокаРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	ИНАГРО_ЗарплатаКадрыРасширенныйКлиент.ВводМесяцаРегулирование(ЭтаФорма, "Объект.ПериодРегистрации", "МесяцСтрока", Направление, Модифицированность);
	ПриИзмененииМесяцаНачисления();
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцСтрокаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ИНАГРО_ЗарплатаКадрыРасширенныйКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	ПриИзмененииМесяцаНачисления();
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцСтрокаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ИНАГРО_ЗарплатаКадрыРасширенныйКлиент.ВводМесяцаОкончаниеВводаТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	ПриИзмененииМесяцаНачисления();
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцНачисленияСтрокойНачалоВыбораЗавершение(ЗначениеВыбрано, ДополнительныеПараметры) Экспорт
	
	ПриИзмененииМесяцаНачисления();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииМесяцаНачисления()
	
	ИНАГРО_ЗарплатаКадрыРасширенныйКлиент.ВводМесяцаПриИзменении(ЭтаФорма, "Объект.ПериодРегистрации", "МесяцСтрока", Модифицированность);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПутевыеЛисты

&НаКлиенте
Процедура ПутевыеЛистыПриИзменении(Элемент)
	
	ИзменитьПараметрыВыбораПолейСубконтоТЧ(ЭтаФорма);
	
	УправлениеФормой(ЭтаФорма);
	
	ОбновитьИтоги(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПутевыеЛистыПриАктивизацииСтроки(Элемент)
	
	УстановитьТЧПомощники();

КонецПроцедуры

&НаКлиенте
Процедура ПутевыеЛистыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)	
	
	Если Копирование И НЕ Отказ Тогда		
		ПутевыеЛистыПередНачаломДобавленияНаСервере(Элемент.ТекущиеДанные.IDСтроки);		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПутевыеЛистыПередНачаломДобавленияНаСервере(IDСтроки)	
	
	IDНовойСтроки = Новый УникальныйИдентификатор;
	
	Для Каждого СтрокаТабличнойЧасти Из Объект.Помощники Цикл
		
		Если СтрокаТабличнойЧасти.IDСтроки = IDСтроки Тогда 			
			
			НоваяСтрока = Объект.Помощники.Добавить();
			НоваяСтрока.Помощник        = СтрокаТабличнойЧасти.Помощник;
			НоваяСтрока.ОплатаПомощника = СтрокаТабличнойЧасти.ОплатаПомощника;
			НоваяСтрока.ПроцентОплаты   = СтрокаТабличнойЧасти.ПроцентОплаты;
			НоваяСтрока.IDСтроки        = IDНовойСтроки; 
			
		КонецЕсли;
		
	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Процедура ПутевыеЛистыПередНачаломИзменения(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.ПутевыеЛисты.ТекущиеДанные; 	
	
	ИзменитьПараметрыВыбораПолейСубконтоТЧ(ЭтаФорма);
	
	УстановитьЗаголовкиИДоступностьСубконтоТЧ(ЭтаФорма, ТекущиеДанные.СчетСписания, Истина, "", Истина);  	
		
КонецПроцедуры

&НаКлиенте
Процедура ПутевыеЛистыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда 
		Если НЕ Копирование Тогда 		
			Элемент.ТекущиеДанные.IDСтроки       = Новый УникальныйИдентификатор; 
			Элемент.ТекущиеДанные.СтатьяЗатратЗП = ПараметрыТехники.СтатьяЗатратРаботы;
		ИначеЕсли Копирование Тогда
			Элемент.ТекущиеДанные.IDСтроки       = IDНовойСтроки;
		КонецЕсли;
	КонецЕсли; 

КонецПроцедуры 

&НаКлиенте
Процедура ПутевыеЛистыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ТекущиеДанные = Элементы.ПутевыеЛисты.ТекущиеДанные;

	Если ТекущиеДанные <> Неопределено Тогда 
		РассчитатьРасходТопливаПоНорме();
		
		РассчитатьНормуРасходаТоплива();
		
		УстановитьТЧПомощники();	
		
		IDСтроки = ТекущиеДанные.IDСтроки;
	
		Если НЕ ТекущиеДанные.ЕстьПомощник Тогда 		
			
			IDСтроки = ТекущиеДанные.IDСтроки;
			
			ПутевыеЛистыПриОкончанииРедактированияНаСервере(IDСтроки); 	
			
		КонецЕсли;
		
		Для Каждого СтрокаТабличнойЧасти Из Объект.ПутевыеЛисты Цикл 		
			Объект.ОтработаноДней = Объект.ОтработаноДней + СтрокаТабличнойЧасти.ОтработаноДней;		
		КонецЦикла; 	
			
		ОбновитьИтоги(ЭтаФорма);   
	КонецЕсли;  	

КонецПроцедуры

&НаСервере
Процедура ПутевыеЛистыПриОкончанииРедактированияНаСервере(IDСтроки)
			
	МассивСтрок = Объект.Помощники.НайтиСтроки(Новый Структура("IDСтроки", IDСтроки));
	Для Каждого СтрокаРаздела Из МассивСтрок Цикл
		Объект.Помощники.Удалить(СтрокаРаздела); 
	КонецЦикла;     

КонецПроцедуры

&НаКлиенте
Процедура ПутевыеЛистыПередУдалением(Элемент, Отказ)
	
	IDСтроки = Элемент.ТекущиеДанные.IDСтроки;
	
	ПутевыеЛистыПередУдалениемНаСервере(IDСтроки); 	

КонецПроцедуры

&НаСервере
Процедура ПутевыеЛистыПередУдалениемНаСервере(IDСтроки)
	
	МассивСтрок = Объект.Помощники.НайтиСтроки(Новый Структура("IDСтроки", IDСтроки));
	Для Каждого СтрокаРаздела Из МассивСтрок Цикл
		Объект.Помощники.Удалить(СтрокаРаздела); 
	КонецЦикла;     	
	
КонецПроцедуры

&НаКлиенте
Процедура ПутевыеЛистыПослеУдаления(Элемент)
	
	Если Объект.ПутевыеЛисты.Количество() = 0 Тогда
		Объект.Помощники.Очистить();
		Возврат;
	КонецЕсли; 	
	
	ОбновитьИтоги(ЭтаФорма);   
	
КонецПроцедуры

&НаКлиенте
Процедура ПутевыеЛистыДатаДокументаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ПутевыеЛисты.ТекущиеДанные;
	
	Если ТекущиеДанные.ДатаДокумента < Объект.ПериодРегистрации ИЛИ ТекущиеДанные.ДатаДокумента > КонецМесяца(Объект.ПериодРегистрации) Тогда
		ТекстСообщения = Нстр("ru='Технологическая операция должна быть в пределах одного месяца!';uk='Технологічна операція повинна бути в межах одного місяця!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		ТекущиеДанные.ДатаДокумента = Дата("00010101");
		Возврат;
	КонецЕсли;
	
	Если (НЕ ЗначениеЗаполнено(ТекущиеДанные.ДатаПо)) ИЛИ ТекущиеДанные.ДатаПо < ТекущиеДанные.ДатаДокумента Тогда 
		
		ТекущиеДанные.ДатаПо         = ТекущиеДанные.ДатаДокумента;
		ТекущиеДанные.ОтработаноДней = 1;
		
	Иначе
		СписокСотрудников = Новый СписокЗначений;
		СписокСотрудников.Добавить(Объект.Водитель);
		
		СтруктураСтроки = Новый Структура("Сотрудник, ДатаДокумента, ДатаПо, 
		|ОтработаноДней, Время, Организация,ОсновноеЗначение"); 
		СтруктураСтроки.Организация = Объект.Организация;
		
		ЗаполнитьЗначенияСвойств(СтруктураСтроки, ТекущиеДанные);
		ПолучитьНормуВремениПоСотруднику(СписокСотрудников, СтруктураСтроки);
		ЗаполнитьЗначенияСвойств(ТекущиеДанные, СтруктураСтроки);
		Если НЕ СтруктураСтроки.ОсновноеЗначение = Неопределено Тогда 
			ТекущиеДанные.ОтработаноДней		= СтруктураСтроки.ОсновноеЗначение;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.ТехнологическаяОперация) Тогда
		РассчитатьЗатратыПоНорме(ТекущиеДанные);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПутевыеЛистыДатаПоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ПутевыеЛисты.ТекущиеДанные;
	
	Если ТекущиеДанные.ДатаПо < Объект.ПериодРегистрации ИЛИ ТекущиеДанные.ДатаПо > КонецМесяца(Объект.ПериодРегистрации) Тогда
		ТекстСообщения = Нстр("ru='Технологическая операция должна быть в пределах одного месяца!';uk='Технологічна операція повинна бути в межах одного місяця!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		ТекущиеДанные.ДатаПо = Дата("00010101");
		Возврат;
	КонецЕсли;
	
	Если (НЕ ЗначениеЗаполнено(ТекущиеДанные.ДатаПо)) ИЛИ ТекущиеДанные.ДатаПо < ТекущиеДанные.ДатаДокумента Тогда 
		
		ТекущиеДанные.ДатаПо         = ТекущиеДанные.ДатаДокумента;
		ТекущиеДанные.ОтработаноДней = 1;
		
	Иначе 		
		СписокСотрудников = Новый СписокЗначений;
		СписокСотрудников.Добавить(Объект.Водитель);
		
		СтруктураСтроки = Новый Структура("Сотрудник, ДатаДокумента, ДатаПо, 
		|ОтработаноДней, Время, Организация,ОсновноеЗначение"); 
		СтруктураСтроки.Организация = Объект.Организация;
		
		ЗаполнитьЗначенияСвойств(СтруктураСтроки, ТекущиеДанные);
		ПолучитьНормуВремениПоСотруднику(СписокСотрудников, СтруктураСтроки);
		ЗаполнитьЗначенияСвойств(ТекущиеДанные, СтруктураСтроки);
		Если НЕ СтруктураСтроки.ОсновноеЗначение = Неопределено Тогда 
			ТекущиеДанные.ОтработаноДней		= СтруктураСтроки.ОсновноеЗначение;
		КонецЕсли;
	КонецЕсли;
	Если ЗначениеЗаполнено(ТекущиеДанные.ТехнологическаяОперация) Тогда
		РассчитатьЗатратыПоНорме(ТекущиеДанные);
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПутевыеЛистыТехнологическаяОперацияПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ПутевыеЛисты.ТекущиеДанные;
	
	ЗаполнитьСтрокуТаблицыПутевыеЛисты(ТекущиеДанные);
	
	РассчитатьЗатратыПоНорме(ТекущиеДанные);
	
КонецПроцедуры 

&НаКлиенте
Процедура ПутевыеЛистыАгрегатПриИзменении(Элемент)

	ТекущиеДанные = Элементы.ПутевыеЛисты.ТекущиеДанные;
	
	ЗаполнитьСтрокуТаблицыПутевыеЛисты(ТекущиеДанные);
	
	РассчитатьЗатратыПоНорме(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ПутевыеЛистыСчетСписанияПриИзменении(Элемент)

	ТекущиеДанные = Элементы.ПутевыеЛисты.ТекущиеДанные;
	
	УстановитьЗаголовкиИДоступностьСубконтоТЧ(ЭтаФорма, ТекущиеДанные.СчетСписания, Истина, "", Истина);

	ПоляОбъекта = Новый Структура("Субконто1, Субконто2, Субконто3", "Субконто1", "Субконто2", "Субконто3");

	ПоляОбъекта.Вставить("Организация", Объект.Организация); 	
	
	БухгалтерскийУчетКлиентСервер.ПриИзмененииСчета(ТекущиеДанные.СчетСписания, ТекущиеДанные, ПоляОбъекта, Истина);
	
	ИзменитьПараметрыВыбораПолейСубконтоТЧ(ЭтаФорма);

	// Получим общие параметры обработки для реквизитов документа
	ПараметрыОбработки = ПодготовитьПараметрыОбработкиПутевыеЛисты(ЭтаФорма, ТекущиеДанные);
	
	// Дополнительные поля, требующиеся для заполнения добавленных колонок табличного поля текущей формы.
	ПараметрыОбработки.ДанныеСтрокиТаблицы.Вставить("Субконто1Доступность", ТекущиеДанные.Субконто1Доступность);
	ПараметрыОбработки.ДанныеСтрокиТаблицы.Вставить("Субконто2Доступность", ТекущиеДанные.Субконто2Доступность);
	ПараметрыОбработки.ДанныеСтрокиТаблицы.Вставить("Субконто3Доступность", ТекущиеДанные.Субконто3Доступность);  	
			
	ЗаполнитьДобавленныеКолонкиСтрокиТаблицыПутевыеЛисты(ПараметрыОбработки.ДанныеСтрокиТаблицы);
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ПараметрыОбработки.ДанныеСтрокиТаблицы);
	
	ПроверкаВидаСубконтоСчетаЗатрат(ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура ПутевыеЛистыСубконтоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ПутевыеЛисты.ТекущиеДанные;

	ИзменитьПараметрыВыбораПолейСубконтоТЧ(ЭтаФорма);

	// Получим общие параметры обработки для реквизитов документа
	ПараметрыОбработки = ПодготовитьПараметрыОбработкиПутевыеЛисты(ЭтаФорма, ТекущиеДанные);
	
	// Дополнительные поля, требующиеся для заполнения добавленных колонок табличного поля текущей формы.
	ПараметрыОбработки.ДанныеСтрокиТаблицы.Вставить("Субконто1Доступность", ТекущиеДанные.Субконто1Доступность);
	ПараметрыОбработки.ДанныеСтрокиТаблицы.Вставить("Субконто2Доступность", ТекущиеДанные.Субконто2Доступность);
	ПараметрыОбработки.ДанныеСтрокиТаблицы.Вставить("Субконто3Доступность", ТекущиеДанные.Субконто3Доступность); 	
		
	ЗаполнитьДобавленныеКолонкиСтрокиТаблицыПутевыеЛисты(ПараметрыОбработки.ДанныеСтрокиТаблицы);
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ПараметрыОбработки.ДанныеСтрокиТаблицы);
	
	Если ПолучитьЗначениеРеквизита(ТекущиеДанные.СчетСписания, "УчетПоНалоговымНазначениямНДС") Тогда 
		
		УстановитьНалоговоеНазаначениеВТЧ(ТекущиеДанные);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПутевыеЛистыСубконтоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	СтрокаТаблицы = Элементы.ПутевыеЛисты.ТекущиеДанные;

	ПараметрыДокумента = СписокПараметровВыбораСубконто(ЭтаФорма, СтрокаТаблицы, "Субконто%Индекс%");
	ПараметрыДокумента.Вставить("СчетУчета", СтрокаТаблицы.СчетСписания);
	ОбщегоНазначенияБПКлиент.НачалоВыбораЗначенияСубконто(ЭтаФорма, Элемент, СтандартнаяОбработка, ПараметрыДокумента);

КонецПроцедуры   

&НаКлиенте
Процедура ПутевыеЛистыЧасовПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ПутевыеЛисты.ТекущиеДанные;
	
	РассчитатьСменныхНорм(ТекущиеДанные); 
	
	РассчитатьЗатратыПоНорме(ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура ПутевыеЛистыЧасовНочныхПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ПутевыеЛисты.ТекущиеДанные;
	
	РассчитатьЗатратыПоНорме(ТекущиеДанные);    

КонецПроцедуры

&НаКлиенте
Процедура ПутевыеЛистыЧасовПраздничныхПриИзменении(Элемент)
		
	ТекущиеДанные = Элементы.ПутевыеЛисты.ТекущиеДанные;
	
	РассчитатьЗатратыПоНорме(ТекущиеДанные); 
	
КонецПроцедуры

&НаКлиенте
Процедура ПутевыеЛистыНормаВыработкиПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ПутевыеЛисты.ТекущиеДанные;
	
	РассчитатьСменныхНорм(ТекущиеДанные);
	
	РассчитатьЗатратыПоНорме(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ПутевыеЛистыСменнаяЭталоннаяВыработкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ПутевыеЛисты.ТекущиеДанные;
	
	РассчитатьЗатратыПоНорме(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ПутевыеЛистыРасценкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ПутевыеЛисты.ТекущиеДанные;
	
	РассчитатьЗатратыПоНорме(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ПутевыеЛистыВыполненоВНатуреПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ПутевыеЛисты.ТекущиеДанные;	
		
	РассчитатьСменныхНорм(ТекущиеДанные);
	
	РассчитатьЗатратыПоНорме(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ПутевыеЛистыСменныхНормПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ПутевыеЛисты.ТекущиеДанные;
	
	РассчитатьЗатратыПоНорме(ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПомощники

&НаКлиенте
Процедура ПомощникиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	ТекущиеДанные = Элементы.ПутевыеЛисты.ТекущиеДанные;
	
	ТекущаяДата = ?(Объект.СводныйПутевойЛист, ТекущиеДанные.ДатаДокумента, Объект.Дата);
	
	Если ТекущиеДанные <> Неопределено Тогда 		
				
		СведенияОТехОперации = ПолучитьСтруктуруРасценокВидаРабот(
			ТекущаяДата,
			ТекущиеДанные.ТехнологическаяОперация,
			ТекущиеДанные.Агрегат);															
		
		Если СведенияОТехОперации.Свойство("ТехнологическаяОперация") Тогда
			Если СведенияОТехОперации.Помощник = Ложь Тогда
				Отказ = Истина;
				ТекстСообщения = НСтр("ru='Для данной технологической операции помощники не предусмотрены!';uk='Для даної технологічної операції помічники не передбачені!'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;   	

КонецПроцедуры

&НаКлиенте
Процедура ПомощникиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элементы.ПутевыеЛисты.ТекущиеДанные;
	
	ТекущаяДата = ?(Объект.СводныйПутевойЛист, ТекущиеДанные.ДатаДокумента, Объект.Дата); 
	
	Если ТекущиеДанные <> Неопределено И НоваяСтрока Тогда 
		
		Элемент.ТекущиеДанные.IDСтроки = ТекущиеДанные.IDСтроки;
																								 
		СведенияОТехОперации = ПолучитьСтруктуруРасценокВидаРабот(
			ТекущаяДата,
			ТекущиеДанные.ТехнологическаяОперация,
			ТекущиеДанные.Агрегат);																							 

		Если СведенияОТехОперации.Свойство("ТехнологическаяОперация") Тогда 
			Элемент.ТекущиеДанные.ПроцентОплаты   = СведенияОТехОперации.ПроцентОплатыПомощников;			
			Элемент.ТекущиеДанные.ОплатаПомощника = (ТекущиеДанные.ОплатаОсновная + ТекущиеДанные.ОплатаДополнительная - ТекущиеДанные.ДоплатаЗаКлассность) * (Элемент.ТекущиеДанные.ПроцентОплаты / 100);
		КонецЕсли;
		
	КонецЕсли;  	

КонецПроцедуры

&НаКлиенте
Процедура ПомощникиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ОбновитьИтоги(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ПомощникиПослеУдаления(Элемент)
	
	ОбновитьИтоги(ЭтаФорма);
	
КонецПроцедуры 

&НаКлиенте
Процедура ПомощникиПроцентОплатыПриИзменении(Элемент)
	
	ТекущиеДанныеПутевыеЛисты = Элементы.ПутевыеЛисты.ТекущиеДанные;
	ТекущиеДанныеПомощники    = Элементы.Помощники.ТекущиеДанные;
	
	ТекущиеДанныеПомощники.ОплатаПомощника = (ТекущиеДанныеПутевыеЛисты.ОплатаОсновная +
											  ТекущиеДанныеПутевыеЛисты.ОплатаДополнительная -
											  ТекущиеДанныеПутевыеЛисты.ДоплатаЗаКлассность) *
											  (ТекущиеДанныеПомощники.ПроцентОплаты / 100);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовСтраницыТопливо

&НаКлиенте
Процедура ВыделитьПерерасходТопливаПриИзменении(Элемент)
	
	Если НЕ Объект.ВыделитьПерерасходТоплива Тогда
		Объект.СчетПерерасхода = Неопределено; 	
		УстановитьЗаголовкиИДоступностьСубконто(ЭтаФорма, Объект.СчетПерерасхода, Истина, "");	
	КонецЕсли;

	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетПерерасходаПриИзменении(Элемент)
	
	УстановитьЗаголовкиИДоступностьСубконто(ЭтаФорма, Объект.СчетПерерасхода, Истина, "");
	
	ПоляОбъекта = Новый Структура("Субконто1, Субконто2, Субконто3",
		"Субконто1", "Субконто2", "Субконто3"); 	
	ПоляОбъекта.Вставить("Организация", Объект.Организация);
	БухгалтерскийУчетКлиентСервер.ПриИзмененииСчета(Объект.СчетПерерасхода, Объект, ПоляОбъекта);
	
	ИзменитьПараметрыВыбораПолейСубконто(ЭтаФорма, "", "СчетПерерасхода");   	
	
	УправлениеФормой(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура СчетПерерасходаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = БухгалтерскийУчетКлиент.СчетМожноИспользоватьВПроводках(ВыбранноеЗначение);

КонецПроцедуры

&НаКлиенте
Процедура СубконтоПриИзменении(Элемент)

	ИзменитьПараметрыВыбораПолейСубконто(ЭтаФорма, "", "СчетПерерасхода");
	УправлениеФормой(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура СубконтоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	ПараметрыДокумента = СписокПараметровВыбораСубконто(ЭтаФорма, Объект, "Субконто%Индекс%");
	ПараметрыДокумента.Вставить("СчетУчета", Объект.СчетПерерасхода); 
	ОбщегоНазначенияБПКлиент.НачалоВыбораЗначенияСубконто(ЭтаФорма, Элемент, СтандартнаяОбработка, ПараметрыДокумента);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТопливо

&НаКлиенте
Процедура ТопливоПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	УстановкаПоказанийБака(); 
	
	ОбновитьДекорациюПоказанийБака();
	
КонецПроцедуры

&НаКлиенте
Процедура ТопливоТопливоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Топливо.ТекущиеДанные;
	
	ДанныеСтрокиТаблицы = Новый Структура("Топливо, ЕдиницаИзмерения, Основное"); 
	ЗаполнитьЗначенияСвойств(ДанныеСтрокиТаблицы, ТекущиеДанные);
	
	ДанныеОбъекта = Новый Структура("Дата, Организация, Автомобиль"); 		
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);
	
	ТопливоТопливоПриИзмененииНаСервере(ДанныеСтрокиТаблицы, ДанныеОбъекта);
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ДанныеСтрокиТаблицы);  
	
КонецПроцедуры  

&НаСервереБезКонтекста
Процедура ТопливоТопливоПриИзмененииНаСервере(СтрокаТабличнойЧасти, Знач ДанныеОбъекта)
	
	СведенияОНоменклатуре = БухгалтерскийУчетПереопределяемый.ПолучитьСведенияОНоменклатуре(СтрокаТабличнойЧасти.Топливо, ДанныеОбъекта);
	
	Если СведенияОНоменклатуре = Неопределено Тогда
		Возврат;
	КонецЕсли;

	СтрокаТабличнойЧасти.ЕдиницаИзмерения = СведенияОНоменклатуре.БазоваяЕдиницаИзмерения;
	
	Если ДанныеОбъекта.Автомобиль.Модель.ОсновноеТопливо = СтрокаТабличнойЧасти.Топливо Тогда
		СтрокаТабличнойЧасти.Основное = Истина;
	Иначе
		СтрокаТабличнойЧасти.Основное = Ложь;
	КонецЕсли;

КонецПроцедуры  

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТопливоИзрасходованное

&НаКлиенте
Процедура ТопливоИзрасходованноеПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ЗатратыТопливаПоФакту(); 		
		
	РассчитатьФактРасходаТоплива();
	
	УстановкаПоказанийБака(); 
	
	ОбновитьДекорациюПоказанийБака();
	
КонецПроцедуры 

&НаКлиенте
Процедура ТопливоИзрасходованноеПослеУдаления(Элемент)
	
	ЗатратыТопливаПоФакту();
	
	РассчитатьФактРасходаТоплива();  	
	
КонецПроцедуры

&НаКлиенте
Процедура ТопливоИзрасходованноеТопливоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ТопливоИзрасходованное.ТекущиеДанные;
	
	ДанныеСтрокиТаблицы = Новый Структура("Топливо, ЕдиницаИзмерения, Основное"); 
	ЗаполнитьЗначенияСвойств(ДанныеСтрокиТаблицы, ТекущиеДанные);
	
	ДанныеОбъекта	= Новый Структура("Дата, Организация, Автомобиль"); 		
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);

	ТопливоИзрасходованноеТопливоПриИзмененииНаСервере(ДанныеСтрокиТаблицы, ДанныеОбъекта);
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ДанныеСтрокиТаблицы);  

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ТопливоИзрасходованноеТопливоПриИзмененииНаСервере(СтрокаТабличнойЧасти, Знач ДанныеОбъекта)
	
	СведенияОНоменклатуре = БухгалтерскийУчетПереопределяемый.ПолучитьСведенияОНоменклатуре(СтрокаТабличнойЧасти.Топливо, ДанныеОбъекта);
	
	Если СведенияОНоменклатуре = Неопределено Тогда
		Возврат;
	КонецЕсли;

	СтрокаТабличнойЧасти.ЕдиницаИзмерения = СведенияОНоменклатуре.БазоваяЕдиницаИзмерения;
	
	Если ДанныеОбъекта.Автомобиль.Модель.ОсновноеТопливо = СтрокаТабличнойЧасти.Топливо Тогда
		СтрокаТабличнойЧасти.Основное = Истина;
	Иначе
		СтрокаТабличнойЧасти.Основное = Ложь;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовСтраницыДополнительно

&НаКлиенте
Процедура БакПослеПриИзменении(Элемент)
	
	ОбновитьДекорациюПоказанийБака(); 	
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьПоказанияБака(Команда)
	
	Объект.БакПосле = Объект.БакДо + ПолучитьИзрасходованноеТопливо();
	
	ОбновитьДекорациюПоказанийБака();
	
КонецПроцедуры  

#КонецОбласти  

#Область ОбработчикиСобытийЭлементовТаблицыФормыНадбавки

&НаКлиенте
Процедура НадбавкиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	РассчитатьРасходТопливаПоНорме();
	
	РассчитатьНормуРасходаТоплива();
	
КонецПроцедуры

&НаКлиенте
Процедура НадбавкиПередУдалением(Элемент, Отказ)
	
	РассчитатьРасходТопливаПоНорме();
	
	РассчитатьНормуРасходаТоплива(); 
	
КонецПроцедуры

&НаКлиенте
Процедура НадбавкиНадбавкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Надбавки.ТекущиеДанные; 
		
	Если ПолучитьЗначениеРеквизита(ТекущиеДанные.Надбавка, "ВПроцентах") Тогда
		ТекущиеДанные.ЕД = "%";
	Иначе 
		ТекущиеДанные.ЕД = "л.";
	КонецЕсли;
	
	ТекущиеДанные.Размер = ПолучитьЗначениеРеквизита(ТекущиеДанные.Надбавка, "РазмерНадбавки");
	
КонецПроцедуры   

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНачисления

&НаКлиенте
Процедура НачисленияПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ОбновитьИтоги(ЭтаФорма);    

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подбор(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.Автомобиль) Тогда
		ТекстСообщения = НСтр("ru='Не заполнена Сельхохзтехника!';uk='Не заповнена Сільгосптехніка!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;

	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Дата",                Объект.Дата);
	ПараметрыОтбора.Вставить("МаркаСельхозтехники", ПолучитьЗначениеРеквизита(Объект.Автомобиль, "Модель"));  
		
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", 	          ПараметрыОтбора);
	ПараметрыФормы.Вставить("РежимВыбора",        Истина); 
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
				
	ОткрытьФорму("РегистрСведений.ИНАГРО_ВидыРаботСельхозтехники.Форма.ФормаПодбора", ПараметрыФормы, ЭтаФорма);	

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТопливо(Команда)
	
	Если Объект.ТопливоВыданное.Количество() > 0 Тогда
		ТекстВопроса = Нстр("ru='Табличная часть будет очищена. Продолжить?';uk='таблична частина буде очищена. Продовжити?'");
		Оповещение = Новый ОписаниеОповещения("ЗаполнитьТопливоЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да); 		
	Иначе		
		ЗаполнитьТопливоНаСервере(); 
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТопливоЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
   	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ТопливоВыданное.Очистить();
	Объект.ЗатратыТопливаПоФакту = 0;
	
	ЗаполнитьТопливоНаСервере();

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТопливоНаСервере()
	
	Для Каждого Строка Из Объект.Автомобиль.Модель.ИспользуемоеТопливо Цикл
		
		НоваяСтрока = Объект.ТопливоВыданное.Добавить();
		НоваяСтрока.Топливо          = Строка.Топливо;
		НоваяСтрока.ЕдиницаИзмерения = НоваяСтрока.Топливо.БазоваяЕдиницаИзмерения;
		НоваяСтрока.Коэффициент      = Строка.Коэффициент;
		НоваяСтрока.Основное         = Строка.Топливо = Объект.Автомобиль.Модель.ОсновноеТопливо; 
		
	КонецЦикла; 
	
КонецПроцедуры   

&НаКлиенте
Процедура ЗаполнитьТопливоИзрасходованное(Команда)
	
	Если Объект.ТопливоИзрасходованное.Количество() > 0 Тогда
		ТекстВопроса = Нстр("ru='Табличная часть будет очищена. Продолжить?';uk='таблична частина буде очищена. Продовжити?'");
		Оповещение = Новый ОписаниеОповещения("ЗаполнитьТопливоИзрасходованноеЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	Иначе
		ЗаполнитьТопливоИзрасходованноеНаСервере();   
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТопливоИзрасходованноеЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
   	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ТопливоИзрасходованное.Очистить();	
	
	ЗаполнитьТопливоИзрасходованноеНаСервере();

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТопливоИзрасходованноеНаСервере()
	
	Для Каждого Строка Из Объект.Автомобиль.Модель.ИспользуемоеТопливо Цикл
		
		НоваяСтрока = Объект.ТопливоИзрасходованное.Добавить();
		НоваяСтрока.Топливо          = Строка.Топливо;
		НоваяСтрока.ЕдиницаИзмерения = НоваяСтрока.Топливо.БазоваяЕдиницаИзмерения;
		НоваяСтрока.Коэффициент      = Строка.Коэффициент;
		НоваяСтрока.Основное         = Строка.Топливо = Объект.Автомобиль.Модель.ОсновноеТопливо; 
		
	КонецЦикла; 
	
	ИтогПоФакту = Объект.ПутевыеЛисты.Итог("РасходФактический");  	
	
	Если Объект.ТопливоИзрасходованное.Количество() > 0 Тогда
		Объект.ТопливоИзрасходованное[0].Количество = ИтогПоФакту;
	КонецЕсли; 
	
	Объект.ЗатратыТопливаПоФакту = ИтогПоФакту;
	
КонецПроцедуры   

&НаКлиенте
Процедура ЗаполнитьВыпускПродукции(Команда)
	
	ЗаполнитьВыпускПродукцииНаСервере();

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВыпускПродукцииНаСервере()

	ДокументОбъект = РеквизитФормыВЗначение("Объект");

	Попытка 
		ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
	Исключение
		ТекстСообщения = НСтр("ru='Не удалось записать документ';uk='Не вдалося записати документ'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецПопытки; 
	
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект"); 	
	
	Документы.ИНАГРО_ПутевойЛистТрактористаМашиниста.ЗаполнитьТЧВыпускПродукции(Объект);

КонецПроцедуры   

&НаКлиенте
Процедура ЗаполнитьНачисления(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Водитель) Тогда
		ТекстСообщения = НСтр("ru='Перед началом заполнения табличной части необходимо выбрать водителя';uk='Перед початком заповнення табличної частини необхідно обрати водія'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		ТекстСообщения = НСтр("ru='Перед началом заполнения табличной части необходимо выбрать организацию';uk='Перед початком заповнення табличної частини необхідно обрати організацію'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;

	Если Объект.Начисления.Количество()>0 Тогда
		
		ТекстВопроса =НСтр("ru='Перед заполнением табличная часть ""Начисления"" будет очищена. Продолжить?';uk='Перед заповненням таблична частина ""Нарахування"" буде очищена. Продовжити?'");
		Обработчик = Новый ОписаниеОповещения("АвтозаполнениеНачисленийПослеОтветаНаВопрос", ЭтотОбъект);
		ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
		Возврат;
	КонецЕсли;
	//
	ЗаполнитьНачисленияНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНачисленияНаСервере()
	
	Документ = РеквизитФормыВЗначение("Объект");
	Документ.Записать();
  	Документ.АвтозаполнениеНачисления();
  	ЗначениеВРеквизитФормы(Документ, "Объект");	
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьНачисления(Команда)
	
	Если Объект.Проведен Тогда 
		ТекстВопроса =НСтр("ru='Автоматически рассчитать документ можно только после отмены его проведения. Выполнить отмену проведения документа?';uk='Автоматично розрахувати документ можна тільки після скасування його проведення. Виконати скасування проведення документа?'");
		Обработчик = Новый ОписаниеОповещения("АвторасчетНачисленияПослеОтветаНаВопросПровести", ЭтотОбъект);
		ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
		Возврат;
	ИначеЕсли Модифицированность Тогда
		ТекстВопроса =НСтр("ru='Автоматически рассчитать документ можно только после его записи. Выполнить запись документа?';uk='Автоматично розрахувати документ можна тільки після його запису. Виконати запис документа?'");
		Обработчик = Новый ОписаниеОповещения("АвторасчетНачисленияПослеОтветаНаВопросПровести", ЭтотОбъект);
		ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
		Возврат;
	КонецЕсли;

	РассчитатьНачисленияНаСервере();
	
	ОбновитьИтоги(ЭтаФорма);  
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьНачисленияНаСервере()
	
	Документ = РеквизитФормыВЗначение("Объект");
	Документ.Записать(РежимЗаписиДокумента.ОтменаПроведения);
  	Документ.РассчитатьНачисления(Объект.Водитель);
  	ЗначениеВРеквизитФормы(Документ, "Объект");	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРассчитатьНачисления(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Водитель) Тогда
		ТекстСообщения = НСтр("ru='Перед началом заполнения табличной части необходимо выбрать водителя';uk='Перед початком заповнення табличної частини необхідно обрати водія'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		ТекстСообщения = НСтр("ru='Перед началом заполнения табличной части необходимо выбрать организацию';uk='Перед початком заповнення табличної частини необхідно обрати організацію'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;

	Если Объект.Начисления.Количество()>0 Тогда
		
		ТекстВопроса =НСтр("ru='Перед заполнением табличная часть ""Начисления"" будет очищена. Продолжить?';uk='Перед заповненням таблична частина ""Нарахування"" буде очищена. Продовжити?'");
		Обработчик = Новый ОписаниеОповещения("АвтозаполнениеРассчетНачисленийПослеОтветаНаВопрос", ЭтотОбъект);
		ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
		Возврат;
	КонецЕсли;
	
	Если Объект.Проведен Тогда 
		ТекстВопроса =НСтр("ru='Автоматически рассчитать документ можно только после отмены его проведения. Выполнить отмену проведения документа?';uk='Автоматично розрахувати документ можна тільки після скасування його проведення. Виконати скасування проведення документа?'");
		Обработчик = Новый ОписаниеОповещения("АвтозаполнениеРассчетНачисленияПослеОтветаНаВопросПровести", ЭтотОбъект);
		ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
		Возврат;
	ИначеЕсли Модифицированность Тогда
		ТекстВопроса =НСтр("ru='Автоматически рассчитать документ можно только после его записи. Выполнить запись документа?';uk='Автоматично розрахувати документ можна тільки після його запису. Виконати запис документа?'");
		Обработчик = Новый ОписаниеОповещения("АвтозаполнениеРассчетНачисленияПослеОтветаНаВопросПровести", ЭтотОбъект);
		ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
		Возврат;
	КонецЕсли;
	
	ЗаполнитьРассчитатьНачисленияНаСервере();
	
	ОбновитьИтоги(ЭтаФорма); 
	
КонецПроцедуры  

&НаСервере
Процедура ЗаполнитьРассчитатьНачисленияНаСервере()
	
	Документ = РеквизитФормыВЗначение("Объект");
	Документ.Записать(РежимЗаписиДокумента.ОтменаПроведения);
  	Документ.АвтозаполнениеНачисления();
  	Документ.РассчитатьНачисления(Объект.Водитель);
	
  	ЗначениеВРеквизитФормы(Документ, "Объект");	
	
КонецПроцедуры

#КонецОбласти    

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьФормуНаСервере()

	УстановитьФункциональныеОпцииФормы();
	
	ТекущаяДатаДокумента = Объект.Дата;	 		
	
	ЗаполнитьСведенияОТС();
	
	УстановкаПоказанийБака();	
	
	ОбновитьДекорациюПоказанийБака(); 		
	
	УстановитьЗаголовкиИДоступностьСубконто(ЭтаФорма, Объект.СчетПерерасхода, Истина, ""); 
	
	ЗаполнитьДобавленныеКолонкиТаблицыПутевыеЛисты();
		
	УправлениеФормой(ЭтаФорма);
	
	УстановитьСостояниеДокумента();

КонецПроцедуры

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы()

	ОбщегоНазначенияБПКлиентСервер.УстановитьПараметрыФункциональныхОпцийФормыДокумента(ЭтаФорма);  	
			
	ПлательщикНДС = УчетнаяПолитика.ПлательщикНДС(Объект.Организация, Объект.Дата);
		
	ПараметрыТехники = ПолучитьПараметрыТехники(Объект.Дата, Объект.Организация);  	
		
КонецПроцедуры 

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)

	Элементы = Форма.Элементы;
	Объект	 = Форма.Объект; 		
	
	Если ЗначениеЗаполнено(Объект.Водитель) Тогда
		Элементы.ДокументОснование.Доступность = ПолучитьЗначениеРеквизита(Объект.Водитель, "ИНАГРО_ДоговорПодряда"); 
	КонецЕсли; 	
	
	Элементы.ПутевыеЛистыДатаДокумента.Видимость = Объект.СводныйПутевойЛист;
	Элементы.ПутевыеЛистыДатаПо.Видимость        = Объект.СводныйПутевойЛист;
	
	Если Объект.ПутевыеЛисты.Количество() = 0 Тогда
		Элементы.ГруппаПомощники.Видимость = Ложь;
	КонецЕсли;    	 
	
	Элементы.СчетПерерасхода.Доступность = Объект.ВыделитьПерерасходТоплива; 
	
	Элементы.ГруппаВыпускПродукции.Видимость = ?(Форма.ПараметрыТехники.РедактироватьАналитикуВыпускаСельхозтехника = Неопределено, Ложь,
												 Форма.ПараметрыТехники.РедактироватьАналитикуВыпускаСельхозтехника);										 
	
	ОбновитьИтоги(Форма);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияБП.СостояниеДокумента(Объект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовкиИДоступностьСубконто(Форма, Счет, ЕстьПодразделение, Постфикс = "")

	ПоляФормы = Новый Структура("Субконто1, Субконто2, Субконто3",
		"Субконто" + Постфикс + "1",
		"Субконто" + Постфикс + "2",
		"Субконто" + Постфикс + "3");
		
	Если ЕстьПодразделение Тогда
		ПоляФормы.Вставить("Подразделение", "ПодразделениеЗатрат" + Постфикс);
	КонецЕсли;
	
	ЗаголовкиПолей = Новый Структура("Субконто1, Субконто2, Субконто3",
		"ЗаголовокСубконто" + Постфикс + "1",
		"ЗаголовокСубконто" + Постфикс + "2",
		"ЗаголовокСубконто" + Постфикс + "3");
	
	Если ЕстьПодразделение Тогда
		ЗаголовкиПолей.Вставить("Подразделение", "ЗаголовокПодразделение" + Постфикс);
	КонецЕсли;
	
	БухгалтерскийУчетКлиентСервер.ПриВыбореСчета(Счет, Форма, ПоляФормы, ЗаголовкиПолей);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовкиИДоступностьСубконтоТЧ(Форма, Счет, ЕстьПодразделение, Постфикс = "", ЭтоТаблица = Ложь);
		
	ПоляФормы = Новый Структура("Субконто1, Субконто2, Субконто3",
								"ПутевыеЛистыСубконто" + Постфикс + "1",
								"ПутевыеЛистыСубконто" + Постфикс + "2",
								"ПутевыеЛистыСубконто" + Постфикс + "3"); 

	БухгалтерскийУчетКлиентСервер.ПриВыбореСчета(Счет, Форма, ПоляФормы, Неопределено, ЭтоТаблица);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ИзменитьПараметрыВыбораПолейСубконто(Форма, Суффикс, ИмяСчета)

	Объект = Форма.Объект;
	
	ПараметрыДокумента = СписокПараметровВыбораСубконто(Форма, Форма.Объект, "Субконто" + Суффикс + "%Индекс%");
	ПараметрыДокумента.Вставить("СчетУчета", Объект.СчетПерерасхода);
	
	БухгалтерскийУчетКлиентСервер.ИзменитьПараметрыВыбораПолейСубконто(
		Форма, 
		Форма.Объект, 
		"Субконто" + Суффикс + "%Индекс%", 
		"Субконто" + Суффикс + "%Индекс%", 
		ПараметрыДокумента);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ИзменитьПараметрыВыбораПолейСубконтоТЧ(Форма)
	
	Если Форма.Элементы.ПутевыеЛисты.ТекущаяСтрока <> Неопределено Тогда 
		СтрокаТаблицы = Форма.Объект.ПутевыеЛисты.НайтиПоИдентификатору(Форма.Элементы.ПутевыеЛисты.ТекущаяСтрока);
		ПараметрыДокумента = СписокПараметровВыбораСубконто(Форма, СтрокаТаблицы, "Субконто%Индекс%");
		ПараметрыДокумента.Вставить("СчетУчета", СтрокаТаблицы.СчетСписания);
		БухгалтерскийУчетКлиентСервер.ИзменитьПараметрыВыбораПолейСубконто(Форма, СтрокаТаблицы, "Субконто%Индекс%", "ПутевыеЛистыСубконто%Индекс%", ПараметрыДокумента);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНалоговоеНазаначениеВТЧ(ТекущиеДанные)
	
	ХарактерЗатрат = УправлениеПроизводствомВызовСервера.ПолучитьХарактерЗатратПоСчетуЗатрат(ТекущиеДанные.СчетСписания, ,Объект.Дата); 			
	НалоговоеНазначение = Неопределено;
	
	СтруктураСубконто = Новый Структура("Субконто1,Субконто2,Субконто3", ТекущиеДанные.Субконто1, ТекущиеДанные.Субконто2, ТекущиеДанные.Субконто3);
	НалоговыйУчет.ОпределениеАналитикиНалоговогоУчетаВПроводкахДляЗатрат(СтруктураСубконто, ТекущиеДанные.СчетСписания, ХарактерЗатрат,
																		 НалоговоеНазначение, , , , , Объект.Дата, ,ПлательщикНДС);
	
	ТекущиеДанные.НалоговоеНазначение = НалоговоеНазначение; 	

КонецПроцедуры

&НаКлиенте
Процедура УстановитьТЧПомощники()
	
	Если Объект.ПутевыеЛисты.Количество() = 0 Тогда
		Элементы.ГруппаПомощники.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.ПутевыеЛисты.ТекущиеДанные;
	
	Если ТекущиеДанные.ЕстьПомощник Тогда
		Элементы.ГруппаПомощники.Видимость = Истина;  
	Иначе
		Элементы.ГруппаПомощники.Видимость = Ложь; 
	КонецЕсли; 	
	
	Элементы.Помощники.ОтборСтрок = Новый ФиксированнаяСтруктура("IDСтроки", ТекущиеДанные.IDСтроки); 	
		
КонецПроцедуры

&НаКлиенте
Функция ПодготовитьПараметрыОбработкиПутевыеЛисты(Форма, СтрокаТабличнойЧасти)
	
	ДанныеСтрокиТаблицы = Новый Структура("СчетСписания, Субконто1, Субконто2, Субконто3");
		
	ЗаполнитьЗначенияСвойств(ДанныеСтрокиТаблицы, СтрокаТабличнойЧасти);
	
	ДанныеОбъекта = Новый Структура("Дата, Организация, ВидОперации, Склад"); 
		
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);
	
	ПараметрыОбработки = Новый Структура();
	ПараметрыОбработки.Вставить("ДанныеСтрокиТаблицы", ДанныеСтрокиТаблицы);
	ПараметрыОбработки.Вставить("ДанныеОбъекта", 	   ДанныеОбъекта);
	
	Возврат ПараметрыОбработки;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СписокПараметровВыбораСубконто(Форма, ПараметрыОбъекта, ШаблонИмяПоляОбъекта)

	СписокПараметров = Новый Структура;
	Для Индекс = 1 По 3 Цикл
		ИмяПоля = СтрЗаменить(ШаблонИмяПоляОбъекта, "%Индекс%", Индекс);
		Если ТипЗнч(ПараметрыОбъекта[ИмяПоля]) = Тип("СправочникСсылка.Контрагенты") Тогда
			СписокПараметров.Вставить("Контрагент", ПараметрыОбъекта[ИмяПоля]);
		ИначеЕсли ТипЗнч(ПараметрыОбъекта[ИмяПоля]) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
			СписокПараметров.Вставить("ДоговорКонтрагента", ПараметрыОбъекта[ИмяПоля]);
		ИначеЕсли ТипЗнч(ПараметрыОбъекта[ИмяПоля]) = Тип("СправочникСсылка.Номенклатура") Тогда
			СписокПараметров.Вставить("Номенклатура", ПараметрыОбъекта[ИмяПоля]);
		ИначеЕсли ТипЗнч(ПараметрыОбъекта[ИмяПоля]) = Тип("СправочникСсылка.Склады") Тогда
			СписокПараметров.Вставить("Склад", ПараметрыОбъекта[ИмяПоля]);
		КонецЕсли;
	КонецЦикла;
	СписокПараметров.Вставить("Организация", Форма.Объект.Организация);
		
	Возврат СписокПараметров;

КонецФункции 

&НаКлиенте
Процедура ПроверкаВидаСубконтоСчетаЗатрат(ТекущиеДанные)
	
	СвойстваСчетСписания = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(ТекущиеДанные.СчетСписания);
	
	Для Индекс = 1 По СвойстваСчетСписания.КоличествоСубконто Цикл
		Если СвойстваСчетСписания["ВидСубконто" + Индекс] = ПредопределенноеЗначение("ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат") Тогда
			ТекущиеДанные["Субконто" + Индекс] = ПараметрыТехники.СтатьяЗатратГСМ;			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДобавленныеКолонкиТаблицыПутевыеЛисты()	
	
	Для Каждого СтрокаТабличнойЧасти Из Объект.ПутевыеЛисты Цикл							
		ЗаполнитьДобавленныеКолонкиСтрокиТаблицыПутевыеЛисты(СтрокаТабличнойЧасти);
	КонецЦикла;	
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьДобавленныеКолонкиСтрокиТаблицыПутевыеЛисты(СтрокаТаблицы)
	
	ПоляОбъектаСубконто	= Новый Структура("Субконто1, Субконто2, Субконто3", "Субконто1", "Субконто2", "Субконто3");

	БухгалтерскийУчетКлиентСервер.УстановитьДоступностьСубконто(СтрокаТаблицы.СчетСписания, СтрокаТаблицы, ПоляОбъектаСубконто);
	
КонецПроцедуры   

&НаСервере
Процедура ЗаполнитьСведенияОТС()
	
	СвойстваТС = РегистрыСведений.ИНАГРО_Сельхозтехника.ПолучитьПоследнее(Объект.Дата, Новый Структура("ОсновноеСредство", Объект.Автомобиль));
	
	ТехХарактеристики = "Марка " + Объект.Автомобиль.Модель.Наименование + ", " + СвойстваТС.МассаТрактора; 											 
		
КонецПроцедуры

&НаСервере
Процедура УстановкаПоказанийБака()
	
	ПоказанияБака = ИНАГРО_Транспорт.ПолучитьПоказанияСпидометраИБака(Объект.Дата, Объект.Автомобиль, Объект.Ссылка);
	
	Объект.БакДо = ПоказанияБака.ПоказанияБака;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьЗатратыПоНорме(СтрокаТабличнойЧасти)
	
	ТекущаяДата = ?(Объект.СводныйПутевойЛист, СтрокаТабличнойЧасти.ДатаДокумента, Объект.Дата);																								 
																							 
	СведенияОТехОперации = ПолучитьСтруктуруРасценокВидаРабот(
			ТекущаяДата,
			СтрокаТабличнойЧасти.ТехнологическаяОперация,
			СтрокаТабличнойЧасти.Агрегат);																							 
	
			Если НЕ СведенияОТехОперации.Свойство("ТехнологическаяОперация") Тогда
				
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
			"ru='Не заполнены данные в регистре сведений ""Виды работ сельхозтехники"" по транспорту ""%1"" и виду работ ""%2""!';
			|uk='Не заповнені дані в регістрі відомостей ""Види робіт сільгосптехніки"" по транспорту ""%1"" та виду рабіт ""%2""!'"),
			Объект.Автомобиль, СтрокаТабличнойЧасти.ТехнологическаяОперация);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	СтрокаТабличнойЧасти.ОплатаОсновная       = 0;
	СтрокаТабличнойЧасти.ОплатаДополнительная = 0;
	
	Если НЕ ЗначениеЗаполнено(Объект.Автомобиль) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.ТехнологическаяОперация) Тогда
		Возврат;
	КонецЕсли;  	
		
	ПараметрыТехники = ПолучитьПараметрыТехники(ТекущаяДата, Объект.Организация);  
	
	// основная  
	
	БазаЗарплаты = 0; 
	
	Если СведенияОТехОперации.Свойство("ТехнологическаяОперация") Тогда 
		Если СведенияОТехОперации.РасценкаК = "КЧасам" Тогда
			БазаЗарплаты = СтрокаТабличнойЧасти.Расценка * СтрокаТабличнойЧасти.Часов;
			Если СтрокаТабличнойЧасти.Часов < 7 Тогда
				БазаЗарплаты = БазаЗарплаты - БазаЗарплаты * СведенияОТехОперации.УдержанияЗаНорму / 100;
			Иначе
				БазаЗарплаты = БазаЗарплаты + БазаЗарплаты * СведенияОТехОперации.НадбавкаЗаНорму / 100;
			КонецЕсли;
		ИначеЕсли СведенияОТехОперации.РасценкаК = "КВыпВНатуре" Тогда
			БазаЗарплаты = СтрокаТабличнойЧасти.Расценка * СтрокаТабличнойЧасти.ВыполненоВНатуре;
			Если СтрокаТабличнойЧасти.ВыполненоВНатуре < СтрокаТабличнойЧасти.НормаВыработки Тогда
				БазаЗарплаты = БазаЗарплаты - БазаЗарплаты * СведенияОТехОперации.УдержанияЗаНорму / 100;
			Иначе
				БазаЗарплаты = БазаЗарплаты + БазаЗарплаты * СведенияОТехОперации.НадбавкаЗаНорму / 100;
			КонецЕсли;
		Иначе
			БазаЗарплаты = СтрокаТабличнойЧасти.Расценка * СтрокаТабличнойЧасти.СменныхНорм;
			Если СтрокаТабличнойЧасти.СменныхНорм > 1 Тогда
				БазаЗарплаты = БазаЗарплаты + БазаЗарплаты * СведенияОТехОперации.НадбавкаЗаНорму / 100;
			Иначе
				БазаЗарплаты = БазаЗарплаты - БазаЗарплаты * СведенияОТехОперации.УдержанияЗаНорму / 100;
			КонецЕсли;
		КонецЕсли;  
	КонецЕсли;  
	
	СтрокаТабличнойЧасти.ОплатаОсновная = БазаЗарплаты;
	
	// За классность
	СтрокаТабличнойЧасти.ДоплатаЗаКлассность = 0;  	
	
	СвойстваВодителя = ПолучитьСвойстваВодителя(ТекущаяДата, Объект.Водитель);	
	
	Если ПараметрыТехники.ДоплачиватьВодителямЗаКлассность Тогда
		Если СвойстваВодителя.КлассностьВодителя = ПредопределенноеЗначение("Перечисление.ИНАГРО_КлассВодителя.ПервыйКласс") Тогда
			// За первый класс 20%
			СтрокаТабличнойЧасти.ДоплатаЗаКлассность = БазаЗарплаты * 0.2;             
		ИначеЕсли СвойстваВодителя.КлассностьВодителя = ПредопределенноеЗначение("Перечисление.ИНАГРО_КлассВодителя.ВторойКласс") Тогда
			// За второй класс 10%
			СтрокаТабличнойЧасти.ДоплатаЗаКлассность = БазаЗарплаты * 0.1;
		КонецЕсли;		
	КонецЕсли;
	
	СтрокаТабличнойЧасти.ОплатаОсновная = СтрокаТабличнойЧасти.ОплатаОсновная;
	СтрокаТабличнойЧасти.ОплатаДополнительная = СтрокаТабличнойЧасти.ОплатаДополнительная + СтрокаТабличнойЧасти.ДоплатаЗаКлассность;
	
	ЗаполнитьСведенияОТС();
	
	// дополнительная  	
	Если СвойстваТС.Гусеничный Тогда
		СтрокаТабличнойЧасти.ОплатаДополнительная = СтрокаТабличнойЧасти.ОплатаДополнительная + БазаЗарплаты * ПараметрыТехники.ПроцентДоплатыЗаГусеничность / 100;
	КонецЕсли;
	
	Если СведенияОТехОперации.Свойство("ТехнологическаяОперация") Тогда 
		Если СведенияОТехОперации.Яды Тогда
			СтрокаТабличнойЧасти.ОплатаДополнительная = СтрокаТабличнойЧасти.ОплатаДополнительная + БазаЗарплаты * СведенияОТехОперации.ПроцентЗаВредность / 100;
		КонецЕсли;
	КонецЕсли;
	
	Если СвойстваТС.ВидТранспорта = ПредопределенноеЗначение("Перечисление.ИНАГРО_ВидыТранспорта.Трактор") Тогда
		СтрокаТабличнойЧасти.ВыполненоУслГа = Окр(СтрокаТабличнойЧасти.СменнаяЭталоннаяВыработка * СтрокаТабличнойЧасти.СменныхНорм, 1);
	КонецЕсли;
	
	// премия за выполнение плана
	ДоплатаЗаВыполненииПлана = 0;
	Если ПараметрыТехники.ДоплачиватьПриВыполненииПлана > 0 Тогда
		Если СтрокаТабличнойЧасти.СменныхНорм >= 1 Тогда
			ДоплатаЗаВыполненииПлана = СтрокаТабличнойЧасти.ОплатаОсновная * ПараметрыТехники.ДоплачиватьПриВыполненииПлана / 100;
		КонецЕсли;
	КонецЕсли;
	СтрокаТабличнойЧасти.ОплатаОсновная = СтрокаТабличнойЧасти.ОплатаОсновная + ДоплатаЗаВыполненииПлана;
	
	// ночные
	ДоплатаЗаНочные = 0;
	Если (ПараметрыТехники.ДоплачиватьЗаНочные > 0) И (СтрокаТабличнойЧасти.ЧасовНочных > 0) Тогда
		ДоплатаЗаНочные = СтрокаТабличнойЧасти.ОплатаОсновная * (СтрокаТабличнойЧасти.ЧасовНочных) / СтрокаТабличнойЧасти.Часов * ПараметрыТехники.ДоплачиватьЗаНочные / 100;
	КонецЕсли;
	
	// праздничные
	ДоплатаЗаПраздничные = 0;
	Если (ПараметрыТехники.ДоплачиватьЗаПраздничные > 0) И (СтрокаТабличнойЧасти.ЧасовПраздничных > 0) Тогда
		ДоплатаЗаПраздничные = СтрокаТабличнойЧасти.ОплатаОсновная * (СтрокаТабличнойЧасти.ЧасовПраздничных) / СтрокаТабличнойЧасти.Часов * ПараметрыТехники.ДоплачиватьЗаПраздничные / 100;
	КонецЕсли;
	
	Если СведенияОТехОперации.Свойство("ТехнологическаяОперация") Тогда 
		Если СведенияОТехОперации.Процент > 0 Тогда
			СтрокаТабличнойЧасти.ОплатаОсновная = СтрокаТабличнойЧасти.ОплатаОсновная * СведенияОТехОперации.Процент;
		КонецЕсли;
	КонецЕсли;
	
	СтрокаТабличнойЧасти.ОплатаОсновная = СтрокаТабличнойЧасти.ОплатаОсновная + ДоплатаЗаПраздничные;
	СтрокаТабличнойЧасти.ОплатаДополнительная = СтрокаТабличнойЧасти.ОплатаДополнительная + ДоплатаЗаНочные;
	// помощники
	Если СтрокаТабличнойЧасти.ЕстьПомощник Тогда
		Для Каждого СтрокаТабличнойЧастиПом Из Объект.Помощники Цикл
			Если СтрокаТабличнойЧасти.IDСтроки = СтрокаТабличнойЧастиПом.IDСтроки Тогда
				СтрокаТабличнойЧастиПом.ОплатаПомощника = (СтрокаТабличнойЧасти.ОплатаОсновная + СтрокаТабличнойЧасти.ОплатаДополнительная - СтрокаТабличнойЧасти.ДоплатаЗаКлассность) * (СтрокаТабличнойЧастиПом.ПроцентОплаты / 100);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли; 	
		
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьНормуРасходаТоплива()
	
	Если Объект.ПутевыеЛисты.Количество() = 0 Тогда
		Объект.ЗатратыТопливаПоНорме = 0;
		Возврат;
	КонецЕсли;
	
	ИтогНорма = 0;
	
	Для Каждого СтрокаТабличнойЧасти Из Объект.ПутевыеЛисты Цикл
		ИтогНорма = ИтогНорма + СтрокаТабличнойЧасти.РасходНаРаботу;
	КонецЦикла;	
	
	Объект.ЗатратыТопливаПоНорме = ИтогНорма;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьФактРасходаТоплива() 
	
	// Расчитать факт расхода топлива для строк ТЧ ПутевыеЛисты
	
	Для Каждого СтрокаТабличнойЧасти Из Объект.ПутевыеЛисты Цикл
		Если СтрокаТабличнойЧасти.РасходТопливаВручную Тогда
			Продолжить;
		КонецЕсли; 
		СтрокаТабличнойЧасти.РасходФактический = 0;
	КонецЦикла;
	
	ЗатратыТопливаПоФакту        = 0;
	ЗатратыТопливаПоФактуПутевых = 0; 
	
	Для Каждого СтрокаТабличнойЧасти Из Объект.ТопливоИзрасходованное Цикл
		ЗатратыТопливаПоФактуПутевых = ЗатратыТопливаПоФактуПутевых + (СтрокаТабличнойЧасти.Количество / ?(СтрокаТабличнойЧасти.Коэффициент = 0, 1, СтрокаТабличнойЧасти.Коэффициент));
	КонецЦикла; 
	
	ЗатратыТопливаПоНормеПутевых = 0;
	ВычестьФактТоплива = 0;
	Для Каждого СтрокаТабличнойЧасти Из Объект.ПутевыеЛисты Цикл
		Если НЕ СтрокаТабличнойЧасти.РасходТопливаВручную Тогда
			ЗатратыТопливаПоНормеПутевых = ЗатратыТопливаПоНормеПутевых + СтрокаТабличнойЧасти.РасходНаРаботу;
		Иначе
			ВычестьФактТоплива = ВычестьФактТоплива + СтрокаТабличнойЧасти.РасходФактический;
		КонецЕсли;
	КонецЦикла; 
	
	ОстатокРасхода = ЗатратыТопливаПоФактуПутевых - ВычестьФактТоплива;
	ЗатратыТопливаПоФактуПутевых = ЗатратыТопливаПоФактуПутевых - ВычестьФактТоплива;
	
	ТаблицаПутевыхЛистов = Объект.ПутевыеЛисты.НайтиСтроки(Новый Структура("РасходТопливаВручную", Ложь));
	
	Индекс = 1;
	Для Каждого СтрокаТабличнойЧасти Из ТаблицаПутевыхЛистов Цикл
		ЗатратыТопливаПоФакту = ЗатратыТопливаПоФактуПутевых * СтрокаТабличнойЧасти.РасходНаРаботу / ?(ЗатратыТопливаПоНормеПутевых = 0, 1, ЗатратыТопливаПоНормеПутевых);
		ЗатратыТопливаПоФакту = Окр(ЗатратыТопливаПоФакту , 3);
		Если Индекс = Объект.ПутевыеЛисты.Количество() Тогда
			СтрокаТабличнойЧасти.РасходФактический = ОстатокРасхода;			
		Иначе
			СтрокаТабличнойЧасти.РасходФактический = ЗатратыТопливаПоФакту;			
		КонецЕсли;
		Индекс = Индекс + 1;
		ОстатокРасхода = ОстатокРасхода - ЗатратыТопливаПоФакту;  
	КонецЦикла;
	
	Если ОстатокРасхода <> 0 Тогда
		ТекстСообщения = НСтр("ru='Расход топлива по операциям не соответствует расходу топлива на закладке Топливо!';uk='Витрати пального по операціях не відповідає витратам пального на закладці Пальне!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли; 
		
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьРасходТопливаПоНорме()
	
	// Надбавки
	СуммаПроцентов      = 0;
	СуммаНадбавокСуммой = 0;
	
	Для Каждого СтрокаНадбавок Из Объект.Надбавки Цикл
		Если ПолучитьЗначениеРеквизита(СтрокаНадбавок.Надбавка, "ВПроцентах") Тогда 
			СуммаПроцентов = СуммаПроцентов + СтрокаНадбавок.Размер;
		Иначе
			СуммаНадбавокСуммой = СуммаНадбавокСуммой + СтрокаНадбавок.Размер;
		КонецЕсли;
	КонецЦикла;
	
	Если Объект.ПутевыеЛисты.Количество() > 0 Тогда 
		НадбавкаПоСтроке = Окр(СуммаНадбавокСуммой / Объект.ПутевыеЛисты.Количество(), 3);
	КонецЕсли;
	
	Для Каждого СтрокаТабличнойЧасти Из Объект.ПутевыеЛисты Цикл
		
		ТекущаяДата = ?(Объект.СводныйПутевойЛист, СтрокаТабличнойЧасти.ДатаДокумента, Объект.Дата); 		
																										 
		СведенияОТехОперации = ПолучитьСтруктуруРасценокВидаРабот(
			ТекущаяДата,
			СтрокаТабличнойЧасти.ТехнологическаяОперация,
			СтрокаТабличнойЧасти.Агрегат);	
			
		НормаСписания = СтрокаТабличнойЧасти.НормаНаРаботу;
		
		Если СведенияОТехОперации.Свойство("ТехнологическаяОперация") Тогда 
			Если СведенияОТехОперации.НормаГСМПо = "КЧасам" Тогда
				СтрокаТабличнойЧасти.РасходНаРаботу = СтрокаТабличнойЧасти.Часов * НормаСписания;	
			ИначеЕсли СведенияОТехОперации.НормаГСМПо = "КГа" Тогда
				СтрокаТабличнойЧасти.РасходНаРаботу = СтрокаТабличнойЧасти.ВыполненоГа * НормаСписания;
			ИначеЕсли СведенияОТехОперации.НормаГСМПо = "КУслГа" Тогда
				СтрокаТабличнойЧасти.РасходНаРаботу = СтрокаТабличнойЧасти.ВыполненоУслГа * НормаСписания;
			Иначе
				СтрокаТабличнойЧасти.РасходНаРаботу = СтрокаТабличнойЧасти.ВыполненоВНатуре * НормаСписания;	
			КонецЕсли;     
		Иначе
			СтрокаТабличнойЧасти.РасходНаРаботу = СтрокаТабличнойЧасти.ВыполненоВНатуре * НормаСписания;	
		КонецЕсли;     
		
		СтрокаТабличнойЧасти.РасходНаРаботу = СтрокаТабличнойЧасти.РасходНаРаботу + (СтрокаТабличнойЧасти.РасходНаРаботу * СуммаПроцентов / 100);
		
		Если СтрокаТабличнойЧасти.НомерСтроки - 1 = Объект.ПутевыеЛисты.Количество() Тогда
			СтрокаТабличнойЧасти.РасходНаРаботу = СтрокаТабличнойЧасти.РасходНаРаботу + СуммаНадбавокСуммой;
		Иначе
			СтрокаТабличнойЧасти.РасходНаРаботу = СтрокаТабличнойЧасти.РасходНаРаботу + НадбавкаПоСтроке;
			СуммаНадбавокСуммой = СуммаНадбавокСуммой - НадбавкаПоСтроке;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры  

&НаКлиенте 
Процедура РассчитатьСменныхНорм(СтрокаТабличнойЧасти)
	
	ТекущаяДата = ТекущаяДатаДокумента;																								 
																							 
	СведенияОТехОперации = ПолучитьСтруктуруРасценокВидаРабот(
			ТекущаяДата,
			СтрокаТабличнойЧасти.ТехнологическаяОперация,
			СтрокаТабличнойЧасти.Агрегат);	
			
	Если НЕ СтрокаТабличнойЧасти.НормаВыработки = 0 Тогда 
		Если СведенияОТехОперации.Свойство("ТехнологическаяОперация") Тогда 
			Если СведенияОТехОперации.СменныхНормОт = "ОтработаныхЧасов" Тогда
				СтрокаТабличнойЧасти.СменныхНорм = ?(СтрокаТабличнойЧасти.НормаВыработки = 0, 0, СтрокаТабличнойЧасти.Часов / СтрокаТабличнойЧасти.НормаВыработки);
			Иначе
				СтрокаТабличнойЧасти.СменныхНорм = ?(СтрокаТабличнойЧасти.НормаВыработки = 0, 0, СтрокаТабличнойЧасти.ВыполненоВНатуре / СтрокаТабличнойЧасти.НормаВыработки);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗатратыТопливаПоФакту()
		
	ЗатратыТопливаПоФакту = 0;
	
	Для Каждого СтрокаТабличнойЧасти Из Объект.ТопливоИзрасходованное Цикл
		
		Если СтрокаТабличнойЧасти.Коэффициент <> 0 Тогда
			ЗатратыТопливаПоФакту = ЗатратыТопливаПоФакту + СтрокаТабличнойЧасти.Количество / СтрокаТабличнойЧасти.Коэффициент;
		Иначе
			ЗатратыТопливаПоФакту = ЗатратыТопливаПоФакту + СтрокаТабличнойЧасти.Количество;
		КонецЕсли;
		
	КонецЦикла;
	
	Объект.ЗатратыТопливаПоФакту = ЗатратыТопливаПоФакту;
		
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСтрокуТаблицыПутевыеЛисты(СтрокаТабличнойЧасти)
	
	ТекущаяДата = ?(Объект.СводныйПутевойЛист, СтрокаТабличнойЧасти.ДатаДокумента, Объект.Дата);
	
	СведенияОТехОперации = ПолучитьСтруктуруРасценокВидаРабот(
		ТекущаяДата,
		СтрокаТабличнойЧасти.ТехнологическаяОперация,
		СтрокаТабличнойЧасти.Агрегат);																							 
																							 
	Если СведенияОТехОперации.Свойство("ТехнологическаяОперация") Тогда 
		
		СтрокаТабличнойЧасти.СменнаяЭталоннаяВыработка = ?(СтрокаТабличнойЧасти.СменнаяЭталоннаяВыработка = 0, СвойстваТС.КоэфУЕГА, СтрокаТабличнойЧасти.СменнаяЭталоннаяВыработка);
		СтрокаТабличнойЧасти.НормаВыработки            = СведенияОТехОперации.НормаВыработки;
		СтрокаТабличнойЧасти.Расценка                  = СведенияОТехОперации.Расценка;
		СтрокаТабличнойЧасти.НормаНаРаботу             = СведенияОТехОперации.НормаРасходаТоплива;
		СтрокаТабличнойЧасти.ЕстьПомощник              = СведенияОТехОперации.Помощник;
		УстановитьТЧПомощники();
		
	Иначе
		
		СтрокаТабличнойЧасти.СменнаяЭталоннаяВыработка = 0;
		СтрокаТабличнойЧасти.НормаВыработки            = 0;
		СтрокаТабличнойЧасти.Расценка                  = 0;
		СтрокаТабличнойЧасти.НормаНаРаботу             = 0;
		СтрокаТабличнойЧасти.ЕстьПомощник              = Ложь;
		УстановитьТЧПомощники();
		
	КонецЕсли;

КонецПроцедуры 

&НаСервере
Процедура ОбновитьДекорациюПоказанийБака()
			
	// Бака
	Если Окр((Объект.БакДо + ПолучитьИзрасходованноеТопливо()), 3) <> Объект.БакПосле Тогда 
		Элементы.ДекорацияПоказанияБака.Видимость = Истина;
	Иначе
		Элементы.ДекорацияПоказанияБака.Видимость = Ложь;
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
//&НаКлиенте
//Процедура ОбработкаВыбораПодбор(ВыбранноеЗначение)
Процедура ОбработкаВыбораПодборСервер(ВыбранноеЗначение)
	
	ТекущиеДанные = Объект.ПутевыеЛисты.Добавить();
	ДанныеСтрокиТаблицы = Новый Структура; 
	ДанныеСтрокиТаблицы.Вставить("ТехнологическаяОперация", ВыбранноеЗначение.ТехнологическаяОперация);
	ДанныеСтрокиТаблицы.Вставить("Агрегат",                  ВыбранноеЗначение.МаркаАгрегата); 		
																									 
	СведенияОТехОперации = ПолучитьСтруктуруРасценокВидаРабот(
		ВыбранноеЗначение.Период,
		ВыбранноеЗначение.ТехнологическаяОперация,
		ВыбранноеЗначение.МаркаАгрегата,
		ВыбранноеЗначение.Организация);																							 
	
	ДанныеСтрокиТаблицы.Вставить("СменнаяЭталоннаяВыработка", СвойстваТС.КоэфУЕГА);
	ДанныеСтрокиТаблицы.Вставить("НормаВыработки",            СведенияОТехОперации.НормаВыработки);
	ДанныеСтрокиТаблицы.Вставить("Расценка",                  СведенияОТехОперации.Расценка);
	ДанныеСтрокиТаблицы.Вставить("НормаНаРаботу",             СведенияОТехОперации.НормаРасходаТоплива);
	ДанныеСтрокиТаблицы.Вставить("ЕстьПомощник",              СведенияОТехОперации.Помощник); 		
	ДанныеСтрокиТаблицы.Вставить("IDСтроки",              	  Новый УникальныйИдентификатор); 		
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ДанныеСтрокиТаблицы);		

КонецПроцедуры

&НаСервере
Функция ПолучитьСтруктуруРасценокВидаРабот(ТекущаяДата, ТехнологическаяОперация, Агрегат, Организация = Неопределено)
	
	Возврат ИНАГРО_Транспорт.ИНАГРО_ПолучитьСтруктуруРасценокВидыРаботСХТехники(
				ТекущаяДата,
				Объект.Автомобиль.Модель,
				ТехнологическаяОперация,
				Агрегат,
				?(Организация = Неопределено, Объект.Организация, Организация));	
		
КонецФункции															

&НаСервереБезКонтекста
Функция ПолучитьПараметрыТехники(Дата, Организация)
	
	Возврат РегистрыСведений.ИНАГРО_УчетнаяПолитика.ПолучитьПоследнее(Дата, Новый Структура("Организация", Организация));
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСвойстваВодителя(Дата, Водитель)
	
	Возврат РегистрыСведений.ИНАГРО_СведенияОВодителе.ПолучитьПоследнее(Дата, Новый Структура("ФизЛицо", Водитель));
	
КонецФункции 

&НаСервере
Функция ПолучитьИзрасходованноеТопливо()
	
	ИзрасходованноеТопливо = 0;
	
	Для Каждого СтрокаТабличнойЧасти Из Объект.ТопливоВыданное Цикл
		ИзрасходованноеТопливо = ИзрасходованноеТопливо + ?(СтрокаТабличнойЧасти.Коэффициент = 0, 1, СтрокаТабличнойЧасти.Количество / СтрокаТабличнойЧасти.Коэффициент);
	КонецЦикла;
	
	Для Каждого СтрокаТабличнойЧасти Из Объект.ТопливоИзрасходованное Цикл
		ИзрасходованноеТопливо = ИзрасходованноеТопливо - ?(СтрокаТабличнойЧасти.Коэффициент = 0, 1, СтрокаТабличнойЧасти.Количество / СтрокаТабличнойЧасти.Коэффициент);
	КонецЦикла;
	
	Возврат ИзрасходованноеТопливо;
	
КонецФункции  

&НаСервереБезКонтекста
Функция ПолучитьЗначениеРеквизита(Ссылка, Реквизит)
	
	Возврат	ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, Реквизит);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьИтоги(Форма)
	
	Объект = Форма.Объект;
	
	СуммаЗППоРаботам     = Объект.ПутевыеЛисты.Итог("ОплатаОсновная") + Объект.ПутевыеЛисты.Итог("ОплатаДополнительная") + Объект.Помощники.Итог("ОплатаПомощника");
	СуммаЗППоНачислениям = Объект.Начисления.Итог("Результат"); 
		
КонецПроцедуры 

&НаСервере
Процедура ПолучитьНормуВремениПоСотруднику(СписокСотрудников, СтруктураСтроки)
	
	ИНАГРО_ПроведениеРасчетов.ПолучитьНормуВремени(СписокСотрудников, СтруктураСтроки);

КонецПроцедуры

#Область ПроцедурыИФункцииОбработкаВопросов

&НаКлиенте
Процедура АвтозаполнениеНачисленийПослеОтветаНаВопрос(Ответ, ПараметрыВыполнения) Экспорт
	Если Ответ <> КодВозвратаДиалога.ОК Тогда
		Возврат;
	КонецЕсли;
	
	Объект.Начисления.Очистить();
		//
	ЗаполнитьНачисленияНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура АвторасчетНачисленияПослеОтветаНаВопросПровести(Ответ, ПараметрыВыполнения) Экспорт
	Если Ответ <> КодВозвратаДиалога.ОК Тогда
		Возврат;
	КонецЕсли;
	
	РассчитатьНачисленияНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура АвтозаполнениеРассчетНачисленияПослеОтветаНаВопросПровести(Ответ, ПараметрыВыполнения) Экспорт
	Если Ответ <> КодВозвратаДиалога.ОК Тогда
		Возврат;
	КонецЕсли;
	
	Объект.Начисления.Очистить();
	
	ЗаполнитьРассчитатьНачисленияНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура АвтозаполнениеРассчетНачисленийПослеОтветаНаВопрос(Ответ, ПараметрыВыполнения) Экспорт
	Если Ответ <> КодВозвратаДиалога.ОК Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.Проведен Тогда 
		ТекстВопроса =НСтр("ru='Автоматически рассчитать документ можно только после отмены его проведения. Выполнить отмену проведения документа?';uk='Автоматично розрахувати документ можна тільки після скасування його проведення. Виконати скасування проведення документа?'");
		Обработчик = Новый ОписаниеОповещения("АвтозаполнениеРассчетНачисленияПослеОтветаНаВопросПровести", ЭтотОбъект);
		ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
		Возврат;
	ИначеЕсли Модифицированность Тогда
		ТекстВопроса =НСтр("ru='Автоматически рассчитать документ можно только после его записи. Выполнить запись документа?';uk='Автоматично розрахувати документ можна тільки після його запису. Виконати запис документа?'");
		Обработчик = Новый ОписаниеОповещения("АвтозаполнениеРассчетНачисленияПослеОтветаНаВопросПровести", ЭтотОбъект);
		ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
		Возврат;
	КонецЕсли;
	
	Объект.Начисления.Очистить();
		//
	ЗаполнитьРассчитатьНачисленияНаСервере();
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункцииБСП

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти
