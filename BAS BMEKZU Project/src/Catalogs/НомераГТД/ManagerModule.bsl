
///////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Функция определяет наличие номера ГТД по коду.
//
// Параметры:
//	СтрокаНомерГТД - Строка - Код номера ГТД
//	Номенклатура - СправочникСсылка.Номенклатура - Выбранный товар
//
// Возвращаемое значение:
//	Булево - Истина - Есть номер ГТД с указанным кодом
//
Функция ЕстьНомерГТД(СтрокаНомерГТД, Номенклатура = Неопределено) Экспорт
	
	ЕстьНомерГТД = Истина;
	
	Если Не ПустаяСтрока(СтрокаНомерГТД)
	   И ЗначениеЗаполнено(Номенклатура) Тогда
	
		Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	Истина КАК ЕстьНомерГТД
		|ИЗ
		|	Справочник.НомераГТД КАК НомераГТД
		|ГДЕ
		|	НомераГТД.Код ПОДОБНО &СтрокаНомерГТД
		|");
		Запрос.УстановитьПараметр("СтрокаНомерГТД", "%"+СокрЛП(СтрокаНомерГТД)+"%");
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ЕстьНомерГТД = Выборка.ЕстьНомерГТД;
		Иначе
			ЕстьНомерГТД = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ЕстьНомерГТД;
	
КонецФункции

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Параметры.СтрокаПоиска = Неопределено Тогда
		Запрос = Новый Запрос("
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДанныеСправочника.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.НомераГТД КАК ДанныеСправочника
		|ГДЕ
		|	Не ДанныеСправочника.Предопределенный
		|");
	Иначе
		Запрос = Новый Запрос("
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДанныеСправочника.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.НомераГТД КАК ДанныеСправочника
		|ГДЕ
		|	ДанныеСправочника.Код ПОДОБНО &СтрокаПоиска
		|");
		Запрос.УстановитьПараметр("СтрокаПоиска", Параметры.СтрокаПоиска + "%");
	КонецЕсли;
	
	ДанныеВыбора = Новый СписокЗначений;
	ДанныеВыбора.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	Если Параметры.Свойство("СоздатьНовый") И Параметры.СоздатьНовый Тогда
		Если ПравоДоступа("Добавление", Метаданные.Справочники.НомераГТД) Тогда
			ДанныеВыбора.Вставить(0, Параметры.СтрокаПоиска, НСтр("ru='<Создать новый>';uk='<Створити новий>'"));
		КонецЕсли;
	КонецЕсли;	

КонецПроцедуры

