////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УправлениеФормой(ЭтаФорма);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура СчетСебестоимостиПриИзменении(Элемент)
	
	УстановитьЗаголовкиИДоступностьСубконто(ЭтаФорма, Объект.СчетСебестоимости, "Себестоимости");
	ПриИзмененииСчета(Объект, Объект.СчетСебестоимости, "Себестоимости");
	
КонецПроцедуры

&НаКлиенте
Процедура СчетСебестоимостиПриВозвратеПриИзменении(Элемент)
	
	УстановитьЗаголовкиИДоступностьСубконто(ЭтаФорма, Объект.СчетСебестоимостиПриВозврате, "СебестоимостиПриВозврате");
	ПриИзмененииСчета(Объект, Объект.СчетСебестоимостиПриВозврате, "СебестоимостиПриВозврате");
	
КонецПроцедуры

&НаКлиенте
Процедура СчетДоходовПриИзменении(Элемент)
	
	УстановитьЗаголовкиИДоступностьСубконто(ЭтаФорма, Объект.СчетДоходов, "Доходов");
	ПриИзмененииСчета(Объект, Объект.СчетДоходов, "Доходов");
	
КонецПроцедуры

&НаКлиенте
Процедура СчетВычетовИзДоходовПриИзменении(Элемент)
	
	УстановитьЗаголовкиИДоступностьСубконто(ЭтаФорма, Объект.СчетВычетовИзДоходов, "ВычетовИзДоходов");
	ПриИзмененииСчета(Объект, Объект.СчетВычетовИзДоходов, "ВычетовИзДоходов");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовкиИДоступностьСубконто(Форма, Счет, Постфикс = "")

	ПоляФормы = Новый Структура("Субконто1, Субконто2, Субконто3",
								"Субконто" + Постфикс + "1",
								"Субконто" + Постфикс + "2",
								"Субконто" + Постфикс + "3");
								
	ЗаголовкиПолей = Новый Структура("Субконто1, Субконто2, Субконто3",
								"ЗаголовокСубконто" + Постфикс + "1",
								"ЗаголовокСубконто" + Постфикс + "2",
								"ЗаголовокСубконто" + Постфикс + "3");

	БухгалтерскийУчетКлиентСервер.ПриВыбореСчета(Счет, Форма, ПоляФормы, ЗаголовкиПолей, Ложь);
	
	УстановитьТекстСубконтоНоменклатурнаяГруппа(Счет, Форма, ПоляФормы, Постфикс);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПриИзмененииСчета(Объект, Счет, Постфикс = "")

	ПоляОбъекта = Новый Структура("Субконто1, Субконто2, Субконто3",
								"Субконто" + Постфикс + "1",
								"Субконто" + Постфикс + "2",
								"Субконто" + Постфикс + "3");

	БухгалтерскийУчетКлиентСервер.ПриИзмененииСчета(Счет, Объект, ПоляОбъекта);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьТекстСубконтоНоменклатурнаяГруппа(Счет, Форма, ПоляФормы, Постфикс)
	
	ДанныеСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Счет);
	
	Для Индекс = 1 По 3 Цикл
		Если ПоляФормы.Свойство("Субконто" + Индекс) Тогда
			Если ДанныеСчета["ВидСубконто" + Индекс] = ПредопределенноеЗначение("ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы") Тогда
				Форма.Элементы["Субконто" + Постфикс + Индекс].ТолькоПросмотр = Истина;
				Форма.Элементы["Субконто" + Постфикс + Индекс].ПодсказкаВвода = "из свойств номенклатуры";
			Иначе
				Форма.Элементы["Субконто" + Постфикс + Индекс].ТолькоПросмотр = Ложь;
				Форма.Элементы["Субконто" + Постфикс + Индекс].ПодсказкаВвода = "";
			КонецЕсли;
		КонецЕсли;		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Объект = Форма.Объект;
	
    УстановитьЗаголовкиИДоступностьСубконто(Форма, Объект.СчетСебестоимости, "Себестоимости");
	УстановитьЗаголовкиИДоступностьСубконто(Форма, Объект.СчетСебестоимостиПриВозврате, "СебестоимостиПриВозврате");
	УстановитьЗаголовкиИДоступностьСубконто(Форма, Объект.СчетДоходов, "Доходов");
	УстановитьЗаголовкиИДоступностьСубконто(Форма, Объект.СчетВычетовИзДоходов, "ВычетовИзДоходов");

КонецПроцедуры // УстановитьВидимостьСубконто()
