

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
	
		ДанныеЗаполнения.Вставить("ВалютаДенежныхСредств", ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета());
		ДанныеЗаполнения.Вставить("Валютный",				Ложь);
		
		Если ДанныеЗаполнения.Свойство("НомерСчета") Тогда	
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения, "Наименование, НомерСчета, Банк, ВалютаДенежныхСредств");	
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если ПометкаУдаления ИЛИ Ссылка.ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;

	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	Валютный = ВалютаРегламентированногоУчета <> ВалютаДенежныхСредств;

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	МассивНепроверяемыхРеквизитов = Новый Массив;
	Если ВалютаДенежныхСредств <> Константы.ВалютаРегламентированногоУчета.Получить()
		// Помеченные на удаление - не проверять
		ИЛИ ПометкаУдаления ИЛИ Ссылка.ПометкаУдаления Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("НомерСчета");
		МассивНепроверяемыхРеквизитов.Добавить("Банк");
	КонецЕсли;

	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	КодБанкаПоСчету = УправлениеДенежнымиСредствами.ПолучитьКодБанкаПоНомеруСчетаIBAN(НомерСчета);
	Если ЗначениеЗаполнено(КодБанкаПоСчету) И КодБанкаПоСчету <> СокрЛП(Банк.Код) Тогда
		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='29 знаков; IBAN UA. Неверно указан банк. Должен быть банк с кодом %1.';uk='29 знаків; IBAN UA. Невірно вказано банк. Має бути банк з кодом %1.'"), КодБанкаПоСчету);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, ЭтотОбъект, , "КодБанка", Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли

