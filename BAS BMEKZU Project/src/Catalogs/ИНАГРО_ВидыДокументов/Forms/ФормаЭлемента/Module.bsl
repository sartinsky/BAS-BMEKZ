#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)	
	
	СписокДокументовККУ = ИНАГРО_ЭлеваторОбновлениеИнформационнойБазы.ПолучитьСписокДокументовККУ();		
	
	Если СписокДокументовККУ.НайтиПоЗначению(Объект.ИмяПоМетаданным) <> Неопределено Тогда
		ЭтоДокументККУ = Истина;
	Иначе
		ЭтоДокументККУ = Ложь;
	КонецЕсли;

	УправлениеФормой(ЭтаФорма);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено(Объект.ИмяПоМетаданным) Тогда
		
		ДополнительныеПараметры = Новый Структура("Отказ", Отказ);

		ТекстВопроса = НСтр("ru='После записи выбор документа из метаданных будет недоступен. Продолжить?';uk='Після запису вибір документа з метаданих буде недоступний. Продовжити?'");
		Оповещение = Новый ОписаниеОповещения("ПередЗаписьюЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена, , КодВозвратаДиалога.ОК);
		
		Отказ = ДополнительныеПараметры.Отказ;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписьюЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.ОК Тогда		
		ДополнительныеПараметры.Отказ = Истина;
		Возврат;
	КонецЕсли; 	
	
	УправлениеФормой(ЭтаФорма);
   
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИмяПоМетаданнымНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СписокДокументов = Новый СписокЗначений;

	ИмяПоМетаданнымНачалоВыбораНаСервере(СписокДокументов);
	
	Оповещение = Новый ОписаниеОповещения("ИмяПоМетаданнымПослеВыбораЭлемента", ЭтотОбъект);
	СписокДокументов.ПоказатьВыборЭлемента(Оповещение, НСтр("ru='Выберите документ';uk='Оберіть документ'"));	

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ИмяПоМетаданнымНачалоВыбораНаСервере(СписокДокументов)
	
	СправочникВидыДокументов = Справочники.ИНАГРО_ВидыДокументов;
	
	Для Каждого Документ Из Метаданные.Документы Цикл		
		Если СправочникВидыДокументов.НайтиПоРеквизиту("ИмяПоМетаданным", Документ.Имя) = СправочникВидыДокументов.ПустаяСсылка() Тогда
			СписокДокументов.Добавить(Документ.Имя);             
		КонецЕсли;		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяПоМетаданнымПослеВыбораЭлемента(ВыбранныйЭлемент, СписокПараметров) Экспорт
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		ТекстСообщения = НСтр("ru='Документ не выбран!';uk='Документ не обраний!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , "Объект.ИмяПоМетаданным");
    Иначе
        Объект.ИмяПоМетаданным = ВыбранныйЭлемент.Значение;
		Объект.Наименование    = ЗаполнитьНаименованиеПоИмяПоМетаданным(Объект.ИмяПоМетаданным);
	КонецЕсли;	
	
	Если СписокДокументовККУ.НайтиПоЗначению(Объект.ИмяПоМетаданным) <> Неопределено Тогда
		ЭтоДокументККУ = Истина;
	Иначе
		ЭтоДокументККУ = Ложь;
	КонецЕсли;

	УправлениеФормой(ЭтаФорма);

КонецПроцедуры 

&НаКлиенте
Процедура СпособНумерацииДокументовПриИзменении(Элемент)
	
	Если Объект.СпособНумерацииДокументов = ПредопределенноеЗначение("Перечисление.ИНАГРО_СпособыНумерацииДокументов.ПрефиксВидаУрожая") Тогда		
		 Объект.ИмяРеквизитаНомераДокумента = "";		
	КонецЕсли;	

	УправлениеФормой(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ИмяРеквизитаНомераДокументаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СписокРеквизитов = Новый СписокЗначений;
	
	Если НЕ ЗначениеЗаполнено(Объект.ИмяПоМетаданным) Тогда
		ТекстСообщения = НСтр("ru='Документ не выбран!';uk='Документ не обраний!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , "Объект.ИмяПоМетаданным");
		Возврат;
	КонецЕсли;
	
	ИмяРеквизитаНомераДокументаНачалоВыбораНаСервере(СписокРеквизитов, Объект.ИмяПоМетаданным);

	Оповещение = Новый ОписаниеОповещения("ИмяРеквизитаНомераДокументаПослеВыбораЭлемента", ЭтотОбъект);
	СписокРеквизитов.ПоказатьВыборЭлемента(Оповещение, НСтр("ru='Выберите реквизит';uk='Оберіть реквізит'"));	

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ИмяРеквизитаНомераДокументаНачалоВыбораНаСервере(СписокРеквизитов, ИмяПоМетаданным)
	
	ИмяДокумента = СокрЛП(ИмяПоМетаданным);	
		
	Для Каждого Реквизит Из Метаданные.Документы[ИмяДокумента].Реквизиты Цикл		
		СписокРеквизитов.Добавить(Реквизит.Имя);		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяРеквизитаНомераДокументаПослеВыбораЭлемента(ВыбранныйЭлемент, СписокПараметров) Экспорт
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		ТекстСообщения = НСтр("ru='Реквизит не выбран!';uk='Реквізит не обраний!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
    Иначе
        Объект.ИмяРеквизитаНомераДокумента = ВыбранныйЭлемент.Значение;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)

	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	ЭтоНовый = НЕ ЗначениеЗаполнено(Объект.Ссылка);
	Элементы.ИмяПоМетаданным.Доступность   = ЭтоНовый;
	Элементы.ИмяПоМетаданным.КнопкаВыбора  = ЭтоНовый;
	Элементы.ИмяПоМетаданным.КнопкаОчистки = ЭтоНовый;
	
	Элементы.ГруппаРеквизитыДокументовККУ.Видимость = Форма.ЭтоДокументККУ;
		
	ВидимостьИмяРеквизитаНомераДокумента = Объект.СпособНумерацииДокументов = ПредопределенноеЗначение("Перечисление.ИНАГРО_СпособыНумерацииДокументов.ПоРеквизитуДокумента")
	                                   ИЛИ Объект.СпособНумерацииДокументов = ПредопределенноеЗначение("Перечисление.ИНАГРО_СпособыНумерацииДокументов.ПоРеквизитуДокументаСПрефиксомВидаУрожая");
												 
	Элементы.ИмяРеквизитаНомераДокумента.Видимость     = ВидимостьИмяРеквизитаНомераДокумента;											 
	Элементы.ИмяРеквизитаНомераДокумента.КнопкаВыбора  = ВидимостьИмяРеквизитаНомераДокумента;
	Элементы.ИмяРеквизитаНомераДокумента.КнопкаОчистки = ВидимостьИмяРеквизитаНомераДокумента;	
	
	Элементы.ДлинаПрефикса.Видимость = (Объект.ИмяПоМетаданным = "ИНАГРО_РеестрТТНВвоз"
	                                  И Объект.СпособНумерацииДокументов = ПредопределенноеЗначение("Перечисление.ИНАГРО_СпособыНумерацииДокументов.ПоРеквизитуДокумента"));
	
	Элементы.ГруппаЖурналыЛаборатории.Видимость = ЗначениеЗаполнено(Объект.ВидЖурналаЛаборатории);
		
	Если    Объект.ИмяПоМетаданным = "ИНАГРО_ТТНВвоз"
		ИЛИ Объект.ИмяПоМетаданным = "ИНАГРО_ТТНВвозЖД"
		ИЛИ Объект.ИмяПоМетаданным = "ИНАГРО_ТТНВнутр"
		ИЛИ Объект.ИмяПоМетаданным = "ИНАГРО_ТТНВывоз"
		ИЛИ Объект.ИмяПоМетаданным = "ИНАГРО_ТТНВывозЖД" Тогда   
		Элементы.ГруппаЖурналыВесовой.Видимость = Истина;
	Иначе
		Элементы.ГруппаЖурналыВесовой.Видимость = Ложь;
	КонецЕсли; 
					
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаполнитьНаименованиеПоИмяПоМетаданным(ИмяПоМетаданным)	
	
	МетаданныеОбъекта = Метаданные.Документы.Найти(ИмяПоМетаданным);
	
	Возврат МетаданныеОбъекта.Синоним;
	
КонецФункции

#КонецОбласти
