Функция ПрочитатьXMLНастройкиMSSQLServer() Экспорт
	НачальныеНастройки = Справочники.скEDI_ВнешниеХранилищаСодержимогоЭлектронныхДокументов.ПолучитьМакет("МакетMSSQLServer").ПолучитьТекст();
	
	ЧтениеXML = новый ЧтениеXML;
	
	ЧтениеXML.УстановитьСтроку(НачальныеНастройки);
	ЧтениеXML.Прочитать();
	
	мДОМ = Новый ПостроительDOM;
	мДокументДОМ 	  = мДОМ.Прочитать(ЧтениеXML);
	
	Результат = Новый Структура("ТипХранилища,Версия", "", "");
	МассивОбъектов = Новый Массив;
	МассивВерсий = Новый Массив;
	//<ТипХранилища>MSSQLServer</ТипХранилища>
	//<Версия>1.0.0.0</Версия>
	//<ОбъектыБазыДанных>
	
	Для Каждого ДочернийУзел1 Из мДокументДОМ.ДочерниеУзлы Цикл
		Если ВРег(ДочернийУзел1.ИмяУзла) = ВРег("НастройкиВнешнегоХранилища") Тогда
			Для Каждого ДочернийУзел2 Из ДочернийУзел1.ДочерниеУзлы Цикл
				Если ВРег(ДочернийУзел2.ИмяУзла) = ВРег("ТипХранилища") Тогда
					Результат.ТипХранилища = ДочернийУзел2.ТекстовоеСодержимое;
				ИначеЕсли ВРег(ДочернийУзел2.ИмяУзла) = ВРег("Версия") Тогда
					Результат.Версия = ДочернийУзел2.ТекстовоеСодержимое;
				ИначеЕсли ВРег(ДочернийУзел2.ИмяУзла) = ВРег("ОбъектыБазыДанных") Тогда
					Для Каждого ДочернийУзел3 Из ДочернийУзел2.ДочерниеУзлы Цикл 
						Если ВРег(ДочернийУзел3.ИмяУзла) = ВРег("ОбъектБазыДанных") Тогда
							АтрибутИмяОбъекта = ДочернийУзел3.Атрибуты.ПолучитьИменованныйЭлемент("ИмяОбъекта"); 
							Если АтрибутИмяОбъекта = Неопределено Тогда
								Продолжить;
							КонецЕсли;
							
							ИмяОбъекта = АтрибутИмяОбъекта.ТекстовоеСодержимое;
							
							АтрибутОписание = ДочернийУзел3.Атрибуты.ПолучитьИменованныйЭлемент("Описание"); 
							Если АтрибутОписание = Неопределено Тогда
								Описание = "";
							Иначе
								Описание = АтрибутОписание.ТекстовоеСодержимое;
							КонецЕсли;
							
							СозданиеОбъекта = "";
							ПроверкаНаличияОбъекта = "";
							ТестОбъекта = "";
							ОбновленияОбъекта = Новый Массив;
							
							Для Каждого ДочернийУзел4 Из ДочернийУзел3.ДочерниеУзлы Цикл 
								Если ВРег(ДочернийУзел4.ИмяУзла) = ВРег("СозданиеОбъекта") Тогда
									СозданиеОбъекта = ДочернийУзел4.ТекстовоеСодержимое;
								ИначеЕсли ВРег(ДочернийУзел4.ИмяУзла) = ВРег("ПроверкаНаличияОбъекта") Тогда
									ПроверкаНаличияОбъекта = ДочернийУзел4.ТекстовоеСодержимое;
								ИначеЕсли ВРег(ДочернийУзел4.ИмяУзла) = ВРег("ТестОбъекта") Тогда
									ТестОбъекта = ДочернийУзел4.ТекстовоеСодержимое;
								КонецЕсли;
							КонецЦикла;
							
							ОписаниеОбъекта = Новый Структура("ИмяОбъекта,Описание,СозданиеОбъекта,ПроверкаНаличияОбъекта,ТестОбъекта,ОбновленияОбъекта", ИмяОбъекта, Описание, СозданиеОбъекта, ПроверкаНаличияОбъекта, ТестОбъекта, ОбновленияОбъекта);
							МассивОбъектов.Добавить(ОписаниеОбъекта);
						КонецЕсли;
					КонецЦикла;
				ИначеЕсли ВРег(ДочернийУзел2.ИмяУзла) = ВРег("Версии") Тогда
					Для Каждого ДочернийУзел3 Из ДочернийУзел2.ДочерниеУзлы Цикл 
						Если ВРег(ДочернийУзел3.ИмяУзла) = ВРег("Версия") Тогда
							НомерВерсии = "";
							Тест = "";
							Обновление = Новый Массив;
							
							Для Каждого ДочернийУзел4 Из ДочернийУзел3.ДочерниеУзлы Цикл 
								Если ВРег(ДочернийУзел4.ИмяУзла) = ВРег("НомерВерсии") Тогда
									НомерВерсии = ДочернийУзел4.ТекстовоеСодержимое;
								ИначеЕсли ВРег(ДочернийУзел4.ИмяУзла) = ВРег("Тест") Тогда
									Тест = ДочернийУзел4.ТекстовоеСодержимое;
								ИначеЕсли ВРег(ДочернийУзел4.ИмяУзла) = ВРег("Обновление") Тогда
									Обновление.Добавить(ДочернийУзел4.ТекстовоеСодержимое);
								КонецЕсли;
							КонецЦикла;
							
							ОписаниеВерсии = Новый Структура("НомерВерсии,Тест,Обновление", НомерВерсии, Тест, Обновление);
							МассивВерсий.Добавить(ОписаниеВерсии);
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	Результат.Вставить("МассивОбъектов", МассивОбъектов);
	Результат.Вставить("МассивВерсий", МассивВерсий);
	
	ЧтениеXML.Закрыть();
	Возврат Результат;
КонецФункции
