
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Отбор.Свойство("ГруппаНалоговогоНазначения", ГруппаНалоговогоНазначения);
	
	Если ЗначениеЗаполнено(ГруппаНалоговогоНазначения) Тогда
	 	// Перобразуем в не отображаемый в настройках формы отбор
		Параметры.Отбор.Удалить("ГруппаНалоговогоНазначения");
		Элементы.ГруппаНалоговогоНазначения.ТолькоПросмотр = Истина;	
		Элементы.ГруппаНалоговогоНазначения.Вид  = ВидПоляФормы.ПолеВвода;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "ГруппаНалоговогоНазначения", ГруппаНалоговогоНазначения, ВидСравненияКомпоновкиДанных.Равно,,Истина);
	Иначе
		Если ЗначениеЗаполнено(Параметры.ТекущаяСтрока) Тогда
			ГруппаНалоговогоНазначения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.ТекущаяСтрока,"ГруппаНалоговогоНазначения");
		Иначе
			ГруппаНалоговогоНазначения = Перечисления.ГруппыНалоговыхНазначений.НалоговыеНазначенияАктивовИВзаиморасчетовПоНДС;
		КонецЕсли; 
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "ГруппаНалоговогоНазначения", ГруппаНалоговогоНазначения, ВидСравненияКомпоновкиДанных.Равно,,Истина);
	КонецЕсли;

	ПоказыватьВстроеннуюСправкуСохраненноеЗначение	= ХранилищеОбщихНастроек.Загрузить("Справочник_НалоговыеНазначенияАктивовИЗатрат",
		"ПоказыватьВстроеннуюСправку");
	ПоказыватьВстроеннуюСправку	= ?(ПоказыватьВстроеннуюСправкуСохраненноеЗначение = Неопределено,
		Истина, ПоказыватьВстроеннуюСправкуСохраненноеЗначение);
		
	УправлениеФормойСервер();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура ГруппаНалоговогоНазначенияПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "ГруппаНалоговогоНазначения", ГруппаНалоговогоНазначения, ВидСравненияКомпоновкиДанных.Равно,,ЗначениеЗаполнено(ГруппаНалоговогоНазначения));
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	ЗаполнитьТекстВстроеннойСправки(Элементы.Список.ТекущиеДанные);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ПоказатьСкрытьВстроеннуюСправку(Команда)

	ПоказыватьВстроеннуюСправку = НЕ ПоказыватьВстроеннуюСправку;
	УправлениеФормойСервер();
	ЗаполнитьТекстВстроеннойСправки(Элементы.Список.ТекущиеДанные);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура УправлениеФормойСервер()
	
	ХранилищеОбщихНастроек.Сохранить("Справочник_НалоговыеНазначенияАктивовИЗатрат", "ПоказыватьВстроеннуюСправку",
		ПоказыватьВстроеннуюСправку);

	Элементы.ВстроеннаяСправка.Видимость = ПоказыватьВстроеннуюСправку;

	Элементы.ФормаПоказатьСкрытьВстроеннуюСправку.Заголовок = ?(ПоказыватьВстроеннуюСправку, НСтр("ru='Скрыть справку';uk='Приховати довідку'"),
		НСтр("ru='Показать справку';uk='Показати довідку'"));
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТекстВстроеннойСправки(Знач НалоговоеНазначениеСтруктура)
	
	НалоговоеНазначение = НалоговоеНазначениеСтруктура.Ссылка;
	
	Если НЕ ПоказыватьВстроеннуюСправку Тогда
		Возврат;
	КонецЕсли;
	
	Если НалоговоеНазначение <> Неопределено Тогда
		ИмяОбласти = Справочники.НалоговыеНазначенияАктивовИЗатрат.ПолучитьИмяПредопределенного(НалоговоеНазначение);
	Иначе
		ИмяОбласти = "ХозДеятельность";
	КонецЕсли;
	
	МакетПомощника = Справочники.НалоговыеНазначенияАктивовИЗатрат.ПолучитьМакет("Справка");
	ОбластьТекстЗаголовок			= ИмяОбласти + "|Заголовок";
	ОбластьТекстаСправки = МакетПомощника.ПолучитьОбласть(ОбластьТекстЗаголовок);
	ТекстЗаголовок = ОбластьТекстаСправки.ТекущаяОбласть.Текст;
	
	ОбластьТекстОбщий			= ИмяОбласти + "|Общий";
	ОбластьТекстаСправки = МакетПомощника.ПолучитьОбласть(ОбластьТекстОбщий);
	ТекстОбщий = ОбластьТекстаСправки.ТекущаяОбласть.Текст;
	
	ТекстОбщий = "<DIV>" + СтрЗаменить(ТекстОбщий, Символы.ПС, "</DIV>" + Символы.ПС + "<DIV>") + "</DIV>";

 	ТекстВстроеннойСправки = Справочники.НалоговыеНазначенияАктивовИЗатрат.ПолучитьМакет("ШаблонВстроеннойСправки").ПолучитьТекст();

	ТекстВстроеннойСправки = СтрЗаменить(ТекстВстроеннойСправки, "%header%", ТекстЗаголовок);
	ТекстВстроеннойСправки = СтрЗаменить(ТекстВстроеннойСправки, "%text%", ТекстОбщий);

	ВстроеннаяСправка = ТекстВстроеннойСправки;
	
КонецПроцедуры

