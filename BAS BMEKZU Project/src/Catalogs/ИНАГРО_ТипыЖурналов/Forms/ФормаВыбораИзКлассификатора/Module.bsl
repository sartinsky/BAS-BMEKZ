#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЭтаФорма.Параметры.Свойство("СтрокаПоиска") Тогда
		СтрокаПоиска = ЭтаФорма.Параметры.СтрокаПоиска;
	КонецЕсли;
	
	Макет = Справочники.ИНАГРО_ТипыЖурналов.ПолучитьМакет("Классификатор");
	ТабДокумент.Вывести(Макет);
	ТабДокумент.ФиксацияСверху = 3;
	
	ОбластьНаименованиеЛево         = Макет.Области.Наименование.Лево;
	ОбластьНаименованиеПраво        = Макет.Области.Наименование.Право;
	ОбластьКодЛево                  = Макет.Области.Код.Лево;
	ОбластьКодПраво                 = Макет.Области.Код.Право;
	ОбластьВидЖурналаЛево           = Макет.Области.ВидЖурнала.Лево;
	ОбластьВидЖурналаПраво          = Макет.Области.ВидЖурнала.Право;
			
	Если НЕ ПустаяСтрока(СтрокаПоиска) Тогда
		НайденнаяОбласть = ТабДокумент.НайтиТекст(СтрокаПоиска, , ТабДокумент.Области.Код, , , , Истина);
		Элементы.ТабДокумент.ТекущаяОбласть = НайденнаяОбласть;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтрокаПоискаПриИзменении(Элемент)
	
	ИскатьСтрокуВТаблице(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	Область = Элементы.ТабДокумент.ТекущаяОбласть;
	ВыполнитьВыбор(Область);
	
КонецПроцедуры

&НаКлиенте
Процедура ИскатьСтрокуВТаблице(Команда)
	
	Если ПустаяСтрока(СтрокаПоиска) Тогда
		ПоказатьПредупреждение( , НСтр("ru='Не задана строка поиска';uk='Не заданий рядок пошуку'"));
		ТекущийЭлемент = Элементы.СтрокаПоиска;
		Возврат;
	КонецЕсли;
	
	НайденнаяОбласть = ТабДокумент.НайтиТекст(СтрокаПоиска, Элементы.ТабДокумент.ТекущаяОбласть, , , , , Истина);
	Если НайденнаяОбласть = Неопределено Тогда
		НайденнаяОбласть = ТабДокумент.НайтиТекст(СтрокаПоиска, , , , , , Истина);
		Если НайденнаяОбласть = Неопределено Тогда
			ОбщегоНазначенияБПКлиентСервер.СообщитьОбОшибке(
				НСтр("ru='Строка не найдена';uk='Рядок не знайдено'"));
			ТекущийЭлемент = Элементы.СтрокаПоиска;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Элементы.ТабДокумент.ТекущаяОбласть = НайденнаяОбласть;
	ТекущийЭлемент = Элементы.СтрокаПоиска;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ТабДокументВыбор(Элемент, Область, СтандартнаяОбработка)
	
	Область = Элементы.ТабДокумент.ТекущаяОбласть;
	ВыполнитьВыбор(Область);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьВыбор(ТекущаяОбласть)
	
	Наименование        = ТабДокумент.Область(ТекущаяОбласть.Верх, ОбластьНаименованиеЛево,
											  ТекущаяОбласть.Низ,  ОбластьНаименованиеПраво).Текст;
	Код                 = ТабДокумент.Область(ТекущаяОбласть.Верх, ОбластьКодЛево,
											  ТекущаяОбласть.Низ,  ОбластьКодПраво).Текст;
	ВидЖурнала          = ТабДокумент.Область(ТекущаяОбласть.Верх, ОбластьВидЖурналаЛево,
											  ТекущаяОбласть.Низ,  ОбластьВидЖурналаПраво).Текст;								
											  
		
	ЗначенияЗаполнения = Новый Структура("Наименование, Код, ВидЖурнала",
										 СтрПолучитьСтроку(Наименование, 1), Код, ПолучитьВидЖурнала(ВидЖурнала));
	
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ТипЖурнала = ПоискСуществующегоТипаЖурнала(ПараметрыФормы);
	
	Если ТипЖурнала <> Неопределено Тогда
		
		ПараметрыФормы = Новый Структура("Ключ", ТипЖурнала);

		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='В справочнике ""Типы журналов"" уже существует элемент с кодом ""%1""! Открыт существующий';uk='У довіднику ""Типи журналів"" вже існує елемент з кодом ""%1""! Відкритий існуючий'"), Наименование);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);

		ОткрытьФорму("Справочник.ИНАГРО_ТипыЖурналов.Форма.ФормаЭлемента", ПараметрыФормы, ЭтаФорма);
		
	Иначе	
		
		ОткрытьФорму("Справочник.ИНАГРО_ТипыЖурналов.Форма.ФормаЭлемента", ПараметрыФормы, ЭтаФорма);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПоискСуществующегоТипаЖурнала(Знач ПараметрыФормы)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ИНАГРО_ТипыЖурналов.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ИНАГРО_ТипыЖурналов КАК ИНАГРО_ТипыЖурналов
		|ГДЕ
		|	ИНАГРО_ТипыЖурналов.Код = &Код";
	
	Запрос.УстановитьПараметр("Код", ПараметрыФормы.ЗначенияЗаполнения.Код);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьВидЖурнала(Знач ВидЖурнала)	

	Возврат Перечисления.ИНАГРО_ВидыЖурналов[ВидЖурнала];
	
КонецФункции

#КонецОбласти