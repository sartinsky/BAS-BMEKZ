////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры, "СотрудникСсылка");
	
	ОформленПриемНаРаботу = Ложь;
	
	КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудников(Истина, СотрудникСсылка, "ОформленПриемНаРаботу", ТекущаяДатаСеанса());
	Если КадровыеДанные.Количество() > 0 Тогда
		ОформленПриемНаРаботу = КадровыеДанные[0].ОформленПриемНаРаботу;
	КонецЕсли; 
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"КнопкаОформитьПриемНаРаботу",
		"Видимость",
		НЕ ОформленПриемНаРаботу);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(КадровыеДокументы.Отбор, "Сотрудник", СотрудникСсылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	СотрудникиКлиент.ЗарегистрироватьОткрытиеФормы(ЭтаФорма, "КадровыеДокументы");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	СотрудникиКлиент.ОбработкаОповещения(ЭтаФорма, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ОформитьПриемНаРаботу(Команда)

	СотрудникиКлиент.ОформитьНаОсновании(ЭтаФорма, СотрудникСсылка, "Документы.ПриемНаРаботу");

КонецПроцедуры

&НаКлиенте
Процедура ОформитьКадровыйПеревод(Команда)

	СотрудникиКлиент.ОформитьНаОсновании(ЭтаФорма, СотрудникСсылка, "Документы.КадровыйПеревод");

КонецПроцедуры

&НаКлиенте
Процедура ОформитьУвольнение(Команда)

	СотрудникиКлиент.ОформитьНаОсновании(ЭтаФорма, СотрудникСсылка, "Документы.Увольнение");

КонецПроцедуры

