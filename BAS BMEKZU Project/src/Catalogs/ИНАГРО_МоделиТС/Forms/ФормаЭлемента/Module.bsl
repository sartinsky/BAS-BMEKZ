#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		 	
	УправлениеФормой(ЭтаФорма);
		
КонецПроцедуры 

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
		
	Если Объект.ИспользуемоеТопливо.Количество() = 0 Тогда
		Объект.ОсновноеТопливо = ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка");
	КонецЕсли;
	
	Если Объект.ОсновноеТопливо.Пустая() Тогда
		
		Если Объект.ИспользуемоеТопливо.Количество() > 1 Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Не назначено основное топливо';uk='Не призначено основне паливо'"), , , , Отказ);
			Возврат;
		ИначеЕсли Объект.ИспользуемоеТопливо.Количество() = 1 Тогда
			Объект.ОсновноеТопливо = Объект.ИспользуемоеТопливо[0].Топливо;	
			Объект.ИспользуемоеТопливо[0].Коэффициент = 1;
		КонецЕсли;
		
	КонецЕсли;  

КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидТехникиПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма); 
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СделатьОсновным(Команда)
	
	ТекущиеДанные = Элементы.Топливо.ТекущиеДанные;
	
	Если Объект.ИспользуемоеТопливо.Количество() > 0 Тогда
		Объект.ОсновноеТопливо = ТекущиеДанные.Топливо;
		ТекущиеДанные.Коэффициент = 1;
	Иначе
		ОбщегоНазначенияБПКлиентСервер.СообщитьОбОшибке(НСтр("ru='Не заполнена таблица Топливо';uk='Не заповнена таблиця Паливо'"));
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНаименование(Команда)
	Объект.Наименование = Объект.МаркаТС + " " + Объект.МодельТС;
КонецПроцедуры

#КонецОбласти    

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ТопливоПередУдалением(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.Топливо.ТекущиеДанные;
	
	Если ТекущиеДанные.Топливо = Объект.ОсновноеТопливо Тогда
		Объект.ОсновноеТопливо = ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка");	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	Объект   = Форма.Объект; 	
		
	Элементы.КоэфУЕГА.Видимость = (Объект.ВидТехники = ПредопределенноеЗначение("Перечисление.ИНАГРО_ВидыТехники.Сельхозтехника"));  

КонецПроцедуры

#КонецОбласти