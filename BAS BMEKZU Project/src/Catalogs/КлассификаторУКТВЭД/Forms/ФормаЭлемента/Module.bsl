&НаКлиенте
Перем ВидКодаДоИзменения;
&НаКлиенте
Перем КодДоИзменения;

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// При создании из поля ввода в наименовании отсутсвуют разделители
	Если Не ЗначениеЗаполнено(Объект.Ссылка) И ЗначениеЗаполнено(Объект.Код) Тогда
		Объект.Код = Элементы.Код.ТекстРедактирования;
	КонецЕсли;
	
	КодДоИзменения = Объект.Код;
	ВидКодаДоИзменения = Объект.Вид;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)

	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	ОповеститьОВыборе(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)

	Оповестить("Запись_КодУКТВЭД", ПараметрыЗаписи, Объект.Ссылка); 
	
КонецПроцедуры

&НаКлиенте
Функция ЭтоКодУслугиИзДКПП(Код)
	
	Результат = Ложь;
	
	Если Найти(Код, ".") > 0 Тогда
		Результат = Истина;
	КонецЕсли; 
	
	Возврат Результат;

КонецФункции // ()

&НаКлиенте
Процедура СформироватьНаименование()

	//Проверим, если наименование и раньше было автоматически сформировано, то можно переформировывать наименование, в противном случае - оставляем без изменений
	АвтоматическиСформированноеНаименование = СокрЛП(КодДоИзменения) + ?(ВидКодаДоИзменения = ПредопределенноеЗначение("Перечисление.ВидыКодовДляНалоговойНакладной.КодТовараИмпортированного")," X","") + 
		"/" + СтрЗаменить(Объект.НаименованиеПолное,Символы.ПС,"");
			
	//Приведем к длине наименования
	АвтоматическиСформированноеНаименование = Лев(АвтоматическиСформированноеНаименование, 150);
			
	Если Объект.Наименование = АвтоматическиСформированноеНаименование Или ПустаяСтрока(Объект.Наименование) Тогда
		Объект.Наименование = СокрЛП(Объект.Код) + ?(Объект.Вид = ПредопределенноеЗначение("Перечисление.ВидыКодовДляНалоговойНакладной.КодТовараИмпортированного")," X","") + 
			"/" + СтрЗаменить(Объект.НаименованиеПолное,Символы.ПС,"");		
	КонецЕсли; 		

КонецПроцедуры

&НаКлиенте
Процедура КодПриИзменении(Элемент)
	СформироватьНаименование();
	КодДоИзменения = Объект.Код;
КонецПроцедуры

&НаКлиенте
Процедура ВидКодаПриИзменении(Элемент)
	СформироватьНаименование();
	ВидКодаДоИзменения = Объект.Вид;
КонецПроцедуры

&НаКлиенте
Процедура ВидКодаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Объект.Код) Тогда
		Если ((ВыбранноеЗначение = ПредопределенноеЗначение("Перечисление.ВидыКодовДляНалоговойНакладной.КодУслуги")) И НЕ ЭтоКодУслугиИзДКПП(Объект.Код)) ИЛИ
			 ((НЕ ВыбранноеЗначение = ПредопределенноеЗначение("Перечисление.ВидыКодовДляНалоговойНакладной.КодУслуги")) И ЭтоКодУслугиИзДКПП(Объект.Код)) Тогда
			 
			СтандартнаяОбработка = Ложь;				 
			ПоказатьПредупреждение(,НСтр("ru='Формат кода не соответствует выбранному виду кода!';uk='Формат коду не відповідає обраному виду коду!'"));
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
