#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Использование = Истина;
	Расписание = Новый ХранилищеЗначения(РегламентныеЗадания.СоздатьРегламентноеЗадание(Метаданные.РегламентныеЗадания.ОбработкаОчередиЗаданийБТС).Расписание);
	
КонецПроцедуры


Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Использование = Неопределено;
	Если Не ДополнительныеСвойства.Свойство("Использование", Использование) Тогда
		ВызватьИсключение НСтр("ru='Необходимо заполнить дополнительное свойство ""Использование""';uk='Необхідно заповнити додаткове властивість ""Використання""'");
	КонецЕсли;
	
	Расписание = Неопределено;
	Если Не ДополнительныеСвойства.Свойство("Расписание", Расписание) Тогда
		ВызватьИсключение НСтр("ru='Необходимо заполнить дополнительное свойство ""Расписание""';uk='Необхідно заповнити додаткове властивість ""Розклад""'");
	КонецЕсли;
	
	ПараметрыЗадания = Новый Массив;
	ПараметрыЗадания.Добавить(Ссылка);
	
	РегламентноеЗадание = Справочники.ДополнительныеОбработчикиОчередиЗаданий.РегламентноеЗадание(Ссылка);
	Если РегламентноеЗадание = Неопределено Тогда
		РегламентноеЗадание = РегламентныеЗадания.СоздатьРегламентноеЗадание(Метаданные.РегламентныеЗадания.ОбработкаОчередиЗаданийБТС);
		РегламентноеЗадание.Ключ = Строка(Ссылка.УникальныйИдентификатор());
	КонецЕсли;
	РегламентноеЗадание.Использование = Использование;
	РегламентноеЗадание.КоличествоПовторовПриАварийномЗавершении = 0;
	РегламентноеЗадание.Наименование = СтрШаблон("%1 (%2)", Метаданные.РегламентныеЗадания.ОбработкаОчередиЗаданийБТС.Синоним, Наименование);
	РегламентноеЗадание.Параметры = ПараметрыЗадания;
	РегламентноеЗадание.Расписание = Расписание;
	
	РегламентноеЗадание.Записать();
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	РегламентноеЗадание = Справочники.ДополнительныеОбработчикиОчередиЗаданий.РегламентноеЗадание(Ссылка);
	Если РегламентноеЗадание <> Неопределено Тогда
		РегламентноеЗадание.Удалить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли