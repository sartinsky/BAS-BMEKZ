
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда
		Использование = Истина;
	КонецЕсли;
	
	Если Расписание = Неопределено Тогда
		Расписание = РегламентныеЗадания.СоздатьРегламентноеЗадание(Метаданные.РегламентныеЗадания.ОбработкаОчередиЗаданийБТС).Расписание;
	КонецЕсли;
	ПредставлениеРасписания = Строка(Расписание);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ШаблоныЗаданийОчереди.ИмяМетода КАК ИмяМетода
	|ИЗ
	|	Справочник.ШаблоныЗаданийОчереди КАК ШаблоныЗаданийОчереди
	|ГДЕ
	|	НЕ ШаблоныЗаданийОчереди.ПометкаУдаления";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Элементы.МетодыМетод.СписокВыбора.Добавить(Выборка.ИмяМетода);
	КонецЦикла;
	
	Для Каждого КлючИЗначение Из ОчередьЗаданийСлужебныйПовтИсп.СоответствиеИменМетодовПсевдонимам() Цикл
		Если Элементы.МетодыМетод.СписокВыбора.НайтиПоЗначению(КлючИЗначение.Значение) = Неопределено Тогда
			Элементы.МетодыМетод.СписокВыбора.Добавить(КлючИЗначение.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Элементы.МетодыМетод.СписокВыбора.СортироватьПоЗначению();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	РегламентноеЗадание = Справочники.ДополнительныеОбработчикиОчередиЗаданий.РегламентноеЗадание(ТекущийОбъект.Ссылка);
	
	Если РегламентноеЗадание <> Неопределено Тогда
		Расписание = РегламентноеЗадание.Расписание;
		Использование = РегламентноеЗадание.Использование;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.ДополнительныеСвойства.Вставить("Расписание", Расписание);
	ТекущийОбъект.ДополнительныеСвойства.Вставить("Использование", Использование);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПредставлениеРасписанияНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Диалог = Новый ДиалогРасписанияРегламентногоЗадания(Расписание);
	Диалог.Показать(Новый ОписаниеОповещения("ЗавершениеРедактированияРасписания", ЭтотОбъект));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗавершениеРедактированияРасписания(НовоеРасписание, ДополнительныеПараметры) Экспорт
	
	Если НовоеРасписание <> Неопределено Тогда
		Расписание = НовоеРасписание;
		ПредставлениеРасписания = Строка(Расписание);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
