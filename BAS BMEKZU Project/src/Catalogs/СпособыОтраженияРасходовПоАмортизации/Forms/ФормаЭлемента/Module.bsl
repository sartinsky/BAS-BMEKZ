
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервере
Процедура НастроитьФормуПоОбъекту()
	
	УстановитьДоступностьСубконтоИЗаполнитьДобавленныеКолонкиТаблиц();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьСписокПараметров(Форма, Объект, ШаблонИмяПоляОбъекта)
	
	СписокПараметров = Новый Структура;
	Для Индекс = 1 По 3 Цикл
		ИмяПоля = СтрЗаменить(ШаблонИмяПоляОбъекта, "%Индекс%", Индекс);
		Если ТипЗнч(Объект[ИмяПоля]) = Тип("СправочникСсылка.Контрагенты") Тогда
			СписокПараметров.Вставить("Контрагент", Объект[ИмяПоля]);
		ИначеЕсли БухгалтерскийУчетКлиентСерверПереопределяемый.ПолучитьОписаниеТиповДоговора().СодержитТип(ТипЗнч(Объект[ИмяПоля])) Тогда
			СписокПараметров.Вставить("ДоговорКонтрагента", Объект[ИмяПоля]);
		ИначеЕсли ТипЗнч(Объект[ИмяПоля]) = Тип("СправочникСсылка.Номенклатура") Тогда
			СписокПараметров.Вставить("Номенклатура", Объект[ИмяПоля]);
		ИначеЕсли ТипЗнч(Объект[ИмяПоля]) = Тип("СправочникСсылка.Склады") Тогда
			СписокПараметров.Вставить("Склад", Объект[ИмяПоля]);
		КонецЕсли;
	КонецЦикла;
	СписокПараметров.Вставить("ОстаткиОбороты", "Дт"); 
	СписокПараметров.Вставить("Организация"   , Форма.Объект.Организация);
	
	Возврат СписокПараметров;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ИзменитьПараметрыВыбораПолейСубконто(Форма)
	
	ТС = Форма.Элементы.Способы.ТекущаяСтрока;
	
	Если ТС = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	СтрокаСпособ = Форма.Объект.Способы.НайтиПоИдентификатору(ТС);
	ПараметрыДокумента = ПолучитьСписокПараметров(Форма, СтрокаСпособ, "Субконто%Индекс%");
	БухгалтерскийУчетКлиентСервер.ИзменитьПараметрыВыбораПолейСубконто(Форма, СтрокаСпособ, "Субконто%Индекс%", "СпособыСубконто%Индекс%", ПараметрыДокумента); 
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовкиИДоступностьСубконто(Форма, Счет, Постфикс = "", ЭтоТаблица = Ложь)

	ПоляФормы = Новый Структура("Субконто1, Субконто2, Субконто3",
								"СпособыСубконто" + Постфикс + "1", 
								"СпособыСубконто" + Постфикс + "2", 
								"СпособыСубконто" + Постфикс + "3");
		
	БухгалтерскийУчетКлиентСервер.ПриВыбореСчета(Счет, Форма, ПоляФормы, Неопределено, ЭтоТаблица);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособыСубконтоПриИзменении(Элемент)
	
	ИзменитьПараметрыВыбораПолейСубконто(ЭтаФорма);
	
	СтрокаСпособ = Элементы.Способы.ТекущиеДанные;
	ПоляСтроки = Новый Структура("Субконто1, Субконто2, Субконто3, СчетЗатрат, НалоговоеНазначение, НалоговоеНазначениеДоходовИЗатрат, НалоговоеНазначениеТекст, НалоговоеНазначениеДоходовИЗатратТекст, НалоговоеНазначениеТолькоПросмотр, НалоговоеНазначениеДоходовИЗатратТолькоПросмотр");
	ЗаполнитьЗначенияСвойств(ПоляСтроки, СтрокаСпособ);
	ЗаполнитьДобавленныеКолонкиТаблиц(ПоляСтроки);
	ЗаполнитьЗначенияСвойств(СтрокаСпособ, ПоляСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособыСубконтоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтрокаТаблицы = Элементы.Способы.ТекущиеДанные;
	
	ПараметрыДокумента = ПолучитьСписокПараметров(ЭтаФорма, СтрокаТаблицы, "Субконто%Индекс%");
	ПараметрыДокумента.Вставить("СчетУчета", СтрокаТаблицы.СчетЗатрат);
	ОбщегоНазначенияБПКлиент.НачалоВыбораЗначенияСубконто(ЭтаФорма, Элемент, СтандартнаяОбработка, ПараметрыДокумента);

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДобавленныеКолонкиТаблиц(СтрокаТаблицы)
	
	ХарактерЗатрат = Неопределено;
	ЦелевоеНалоговоеНазначение = Неопределено;
	СтруктураСубконто = Новый Структура("Субконто1,Субконто2,Субконто3",СтрокаТаблицы.Субконто1, СтрокаТаблицы.Субконто2, СтрокаТаблицы.Субконто3);
	НалоговыйУчет.ОпределениеАналитикиНалоговогоУчетаВПроводкахДляЗатрат(СтруктураСубконто, СтрокаТаблицы.СчетЗатрат, ХарактерЗатрат,
													       	ЦелевоеНалоговоеНазначение, , 
															СтрокаТаблицы.НалоговоеНазначение, СтрокаТаблицы.НалоговоеНазначениеДоходовИЗатрат);

	СтрокаТаблицы.НалоговоеНазначениеТекст 				= "";														
	СтрокаТаблицы.НалоговоеНазначениеДоходовИЗатратТекст = "";														
	СтрокаТаблицы.НалоговоеНазначениеТолькоПросмотр  = НЕ (ХарактерЗатрат = "ОПЗ");
	СтрокаТаблицы.НалоговоеНазначениеДоходовИЗатратТолькоПросмотр  = НЕ (ХарактерЗатрат = "Затраты");
	
	// для ОПЗ мы будем отображать как налоговое назначение по прибыли (по НК до 01.08.2011) так и по НДС (после 01.08.2011, согласно Закона 3609)
	Если ХарактерЗатрат = "ОПЗ" Тогда
		
		СтрокаТаблицы.НалоговоеНазначениеДоходовИЗатратТолькоПросмотр  = Ложь;
		СтрокаТаблицы.НалоговоеНазначениеДоходовИЗатратТекст = "" + СтрокаТаблицы.НалоговоеНазначениеДоходовИЗатрат + НСтр("ru=' (до 01.08.2011 для ОПЗ)';uk=' (до 01.08.2011 для ЗВВ)'");
		
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.НалоговоеНазначение) Тогда
			СтрокаТаблицы.НалоговоеНазначениеТекст = НСтр("ru='<соответствует нал. назн. ОС>';uk='<відповідає подат. призн. ОЗ>'");	
		КонецЕсли;
		
	ИначеЕсли (ХарактерЗатрат = "Производство" ИЛИ ХарактерЗатрат = "Строительство")
			И ЗначениеЗаполнено(ЦелевоеНалоговоеНазначение) Тогда
		    СтрокаТаблицы.НалоговоеНазначениеТекст = "<"+Строка(ЦелевоеНалоговоеНазначение)+">";	
	КонецЕсли;
	
КонецПроцедуры
	
&НаСервере
Процедура УстановитьДоступностьСубконтоИЗаполнитьДобавленныеКолонкиТаблиц()

	ТипПодразделение = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Подразделения;
	
	Для Каждого Проводка Из Объект.Способы Цикл
		
		ПоляОбъекта = Новый Структура("Субконто1, Субконто2, Субконто3",
		"Субконто1", "Субконто2", "Субконто3");
		БухгалтерскийУчетКлиентСервер.УстановитьДоступностьСубконто(Проводка.СчетЗатрат, Проводка, ПоляОбъекта);
		
		ЕстьСубконтоПодразделение = Ложь;
		ДанныеСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетЗатрат);
		
		Для Н = 1 По 3 Цикл
			ВидСубконто = ДанныеСчета["ВидСубконто" + Н];
			Если ВидСубконто = ТипПодразделение Тогда
				ЕстьСубконтоПодразделение = Истина;
				Прервать
			КонецЕсли;
		КонецЦикла;
		
		Если ЕстьСубконтоПодразделение Тогда
			Проводка["Субконто" + Н + "ТипПодразделение"] = Истина;
		КонецЕсли;
		
		ЗаполнитьДобавленныеКолонкиТаблиц(Проводка);
																
	КонецЦикла;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ Способы

&НаКлиенте
Процедура СпособыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	СтрокаСпособ = Элементы.Способы.ТекущиеДанные;
	
	Если НоваяСтрока Тогда
		СтрокаСпособ.Коэффициент = Истина;
	КонецЕсли;
	
	ИзменитьПараметрыВыбораПолейСубконто(ЭтаФорма);
	
	УстановитьЗаголовкиИДоступностьСубконто(ЭтаФорма, СтрокаСпособ.СчетЗатрат, "", Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособыСчетЗатратПриИзменении(Элемент)
	
	СтрокаСпособ = Элементы.Способы.ТекущиеДанные;
	
	УстановитьЗаголовкиИДоступностьСубконто(ЭтаФорма, СтрокаСпособ.СчетЗатрат, "", Истина);
	
	ПоляОбъекта = Новый Структура("Субконто1, Субконто2, Субконто3",
		"Субконто1", "Субконто2", "Субконто3");
	ПоляОбъекта.Вставить("Организация"  , Объект.Организация);
	БухгалтерскийУчетКлиентСервер.ПриИзмененииСчета(СтрокаСпособ.СчетЗатрат, СтрокаСпособ, ПоляОбъекта, Истина);
	ИзменитьПараметрыВыбораПолейСубконто(ЭтаФорма);
	
	ПоляСтроки = Новый Структура("Субконто1, Субконто2, Субконто3, СчетЗатрат, НалоговоеНазначение, НалоговоеНазначениеДоходовИЗатрат, НалоговоеНазначениеТекст, НалоговоеНазначениеДоходовИЗатратТекст, НалоговоеНазначениеТолькоПросмотр, НалоговоеНазначениеДоходовИЗатратТолькоПросмотр");
	ЗаполнитьЗначенияСвойств(ПоляСтроки, СтрокаСпособ);
	ЗаполнитьДобавленныеКолонкиТаблиц(ПоляСтроки);
	ЗаполнитьЗначенияСвойств(СтрокаСпособ, ПоляСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособыНалоговоеНазначениеДоходовИЗатратПриИзменении(Элемент)
	СтрокаСпособ = Элементы.Способы.ТекущиеДанные;
	ПоляСтроки = Новый Структура("Субконто1, Субконто2, Субконто3, СчетЗатрат, НалоговоеНазначение, НалоговоеНазначениеДоходовИЗатрат, НалоговоеНазначениеТекст, НалоговоеНазначениеДоходовИЗатратТекст, НалоговоеНазначениеТолькоПросмотр, НалоговоеНазначениеДоходовИЗатратТолькоПросмотр");
	ЗаполнитьЗначенияСвойств(ПоляСтроки, СтрокаСпособ);
	ЗаполнитьДобавленныеКолонкиТаблиц(ПоляСтроки);
	ЗаполнитьЗначенияСвойств(СтрокаСпособ, ПоляСтроки);
КонецПроцедуры

&НаКлиенте
Процедура СпособыНалоговоеНазначениеПриИзменении(Элемент)
	СтрокаСпособ = Элементы.Способы.ТекущиеДанные;
	ПоляСтроки = Новый Структура("Субконто1, Субконто2, Субконто3, СчетЗатрат, НалоговоеНазначение, НалоговоеНазначениеДоходовИЗатрат, НалоговоеНазначениеТекст, НалоговоеНазначениеДоходовИЗатратТекст, НалоговоеНазначениеТолькоПросмотр, НалоговоеНазначениеДоходовИЗатратТолькоПросмотр");
	ЗаполнитьЗначенияСвойств(ПоляСтроки, СтрокаСпособ);
	ЗаполнитьДобавленныеКолонкиТаблиц(ПоляСтроки);
	ЗаполнитьЗначенияСвойств(СтрокаСпособ, ПоляСтроки);
КонецПроцедуры

&НаКлиенте
Процедура СпособыСубконто1ПриИзменении(Элемент)
	
	СпособыСубконтоПриИзменении(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособыСубконто2ПриИзменении(Элемент)
	
	СпособыСубконтоПриИзменении(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособыСубконто3ПриИзменении(Элемент)
	
	СпособыСубконтоПриИзменении(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособыСубконто1НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СпособыСубконтоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособыСубконто2НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СпособыСубконтоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособыСубконто3НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СпособыСубконтоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда
		
		НастроитьФормуПоОбъекту();
		
		Если ЭтаФорма.Параметры.Свойство("Организация") И ЗначениеЗаполнено(ЭтаФорма.Параметры.Организация) Тогда
			Объект.Организация = ЭтаФорма.Параметры.Организация;
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	НастроитьФормуПоОбъекту();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	УстановитьДоступностьСубконтоИЗаполнитьДобавленныеКолонкиТаблиц();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ИзменитьПараметрыВыбораПолейСубконто(ЭтаФорма);
КонецПроцедуры



