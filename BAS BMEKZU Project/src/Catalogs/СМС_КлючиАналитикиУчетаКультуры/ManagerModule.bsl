#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура устанавливает пометку на удаление для найденных элементов справочника.
//
// Параметры:
//	СтруктураПараметров - Структура - Параметры выбора элементов справочника
//	ПометкаУдаления - Булево - Признак установки пометки на удаление
//
Процедура УстановитьПометкуУдаления(СтруктураПараметров, ПометкаУдаления) Экспорт

	УстановитьПривилегированныйРежим(Истина);

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Таблица.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СМС_КлючиАналитикиУчетаКультуры КАК Таблица
	|ГДЕ
	|	Таблица.ПометкаУдаления <> &ПометкаУдаления
	|";
	Запрос = Новый Запрос;
	Если СтруктураПараметров.Свойство("Номенклатура") Тогда

		Запрос.УстановитьПараметр("Номенклатура", СтруктураПараметров.Номенклатура);
		ТекстЗапроса = ТекстЗапроса + " И Таблица.Номенклатура = &Номенклатура";

	КонецЕсли;
	Если СтруктураПараметров.Свойство("Договор") Тогда

		Запрос.УстановитьПараметр("Договор", СтруктураПараметров.Договор);
		ТекстЗапроса = ТекстЗапроса + " И Таблица.Договор = &Договор";

	КонецЕсли;
	Если СтруктураПараметров.Свойство("Контрагент") Тогда

		Запрос.УстановитьПараметр("Контрагент", СтруктураПараметров.Контрагент);
		ТекстЗапроса = ТекстЗапроса + " И Таблица.Контрагент = &Контрагент";

	КонецЕсли;
	
	Если СтруктураПараметров.Свойство("Урожай") Тогда

		Запрос.УстановитьПараметр("Урожай", СтруктураПараметров.Урожай);
		ТекстЗапроса = ТекстЗапроса + " И Таблица.Урожай = &Урожай";

	КонецЕсли;
	Если СтруктураПараметров.Свойство("ВидХранения") Тогда

		Запрос.УстановитьПараметр("ВидХранения", СтруктураПараметров.ВидХранения);
		ТекстЗапроса = ТекстЗапроса + " И Таблица.ВидХранения = &ВидХранения";

	КонецЕсли;
	Если СтруктураПараметров.Свойство("ВидФасовки") Тогда

		Запрос.УстановитьПараметр("ВидФасовки", СтруктураПараметров.ВидФасовки);
		ТекстЗапроса = ТекстЗапроса + " И Таблица.ВидФасовки = &ВидФасовки";

	КонецЕсли;
	Если СтруктураПараметров.Свойство("МестоХранения") Тогда

		Запрос.УстановитьПараметр("МестоХранения", СтруктураПараметров.ВидФасовки);
		ТекстЗапроса = ТекстЗапроса + " И Таблица.МестоХранения = &МестоХранения";

	КонецЕсли;
	
	Запрос.УстановитьПараметр("ПометкаУдаления", ПометкаУдаления);

	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл

		Выборка.Ссылка.ПолучитьОбъект().УстановитьПометкуУдаления(ПометкаУдаления);

	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЗаменаДублейКлючейАналитики

Процедура ЗаменитьДублиКлючейАналитики() Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ДанныеСправочника.Ссылка КАК Ссылка,
	|	ДанныеСправочника.ПометкаУдаления КАК ПометкаУдаления,
	|	Аналитика.КлючАналитики КАК КлючАналитики
	|ИЗ
	|	Справочник.СМС_КлючиАналитикиУчетаКультуры КАК ДанныеСправочника
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СМС_АналитикаУчетаКультуры КАК ДанныеРегистра
	|	ПО
	|		ДанныеСправочника.Ссылка = ДанныеРегистра.КлючАналитики
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СМС_АналитикаУчетаКультуры КАК Аналитика
	|	ПО
	|		ДанныеСправочника.Номенклатура		= Аналитика.Номенклатура
	|		И ДанныеСправочника.Договор			= Аналитика.Договор
	|		И ДанныеСправочника.Контрагент		= Аналитика.Контрагент
	|		И ДанныеСправочника.Склад			= Аналитика.Склад
	|		И ДанныеСправочника.Урожай			= Аналитика.Урожай
	|		И ДанныеСправочника.ВидХранения		= Аналитика.ВидХранения
	|		И ДанныеСправочника.ВидФасовки		= Аналитика.ВидФасовки
	|		И ДанныеСправочника.МестоХранения	= Аналитика.МестоХранения
	|ГДЕ
	|	ДанныеРегистра.КлючАналитики ЕСТЬ NULL
	|");
	
	// Сформируем соответствие ключей аналитики.
	СоответствиеАналитик = Новый Соответствие;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
	
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			СоответствиеАналитик.Вставить(Выборка.Ссылка, Выборка.КлючАналитики);
			
			Если Не Выборка.ПометкаУдаления Тогда
				СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
				Попытка
					СправочникОбъект.УстановитьПометкуУдаления(Истина, Ложь);
				Исключение
				КонецПопытки;
			КонецЕсли;

		КонецЦикла;
		СМС_УчетОстатковКультурДоПартий_Сервер.ЗаменитьСсылки(СоответствиеАналитик);
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли