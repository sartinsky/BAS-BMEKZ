#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЭтаФорма.Параметры.Свойство("СтрокаПоиска") Тогда
		СтрокаПоиска = ЭтаФорма.Параметры.СтрокаПоиска;
	КонецЕсли;
	
	Макет = Справочники.ИНАГРО_ВидыКультур.ПолучитьМакет("Классификатор");
	ТабДокумент.Вывести(Макет);
	ТабДокумент.ФиксацияСверху = 3;
	
	ОбластьНаименованиеЛево          = Макет.Области.Наименование.Лево;
	ОбластьНаименованиеПраво         = Макет.Области.Наименование.Право;
	ОбластьГОСТЛево                  = Макет.Области.ГОСТ.Лево;
	ОбластьГОСТПраво                 = Макет.Области.ГОСТ.Право;
	ОбластьРассчитыватьХранениеЛево  = Макет.Области.РассчитыватьХранение.Лево;
	ОбластьРассчитыватьХранениеПраво = Макет.Области.РассчитыватьХранение.Право;
		
	Если НЕ ПустаяСтрока(СтрокаПоиска) Тогда
		НайденнаяОбласть = ТабДокумент.НайтиТекст(СтрокаПоиска, , ТабДокумент.Области.Наименование, , , , Истина);
		Элементы.ТабДокумент.ТекущаяОбласть = НайденнаяОбласть;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтрокаПоискаПриИзменении(Элемент)
	
	ИскатьСтрокуВТаблице(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	Область = Элементы.ТабДокумент.ТекущаяОбласть;
	ВыполнитьВыбор(Область);
	
КонецПроцедуры

&НаКлиенте
Процедура ИскатьСтрокуВТаблице(Команда)
	
	Если ПустаяСтрока(СтрокаПоиска) Тогда
		ПоказатьПредупреждение( , НСтр("ru='Не задана строка поиска';uk='Не заданий рядок пошуку'"));
		ТекущийЭлемент = Элементы.СтрокаПоиска;
		Возврат;
	КонецЕсли;
	
	НайденнаяОбласть = ТабДокумент.НайтиТекст(СтрокаПоиска, Элементы.ТабДокумент.ТекущаяОбласть, , , , , Истина);
	Если НайденнаяОбласть = Неопределено Тогда
		НайденнаяОбласть = ТабДокумент.НайтиТекст(СтрокаПоиска, , , , , , Истина);
		Если НайденнаяОбласть = Неопределено Тогда
			ОбщегоНазначенияБПКлиентСервер.СообщитьОбОшибке(
				НСтр("ru='Строка не найдена';uk='Рядок не знайдено'"));
			ТекущийЭлемент = Элементы.СтрокаПоиска;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Элементы.ТабДокумент.ТекущаяОбласть = НайденнаяОбласть;
	ТекущийЭлемент = Элементы.СтрокаПоиска;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ТабДокументВыбор(Элемент, Область, СтандартнаяОбработка)
	
	Область = Элементы.ТабДокумент.ТекущаяОбласть;
	ВыполнитьВыбор(Область);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьВыбор(ТекущаяОбласть)
	
	Наименование         = ТабДокумент.Область(ТекущаяОбласть.Верх, ОбластьНаименованиеЛево,
											   ТекущаяОбласть.Низ,  ОбластьНаименованиеПраво).Текст;
	ГОСТ                 = ТабДокумент.Область(ТекущаяОбласть.Верх, ОбластьГОСТЛево,
											   ТекущаяОбласть.Низ,  ОбластьГОСТПраво).Текст;
	РассчитыватьХранение = ТабДокумент.Область(ТекущаяОбласть.Верх, ОбластьРассчитыватьХранениеЛево,
											   ТекущаяОбласть.Низ,  ОбластьРассчитыватьХранениеПраво).Текст;
		
	ЗначенияЗаполнения = Новый Структура("Наименование, ГОСТ, РассчитыватьХранение",
										 СтрПолучитьСтроку(Наименование, 1), СтрПолучитьСтроку(ГОСТ, 1), СтрПолучитьСтроку(РассчитыватьХранение, 1));
	
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ВидКультуры = ПоискСуществующегоВидаКультуры(ПараметрыФормы);
	
	Если ВидКультуры <> Неопределено Тогда
		
		ПараметрыФормы = Новый Структура("Ключ", ВидКультуры);

		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='В справочнике ""Виды культур"" уже существует элемент с кодом ""%1""! Открыт существующий';uk='У довіднику ""Види культур"" вже існує елемент з кодом ""%1""! Відкритий існуючий'"), Наименование);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);

		ОткрытьФорму("Справочник.ИНАГРО_ВидыКультур.Форма.ФормаЭлемента", ПараметрыФормы, ЭтаФорма);
		
	Иначе	
		
		ОткрытьФорму("Справочник.ИНАГРО_ВидыКультур.Форма.ФормаЭлемента", ПараметрыФормы, ЭтаФорма);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПоискСуществующегоВидаКультуры(Знач ПараметрыФормы)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ИНАГРО_ВидыКультур.Ссылка
		|ИЗ
		|	Справочник.ИНАГРО_ВидыКультур КАК ИНАГРО_ВидыКультур
		|ГДЕ
		|	ИНАГРО_ВидыКультур.Наименование = &Наименование";
	
	Запрос.УстановитьПараметр("Наименование", ПараметрыФормы.ЗначенияЗаполнения.Наименование);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

#КонецОбласти
