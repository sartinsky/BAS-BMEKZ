
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	
	Если Объект.ВидыХарактеристик.Количество()> 0 Тогда
		ТекстВопроса = НСтр("ru='Табличная часть будет очищена. Продолжить?';uk='Таблична частина буде очищена. Продовжити?'");
		Оповещение = Новый ОписаниеОповещения("ЗаполнитьЗавершение", ЭтотОбъект, Параметры);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);  
	Иначе
		ЗаполнитьПоУмолчанию(); 
	КонецЕсли; 	

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
   	
    Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда 		
		Возврат;
	КонецЕсли;
	
	Объект.ВидыХарактеристик.Очистить();  	
	
	ЗаполнитьПоУмолчанию(); 
			
КонецПроцедуры  

&НаКлиенте
Процедура ЗаполнитьПоУмолчанию()
	 	
	МакетПодбора = ПолучитьМакет();
	
	СписокОбластей = Новый СписокЗначений;

	Для Каждого Область Из МакетПодбора.Области Цикл
		СписокОбластей.Добавить(Область.Имя);
	КонецЦикла;
	
	Для Каждого Строка Из СписокОбластей Цикл
		Строка.Представление = ПолучитьПредставлениеИмени(Строка.Значение);
	КонецЦикла;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("МакетПодбора", МакетПодбора);	
			
	Оповещение = Новый ОписаниеОповещения("ЗаполнитьПоУмолчаниюПродолжение", ЭтотОбъект, ДополнительныеПараметры); 
	
	СписокОбластей.ПоказатьВыборЭлемента(Оповещение, НСтр("ru='Выберите культуру';uk='Виберіть культуру'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоУмолчаниюПродолжение(ЭлементПодбора, ДополнительныеПараметры) Экспорт
	
	Если ЭлементПодбора = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Подбор = Новый Структура;
	Подбор.Вставить("МакетПодбора",           ДополнительныеПараметры.МакетПодбора);
	Подбор.Вставить("ЗначениеЭлементПодбора", ЭлементПодбора.Значение);
	
	ЗаполнитьХарактеристики(Подбор);
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВидыХарактеристикВидХарактеристикиПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ВидыХарактеристик.ТекущиеДанные;
	
	ДанныеСтрокиТаблицы = Новый Структура("ВидХарактеристики, ЕдиницаИзмерения");
	ЗаполнитьЗначенияСвойств(ДанныеСтрокиТаблицы, ТекущиеДанные);

	ВидыХарактеристикВидХарактеристикиПриИзмененииНаСервере(ДанныеСтрокиТаблицы);
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ДанныеСтрокиТаблицы);	

КонецПроцедуры

&НаСервере
Функция ПолучитьМакет()
	
	МакетПодбора = Справочники.ИНАГРО_ВидыКультур.ПолучитьМакет("КлассификаторЭлемента");

	Возврат МакетПодбора;

КонецФункции

&НаКлиенте
Функция ПолучитьПредставлениеИмени(Имя)
	
	Представление = СтрЗаменить(Имя, "_", " ");
	
	Возврат Представление;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьХарактеристики(Подбор)
	
	МакетПодбора   = Подбор.МакетПодбора;	
	ОбластьПодбора = МакетПодбора.Область(Подбор.ЗначениеЭлементПодбора);
	
	Для НомерСтроки = ОбластьПодбора.Верх По ОбластьПодбора.Низ  Цикл
		
		Для НомерКолонки = ОбластьПодбора.Лево По ОбластьПодбора.Право Цикл
			
			Ячейка = МакетПодбора.Область("R" + Строка(НомерСтроки) + "|C" + Строка(НомерКолонки + 1));
			
			Если Ячейка.Текст = "" Тогда 
				Продолжить;
			КонецЕсли;
			
			Попытка
				ВидХарактеристики = ПланыВидовХарактеристик.ИНАГРО_ХарактеристикиВидовКультур[Ячейка.Текст];
				НоваяСтрока = Объект.ВидыХарактеристик.Добавить();
				НоваяСтрока.ВидХарактеристики = ВидХарактеристики;
			Исключение
				ТекстСообщения = НСтр("ru='Элемент %1 не найден';uk='Елемент %1 не знайдено'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения,Ячейка.Текст));
			КонецПопытки;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Если РегистрыСведений.ИНАГРО_МасличныеКультуры.Получить(Новый Структура("ВидКультуры", Объект.Ссылка)).МасличноеЗерно Тогда
		НоваяСтрока = Объект.ВидыХарактеристик.Добавить();
		НоваяСтрока.ВидХарактеристики = ПланыВидовХарактеристик.ИНАГРО_ХарактеристикиВидовКультур.Масличность;
	КонецЕсли;
	
	ЗаполнитьЕдиницаИзмерения();
	
Конецпроцедуры

&НаСервере
Процедура ЗаполнитьЕдиницаИзмерения()
	
	Для Каждого СтрокаТабличнойЧасти Из Объект.ВидыХарактеристик Цикл
		СтрокаТабличнойЧасти.ЕдиницаИзмерения = СтрокаТабличнойЧасти.ВидХарактеристики.ЕдиницаИзмерения;		
	КонецЦикла;
	
КонецПроцедуры

#Область ОбработчикиСобытийТаблицФормы

&НаСервереБезКонтекста
Процедура ВидыХарактеристикВидХарактеристикиПриИзмененииНаСервере(СтрокаТабличнойЧасти)
	
	СтрокаТабличнойЧасти.ЕдиницаИзмерения = СтрокаТабличнойЧасти.ВидХарактеристики.ЕдиницаИзмерения;
	
КонецПроцедуры 
 
#КонецОбласти

#КонецОбласти