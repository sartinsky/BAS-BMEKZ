////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Заголовок = Параметры.Фамилия + " " + Параметры.Имя + " " + Параметры.Отчество;
	
	Если Параметры.ВозможнаПроверкаПоДРФО Тогда
		
		Элементы.ДекорацияИнформация.Заголовок = НСтр("ru='Найден человек с похожим именем.
|Для того, что бы проверить, чот это тот
|человек, данные которого вводятся
|введите ДРФО нового человека.';uk=""Знайдена людина зі схожим ім'ям.
|Для того, що б перевірити, що це та
|людина, дані якої вводяться
|введіть ДРФО нової людини.""");
		
		Элементы.ГруппаРежимПроверкиДРФО.ТекущаяСтраница = Элементы.СтраницаНадписьДРФО;
		
		
	КонецЕсли;
	
	УстановитьСтраницыВвводаДРФО(ЭтаФорма);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ДРФОПриИзменении(Элемент)
	
	СообщенияПроверки = "";
	
	Если НЕ ПустаяСтрока(ДРФО) Тогда
		
		Если ФизическиеЛицаКлиентСервер.ДРФОСоответствуетТребованиям(ДРФО, СообщенияПроверки) Тогда
		
			Если ИдентификаторФизическогоЛицаУникален("ДРФО", ДРФО) Тогда
			
				ПоДРФОПроверкаПройдена = Истина;
				
				ПроверкаПройдена();
		
			Иначе
			
				ПоДРФОПроверкаПройдена = Ложь;
				
				ДРФОКартинка = БиблиотекаКартинок.ПротоколСОшибкой;
			
				СообщенияПроверки = НСтр("ru='Найден человек с таким же ДРФО. Обратитесь к администратору информационной системы для устранения проблемы.';uk='Знайдена людина з таким же ДРФО. Зверніться до адміністратора інформаційної системи для усунення проблеми.'");
				
				Элементы.ГруппаРежимПроверкиДРФО.ТолькоПросмотр = Истина;
				
			КонецЕсли;
			
		Иначе
		
			ПоДРФОПроверкаПройдена = Ложь;
				
			ДРФОКартинка = БиблиотекаКартинок.Предупреждение;
			
		КонецЕсли;
		
	Иначе
		
		ДРФОКартинка = Новый Картинка;
		
		Элементы.ГруппаРежимПроверкиДРФО.ТолькоПросмотр = Ложь;
				
	КонецЕсли;
	
	Элементы.ДРФО.Подсказка = СообщенияПроверки;
	Элементы.ДРФОКартинка.Подсказка = СообщенияПроверки;
	
КонецПроцедуры

&НаКлиенте
Процедура РежимПроверкиДРФО1ПриИзменении(Элемент)
	
	УстановитьСтраницыВвводаДРФО(ЭтаФорма);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура ПроверкаПройдена()
	
	Если ПоДРФОПроверкаПройдена 
		 Тогда
		
		СтруктураВозварта = Новый Структура();
		
		Если ПоДРФОПроверкаПройдена Тогда
			СтруктураВозварта.Вставить("ДРФО", ДРФО);
		КонецЕсли; 
		
		Закрыть(СтруктураВозварта);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ИдентификаторФизическогоЛицаУникален(Идентификатор, ЗначениеИдентификатора)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ФизическиеЛица.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ФизическиеЛица КАК ФизическиеЛица
	|ГДЕ
	|	ФизическиеЛица.ДРФО = &ЗначениеИдентификатора";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, ".ДРФО", "." + Идентификатор);
	
	Запрос.УстановитьПараметр("ЗначениеИдентификатора", ЗначениеИдентификатора);
	
	УстановитьПривилегированныйРежим(Истина);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат РезультатЗапроса.Пустой();
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСтраницыВвводаДРФО(Форма)
	
	Элементы = Форма.Элементы;
	
	Если Форма.РежимПроверки = 0 Тогда
		
		Элементы.ГруппаСтраницыДРФО.ТекущаяСтраница = Элементы.СтраницаДРФОДоступен;
		
	Иначе
		
		Элементы.ГруппаСтраницыДРФО.ТекущаяСтраница = Элементы.СтраницаДРФОНеДоступен;
		
	КонецЕсли;
	
КонецПроцедуры
