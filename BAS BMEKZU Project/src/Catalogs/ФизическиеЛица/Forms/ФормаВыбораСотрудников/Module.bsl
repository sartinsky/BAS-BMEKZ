
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НаименованиеОрганизации = "";
	Если Параметры.Отбор.Свойство("Организация") Тогда
		НаименованиеОрганизации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Отбор.Организация, "Наименование");
	КонецЕсли;
	
	Если ПустаяСтрока(НаименованиеОрганизации) Тогда
		НаименованиеОрганизации = НСтр("ru='<Организация не указана>';uk='<Організація не зазначена>'");
	КонецЕсли;
	
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Сотрудники (%1)';uk='Співробітники (%1)'"), НаименованиеОрганизации);
	
	Если Параметры.РежимВыбора и Параметры.ЗакрыватьПриВыборе = Ложь Тогда
		Элементы.Список.МножественныйВыбор = Истина;
		Элементы.Список.РежимВыделения = РежимВыделенияТаблицы.Множественный;
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("ДатаПримененияОтбора") 
		И ЗначениеЗаполнено(Параметры.Отбор.ДатаПримененияОтбора) Тогда
		
		ЗаголовокГруппы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Работающие на %1';uk='Працюють на %1'"),
			Формат(Параметры.Отбор.ДатаПримененияОтбора, "ДЛФ=DD"));
		
		ГруппаИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(Список.Отбор.Элементы, ЗаголовокГруппы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		ГруппаИ.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ;
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			ГруппаИ, "ДатаПриема", Параметры.Отбор.ДатаПримененияОтбора, ВидСравненияКомпоновкиДанных.МеньшеИлиРавно);
			
		ГруппаИЛИДляДатаПриема = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ГруппаИ.Элементы, "ГруппаИЛИДляДатаПриема", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			ГруппаИЛИДляДатаПриема, "ДатаПриема", , ВидСравненияКомпоновкиДанных.Заполнено);
			
		ГруппаИЛИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ГруппаИ.Элементы, "ГруппаИЛИ", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			ГруппаИЛИ, "ДатаУвольнения", Параметры.Отбор.ДатаПримененияОтбора, ВидСравненияКомпоновкиДанных.БольшеИлиРавно);
			
		ГруппаИЛИДляДатаУвольнения = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ГруппаИЛИ.Элементы, "ГруппаИЛИДляДатаУвольнения", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			ГруппаИЛИДляДатаУвольнения, "ДатаУвольнения", , ВидСравненияКомпоновкиДанных.НеЗаполнено);
		
		Параметры.Отбор.Удалить("ДатаПримененияОтбора");
		
	КонецЕсли; 
		
КонецПроцедуры
