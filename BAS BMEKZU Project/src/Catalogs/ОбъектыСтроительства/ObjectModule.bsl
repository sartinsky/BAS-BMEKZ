////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ

// Функция проверяет, существуют ли движения по ОбъектыСтроительства.
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  Истина - если есть движения, Ложь - если нет.
//
Функция СуществуютСсылки()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Значение", Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	1
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Субконто КАК ХозрасчетныйСубконто
	|ГДЕ
	|	ХозрасчетныйСубконто.Вид = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОбъектыСтроительства)
	|	И ХозрасчетныйСубконто.Значение = &Значение"; 

	СтатусВозврата = НЕ Запрос.Выполнить().Пустой();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат СтатусВозврата;
	
КонецФункции // СуществуютСсылки()

// Процедура проверяет, существуют ли движения в регистрах по данному
// элементу справочника. Если существуют и изменялись определенные реквизиты,
// то запрещает запись.
Процедура ПроверитьВозможностьЗаписиЭлемента(Отказ)

	Если ЭтоНовый() Тогда
		
		// Проверять нечего
		Возврат
		
	КонецЕсли;

	// Получение старых значений
	НалоговоеНазначениеСтарый  = Ссылка.НалоговоеНазначение;
	// Проверка изменений, если старое значение было заполнено
	ИзмененНалоговоеНазначение = (НалоговоеНазначение <> НалоговоеНазначениеСтарый) И ЗначениеЗаполнено(НалоговоеНазначениеСтарый);

	Если ИзмененНалоговоеНазначение Тогда
		
		Если СуществуютСсылки() Тогда
			
			ТекстСообщения = НСтр("ru='Объект строительства ""%1"" участвует в движениях по регистру бухгалтерии ""Хозрасчетный"".
|Нельзя изменять налоговое назначение ""%2""!';uk='Об''єкт будівництва ""%1"" бере участь у рухах по регістру бухгалтерії ""Госпрозрахунковий""
|Не можна змінювати податкове призначення ""%2""!'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ТекстСообщения, СокрЛП(Наименование), НалоговоеНазначениеСтарый);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка, "НалоговоеНазначение", "Объект", Отказ);
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры // ПроверитьВозможностьЗаписиЭлемента()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьВозможностьЗаписиЭлемента(Отказ);

КонецПроцедуры
