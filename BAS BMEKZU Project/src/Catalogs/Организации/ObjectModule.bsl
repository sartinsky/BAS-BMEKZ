#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ИндивидуальныйПредприниматель");
		Если НЕ ОбособленноеПодразделение Тогда
			МассивНепроверяемыхРеквизитов.Добавить("ГоловнаяОрганизация");
		КонецЕсли; 
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("ГоловнаяОрганизация");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбособленноеПодразделение И (ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо) Тогда
		ОбособленноеПодразделение = Ложь;
	КонецЕсли;
	
	Если НЕ ОбособленноеПодразделение Тогда
		
		Если ЭтоНовый() Тогда
			Если ПолучитьСсылкуНового().Пустая() Тогда
				УстановитьСсылкуНового(Справочники.Организации.ПолучитьСсылку());
				ГоловнаяОрганизация = ПолучитьСсылкуНового();
			КонецЕсли;
		ИначеЕсли ГоловнаяОрганизация <> Ссылка Тогда
			ГоловнаяОрганизация = Ссылка;
		КонецЕсли;
		
	КонецЕсли;

	УправлениеКонтактнойИнформациейБП.ПередЗаписьюИсторииКонтактнойИнформацииОбъекта(ЭтотОбъект);

	Если НЕ РегламентированнаяОтчетностьВызовСервера.ИспользуютсяВстроенныеРегламентированныеОтчеты()
		И НЕ ЗначениеЗаполнено(ДатаНачалаИспользованияЗвит1С) Тогда
		
		ДатаНачалаИспользованияЗвит1С = '19800101';
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	
	КодПоЕДРПОУ = "";
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	СоздатьОсновноеПодразделение();	

//++ БУ ЗИК
	УчетЗарплаты.СоздатьПараметрыУчетаЗарплаты(Ссылка);
//-- БУ ЗИК
	
	ОбщегоНазначенияБПВызовСервера.УстановитьОсновнуюОрганизациюТекущемуПользователю();
	
	Если НЕ Отказ  Тогда
		ОбновитьПовторноИспользуемыеЗначения();
	КонецЕсли;	
	
КонецПроцедуры


Процедура СоздатьОсновноеПодразделение()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПодразделенияОрганизаций.Ссылка
	|ИЗ
	|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
	|ГДЕ
	|	ПодразделенияОрганизаций.Владелец = &Организация";
	Запрос.УстановитьПараметр("Организация", Ссылка);
	
	Если Запрос.Выполнить().Выбрать().Количество() > 0 Тогда
		Возврат;
	КонецЕсли;
	
	Подразделение = Справочники.ПодразделенияОрганизаций.СоздатьЭлемент();
	Подразделение.Владелец = Ссылка;
	Подразделение.Наименование = НСтр("ru='Основное подразделение';uk='Основний підрозділ'",Локализация.КодЯзыкаИнформационнойБазы());
	Подразделение.Записать();
		
КонецПроцедуры


#КонецЕсли
