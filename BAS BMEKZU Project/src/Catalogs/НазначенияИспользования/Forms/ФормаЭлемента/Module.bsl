////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)

	ЭтоНеМБП = (Форма.Объект.ВидМалоценногоАктива <> ПредопределенноеЗначение("Перечисление.ВидыМалоценныхАктивов.МалоценныйБыстроизнашивающийсяПредмет"));
	Форма.Элементы.ГруппаСчетаНеМБП.Видимость 	= ЭтоНеМБП;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьЗначенияРасшифровок(Номенклатура, Коэффициент, ПредставлениеКоэффициент, ЕдиницаИзмерения, СрокИспользования, РашифровкаСрокИспользования)

	Коэффициент         = Справочники.Номенклатура.ПолучитьКоэффициентЕдиницыИзмерения(Номенклатура, ЕдиницаИзмерения);
	
	ПредставлениеКоэффициент = "К.: " + Формат(Коэффициент, "ЧДЦ=3");
	
	РашифровкаСрокИспользования = УправлениеВнеоборотнымиАктивамиКлиентСервер.РасшифровкаСрокаПолезногоИспользования(СрокИспользования);
	
КонецПроцедуры
 
&НаСервере
Процедура ПриИзмененииВладельца()
	
	Если Объект.Владелец.Услуга Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Для услуг не применяются назначения использования!';uk='Для послуг не застосовуються призначення використання!'"));
		Объект.Владелец = Справочники.Номенклатура.ПустаяСсылка();
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.Наименование) Тогда
		
		Объект.Наименование = Объект.Владелец;	
		
	КонецЕсли;
	
	Объект.ЕдиницаИзмерения    = Объект.Владелец.БазоваяЕдиницаИзмерения;
	
КонецПроцедуры	

&НаСервере
Процедура ПроверитьПравильностьЗаполненияСчетаПередачиБУ(Сообщать = Ложь)
	
	РеквизитФормыВЗначение("Объект").ПроверитьПравильностьЗаполненияСчетаПередачиБУ(Объект.ВидМалоценногоАктива, Объект.СчетПередачиБУ, Сообщать);
	
КонецПроцедуры	

&НаСервере
Процедура ПроверитьПравильностьЗаполненияСчетаАмортизацииБУ(Сообщать = Ложь)
	
	РеквизитФормыВЗначение("Объект").ПроверитьПравильностьЗаполненияСчетаАмортизацииБУ(Объект.ВидМалоценногоАктива, Объект.СчетАмортизацииБУ, Сообщать);
	
КонецПроцедуры	

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура ВладелецПриИзменении(Элемент)
	
	ПриИзмененииВладельца();
	
	УстановитьЗначенияРасшифровок(Объект.Владелец, Объект.Коэффициент, ПредставлениеКоэффициент, Объект.ЕдиницаИзмерения, Объект.СрокПолезногоИспользования, РашифровкаСрокПолезногоИспользования);
	
КонецПроцедуры

&НаКлиенте
Процедура СрокПолезногоИспользованияПриИзменении(Элемент)
	
	УстановитьЗначенияРасшифровок(Объект.Владелец, Объект.Коэффициент, ПредставлениеКоэффициент, Объект.ЕдиницаИзмерения, Объект.СрокПолезногоИспользования, РашифровкаСрокПолезногоИспользования);
	
КонецПроцедуры

&НаКлиенте
Процедура ЕдиницаИзмеренияПриИзменении(Элемент)
	
	УстановитьЗначенияРасшифровок(Объект.Владелец, Объект.Коэффициент, ПредставлениеКоэффициент, Объект.ЕдиницаИзмерения, Объект.СрокПолезногоИспользования, РашифровкаСрокПолезногоИспользования);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидМалоценногоАктиваПриИзменении(Элемент)
	
	ПроверитьПравильностьЗаполненияСчетаПередачиБУ(Ложь);
	ПроверитьПравильностьЗаполненияСчетаАмортизацииБУ(Ложь);
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетПередачиБУПриИзменении(Элемент)
	
	ПроверитьПравильностьЗаполненияСчетаПередачиБУ(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетАмортизацииБУПриИзменении(Элемент)
	
	ПроверитьПравильностьЗаполненияСчетаАмортизацииБУ(Истина);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьЗначенияРасшифровок(Объект.Владелец, Объект.Коэффициент, ПредставлениеКоэффициент, Объект.ЕдиницаИзмерения, Объект.СрокПолезногоИспользования, РашифровкаСрокПолезногоИспользования);
	
	УправлениеФормой(ЭтаФорма);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
КонецПроцедуры
