#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
//+++	
// Возвращает реквизиты справочника, которые образуют естественный ключ
//  для элементов справочника.
//
// Возвращаемое значение: Массив(Строка) - массив имен реквизитов, образующих
//  естественный ключ.
//
Функция ПоляЕстественногоКлюча() Экспорт
	
	Результат = Новый Массив();
	
	Результат.Добавить("ИсточникОтчета");
	
	Возврат Результат;
	
КонецФункции


// Функция возвращает регламентированный отчет по переданой ссылке на справочник
//
Функция ПолучитьРегламентированныйОтчетПоСсылке(Ссылка) Экспорт
	
	ИсточникОтчета = Ссылка.ИсточникОтчета;
	Если Не ЗначениеЗаполнено(ИсточникОтчета) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Ссылка = РегламентированнаяОтчетность.ПолучитьРеглОтчетПоУмолчанию(ИсточникОтчета) Тогда
		Возврат РегламентированнаяОтчетность.РеглОтчеты(ИсточникОтчета);
	КонецЕсли;
	
	Если Не Ссылка.ВнешнийОтчетИспользовать Тогда
		Если Метаданные.Отчеты.Найти(ИсточникОтчета) <> Неопределено Тогда
			Возврат Отчеты[ИсточникОтчета];
		Иначе        
			Возврат Неопределено;
		КонецЕсли; 
	Иначе
		Попытка
			ВременныйФайл = ПолучитьИмяВременногоФайла();
			Ссылка.ВнешнийОтчетХранилище.Получить().Записать(ВременныйФайл);
			ВнешнийОтчет = ВнешниеОтчеты.Создать(ВременныйФайл);
			Попытка
				ВнешнийОтчет.мПолноеИмяФайлаВнешнейОбработки = ВременныйФайл;
			Исключение
			КонецПопытки;
			Возврат ВнешнийОтчет;
		Исключение
			Возврат Неопределено;
		Конецпопытки;
	
	КонецЕсли;
	
КонецФункции

// Функция получает список поддерживаемых схем из ТекОтчет,
// дополняет им переденную кеш таблицу и возвращает массив добавленных строк
Процедура ДополнитьКешТаблицу(ПараметрОтчет, КешТаблица) Экспорт
	
	ТекущийОтчет = ПолучитьРегламентированныйОтчетПоСсылке(ПараметрОтчет);
	
	ТекОтчетМетаданные = Метаданные.НайтиПоТипу(ТипЗнч(ТекущийОтчет));
	
	Если ТекОтчетМетаданные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекОтчетМетаданные.Макеты.Найти("ПоказателиЗвит1С") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МакетПоказателиЗвит1С = ТекущийОтчет.ПолучитьМакет("ПоказателиЗвит1С");
	МакетПоказателиЗвит1С.КодЯзыкаМакета = "ru";
	
	Если МакетПоказателиЗвит1С.Области.Найти("Структура") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбластьСтруктура = МакетПоказателиЗвит1С.ПолучитьОбласть("Структура");
	
	Для сч = 1 По ОбластьСтруктура.ВысотаТаблицы Цикл
		ИмяСхемыЗвит1С = СокрЛП(ОбластьСтруктура.Область(сч, 1, сч, 1).Текст);
		ИмяОбласти     = СокрЛП(ОбластьСтруктура.Область(сч, 2, сч, 2).Текст);
		ИмяФормыОтчета = СокрЛП(ОбластьСтруктура.Область(сч, 3, сч, 3).Текст);
		
		Если ПустаяСтрока(ИмяСхемыЗвит1С) ИЛИ Лев(ИмяСхемыЗвит1С, 2)= "//" 
		 ИЛИ ПустаяСтрока(ИмяФормыОтчета) Тогда
			Продолжить; 
		КонецЕсли;
		
		// Для наглядности не заполняем параметр "ИмяОбласти" если он совпадает с "ИмяСхемы"
		Если ПустаяСтрока(ИмяОбласти) Тогда
			ИмяОбласти = ИмяСхемыЗвит1С;
		КонецЕсли;
		
		НоваяСтрока = КешТаблица.Добавить();
		
		НоваяСтрока.ИмяСхемыЗвит1С = ИмяСхемыЗвит1С;
		НоваяСтрока.ИмяФормы	   = ИмяФормыОтчета;
		НоваяСтрока.ИмяОбласти     = ИмяОбласти;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли