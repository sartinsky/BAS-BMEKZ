
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ХарактерЗатрат = УправлениеПроизводствомВызовСервера.ПолучитьХарактерЗатратПоСчетуЗатрат(СчетБУ, Неопределено);
	
	Если (ХарактерЗатрат <> "Производство" И ХарактерЗатрат <> "Строительство" И ХарактерЗатрат <> "ОПЗ") И ЗначениеЗаполнено(НалоговоеНазначение) Тогда
		НалоговоеНазначение = Справочники.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка();
	КонецЕсли;
	Если (ХарактерЗатрат <> "Затраты" И ХарактерЗатрат <> "ОПЗ") И ЗначениеЗаполнено(НалоговоеНазначениеДоходовИЗатрат) Тогда
		НалоговоеНазначениеДоходовИЗатрат = Справочники.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив();
	
	ДанныеСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СчетБУ);
	Для Индекс = 1 По 3 Цикл
		Если Индекс > ДанныеСчета.КоличествоСубконто Тогда
			МассивНепроверяемыхРеквизитов.Добавить("СубконтоБУ" + Индекс);
		КонецЕсли;
	КонецЦикла;
	
	ХарактерЗатрат = УправлениеПроизводствомВызовСервера.ПолучитьХарактерЗатратПоСчетуЗатрат(СчетБУ, Неопределено);
	
	ТипОбъектСтроительста = Тип("СправочникСсылка.ОбъектыСтроительства");
	ТипНМА                = Тип("СправочникСсылка.НематериальныеАктивы");
	ТипНГ                 = Тип("СправочникСсылка.НоменклатурныеГруппы");
	
	Если ХарактерЗатрат = "Производство"
	 ИЛИ ХарактерЗатрат = "Строительство" Тогда
		
		ЦелевоеНалоговоеНазначение = Неопределено;
		
		Для Н = 1 По 3 Цикл
			
			ЗначениеСубконто = ЭтотОбъект["СубконтоБУ" + Н];
			
			Если ТипЗнч(ЗначениеСубконто) = ТипНГ Тогда
				
				ЦелевоеНалоговоеНазначение = ЭтотОбъект["СубконтоБУ" +Н].НалоговоеНазначениеВПроизводстве;
				Прервать;
				
			ИначеЕсли ТипЗнч(ЗначениеСубконто) = ТипОбъектСтроительста
				  ИЛИ ТипЗнч(ЗначениеСубконто) = ТипНМА Тогда
				  
				ЦелевоеНалоговоеНазначение = ЭтотОбъект["СубконтоБУ" +Н].НалоговоеНазначение;
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если Не ЗначениеЗаполнено(ЦелевоеНалоговоеНазначение) Тогда
			// скорее всего налоговый учет не ведется, проверять не будем соответствие
			Возврат;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(НалоговоеНазначение) Тогда
			СтрокаСообщения = НСтр("ru='В аналитике счета указано налговое назначение. Необходимо указать налоговое назначения (НДС) для элемента справочника!';uk='У аналітиці рахунка зазначене податкове призначення. Необхідно зазначити податкове призначення (ПДВ) для елемента довідника!'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщения, ЭтотОбъект, "НалоговоеНазначение", , Отказ);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		Если НалоговоеНазначение = Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_Пропорционально Тогда
			
			Если ЦелевоеНалоговоеНазначение = Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность Тогда
				СтрокаСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru=' Налоговое назначение <%1> нельзя относить на аналитику по нехоз. деятельности (<%2>)';uk=' Податкове призначення <%1> не можна відносити на аналітику по негосп. діяльності (<%2>)'"), НалоговоеНазначение, ЦелевоеНалоговоеНазначение);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщения, ЭтотОбъект, "НалоговоеНазначение", , Отказ);
				Отказ = Истина;
				Возврат;
			КонецЕсли;
		
		ИначеЕсли ЦелевоеНалоговоеНазначение <> НалоговоеНазначение Тогда
			
			СтрокаСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru=' Налоговое назначение <%1> не совпадает с аналитикой затрат <%2>';uk=' Податкове призначення <%1> не збігається з аналітикою витрат <%2>'"), НалоговоеНазначение, ЦелевоеНалоговоеНазначение);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщения, ЭтотОбъект, "НалоговоеНазначение", , Отказ);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
	ИначеЕсли ХарактерЗатрат = "ОПЗ" Тогда
		Если НалоговоеНазначение = Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность Тогда
			СтрокаСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru=' Налоговое назначение <%1> нельзя относить выбирать при отнесении затрат на 91 счет!';uk=' Податкове призначення <%1> не можна обирати при віднесенні витрат на 91 рахунок!'"), НалоговоеНазначение);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщения, ЭтотОбъект, "НалоговоеНазначение", , Отказ);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

#КонецЕсли