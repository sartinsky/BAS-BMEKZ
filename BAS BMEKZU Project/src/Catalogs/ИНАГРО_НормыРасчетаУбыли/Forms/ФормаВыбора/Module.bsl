#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьПослеДобавления" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодобратьИзМакета(Команда)
	
	СтрокаПоиска = "";
	
	ТекДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекДанные <> Неопределено Тогда
		СтрокаПоиска = ТекДанные.Код;
	КонецЕсли;
	
	СтруктураПараметров = Новый Структура("СтрокаПоиска", СтрокаПоиска);
	
	ОткрытьФорму("Справочник.ИНАГРО_НормыРасчетаУбыли.Форма.ФормаВыбораИзКлассификатора", СтруктураПараметров, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
	
&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
	Текст = НСтр("ru='Есть возможность подобрать лабораторные журналы из классификатора.
|Подобрать?';uk='Є можливість підібрати лабораторні журнали з класифікатора.
|Підібрати?'");
		
	ДополнительныеПараметры = Новый Структура;
	Если Копирование Тогда
		ЗначенияЗаполнения = Новый Структура;
		ЗначенияЗаполнения.Вставить("Код",            Элемент.ТекущиеДанные.Код);
		ЗначенияЗаполнения.Вставить("ВидКультуры",    Элемент.ТекущиеДанные.ВидКультуры);
		ЗначенияЗаполнения.Вставить("СпособХранения", Элемент.ТекущиеДанные.СпособХранения);
		ЗначенияЗаполнения.Вставить("П1д",            Элемент.ТекущиеДанные.п1д);
		ЗначенияЗаполнения.Вставить("П3м",            Элемент.ТекущиеДанные.п3м);
		ЗначенияЗаполнения.Вставить("П6м",            Элемент.ТекущиеДанные.п6м);
		ЗначенияЗаполнения.Вставить("П1г",            Элемент.ТекущиеДанные.п1г);
		ДополнительныеПараметры.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ВопросПодобратьНормыРасчетаУбылиЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ПоказатьВопрос(Оповещение, Текст, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
	
КонецПроцедуры

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура ВопросПодобратьНормыРасчетаУбылиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПодобратьИзМакета(Неопределено);
	Иначе
		ОткрытьФорму("Справочник.ИНАГРО_НормыРасчетаУбыли.Форма.ФормаЭлемента", ДополнительныеПараметры, ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти