#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если Параметры.Ключ.Пустая() Тогда
		
		Если ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
			// При копировании очищаем свойства, которые не могут быть продублированы.
			Объект.Идентификатор = СформироватьИдентификаторПоказателя(Объект.Идентификатор, Объект.Ссылка);
		КонецЕсли;
		
	КонецЕсли;
	
	Если Объект.Предопределенный Тогда
		УстановитьДоступностьПолейСлужебногоПоказателя();
	Иначе
		УстановитьРеквизитыПолейИспользованияЗначений(ЭтаФорма);
	КонецЕсли;
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
		
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.Печать
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	БылоНаименование = Объект.Наименование;
	БылИдентификатор = Объект.Идентификатор;
		
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ЕстьРазделители = Ложь;
	Для Позиция = 1 По СтрДлина(Объект.Идентификатор) Цикл
		Если СтроковыеФункцииКлиентСервер.ЭтоРазделительСлов(КодСимвола(Объект.Идентификатор, Позиция)) Тогда
			ЕстьРазделители = Истина;
			Объект.Идентификатор = СтрЗаменить(Объект.Идентификатор, Сред(Объект.Идентификатор, Позиция, 1), "?");
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	ПрежнийИдентификатор = ИдентификаторПоПредставлению(БылоНаименование);
	
	Если Не ЗначениеЗаполнено(Объект.Идентификатор) 
		Или ПрежнийИдентификатор = Объект.Идентификатор Тогда
		Объект.Идентификатор = ИдентификаторПоПредставлению(Объект.Наименование);
		БылИдентификатор = Объект.Идентификатор;
	КонецЕсли;
	
	БылоНаименование = Объект.Наименование;
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция СформироватьИдентификаторПоказателя(Идентификатор, Ссылка)
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
	Запрос.УстановитьПараметр("КоличествоСимволов", СтрДлина(Идентификатор));
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ПоказателиРасчетаЗарплаты.Идентификатор
	               |ИЗ
	               |	Справочник.ИНАГРО_ПоказателиСхемМотивации КАК ПоказателиРасчетаЗарплаты
	               |ГДЕ
	               |	ПОДСТРОКА(ПоказателиРасчетаЗарплаты.Идентификатор, 1, &КоличествоСимволов) = &Идентификатор
	               |	И ПоказателиРасчетаЗарплаты.Ссылка <> &Ссылка";
				   
	ТекущиеИдентификаторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Идентификатор");			   
	
	Сч = 1;
	Пока Истина Цикл 
		НовыйИдентификатор = Идентификатор + Сч;
		Если ТекущиеИдентификаторы.Найти(НовыйИдентификатор) = Неопределено Тогда 
			Возврат НовыйИдентификатор;
		КонецЕсли;
		Сч = Сч + 1;
	КонецЦикла;
	
КонецФункции

&НаСервере
Процедура УстановитьДоступностьПолейСлужебногоПоказателя()
	
	// Если показатель служебный, 
	// то редактировать можно только отдельные поля.
	ДоступныеПоля = Новый Массив;
	ДоступныеПоля.Добавить(Элементы.Наименование);
	
	Для Каждого Элемент Из Элементы Цикл
		Если ДоступныеПоля.Найти(Элемент) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Если ТипЗнч(Элемент) = Тип("ПолеФормы") 
			И Элемент.Вид <> ВидПоляФормы.ПолеНадписи Тогда
			Элемент.Доступность = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ИдентификаторПоПредставлению(Знач Представление)
	
	Идентификатор = "";
	БылРазделитель = Ложь;
	Для НомСимвола = 1 По СтрДлина(Представление) Цикл
		Символ = Сред(Представление, НомСимвола, 1);
		Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Символ) 
			И ПустаяСтрока(Идентификатор) Тогда
			// цифры в начале пропускаем
			Продолжить;
		КонецЕсли;
		Если СтроковыеФункцииКлиентСервер.ЭтоРазделительСлов(КодСимвола(Символ)) Тогда
			БылРазделитель = Истина;
		Иначе
			Если БылРазделитель Тогда
				БылРазделитель = Ложь;
				Символ = ВРег(Символ);
			КонецЕсли;
			Идентификатор = Идентификатор + Символ;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Идентификатор;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьРеквизитыПолейИспользованияЗначений(Форма)
	
	ДоступностьЭлементов = Не Форма.Объект.Предопределенный;
	
	Форма.Элементы.ВидПоказателя.Доступность = ДоступностьЭлементов;
	Форма.Элементы.ВозможностьИзменения.Доступность =  ДоступностьЭлементов;
	Форма.Элементы.ТипПоказателя.Доступность = ДоступностьЭлементов;
	Форма.Элементы.Идентификатор.Доступность = ДоступностьЭлементов;
	
	Форма.Элементы.ТипПоказателя.АвтоОтметкаНезаполненного	= ДоступностьЭлементов;
	Форма.Элементы.ТипПоказателя.ОтметкаНезаполненного		= ДоступностьЭлементов;
	
КонецПроцедуры

#КонецОбласти
