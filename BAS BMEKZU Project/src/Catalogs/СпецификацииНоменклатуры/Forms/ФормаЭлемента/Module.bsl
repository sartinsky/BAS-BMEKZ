
&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ИсточникВыбора.ИмяФормы = "Обработка.ПодборНоменклатуры.Форма.Форма" Тогда
		ОбработкаВыбораПодборНаСервере(ВыбранноеЗначение, "ИсходныеКомплектующие");
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ПодобратьМатериалы(Команда)
	
	ОткрытьФорму(
		"Обработка.ПодборНоменклатуры.Форма.Форма", 
		Новый Структура, 
		ЭтаФорма, 
		УникальныйИдентификатор);
		
КонецПроцедуры
	
	
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура НоменклатураПриИзменении(Элемент)
	
   ПриИзмененииНоменклатурыСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ЕдиницаИзмеренияПриИзменении(Элемент)
	
	ПриИзмененииЕдиницыИзмеренияСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ИсходныеКомплектующиеНоменклатураПриИзменении(Элемент)
	
	ИсходныеКомплектующиеПриИзмененииНоменклатурыСервер(Элементы.ИсходныеКомплектующие.ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсходныеКомплектующиеЕдиницаИзмеренияПриИзменении(Элемент)
	
	ИсходныеКомплектующиеПриИзмененииЕдиницыИзмеренияСервер(Элементы.ИсходныеКомплектующие.ТекущаяСтрока)
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Прочее

&НаСервере
Процедура ОбработкаВыбораПодборНаСервере(ВыбранноеЗначение, ИмяТаблицы)
	
	ПодобранныеМатериалы = ПолучитьИзВременногоХранилища(ВыбранноеЗначение.АдресПодобраннойНоменклатурыВХранилище);
	
	Для Каждого СтрокаТовара Из ПодобранныеМатериалы Цикл
		
		СтруктураОтбора = Новый Структура("Номенклатура, ЕдиницаИзмерения", СтрокаТовара.Номенклатура, СтрокаТовара.ЕдиницаИзмерения);
		СтрокаТабличнойЧасти = НайтиСтрокуТабличнойЧасти(ИмяТаблицы, СтруктураОтбора);
		
		Если СтрокаТабличнойЧасти <> Неопределено Тогда
			// Нашли - увеличиваем количество.
			СтрокаТабличнойЧасти.Количество = СтрокаТабличнойЧасти.Количество + СтрокаТовара.Количество;
			
		Иначе
			
			СтрокаТабличнойЧасти = Объект[ИмяТаблицы].Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, СтрокаТовара);
			
		КонецЕсли;
		

	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция НайтиСтрокуТабличнойЧасти(ИмяТабличнойЧасти, СтруктураОтбора)

	СтрокаТабличнойЧасти = Неопределено;

	МассивНайденныхСтрок = Объект[ИмяТабличнойЧасти].НайтиСтроки(СтруктураОтбора);
	Если МассивНайденныхСтрок.Количество() > 0 Тогда
		// Нашли. Вернем первую найденную строку.
		СтрокаТабличнойЧасти = МассивНайденныхСтрок[0];
	КонецЕсли;

	Возврат СтрокаТабличнойЧасти;

КонецФункции

&НаСервере
Процедура ПриИзмененииНоменклатурыСервер()
	
	Если ЗначениеЗаполнено(Объект.Владелец) Тогда
		Объект.ЕдиницаИзмерения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Владелец, "БазоваяЕдиницаИзмерения").БазоваяЕдиницаИзмерения;
		Объект.Коэффициент = 1;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииЕдиницыИзмеренияСервер()
	
	Если ЗначениеЗаполнено(Объект.ЕдиницаИзмерения) Тогда
		Объект.Коэффициент = Справочники.Номенклатура.ПолучитьКоэффициентЕдиницыИзмерения(Объект.Владелец, Объект.ЕдиницаИзмерения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИсходныеКомплектующиеПриИзмененииНоменклатурыСервер(ТекущаяСтрока)
	
	СтрокаТЧ = Объект.ИсходныеКомплектующие.НайтиПоИдентификатору(ТекущаяСтрока);
	
	Если СтрокаТЧ <> Неопределено И ЗначениеЗаполнено(СтрокаТЧ.Номенклатура) Тогда
		СтрокаТЧ.ЕдиницаИзмерения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТЧ.Номенклатура, "БазоваяЕдиницаИзмерения");
		СтрокаТЧ.Коэффициент = 1;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИсходныеКомплектующиеПриИзмененииЕдиницыИзмеренияСервер(ТекущаяСтрока)
	
	СтрокаТЧ = Объект.ИсходныеКомплектующие.НайтиПоИдентификатору(ТекущаяСтрока);
	
	Если СтрокаТЧ <> Неопределено И ЗначениеЗаполнено(СтрокаТЧ.ЕдиницаИзмерения) Тогда
		СтрокаТЧ.Коэффициент = Справочники.Номенклатура.ПолучитьКоэффициентЕдиницыИзмерения(СтрокаТЧ.Номенклатура, СтрокаТЧ.ЕдиницаИзмерения);
	КонецЕсли;
	
КонецПроцедуры