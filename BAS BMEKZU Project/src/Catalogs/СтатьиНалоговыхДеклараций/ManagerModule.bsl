///////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

Функция ПолучитьСписокСтатейПоНаборуСтатей(НаборСтатейНалоговыхДеклараций, ВключаяРодителей = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НаборыСтатейНалоговыхДеклараций.Статья КАК Статья
	|ИЗ
	|	РегистрСведений.НаборыСтатейНалоговыхДеклараций КАК НаборыСтатейНалоговыхДеклараций
	|ГДЕ
	|	НаборыСтатейНалоговыхДеклараций.Набор = &Набор";
	
	Если ВключаяРодителей Тогда
		Запрос.Текст = Запрос.Текст + "
		|ИТОГИ ПО
		|	Статья ИЕРАРХИЯ";		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Набор", НаборСтатейНалоговыхДеклараций);
	
	СписокСтатейСРодителями = Новый СписокЗначений;
	СписокСтатейСРодителями.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Статья"));
	
	Возврат СписокСтатейСРодителями;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ 

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Перем ВидНалоговойДекларации, НаборСтатейНалоговыхДеклараций;
	
	Если Параметры.Свойство("ВидНалоговойДекларации", ВидНалоговойДекларации) Тогда
		Параметры.Отбор.Вставить("ВидНалоговойДекларации", ВидНалоговойДекларации);
	КонецЕсли;
	
	Если Параметры.Свойство("НаборСтатейНалоговыхДеклараций", НаборСтатейНалоговыхДеклараций) Тогда
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = СформироватьСодержимоеНабораСтатей(НаборСтатейНалоговыхДеклараций, Параметры.СтрокаПоиска);
	Иначе
		Возврат;
	КонецЕсли;

	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Обновление информационной базы
Процедура ОбновитьПредопределенныеСтатьи() Экспорт
	
	Обработки.НачальноеЗаполнениеИОбновлениеОбъектов.ЗаполнитьОбъект("Справочник", "СтатьиНалоговыхДеклараций");
	
КонецПроцедуры

Функция СформироватьСодержимоеНабораСтатей (Набор, Текст) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Набор",Набор);
	Запрос.УстановитьПараметр("ТекстПоиска",Текст + "%");
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	НаборыСтатейНалоговыхДеклараций.Статья,
	               |	ПРЕДСТАВЛЕНИЕ(НаборыСтатейНалоговыхДеклараций.Статья) КАК СтатьяПредставление
	               |ИЗ
	               |	РегистрСведений.НаборыСтатейНалоговыхДеклараций КАК НаборыСтатейНалоговыхДеклараций
	               |ГДЕ
	               |	НаборыСтатейНалоговыхДеклараций.Набор = &Набор
	               |	И НаборыСтатейНалоговыхДеклараций.Статья.Наименование ПОДОБНО &ТекстПоиска";
				   
	Результат = Запрос.Выполнить().Выбрать();
	
	СписокСтатей = Новый СписокЗначений;
	
	
	Пока Результат.Следующий() Цикл
		СписокСтатей.Добавить(Результат.Статья,Результат.СтатьяПредставление);
	КонецЦикла;

	Возврат СписокСтатей;
	
КонецФункции	
