#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЭтаФорма.Параметры.Свойство("СтрокаПоиска") Тогда
		СтрокаПоиска = ЭтаФорма.Параметры.СтрокаПоиска;
	КонецЕсли;
	
	Макет = Справочники.ИНАГРО_ВидыХранения.ПолучитьМакет("Классификатор");
	ТабДокумент.Вывести(Макет);
	ТабДокумент.ФиксацияСверху = 3;
	
	ОбластьНаименованиеЛево         = Макет.Области.Наименование.Лево;
	ОбластьНаименованиеПраво        = Макет.Области.Наименование.Право;
	ОбластьКодЛево                  = Макет.Области.Код.Лево;
	ОбластьКодПраво                 = Макет.Области.Код.Право;
	ОбластьDRU_КодВидаХраненияЛево  = Макет.Области.DRU_КодВидаХранения.Лево;
	ОбластьDRU_КодВидаХраненияПраво = Макет.Области.DRU_КодВидаХранения.Право;
	ОбластьТипДоговораЛево          = Макет.Области.ТипДоговора.Лево;
	ОбластьТипДоговораПраво         = Макет.Области.ТипДоговора.Право;
			
	Если НЕ ПустаяСтрока(СтрокаПоиска) Тогда
		НайденнаяОбласть = ТабДокумент.НайтиТекст(СтрокаПоиска, , ТабДокумент.Области.Код, , , , Истина);
		Элементы.ТабДокумент.ТекущаяОбласть = НайденнаяОбласть;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтрокаПоискаПриИзменении(Элемент)
	
	ИскатьСтрокуВТаблице(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	Область = Элементы.ТабДокумент.ТекущаяОбласть;
	ВыполнитьВыбор(Область);
	
КонецПроцедуры

&НаКлиенте
Процедура ИскатьСтрокуВТаблице(Команда)
	
	Если ПустаяСтрока(СтрокаПоиска) Тогда
		ПоказатьПредупреждение( , НСтр("ru='Не задана строка поиска';uk='Не заданий рядок пошуку'"));
		ТекущийЭлемент = Элементы.СтрокаПоиска;
		Возврат;
	КонецЕсли;
	
	НайденнаяОбласть = ТабДокумент.НайтиТекст(СтрокаПоиска, Элементы.ТабДокумент.ТекущаяОбласть, , , , , Истина);
	Если НайденнаяОбласть = Неопределено Тогда
		НайденнаяОбласть = ТабДокумент.НайтиТекст(СтрокаПоиска, , , , , , Истина);
		Если НайденнаяОбласть = Неопределено Тогда
			ОбщегоНазначенияБПКлиентСервер.СообщитьОбОшибке(
				НСтр("ru='Строка не найдена';uk='Рядок не знайдено'"));
			ТекущийЭлемент = Элементы.СтрокаПоиска;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Элементы.ТабДокумент.ТекущаяОбласть = НайденнаяОбласть;
	ТекущийЭлемент = Элементы.СтрокаПоиска;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ТабДокументВыбор(Элемент, Область, СтандартнаяОбработка)
	
	Область = Элементы.ТабДокумент.ТекущаяОбласть;
	ВыполнитьВыбор(Область);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьВыбор(ТекущаяОбласть)
	
	Наименование        = ТабДокумент.Область(ТекущаяОбласть.Верх, ОбластьНаименованиеЛево,
											  ТекущаяОбласть.Низ,  ОбластьНаименованиеПраво).Текст;
	Код                 = ТабДокумент.Область(ТекущаяОбласть.Верх, ОбластьКодЛево,
											  ТекущаяОбласть.Низ,  ОбластьКодПраво).Текст;
	DRU_КодВидаХранения = ТабДокумент.Область(ТекущаяОбласть.Верх, ОбластьDRU_КодВидаХраненияЛево,
											  ТекущаяОбласть.Низ,  ОбластьDRU_КодВидаХраненияПраво).Текст;
	ТипДоговора         = ТабДокумент.Область(ТекущаяОбласть.Верх, ОбластьТипДоговораЛево,
											  ТекущаяОбласть.Низ,  ОбластьТипДоговораПраво).Текст;								
											  
		
	ЗначенияЗаполнения = Новый Структура("Наименование, Код, DRU_КодВидаХранения, ТипДоговора",
										 СтрПолучитьСтроку(Наименование, 1), Код, СтрПолучитьСтроку(DRU_КодВидаХранения, 1), ПолучитьТипДоговора(ТипДоговора));
	
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ВидХранения = ПоискСуществующегоВидаХранения(ПараметрыФормы);
	
	Если ВидХранения <> Неопределено Тогда
		
		ПараметрыФормы = Новый Структура("Ключ", ВидХранения);

		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='В справочнике ""Виды хранения"" уже существует элемент с кодом ""%1""! Открыт существующий';uk='У довіднику ""Види зберігання"" вже існує елемент з кодом ""%1""! Відкритий існуючий'"), Наименование);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);

		ОткрытьФорму("Справочник.ИНАГРО_ВидыХранения.Форма.ФормаЭлемента", ПараметрыФормы, ЭтаФорма);
		
	Иначе	
		
		ОткрытьФорму("Справочник.ИНАГРО_ВидыХранения.Форма.ФормаЭлемента", ПараметрыФормы, ЭтаФорма);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПоискСуществующегоВидаХранения(Знач ПараметрыФормы)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ИНАГРО_ВидыХранения.Ссылка
		|ИЗ
		|	Справочник.ИНАГРО_ВидыХранения КАК ИНАГРО_ВидыХранения
		|ГДЕ
		|	ИНАГРО_ВидыХранения.Код = &Код";
	
	Запрос.УстановитьПараметр("Код", ПараметрыФормы.ЗначенияЗаполнения.Код);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьТипДоговора(Знач ТипДоговора)	

	Возврат Справочники.ВидыВзаиморасчетов[ТипДоговора];
	
КонецФункции

#КонецОбласти