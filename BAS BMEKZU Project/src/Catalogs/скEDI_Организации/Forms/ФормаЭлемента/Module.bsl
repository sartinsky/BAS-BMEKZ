

&НаКлиенте
Функция Проверить(Значение, ОбязательныйСимвол, ЗапретныеСимволы)
	ОбязательныйСимвол = ВРег(ОбязательныйСимвол);
	ЗапретныеСимволы = ВРег(ЗапретныеСимволы);
	ОбязательныйСимволНайден = Ложь;
	Для Н = 1 по СтрДлина(Значение) Цикл
		ТекСимвол = ВРег(Сред(Значение, Н, 1));
		Если ОбязательныйСимволНайден Тогда
			Если ТекСимвол = ОбязательныйСимвол Тогда
				Возврат Ложь;
			КонецЕсли;
		Иначе
			Если ТекСимвол = ОбязательныйСимвол Тогда
				ОбязательныйСимволНайден = Истина;
			КонецЕсли;
		КонецЕсли;
		Если Найти(ЗапретныеСимволы, ТекСимвол) > 0 Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	Если ОбязательныйСимволНайден Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
КонецФункции


&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Если ЗначениеЗаполнено(Объект.АдресЭлектроннойПочты) Тогда
		Если Не Проверить(Объект.АдресЭлектроннойПочты, "@", "АБВГДЕЁЖЗИЙКЛМНОПРСТУФХЦШЩЪЫЬЭЮЯҐЄІЇ") Тогда
			Сообщить(НСтр("ru = 'Адрес электронной почты не корректный.'; uk = 'Адреса електронної пошти не коректна.'"));
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры


&НаСервереБезКонтекста
Функция ПолучитьСписокИдентификаторовПрограммыНаСервере(ЕДРПОУ)
	Возврат скEDI_ОбщегоНазначения.ПолучитьСписокИдентификаторовПрограммы(ЕДРПОУ);
КонецФункции


&НаКлиенте
Процедура ИдентификаторПрограммыНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ДанныеВыбора = ПолучитьСписокИдентификаторовПрограммыНаСервере(Объект.Код);
	СтандартнаяОбработка = Ложь;
КонецПроцедуры


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Объект.Ссылка.Пустая() Тогда
		Объект.СпособОтправкиНалоговыхДокументовКонтрагенту = ПредопределенноеЗначение("Перечисление.скEDI_СпособыОтправкиНалоговыхДокументовКонтрагенту.ЧерезДФС");
		Объект.ВыводитьИнформациюОПодписантах = Истина;
	КонецЕсли;
КонецПроцедуры


&НаСервереБезКонтекста
Функция ПолучитьДанныеПоОрганизация1СНаСервере(пОрганизация1С)
	ДанныеПоОрганизаци = скEDI_НастройкиПодКонфигурацию.ПолучитьДанныеПоОгранизации(пОрганизация1С);
	Возврат ДанныеПоОрганизаци;
КонецФункции


&НаКлиенте
Процедура Организация1СПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Объект.Организация1С) Тогда
		ДанныеПоОрганизаци = ПолучитьДанныеПоОрганизация1СНаСервере(Объект.Организация1С);
		Объект.Код = ДанныеПоОрганизаци.ЕДРПОУ;
		Объект.Филиал = ДанныеПоОрганизаци.КодФилиала;
		Объект.Наименование = ДанныеПоОрганизаци.Наименование;
		
		Если СтрДлина(Объект.Код) = 10 Тогда
			Объект.ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.скEDI_ЮрФизЛицо.ФизЛицо");
		Иначе
			Объект.ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.скEDI_ЮрФизЛицо.ЮрЛицо");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
