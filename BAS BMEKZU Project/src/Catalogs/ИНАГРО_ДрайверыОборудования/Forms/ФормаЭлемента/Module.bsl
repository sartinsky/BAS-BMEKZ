
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест"
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("ПолноеИмяФайла") Тогда
		ИмяФайлДрайвераДляЗагрузки = Параметры.ПолноеИмяФайла;
	КонецЕсли;
	
	ДополнительнаяИнформация = "";
	ПоставляемыйВСоставеКонфигурации = Объект.Предопределенный;
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.ПоставляетсяДистрибутивом = Истина;
	КонецЕсли;
	
	Если НЕ ПоставляемыйВСоставеКонфигурации И НЕ ПустаяСтрока(Объект.ИмяФайлаДрайвера) Тогда
		СсылкаНаДрайвер = ПолучитьНавигационнуюСсылку(Объект.Ссылка, "ЗагруженныйДрайвер");
		ИмяФайлДрайвераНаФорме = Объект.ИмяФайлаДрайвера;
	КонецЕсли;
	
	Элементы.ИмяФайлаДрайвера.Видимость = НЕ ПоставляемыйВСоставеКонфигурации;
	Элементы.ИмяМакетаДрайвера.Видимость = ПоставляемыйВСоставеКонфигурации;
	Элементы.ТипОборудования.ТолькоПросмотр = ПоставляемыйВСоставеКонфигурации;
	Элементы.Наименование.ТолькоПросмотр = ПоставляемыйВСоставеКонфигурации;
	Элементы.ИдентификаторОбъекта.ТолькоПросмотр = ПоставляемыйВСоставеКонфигурации;
	Элементы.ИдентификаторОбъекта.ПодсказкаВвода = ?(ПоставляемыйВСоставеКонфигурации, НСтр("ru='<Не указан>';uk='<Не введено>'"), 
		НСтр("ru='<ProgID компоненты не введен>';uk='<ProgID компоненти не введений>'"));
	Элементы.ИмяМакетаДрайвера.ПодсказкаВвода = ?(ПоставляемыйВСоставеКонфигурации, НСтр("ru='<Не указан>';uk='<Не введено>'"), "");
	
	Элементы.Сохранить.Видимость = НЕ ПоставляемыйВСоставеКонфигурации;
	Элементы.ЗаписатьИЗакрыть.Видимость = НЕ ПоставляемыйВСоставеКонфигурации;
	Элементы.ФормаЗакрыть.Видимость =ПоставляемыйВСоставеКонфигурации;
	Элементы.ФормаЗакрыть.КнопкаПоУмолчанию = Элементы.ФормаЗакрыть.Видимость;
		 
	// Загрузка и установка списка доступных макетов с драйверами
	Для каждого МакетДрайвера Из Метаданные.ОбщиеМакеты Цикл
		Если Найти(МакетДрайвера.Имя, "Драйвер") > 0 Тогда
			Элементы.ИмяМакетаДрайвера.СписокВыбора.Добавить(МакетДрайвера.Имя);
		КонецЕсли;
	КонецЦикла;  
	
	ЦветТекста = ЦветаСтиля.ЦветТекстаФормы;
	ЦветУстановки = ЦветаСтиля.ЦветФонаВыделенияПоля;
	ЦветОшибки = ЦветаСтиля.ЦветОтрицательногоЧисла;
		

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ТипОборудованияПриИзменении(Неопределено);

	Если НЕ ПустаяСтрока(ИмяФайлДрайвераДляЗагрузки) Тогда
		ЗагрузитьФайлДрайвера(ИмяФайлДрайвераДляЗагрузки);
	Иначе
		ОбновитьСостояниеЭлементов();
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(Объект.Ссылка) Тогда
		ОбновитьСтатусДрайвера();
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// Получить файл из хранилища и поместить его в объект
	Если НЕ ПоставляемыйВСоставеКонфигурации И ЭтоАдресВременногоХранилища(СсылкаНаДрайвер) Тогда
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(СсылкаНаДрайвер);
		ТекущийОбъект.ЗагруженныйДрайвер = Новый ХранилищеЗначения(ДвоичныеДанные, Новый СжатиеДанных(5));
		ТекущийОбъект.ИмяФайлаДрайвера = ИмяФайлДрайвераНаФорме;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если НЕ ПоставляемыйВСоставеКонфигурации И НЕ ПустаяСтрока(Объект.ИмяФайлаДрайвера) Тогда
		СсылкаНаДрайвер = ПолучитьНавигационнуюСсылку(Объект.Ссылка, "ЗагруженныйДрайвер");
		ИмяФайлДрайвераНаФорме = Объект.ИмяФайлаДрайвера;
	КонецЕсли;
	
	ОбновитьСостояниеЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ПустаяСтрока(Объект.ТипОборудования) Тогда 
		Отказ = Истина;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Тип оборудования не указан.';uk='Тип обладнання не вказано.'")); 
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(Объект.Наименование) Тогда 
		Отказ = Истина;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Наименование не указано.';uk='Найменування не зазначене.'")); 
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыгрузитьФайлДрайвераКоманда(Команда)
	
	Если ПоставляемыйВСоставеКонфигурации Тогда
		
		Если ПустаяСтрока(Объект.ИмяМакетаДрайвера) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Имя макета драйвера не указано.';uk='Ім''я макета драйвера не вказано.'"));
			Возврат;
		Иначе
			ВыгрузитьМакетДрайвера();
		КонецЕсли;
		
	Иначе 
		
		Если ПустаяСтрока(Объект.ИмяФайлаДрайвера) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Файл драйвера не загружен.';uk='Файл драйверу не завантажений.'"));
			Возврат;
		КонецЕсли;
		
		Если Модифицированность Тогда
			Текст = НСтр("ru='Продолжение операции возможно только после записи данных.
                |Записать данные и продолжить?'
                |;uk='Продовження операції можливо тільки після запису даних.
                |Записати дані і продовжити?'");
			Оповещение = Новый ОписаниеОповещения("ВыгрузитьФайлДрайвераЗавершение", ЭтотОбъект);
			ПоказатьВопрос(Оповещение, Текст, РежимДиалогаВопрос.ДаНет);
		Иначе
			ВыгрузитьФайлДрайвера();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьФайлДрайвераЗавершение(Результат, Параметры)Экспорт 
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Если Модифицированность И НЕ Записать() Тогда
			Возврат;
		КонецЕсли;
		ВыгрузитьФайлДрайвера();
	КонецЕсли;  
	
КонецПроцедуры 

&НаКлиенте
Процедура ЗагрузитьФайлДрайвераКоманда(Команда)
	
	#Если ВебКлиент Тогда
		ПоказатьПредупреждение(, НСтр("ru='Данный функционал доступен только в режиме тонкого и толстого клиента.';uk='Даний функціонал доступний тільки в режимі тонкого і товстого клієнта.'"));
		Возврат;
	#КонецЕсли
	
	ПолноеИмяФайла = "";
	
	Если ИНАГРО_МенеджерОборудованияКлиент.ВыборФайлаДрайвера(ПолноеИмяФайла) Тогда
		ЗагрузитьФайлДрайвера(ПолноеИмяФайла);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДрайверКоманда(Команда)
	
	Если Модифицированность Тогда
		Текст = НСтр("ru='Продолжение операции возможно только после записи данных.
            |Записать данные и продолжить?'
            |;uk='Продовження операції можливо тільки після запису даних.
            |Записати дані і продовжити?'");
		Оповещение = Новый ОписаниеОповещения("УстановитьДрайверЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, Текст, РежимДиалогаВопрос.ДаНет);
	Иначе
		УстановитьДрайвер();
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДрайверЗавершение(Результат, Параметры) Экспорт 
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Если Модифицированность И НЕ Записать() Тогда
			Возврат;
		КонецЕсли;
		УстановитьДрайвер();
	КонецЕсли;  
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПрочитатьИнформацииОДрайвере(ИнформацияФайла)
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(ИнформацияФайла);
	ЧтениеXML.ПерейтиКСодержимому();
	
	Если ЧтениеXML.Имя = "drivers" и ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда  
		Пока ЧтениеXML.Прочитать() Цикл 
			Если ЧтениеXML.Имя = "component" и ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда  
				Объект.ИдентификаторОбъекта = ЧтениеXML.ЗначениеАтрибута("progid");
				Объект.Наименование = ЧтениеXML.ЗначениеАтрибута("name");
				ВремТипОборудования = ЧтениеXML.ЗначениеАтрибута("type");
				Если НЕ ПустаяСтрока(ВремТипОборудования) Тогда
					Объект.ТипОборудования = ИНАГРО_МенеджерОборудованияВызовСервера.ПолучитьТипОборудования(ВремТипОборудования);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;  
	КонецЕсли;
	ЧтениеXML.Закрыть(); 
	
КонецПроцедуры


&НаКлиенте
Процедура ЗагрузитьФайлДрайвера(ПолноеИмяФайла)
	
	ВремФайлДрайвера = Новый Файл(ПолноеИмяФайла);
	
	Если ПолучитьИнформациюДрайвераПоФайлу(ВремФайлДрайвера.ПолноеИмя) Тогда
		ПомещенныеФайлы = Новый Массив;
		Если ПоместитьФайлы(, ПомещенныеФайлы, ВремФайлДрайвера.ПолноеИмя, Ложь, ЭтаФорма.УникальныйИдентификатор) Тогда
			ИмяФайлДрайвераНаФорме = ВремФайлДрайвера.Имя;
			СсылкаНаДрайвер = ПомещенныеФайлы[0].Хранение;
			ОбновитьСостояниеЭлементов();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьИнформациюДрайвераПоФайлу(ПолноеИмяФайла) 
	
	Результат = Ложь;
	
	ФайлДрайвера = Новый Файл(ПолноеИмяФайла);
	РасширениеФайла = ВРег(ФайлДрайвера.Расширение);
	
	Если НЕ ИНАГРО_МенеджерОборудованияКлиентПовтИсп.ЭтоLinuxКлиент() И РасширениеФайла = ".EXE" Тогда
		
		// Файл драйвера поставляется дистрибутивом
		Объект.ПоставляетсяДистрибутивом = Истина; 
		Результат = Истина;
		Возврат Результат;
		
	#Если МобильныйКлиент ИЛИ ВебКлиент Тогда	
		ТекстСообщения = Нстр("ru = 'Мобильное приложение и Веб-клиент не поддерживает работу ZipФайла.'; uk = 'Мобільний додаток та веб-клієнт не підтримує роботу ZipФайла.'");
		ВызватьИсключение(ТекстСообщения);
	#Иначе
		
	ИначеЕсли РасширениеФайла = ".ZIP" Тогда
		
		// Распаковка архива с драйвером во временный каталог
		ВременныйКаталог = КаталогВременныхФайлов() + "cel\";
		
		ВременныйФайл = Новый ЧтениеZipФайла();
		ВременныйФайл.Открыть(ПолноеИмяФайла);		
		
		Для Каждого ЭлементАрхива Из ВременныйФайл.Элементы Цикл
			МанифестНайден = Ложь;
			
			// Проверяем, есть ли файл манифеста
			Если ВРег(ЭлементАрхива.Имя) = "MANIFEST.XML" Тогда
				Объект.ПоставляетсяДистрибутивом = Ложь; 
				МанифестНайден = Истина;
				Результат = Истина;
			КонецЕсли;
			
			// Проверяем, есть ли файл информации
			Если ВРег(ЭлементАрхива.Имя) = "INFO.XML" Тогда
				 ВременныйФайл.Извлечь(ЭлементАрхива, ВременныйКаталог);
				 ФайлИнформации = Новый ЧтениеТекста(ВременныйКаталог + "INFO.XML", КодировкаТекста.UTF8);
				 ПрочитатьИнформацииОДрайвере(ФайлИнформации.Прочитать());
				 ФайлИнформации.Закрыть(); 
				 УдалитьФайлы(ВременныйКаталог + "INFO.XML");
			КонецЕсли;
		
			// Драйвер поставляется дистрибутивом упакованным в архив
			Если НЕ ИНАГРО_МенеджерОборудованияКлиентПовтИсп.ЭтоLinuxКлиент() И НЕ МанифестНайден Тогда
				Если (ВРег(ЭлементАрхива.Имя) = "SETUP.EXE" Или ВРег(ЭлементАрхива.Имя) = ВРег(ФайлДрайвера.ИмяБезРасширения) + ".EXE") Тогда
					Объект.ПоставляетсяДистрибутивом = Истина; 
					Результат = Истина;
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
		Если ПустаяСтрока(Объект.ИдентификаторОбъекта) Тогда
			Объект.ИдентификаторОбъекта = "AddIn.None";
		КонецЕсли;
		
		Возврат Результат;
	#КонецЕсли
	
	Иначе
		ПоказатьПредупреждение(, НСтр("ru='Неверное расширение файла.';uk='Невірне розширення файлу.'"));
		Возврат Результат;
	КонецЕсли;

КонецФункции

&НаКлиенте
Процедура ОбновитьСтатусДрайвера();
	
	ДанныеДрайвера = Новый Структура();
	ДанныеДрайвера.Вставить("ДрайверОборудования"       , Объект.Ссылка);
	ДанныеДрайвера.Вставить("ВСоставеКонфигурации"      , Объект.Предопределенный);
	ДанныеДрайвера.Вставить("ИдентификаторОбъекта"      , Объект.ИдентификаторОбъекта);
	ДанныеДрайвера.Вставить("ПоставляетсяДистрибутивом" , Объект.ПоставляетсяДистрибутивом);
	ДанныеДрайвера.Вставить("ИмяМакетаДрайвера"         , Объект.ИмяМакетаДрайвера);
	ДанныеДрайвера.Вставить("ИмяФайлаДрайвера"          , Объект.ИмяФайлаДрайвера);
	
	ТекстОшибки = "";
	ОбъектДрайвера = ИНАГРО_МенеджерОборудованияКлиент.ПолучитьОбъектДрайвера(ДанныеДрайвера, ТекстОшибки);
	
	Элементы.ТекущийСтатусДрайвера.ЦветТекста = ЦветТекста;
	
	Если ПустаяСтрока(Объект.ИдентификаторОбъекта) И ПоставляемыйВСоставеКонфигурации Тогда
		ТекущийСтатусДрайвера = НСтр("ru='Установка драйвера не требуется.';uk='Установка драйверів не потрібна.'");
	ИначеЕсли ПустаяСтрока(ОбъектДрайвера) Тогда
		ТекущийСтатусДрайвера = НСтр("ru='Не установлен на текущем компьютере.';uk='Не встановлений на поточному комп''ютері.'") + Символы.НПП + ТекстОшибки;
		Элементы.ТекущийСтатусДрайвера.ЦветТекста = ЦветОшибки;
	Иначе
		Элементы.ФормаУстановитьДрайвер.Доступность = Ложь;
		Элементы.ТекущийСтатусДрайвера.ЦветТекста = ЦветУстановки;
		
		ВерсияДрайвера = "";
		Попытка
			ВерсияДрайвера = ОбъектДрайвера.ПолучитьНомерВерсии();
		Исключение
		КонецПопытки;
		
		Попытка
			НоваяАрхитектура          = Ложь;
			НаименованиеДрайвера      = "";
			ОписаниеДрайвера          = "";
			ТипОборудования           = "";
			ИнтеграционнаяБиблиотека  = Истина;
			ОсновнойДрайверУстановлен = Ложь;
			РевизияИнтерфейса         = 1012;
			URLЗагрузкиДрайвера       = "";
			ПараметрыДрайвера         = "";
			ДополнительныеДействия    = "";
			ОбъектДрайвера.ПолучитьОписание(НаименованиеДрайвера, ОписаниеДрайвера, ТипОборудования, РевизияИнтерфейса, 
											ИнтеграционнаяБиблиотека, ОсновнойДрайверУстановлен, URLЗагрузкиДрайвера);
			НоваяАрхитектура = Истина;
		Исключение
		КонецПопытки;
		
		Если НоваяАрхитектура И ИнтеграционнаяБиблиотека Тогда
			ТекущийСтатусДрайвера = НСтр("ru='Установлена интеграционная библиотека.';uk='Встановлена інтеграційна бібліотека.'");
			ТекущийСтатусДрайвера = ТекущийСтатусДрайвера + ?(ОсновнойДрайверУстановлен, НСтр("ru='Установлена основная поставка драйвера.';uk='Встановлена основна поставка драйверу.'"),
																						 НСтр("ru='Основная поставка драйвера не установлена.';uk='Основна поставка драйвера не встановлена.'")); 
		Иначе
			ТекущийСтатусДрайвера = НСтр("ru='Установлен на текущем компьютере.';uk='Встановлений на поточному комп''ютері.'");
		КонецЕсли;
		
		Если Не ПустаяСтрока(ВерсияДрайвера) Тогда
			ТекущийСтатусДрайвера = ТекущийСтатусДрайвера + СтрЗаменить(НСтр("ru=' (Версия: %s)';uk=' (Версія: %s)'"), "%s", ВерсияДрайвера);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСостояниеЭлементов();
	
	Если ПоставляемыйВСоставеКонфигурации и ПустаяСтрока(Объект.ИмяМакетаДрайвера) Тогда
		ВидимостьВыгрузитьФайл = Ложь;
	ИначеЕсли НЕ ПоставляемыйВСоставеКонфигурации и ПустаяСтрока(ИмяФайлДрайвераНаФорме) Тогда
		ВидимостьВыгрузитьФайл = Ложь;
	Иначе
		ВидимостьВыгрузитьФайл = НЕ ПустаяСтрока(Объект.Ссылка);
	КонецЕсли;
		
	Элементы.ФормаВыгрузитьФайлДрайвера.Видимость = ВидимостьВыгрузитьФайл;
	Элементы.ФормаУстановитьДрайвер.Видимость     = ВидимостьВыгрузитьФайл;
	Элементы.ФормаЗагрузитьФайлДрайвера.Видимость = НЕ ПоставляемыйВСоставеКонфигурации;
	
	Если НЕ ПустаяСтрока(ИмяФайлДрайвераНаФорме) Или ПоставляемыйВСоставеКонфигурации Тогда
		Если ПустаяСтрока(Объект.ИдентификаторОбъекта) Тогда
			ДополнительнаяИнформация = НСтр("ru='Не указан ProgID компоненты или установка драйвера не требуется.';uk='Не зазначений ProgID компоненти або встановленния драйверів не потрібно.'");
		ИначеЕсли Объект.ПоставляетсяДистрибутивом Тогда
			ДополнительнаяИнформация = НСтр("ru='Драйвер поставляется в виде дистрибутива.';uk='Драйвер поставляється у вигляді дистрибутива.'");
		Иначе
			ДополнительнаяИнформация = НСтр("ru='Драйвер поставляется в виде компоненты в архиве.';uk='Драйвер поставляється у вигляді компоненти в архіві.'");
		КонецЕсли;
	Иначе
		ДополнительнаяИнформация = НСтр("ru='Подключение установленного драйвера на локальных компьютерах.';uk='Підключення встановленого драйвера на локальних комп''ютерах.'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьМакетДрайвера()
	
	ВремИмяФайла = ?(ПустаяСтрока(Объект.ИмяФайлаДрайвера), Объект.ИмяМакетаДрайвера + ".zip", Объект.ИмяФайлаДрайвера);
	Если ВРег(Прав(ВремИмяФайла, 4)) = ".EXE" Тогда  
		ВремИмяФайла = Лев(ВремИмяФайла, СтрДлина(ВремИмяФайла) - 4) + ".zip";  
	КонецЕсли;
	СсылкаНаФайл = ИНАГРО_МенеджерОборудованияВызовСервера.ПолучитьМакетССервера(Объект.ИмяМакетаДрайвера);
	ПолучитьФайл(СсылкаНаФайл, ВремИмяФайла); 
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьФайлДрайвера()
	
	СсылкаНаФайлВИБ = ПолучитьНавигационнуюСсылку(Объект.Ссылка, "ЗагруженныйДрайвер");
	ПолучитьФайл(СсылкаНаФайлВИБ, Объект.ИмяФайлаДрайвера); 
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДрайверИзАрхиваПриЗавершении(Результат) Экспорт 
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Установка драйвера завершена.';uk='Установка драйвера завершена.'")); 
	ОбновитьСтатусДрайвера();
	
КонецПроцедуры 

&НаКлиенте
Процедура УстановитьДрайверИзДистрибутиваПриЗавершении(Результат, Параметры) Экспорт 
	
	Если Результат Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Установка драйвера завершена.';uk='Установка драйвера завершена.'")); 
		ОбновитьСтатусДрайвера();
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='При установке драйвера из дистрибутива произошла ошибка.';uk='При встановленні драйвера з дистрибутива сталася помилка.'")); 
	КонецЕсли;

КонецПроцедуры 

&НаКлиенте
Процедура УстановитьДрайвер()
	
	ОчиститьСообщения();
	
	ОповещенияДрайверИзДистрибутиваПриЗавершении = Новый ОписаниеОповещения("УстановитьДрайверИзДистрибутиваПриЗавершении", ЭтотОбъект);
	ОповещенияДрайверИзАрхиваПриЗавершении = Новый ОписаниеОповещения("УстановитьДрайверИзАрхиваПриЗавершении", ЭтотОбъект);
	
	ИНАГРО_МенеджерОборудованияКлиент.УстановитьДрайвер(Объект.Ссылка, ОповещенияДрайверИзДистрибутиваПриЗавершении, ОповещенияДрайверИзАрхиваПриЗавершении);
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТипОборудованияПриИзменении(Элемент)
	
	Результат = Объект.ТипОборудования = ПредопределенноеЗначение("Перечисление.ИНАГРО_ТипыПодключаемогоОборудования.ЭлектронныеВесы");

	Элементы.ВнешнийДрайвер.Видимость 			= Результат;
	Элементы.АсинхронныйМетодРаботы.Видимость 	= Результат;		
	
КонецПроцедуры

#КонецОбласти