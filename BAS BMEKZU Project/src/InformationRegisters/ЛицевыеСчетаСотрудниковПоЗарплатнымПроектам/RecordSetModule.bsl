#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьНаборЗаписей(ЭтотОбъект, Отказ);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

//Проверяет номера лицевых счетов в наборе записей, зарегистрированы они или нет.
//
// Параметры:
//		НаборЗаписей - Набор записей регистра сведений "Лицевые счета сотрудников по зарплатным проектам"
//		Отказ - Если Истина, то запись набора не производится, иначе Ложь
//
Процедура ПроверитьНаборЗаписей(НаборЗаписей, Отказ)
	Если НаборЗаписей.ДополнительныеСвойства.Свойство("ЗагрузкаДанных") = Ложь Тогда
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		
		Запрос.УстановитьПараметр("НаборЗаписей", НаборЗаписей.Выгрузить());
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	НаборЗаписей.Организация КАК Организация,
		|	НаборЗаписей.ЗарплатныйПроект КАК ЗарплатныйПроект,
		|	НаборЗаписей.ФизическоеЛицо КАК ФизическоеЛицо,
		|	НаборЗаписей.НомерЛицевогоСчета КАК НомерЛицевогоСчета
		|ПОМЕСТИТЬ ВТНаборЗаписей
		|ИЗ
		|	&НаборЗаписей КАК НаборЗаписей
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЛицевыеСчета.Организация КАК Организация,
		|	ЛицевыеСчета.ЗарплатныйПроект КАК ЗарплатныйПроект,
		|	ЛицевыеСчета.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ЛицевыеСчета.НомерЛицевогоСчета КАК НомерЛицевогоСчета,
		|	МАКСИМУМ(Сотрудники.Ссылка) КАК Сотрудник
		|ИЗ
		|	РегистрСведений.ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам КАК ЛицевыеСчета
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНаборЗаписей КАК НаборЗаписей
		|		ПО ЛицевыеСчета.Организация = НаборЗаписей.Организация
		|			И ЛицевыеСчета.ЗарплатныйПроект = НаборЗаписей.ЗарплатныйПроект
		|			И ЛицевыеСчета.НомерЛицевогоСчета = НаборЗаписей.НомерЛицевогоСчета
		|			И ЛицевыеСчета.ФизическоеЛицо <> НаборЗаписей.ФизическоеЛицо
		|			И ЛицевыеСчета.НомерЛицевогоСчета <> """"
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
		|		ПО ЛицевыеСчета.ФизическоеЛицо = Сотрудники.ФизическоеЛицо
		|			И ЛицевыеСчета.Организация = Сотрудники.ГоловнаяОрганизация
		|
		|СГРУППИРОВАТЬ ПО
		|	ЛицевыеСчета.Организация,
		|	ЛицевыеСчета.ЗарплатныйПроект,
		|	ЛицевыеСчета.ФизическоеЛицо,
		|	ЛицевыеСчета.НомерЛицевогоСчета";
		
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			
			Выборка = Результат.Выбрать();
			Пока Выборка.Следующий() Цикл
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru='Обнаружен сотрудник с точно таким же номером лицевого счета - %1.';uk='Виявлений співробітник з точно таким же номером особового рахунку - %1.'"),
						Выборка.ФизическоеЛицо);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Выборка.Сотрудник, , , Отказ);
			КонецЦикла;
			
		КонецЕсли;
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецЕсли
