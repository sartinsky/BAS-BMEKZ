////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Для каждого ОтветственноеЛицо Из Перечисления.ОтветственныеЛицаОрганизаций Цикл
		ВидыОтветственныхЛиц.Добавить(ОтветственноеЛицо);
	КонецЦикла;
	
	Если Параметры.Отбор.Свойство("СтруктурнаяЕдиница") Тогда
		СтруктурнаяЕдиница	= Параметры.Отбор.СтруктурнаяЕдиница;
	Иначе
		СтруктурнаяЕдиница	= БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("ОтветственноеЛицо") Тогда
		НайденноеЗначение	= ВидыОтветственныхЛиц.НайтиПоЗначению(Параметры.Отбор.ОтветственноеЛицо);
		Если НЕ НайденноеЗначение = Неопределено Тогда
			Элементы.ВидыОтветственныхЛиц.ТекущаяСтрока	= НайденноеЗначение.ПолучитьИдентификатор();
		КонецЕсли;
		Параметры.Отбор.Удалить("ОтветственноеЛицо");
	КонецЕсли;
	
	Организация					= ОрганизацияСтруктурнойЕдиницы(СтруктурнаяЕдиница);
	ОбособленноеПодразделениеОрганизации	= ОбособленноеПодразделениеСтруктурнойЕдиницы(СтруктурнаяЕдиница);
	
	ОбщегоНазначенияБПВызовСервера.УстановитьОтборПоОсновнойОрганизации(ЭтаФорма,, "СтруктурнаяЕдиница");
	
	Элементы.СтруктурнаяЕдиница.Видимость		= НЕ ЗначениеЗаполнено(СтруктурнаяЕдиница);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ПОЛЕЙ ФОРМЫ

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "СтруктурнаяЕдиница", Организация, ЗначениеЗаполнено(Организация));
	
КонецПроцедуры

&НаКлиенте
Процедура ОбособленноеПодразделениеОрганизацииПриИзменении(Элемент)
	
	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "СтруктурнаяЕдиница", ОбособленноеПодразделениеОрганизации, ЗначениеЗаполнено(ОбособленноеПодразделениеОрганизации));
	
КонецПроцедуры

&НаКлиенте
Процедура ВидыОтветственныхЛицПриАктивизацииСтроки(Элемент)
	
	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "ОтветственноеЛицо", Элемент.ТекущиеДанные.Значение, Истина);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервереБезКонтекста
Функция ОрганизацияСтруктурнойЕдиницы(СтруктурнаяЕдиница)
	
	Организация	= ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка");
	
	Если ЗначениеЗаполнено(СтруктурнаяЕдиница) Тогда
		
		Если ЗначениеЗаполнено(СтруктурнаяЕдиница) И ТипЗнч(СтруктурнаяЕдиница) = Тип("СправочникСсылка.ОбособленныеПодразделенияОрганизаций") Тогда
			Организация	= ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтруктурнаяЕдиница, "Владелец");
		ИначеЕсли ТипЗнч(СтруктурнаяЕдиница) = Тип("СправочникСсылка.Организации") Тогда
			Организация	= СтруктурнаяЕдиница;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Организация;
	
КонецФункции

&НаСервереБезКонтекста
Функция ОбособленноеПодразделениеСтруктурнойЕдиницы(СтруктурнаяЕдиница)
	
	ОбособленноеПодразделение	= ПредопределенноеЗначение("Справочник.ОбособленныеПодразделенияОрганизаций.ПустаяСсылка");
	
	Если ЗначениеЗаполнено(СтруктурнаяЕдиница) Тогда
		
		Если ТипЗнч(СтруктурнаяЕдиница) = Тип("СправочникСсылка.ОбособленныеПодразделенияОрганизаций") Тогда
			ОбособленноеПодразделение	= СтруктурнаяЕдиница;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ОбособленноеПодразделение;
	
КонецФункции


&НаКлиенте
Процедура СписокПослеУдаления(Элемент)
	
	Оповестить("ИзменениеОтветственныхЛиц", Организация);
	Оповестить("ИзмененСписокОтветственныхЛиц", Организация);
	
КонецПроцедуры

