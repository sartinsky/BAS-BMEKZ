
&НаСервере
Функция ПроверкаТекстаЗапроса(ТекстЗапроса, ТекстСообщения)
	ТекстСообщения = "";
	//ПроверкаПараметров
	Запрос = Новый Запрос(ТекстЗапроса);
	Попытка
		ПараметрыЗапроса = Запрос.НайтиПараметры();
	Исключение
		ТекстСообщения = СтрЗаменить(Сред(ОписаниеОшибки(),83),"Синтаксическая ошибка","Синтаксическая ошибка в тексте запроса:");
		Возврат Ложь;
	КонецПопытки;
	ЕстьПараметрСсылка = Ложь;
	ЕстьДругиеПараметры = Ложь;
	Для каждого ПараметрЗапроса Из ПараметрыЗапроса Цикл
		ИмяПараметра =  ПараметрЗапроса.Имя;
		Если ВРег(ИмяПараметра) = "ССЫЛКА" Тогда
			ЕстьПараметрСсылка = Истина;
		Иначе
			ТекстСообщения = ТекстСообщения + ?(ТекстСообщения = "", "", Символы.ПС)
			+НСтр("ru = 'В запросе обнаружен неподдерживаемый параметр'; uk = 'В запросі виявлено параметр, що не підтримується'") + ": """ + ИмяПараметра + """";
			ЕстьДругиеПараметры = Истина;
		КонецЕсли;
	КонецЦикла; 
	Если не ЕстьПараметрСсылка Тогда
		ТекстСообщения = ТекстСообщения + ?(ТекстСообщения = "", "", Символы.ПС)
		+НСтр("ru = 'В запросе не обнаружен параметр ""Ссылка""'; uk = 'В запросі не виявлено параметр ""Ссылка""'");
	КонецЕсли;
	Если ЗначениеЗаполнено(ТекстСообщения) Тогда
		Возврат Ложь;
	КонецЕсли;
	Возврат Истина;
КонецФункции

&НаКлиенте
Процедура ПослеРедактированияТекстаЗапроса(Текст, ДополнительныеПараметры) Экспорт
	Если Текст <> Неопределено Тогда
		ТекстСообщения = "";
		Если НЕ ПроверкаТекстаЗапроса(Текст, ТекстСообщения) Тогда
			Если скEDI_ОбщегоНазначения.ЭтоПлатформа82() Тогда
				Предупреждение(ТекстСообщения);
			Иначе
				скEDI_ОткрытиеФормБезМодальности.ПоказатьПредупреждение_83(ТекстСообщения);
			КонецЕсли;
		Иначе
			Запись.ТекстЗапроса = Текст;	
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТекстЗапросаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ТекстЗапроса = Запись.ТекстЗапроса;
	КонструкторЗапроса = Новый КонструкторЗапроса;
	Если СтрДлина(СокрЛП(ТекстЗапроса)) > 0 Тогда
		//Попытка
			КонструкторЗапроса.Текст = ТекстЗапроса;
		//Исключение
		//	Предупреждение(ОписаниеОшибки());
		//	Режим = РежимДиалогаВопрос.ДаНет;                                                         
		//	Ответ = Вопрос("Удалить текст запроса?", Режим, 0);
		//	Если Ответ <> КодВозвратаДиалога.Да Тогда
		//		Возврат;
		//	КонецЕсли;
		//	
		//	КонструкторЗапроса = Новый КонструкторЗапроса;
		//КонецПопытки;
	КонецЕсли;
	
	Если скEDI_ОбщегоНазначения.ЭтоПлатформа82() Тогда
		Если КонструкторЗапроса.ОткрытьМодально() = Истина Тогда
			ПослеРедактированияТекстаЗапроса(КонструкторЗапроса.Текст, Неопределено);
		КонецЕсли;
	Иначе
		скEDI_ОткрытиеФормБезМодальности.ОткрытьФормуКонструктораЗапроса(КонструкторЗапроса, ЭтаФорма);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Организация = Запись.ВидЭлектронногоДокумента.Владелец;
	
	Элементы.ВидДокумента1С.РежимВыбораИзСписка = Истина;
	Элементы.ВидДокумента1С.СписокВыбора.ЗагрузитьЗначения(скEDI_НастройкиПодКонфигурацию.ПолучитьМассивИспользуемыхВКонфигурацииВидовДокументов1С());
КонецПроцедуры
