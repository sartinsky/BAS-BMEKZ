#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

Процедура УстановитьЗначениеРеквизита(ЗаписьРегистра, ДанныеЗаполнения, ИмяРеквизита, ЗначениеПоУмолчанию)

	Если НЕ ДанныеЗаполнения.Свойство(ИмяРеквизита, ЗаписьРегистра[ИмяРеквизита]) Тогда
		ЗаписьРегистра[ИмяРеквизита] = ЗначениеПоУмолчанию;
	КонецЕсли;

КонецПроцедуры

Процедура УстановкаПараметровУчетнойПолитикиПоУмолчанию(ЗаписьРегистра, ДанныеЗаполнения) Экспорт

	УстановитьЗначениеРеквизита(ЗаписьРегистра, ДанныеЗаполнения, "Организация", БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация"));
	УстановитьЗначениеРеквизита(ЗаписьРегистра, ДанныеЗаполнения, "Период", НачалоГода(ТекущаяДатаСеанса()));
	
	ЭтоФизЛицо	= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗаписьРегистра.Организация, "ЮридическоеФизическоеЛицо")
		= Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
	
	// Общие сведения
	СхемаНалогообложенияПоУмолчанию = ПолучитьСхемуНалогообложенияПоУмолчанию(ЭтоФизЛицо);
	
	УстановитьЗначениеРеквизита(ЗаписьРегистра, ДанныеЗаполнения, "СхемаНалогообложения", СхемаНалогообложенияПоУмолчанию);
	
	РеквизитыСхемаНалогообложения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		СхемаНалогообложенияПоУмолчанию, "НалогНаПрибыль,НДС,ЕдиныйНалог");
		
	УстановитьЗначениеРеквизита(ЗаписьРегистра, ДанныеЗаполнения, "ПлательщикНалогаНаПрибыль", РеквизитыСхемаНалогообложения.НалогНаПрибыль);
	УстановитьЗначениеРеквизита(ЗаписьРегистра, ДанныеЗаполнения, "ПлательщикНДС", РеквизитыСхемаНалогообложения.НДС);
	УстановитьЗначениеРеквизита(ЗаписьРегистра, ДанныеЗаполнения, "ПлательщикЕдиногоНалога", РеквизитыСхемаНалогообложения.ЕдиныйНалог);
	
	
	УстановитьЗначениеРеквизита(ЗаписьРегистра, ДанныеЗаполнения, "ИспользуемыеКлассыСчетовРасходов", Перечисления.КлассыСчетовРасходов.Класс8и9);
	
	// Налог на прибыль

	// НДС
	
	// Единый налог
	Если РеквизитыСхемаНалогообложения.ЕдиныйНалог Тогда
		УстановитьЗначениеРеквизита(ЗаписьРегистра, ДанныеЗаполнения, "ГруппаПлательщикаЕдиногоНалога", Перечисления.ГруппыПлательщиковЕдиногоНалога.ПерваяГруппа);
	КонецЕсли;
	
	
	// Запасы
	УстановитьЗначениеРеквизита(ЗаписьРегистра, ДанныеЗаполнения, "СпособОценкиМПЗ", 
		Перечисления.СпособыОценки.ПоСредней);
		
	// Розница
	УстановитьЗначениеРеквизита(ЗаписьРегистра, ДанныеЗаполнения, "СпособОценкиТоваровВРознице", 
		Перечисления.СпособыОценкиТоваровВРознице.ПоСтоимостиПриобретения);

	// Производство
	УстановитьЗначениеРеквизита(
		ЗаписьРегистра, 
		ДанныеЗаполнения, 
		"ПорядокРаспределенияРасходовНаОказаниеВнутреннихУслуг",
		Перечисления.ПорядокРаспределенияРасходовНаОказаниеВнутреннихУслуг.ПоПлановымЦенам);
		
	УстановитьЗначениеРеквизита(
		ЗаписьРегистра, 
		ДанныеЗаполнения, 
		"ПорядокРаспределенияРасходовНаОказаниеУслуг",
		Перечисления.ПорядокРаспределенияРасходовНаОказаниеУслуг.ПоПлановымЦенам);

	// Производство: Выпуск продукции, услуг
	УстановитьЗначениеРеквизита(ЗаписьРегистра, ДанныеЗаполнения, "СпособРасчетаСебестоимостиПроизводства", 
		Перечисления.СпособыРасчетаСебестоимостиПродукции.ПоПеределам);
		
	
КонецПроцедуры

Процедура УстановкаПараметровУчетнойПолитикиПоУмолчаниюНаПериод(ЗаписьРегистра) Экспорт


КонецПроцедуры


Функция ПолучитьСхемуНалогообложенияПоУмолчанию(ЭтоФизЛицо = Ложь) Экспорт
	
	Если ЭтоФизЛицо Тогда
		СхемаНалогообложенияПоУмолчанию	= Справочники.СхемыНалогообложения.ЕдиныйНалогИНДС;
	Иначе
		СхемаНалогообложенияПоУмолчанию	= Справочники.СхемыНалогообложения.НалогНаПрибыльИНДС;
	КонецЕсли;
	
	Возврат СхемаНалогообложенияПоУмолчанию;
	
КонецФункции

#КонецЕсли
