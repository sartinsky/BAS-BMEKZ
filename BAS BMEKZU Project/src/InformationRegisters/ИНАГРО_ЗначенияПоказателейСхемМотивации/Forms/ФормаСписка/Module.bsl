#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)	
	
	ЗначенияДляЗаполнения = Новый Структура("Месяц, Организация", "ПериодДействия", "Организация");		
	ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтаФорма, ЗначенияДляЗаполнения);
	
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДате(ЭтаФорма, "ПериодДействия", "ПериодДействияСтрокой"); 
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры.Отбор);
	
	ИспользуетсяНесколькоОрганизаций = Справочники.Организации.ИспользуетсяНесколькоОрганизаций();
	
	ОбновитьОтбор(ЭтаФорма);
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзменениеОсновнойОрганизации" Тогда
		ОбщегоНазначенияБПКлиент.ИзменитьОтборПоОсновнойОрганизации(Список, ,Параметр);
	КонецЕсли;	
	
	ОбновитьОтбор(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти  

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	
	ОбновитьОтбор(ЭтаФорма);
	
КонецПроцедуры

#Область РедактированиеМесяцаСтрокой

&НаКлиенте
Процедура ПериодДействияПриИзменении(Элемент)
	
	ЗарплатаКадрыКлиент.ВводМесяцаПриИзменении(ЭтаФорма, "ПериодДействия", "ПериодДействияСтрокой", Модифицированность);
	
	ОбновитьОтбор(ЭтаФорма);
		
КонецПроцедуры

&НаКлиенте
Процедура ПериодДействияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("ПериодДействияНачалоВыбораЗавершение", ЭтотОбъект, Параметры);

	ЗарплатаКадрыКлиент.ВводМесяцаНачалоВыбора(ЭтаФорма, ЭтаФорма, "ПериодДействия", "ПериодДействияСтрокой", , Оповещение);	
		
КонецПроцедуры

&НаКлиенте
Процедура ПериодДействияНачалоВыбораЗавершение(ЗначениеВыбрано, ДополнительныеПараметры) Экспорт
	
	ОбновитьОтбор(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодДействияОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодДействияРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаРегулирование(ЭтаФорма, "ПериодДействия", "ПериодДействияСтрокой", Направление, Модифицированность);
	
	ОбновитьОтбор(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодДействияАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);	
		
КонецПроцедуры

&НаКлиенте
Процедура ПериодДействияОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаОкончаниеВводаТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);	
		
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки)
	
	ОбщегоНазначенияБП.ВосстановитьОтборСписка(Список, Настройки, "Организация");
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		ТекстСообщения = НСтр("ru='Не заполнено поле Организация!';uk='Не заповнене поле Організація!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Организация");
		Возврат; 		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПериодДействияСтрокой) Тогда
		ТекстСообщения = НСтр("ru='Не заполнено поле Период действия!';uk='Не заповнене поле Період дії!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ПериодДействияСтрокой");
		Возврат; 		
	КонецЕсли;
	
	ЗаполнитьНаСервере();
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Организация",    Организация);
	ПараметрыОтбора.Вставить("ПериодДействия", ПериодДействия);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", ПараметрыОтбора);
		
	ОткрытьФорму("РегистрСведений.ИНАГРО_ЗначенияПоказателейСхемМотивации.Форма.ФормаНабораЗаписей", ПараметрыФормы, ЭтаФорма, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	НаборЗаписей = РегистрыСведений.ИНАГРО_ЗначенияПоказателейСхемМотивации.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Организация.Установить(Организация);
	НаборЗаписей.Отбор.ПериодДействия.Установить(ПериодДействия);
	НаборЗаписей.Прочитать();	
	НаборЗаписей.Загрузить(Автозаполнение(Организация, ПериодДействия));
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьОтбор(Форма)
	
	// Обработка быстрых отборов
	
	Список = Форма.Список;
	Список.Отбор.Элементы.Очистить();
	
	Если ЗначениеЗаполнено(Форма.Организация) И (Форма.ИспользуетсяНесколькоОрганизаций) Тогда
	
		Отбор = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Организация");
		Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		Отбор.ПравоеЗначение = Форма.Организация;
		
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(Форма.ПериодДействия) Тогда
	
		Отбор = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПериодДействия");
		Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		Отбор.ПравоеЗначение = Форма.ПериодДействия;
		
	КонецЕсли;	
		
КонецПроцедуры	

&НаСервере
Функция Автозаполнение(Организация, ПериодДействия, Сотрудники = Неопределено, Показатели = Неопределено)
			
	СписокНедоступныхВозможностейИзменения = Новый Массив;
	СписокНедоступныхВозможностейИзменения.Добавить(Перечисления.ИНАГРО_ИзменениеПоказателейСхемМотивации.ПустаяСсылка());
	СписокНедоступныхВозможностейИзменения.Добавить(Перечисления.ИНАГРО_ИзменениеПоказателейСхемМотивации.НеИзменяется);
	СписокНедоступныхВозможностейИзменения.Добавить(Перечисления.ИНАГРО_ИзменениеПоказателейСхемМотивации.ИзменяетсяПриРасчете);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПарамВозможностьИзменения", СписокНедоступныхВозможностейИзменения);
	Запрос.УстановитьПараметр("ПарамДата",                 ПериодДействия);
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;	
	
	Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(ПериодДействия));
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Сотрудники",  Сотрудники);
	Запрос.УстановитьПараметр("Показатели",  Показатели);
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;		
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	РаботникиОрганизаций.Сотрудник КАК Сотрудник,
		|	РаботникиОрганизаций.ПодразделениеОрганизации КАК Подразделение
		|ПОМЕСТИТЬ ВТСотрудники
		|ИЗ
		|	РегистрСведений.ИНАГРО_РаботникиОрганизаций.СрезПоследних(&ПарамДата, Организация = &Организация"+ ?(ЗначениеЗаполнено(Сотрудники), "И Сотрудник В (&Сотрудники)", "") + ") КАК РаботникиОрганизаций
		|ГДЕ
		|	РаботникиОрганизаций.ПричинаИзмененияСостояния <> ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	РаботникиОрганизаций.Сотрудник,
		|	РаботникиОрганизаций.ПодразделениеОрганизации
		|ИЗ
		|	РегистрСведений.ИНАГРО_РаботникиОрганизаций КАК РаботникиОрганизаций
		|ГДЕ
		|	РаботникиОрганизаций.Период > &ПарамДата
		|	И РаботникиОрганизаций.Период <= &КонецМесяца
		|	И РаботникиОрганизаций.Организация = &Организация		
		|" + ?(ЗначениеЗаполнено(Сотрудники), "И РаботникиОрганизаций.Сотрудник В (&Сотрудники)", "") + "	
		|ИНДЕКСИРОВАТЬ ПО
		|	Сотрудник
		|"; 
	
	Запрос.Выполнить();
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|		&Организация КАК Организация,
		|		Сотрудники.Подразделение КАК Подразделение,
		|		&ПарамДата КАК ПериодДействия,
		|		ВложенныйЗапрос.Сотрудник КАК Сотрудник,
		|		ВложенныйЗапрос.Показатель КАК Показатель,
		|		СУММА(ЕСТЬNULL(ВложенныйЗапрос.Значение, 0)) КАК Значение
		|	ПОМЕСТИТЬ ВТПлановыеПоказатели
		|	ИЗ
		|		(ВЫБРАТЬ
		|			ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Сотрудник КАК Сотрудник,
		|			Показатели.Показатель КАК Показатель,
		|			СУММА(ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Показатель1) КАК Значение
		|		ИЗ
		|			РегистрСведений.ИНАГРО_ПлановыеНачисленияРаботниковОрганизаций.СрезПоследних(&ПарамДата, Организация = &Организация  И Сотрудник В (ВЫБРАТЬ Сотрудник ИЗ ВТСотрудники)) КАК ПлановыеНачисленияРаботниковОрганизацийСрезПоследних
		|				ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.ИНАГРО_Начисления.Показатели КАК Показатели
		|				ПО (Показатели.НомерСтроки = 1)
		|					И (Показатели.Ссылка = ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ВидРасчета)
		|					И ((НЕ Показатели.Показатель.Предопределенный))
		|		ГДЕ
		|			Показатели.Показатель ЕСТЬ НЕ NULL 
		|			И (НЕ Показатели.Показатель.Предопределенный)
		|		
		|		СГРУППИРОВАТЬ ПО
		|			ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Сотрудник,
		|			Показатели.Показатель
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Сотрудник,
		|			Показатели.Показатель,
		|			СУММА(ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Показатель2)
		|		ИЗ
		|			РегистрСведений.ИНАГРО_ПлановыеНачисленияРаботниковОрганизаций.СрезПоследних(&ПарамДата, Организация = &Организация И Сотрудник В (ВЫБРАТЬ Сотрудник ИЗ ВТСотрудники)) КАК ПлановыеНачисленияРаботниковОрганизацийСрезПоследних
		|				ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.ИНАГРО_Начисления.Показатели КАК Показатели
		|				ПО (Показатели.НомерСтроки = 2)
		|					И (Показатели.Ссылка = ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ВидРасчета)
		|					И ((НЕ Показатели.Показатель.Предопределенный))
		|		ГДЕ
		|			Показатели.Показатель ЕСТЬ НЕ NULL 
		|			И (НЕ Показатели.Показатель.Предопределенный)
		|		
		|		СГРУППИРОВАТЬ ПО
		|			ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Сотрудник,
		|			Показатели.Показатель
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Сотрудник,
		|			Показатели.Показатель,
		|			СУММА(ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Показатель3)
		|		ИЗ
		|			РегистрСведений.ИНАГРО_ПлановыеНачисленияРаботниковОрганизаций.СрезПоследних(&ПарамДата, Организация = &Организация И Сотрудник В (ВЫБРАТЬ Сотрудник ИЗ ВТСотрудники)) КАК ПлановыеНачисленияРаботниковОрганизацийСрезПоследних
		|				ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.ИНАГРО_Начисления.Показатели КАК Показатели
		|				ПО (Показатели.НомерСтроки = 3)
		|					И (Показатели.Ссылка = ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ВидРасчета)
		|					И ((НЕ Показатели.Показатель.Предопределенный))
		|		ГДЕ
		|			Показатели.Показатель ЕСТЬ НЕ NULL 
		|			И (НЕ Показатели.Показатель.Предопределенный)
		|		
		|		СГРУППИРОВАТЬ ПО
		|			ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Сотрудник,
		|			Показатели.Показатель
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Сотрудник,
		|			Показатели.Показатель,
		|			СУММА(ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Показатель4)
		|		ИЗ
		|			РегистрСведений.ИНАГРО_ПлановыеНачисленияРаботниковОрганизаций.СрезПоследних(&ПарамДата, Организация = &Организация И Сотрудник В (ВЫБРАТЬ Сотрудник ИЗ ВТСотрудники)) КАК ПлановыеНачисленияРаботниковОрганизацийСрезПоследних
		|				ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.ИНАГРО_Начисления.Показатели КАК Показатели
		|				ПО (Показатели.НомерСтроки = 4)
		|					И (Показатели.Ссылка = ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ВидРасчета)
		|					И ((НЕ Показатели.Показатель.Предопределенный))
		|		ГДЕ
		|			Показатели.Показатель ЕСТЬ НЕ NULL 
		|			И (НЕ Показатели.Показатель.Предопределенный)
		|		
		|		СГРУППИРОВАТЬ ПО
		|			ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Сотрудник,
		|			Показатели.Показатель
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Сотрудник,
		|			Показатели.Показатель,
		|			СУММА(ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Показатель5)
		|		ИЗ
		|			РегистрСведений.ИНАГРО_ПлановыеНачисленияРаботниковОрганизаций.СрезПоследних(&ПарамДата, Организация = &Организация И Сотрудник В (ВЫБРАТЬ Сотрудник ИЗ ВТСотрудники)) КАК ПлановыеНачисленияРаботниковОрганизацийСрезПоследних
		|				ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.ИНАГРО_Начисления.Показатели КАК Показатели
		|				ПО (Показатели.НомерСтроки = 5)
		|					И (Показатели.Ссылка = ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ВидРасчета)
		|					И ((НЕ Показатели.Показатель.Предопределенный))
		|		ГДЕ
		|			Показатели.Показатель ЕСТЬ НЕ NULL 
		|			И (НЕ Показатели.Показатель.Предопределенный)
		|		
		|		СГРУППИРОВАТЬ ПО
		|			ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Сотрудник,
		|			Показатели.Показатель
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Сотрудник,
		|			Показатели.Показатель,
		|			СУММА(ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Показатель6)
		|		ИЗ
		|			РегистрСведений.ИНАГРО_ПлановыеНачисленияРаботниковОрганизаций.СрезПоследних(&ПарамДата, Организация = &Организация И Сотрудник В (ВЫБРАТЬ Сотрудник ИЗ ВТСотрудники)) КАК ПлановыеНачисленияРаботниковОрганизацийСрезПоследних
		|				ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.ИНАГРО_Начисления.Показатели КАК Показатели
		|				ПО (Показатели.НомерСтроки = 6)
		|					И (Показатели.Ссылка = ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ВидРасчета)
		|					И ((НЕ Показатели.Показатель.Предопределенный))
		|		ГДЕ
		|			Показатели.Показатель ЕСТЬ НЕ NULL 
		|			И (НЕ Показатели.Показатель.Предопределенный)
		|		
		|		СГРУППИРОВАТЬ ПО
		|			ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Сотрудник,
		|			Показатели.Показатель
		|		
		|) КАК ВложенныйЗапрос
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСотрудники КАК Сотрудники
		|		ПО Сотрудники.Сотрудник = ВложенныйЗапрос.Сотрудник
		|	
		|	СГРУППИРОВАТЬ ПО
		|		Сотрудники.Подразделение,
		|		ВложенныйЗапрос.Сотрудник,
		|		ВложенныйЗапрос.Показатель
		|ИНДЕКСИРОВАТЬ ПО
		|	Показатель,
		|	Сотрудник,
		|	Подразделение";
	
	Запрос.Выполнить();	
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Показатели.Организация КАК Организация,
		|	Показатели.Подразделение КАК Подразделение,
		|	Показатели.ПериодДействия КАК ПериодДействия,
		|	Показатели.Сотрудник КАК Сотрудник,
		|	Показатели.Показатель КАК Показатель,
		|	СУММА(Показатели.Значение) КАК Значение
		|ИЗ
		|	(ВЫБРАТЬ
		|		ПлановыеПоказатели.Организация КАК Организация,
		|		ПлановыеПоказатели.Подразделение КАК Подразделение,
		|		ПлановыеПоказатели.ПериодДействия КАК ПериодДействия,
		|		ПлановыеПоказатели.Сотрудник КАК Сотрудник,
		|		ПлановыеПоказатели.Показатель КАК Показатель,
		|		ПлановыеПоказатели.Значение КАК Значение
		|	ИЗ
		|		ВТПлановыеПоказатели КАК ПлановыеПоказатели
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		&Организация,
		|		ЗначенияПоказателейСхемМотивации.Подразделение,
		|		&ПарамДата,
		|		ЗначенияПоказателейСхемМотивации.Сотрудник,
		|		ЗначенияПоказателейСхемМотивации.Показатель,
		|		ЕСТЬNULL(ЗначенияПоказателейСхемМотивации.Значение, 0)
		|	ИЗ
		|		РегистрСведений.ИНАГРО_ЗначенияПоказателейСхемМотивации КАК ЗначенияПоказателейСхемМотивации
		|	ГДЕ
		|		ЗначенияПоказателейСхемМотивации.Организация = &Организация
		|		И НЕ ЗначенияПоказателейСхемМотивации.Показатель.Предопределенный
		|		И ЗначенияПоказателейСхемМотивации.Показатель В(&Показатели)
		|		И ЗначенияПоказателейСхемМотивации.ПериодДействия = &ПарамДата) КАК Показатели
		|ГДЕ
		|	Показатели.Показатель.ВозможностьИзменения = ЗНАЧЕНИЕ(Перечисление.ИНАГРО_ИзменениеПоказателейСхемМотивации.ВиденНоНеРедактируетсяПриРасчете)
		|
		|СГРУППИРОВАТЬ ПО
		|	Показатели.Сотрудник,
		|	Показатели.Показатель,
		|	Показатели.Подразделение,
		|	Показатели.Организация,
		|	Показатели.ПериодДействия
		|
		|УПОРЯДОЧИТЬ ПО
		|	Организация,
		|	Подразделение,
		|	Сотрудник,
		|	Показатель";
		
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти