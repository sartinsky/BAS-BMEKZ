#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МожноРедактировать = ПравоДоступа("Редактирование", Метаданные.Документы.ОперацияБух);
	Элементы.СписокКонтекстноеМенюИзменитьВыделенные.Видимость = МожноРедактировать;
	
	ЗапрещенныеПоля = Новый Массив;
	ЗапрещенныеПоля.Добавить("ПараметрыДанных");
	Список.УстановитьОграниченияИспользованияВОтборе(ЗапрещенныеПоля);
	
	Если Параметры.КлючНазначенияИспользования = "ДокументыПокупателей"
	 Или Параметры.КлючНазначенияИспользования = "ДокументыПоставщиков" Тогда
	 
		ТипыДокументов = ЖурналыДокументов.ЖурналОпераций.СписокТиповПоНазначениюИспользования(
			Параметры.КлючНазначенияИспользования);
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
			Список, "ОтборТипыДокументов", ТипыДокументов, Истина);
	КонецЕсли;

	ИспользуетсяНесколькоОрганизаций = Справочники.Организации.ИспользуетсяНесколькоОрганизаций();
	
	ТипИнформации = "Контрагент";
	УстановитьОграничениеТипаОтбораИнформации(ЭтотОбъект);
	
	Если Не ПустаяСтрока(Параметры.Заголовок) Тогда
		АвтоЗаголовок = Ложь;
		Заголовок = Параметры.Заголовок;
	КонецЕсли;
	
	Если КлючНазначенияИспользования = "ДокументыПоКонтрагенту" Тогда
		
		// Отбор по контрагенту
		ОтборИнформацияИспользование = Параметры.Отбор.Свойство("Контрагент", ОтборИнформация)
			И ЗначениеЗаполнено(ОтборИнформация);
		Если Не ОтборИнформацияИспользование Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Не указан контрагент для показа документов.';uk='Не зазначений контрагент для показу документів.'"));
			Отказ = Истина;
		КонецЕсли;
		
		ИспользуетсяОтборПоКритериюОтбора = Истина; // отбор по контрагенту накладывается при открытии и не изменяется
		
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
			Список, "ОтборКонтрагент", ОтборИнформация, ОтборИнформацияИспользование);
		ОтборКонтрагент = ОтборИнформация;
		Элементы.ГруппаОтборИнформация.Видимость = Ложь;
		Параметры.Отбор.Удалить("Контрагент");
		Элементы.СписокИнформация.Видимость = Ложь;
		
		Элементы.СписокОрганизация.Видимость = ИспользуетсяНесколькоОрганизаций;
		// Отбор по основной организации
		Если ИспользуетсяНесколькоОрганизаций Тогда
			ОтборОрганизация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
			ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
			ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
		КонецЕсли;
		
		УстановитьСвязиПараметровВыбораДоговораКонтрагента();
		
	ИначеЕсли КлючНазначенияИспользования = "ДокументыПоДоговоруКонтрагента" Тогда
		
		ОтборДоговорКонтрагентаИспользование = Параметры.Отбор.Свойство("ДоговорКонтрагента", ОтборДоговорКонтрагента)
			И ЗначениеЗаполнено(ОтборДоговорКонтрагента);
		Если Не ОтборДоговорКонтрагентаИспользование Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Не указан договор контрагента для показа документов.';uk='Не зазначений договір контрагента для показу документів.'"));
			Отказ = Истина;
		КонецЕсли;
		
		ИспользуетсяОтборПоКритериюОтбора = Истина; // отбор по договору накладывается при открытии и не изменяется
		
		ОтборИнформацияИспользование = Ложь;
		Элементы.ГруппаОтборИнформация.Видимость = Ложь;
		Элементы.СписокИнформация.Видимость = Ложь;
		
		ОтборОрганизацияИспользование = Ложь;
		Элементы.ГруппаОтборОрганизация.Видимость = Ложь;
		Элементы.СписокОрганизация.Видимость = ИспользуетсяНесколькоОрганизаций;
			
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
			Список, "ОтборДоговорКонтрагента", ОтборДоговорКонтрагента, ОтборДоговорКонтрагентаИспользование);
		Элементы.ГруппаОтборДоговорКонтрагента.Видимость = Ложь;
		Параметры.Отбор.Удалить("ДоговорКонтрагента");
		
	Иначе
		
		ИспользуетсяОтборПоКритериюОтбора = Ложь; // отборы по контрагенту и договору могут изменяться после открытия
		
		Элементы.ГруппаОтборДоговорКонтрагента.Видимость = Ложь;
		
		// Отбор по основной организации
		Если ИспользуетсяНесколькоОрганизаций Тогда	
			ОтборОрганизация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
			ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
			ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
		КонецЕсли;
		
		// Видимость граф журнала
		Элементы.СписокОрганизация.Видимость = ИспользуетсяНесколькоОрганизаций;
		
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.ГруппаКоманды;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды 
	
	АдресХранилищаНастройкиДинСпискаДляРеестра = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	
	Если Параметры.Свойство("РежимВыбора") Тогда
		Элементы.Список.РежимВыбора = Параметры.РежимВыбора;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "ИзменениеОсновнойОрганизации" Тогда
		ОтборОрганизация = Параметр;
		ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
		ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
		Если КлючНазначенияИспользования = "ДокументыПоКонтрагенту" Тогда
			УстановитьСвязиПараметровВыбораДоговораКонтрагента();
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ВыбранноеЗначение) <> Тип("Строка") Или ПустаяСтрока(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	
	Если ЗначениеЗаполнено(ОтборДокумент) Тогда
		
		ПараметрыФормы.Вставить("Основание", ОтборДокумент);
		
		ОтборДокумент = Неопределено;
		
	Иначе
		
		ЗначенияЗаполнения = Новый Структура;
			
		Если ОтборОрганизацияИспользование Тогда
			ЗначенияЗаполнения.Вставить("Организация", ОтборОрганизация);
		КонецЕсли;
		
		Если ОтборИнформацияИспользование Тогда
			Если ТипИнформации = "Сотрудник" Тогда
				ЗначенияЗаполнения.Вставить("ФизЛицо", ОтборИнформация);
			Иначе
				Если ВыбранноеЗначение = "КорректировкаДолга" Тогда
					ЗначенияЗаполнения.Вставить("КонтрагентДебитор",  ОтборИнформация);
					ЗначенияЗаполнения.Вставить("КонтрагентКредитор", ОтборИнформация);
				Иначе
					ЗначенияЗаполнения.Вставить("Контрагент", ОтборИнформация);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если ОтборДоговорКонтрагентаИспользование Тогда
			ЗначенияЗаполнения.Вставить("Контрагент", ОтборИнформация);
			ЗначенияЗаполнения.Вставить("ДоговорКонтрагента", ОтборДоговорКонтрагента);
		КонецЕсли;
		
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
			
	КонецЕсли; 
	
	ОткрытьФорму("Документ." + ВыбранноеЗначение + ".ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если ПустаяСтрока(КлючНазначенияИспользования) Тогда
		
		Если ТипЗнч(Настройки["ОтборИнформация"]) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
			Если Элементы.ТипИнформации.СписокВыбора.НайтиПоЗначению("Сотрудник") <> Неопределено Тогда
				ТипИнформации = "Сотрудник";
			Иначе
				ТипИнформации                = "Контрагент";
				ОтборИнформация              = ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка");
				ОтборИнформацияИспользование = Ложь;
			КонецЕсли;
		Иначе
			ТипИнформации = "Контрагент";
			ОтборКонтрагент = ОтборИнформация;
		КонецЕсли;
		
		УстановитьОграничениеТипаОтбораИнформации(ЭтотОбъект);
		ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Информация");
		
	Иначе
		// По графе "Информация" может быть быстрый отбор только по контрагенту.
		
		ТипИнформации = "Контрагент";
		Если ТипЗнч(ОтборИнформация) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
			// Обрабатываем некорректно сохраненные настройки.
			ОтборИнформация              = ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка");
			ОтборИнформацияИспользование = Ложь;
			
			УстановитьОграничениеТипаОтбораИнформации(ЭтотОбъект);
			
		КонецЕсли;
		
		Если ИспользуетсяОтборПоКритериюОтбора Тогда
			
			Если КлючНазначенияИспользования = "ДокументыПоКонтрагенту" Тогда // не изменяем контрагента
				
				ОтборИнформация = ОтборКонтрагент;
				
				Если ЗначениеЗаполнено(ОтборИнформация)
				   И ЗначениеЗаполнено(ОтборДоговорКонтрагента)
				   И ОтборКонтрагент <> ПараметрыВыбораДоговораКонтрагента(ОтборДоговорКонтрагента).Владелец Тогда
					// Обрабатываем некорректно сохраненные настройки.
					ОтборДоговорКонтрагентаИспользование = Ложь;
					ОтборДоговорКонтрагента = Неопределено;
				КонецЕсли;
				УстановитьСвязиПараметровВыбораДоговораКонтрагента();
				ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "ДоговорКонтрагента");
				
			ИначеЕсли КлючНазначенияИспользования = "ДокументыПоДоговоруКонтрагента" Тогда // не изменяем договор
				
				ПараметрКомпоновкиДанных = Новый ПараметрКомпоновкиДанных("ОтборДоговорКонтрагента");
				ЗначениеПараметраКомпоновкиДанных = Список.Параметры.НайтиЗначениеПараметра(ПараметрКомпоновкиДанных);
				ОтборДоговорКонтрагента = ?(ЗначениеПараметраКомпоновкиДанных = Неопределено,
					Справочники.ДоговорыКонтрагентов.ПустаяСсылка(),
					ЗначениеПараметраКомпоновкиДанных.Значение);
					
			Иначе // обрабатываем некорректно сохраненные настройки
					
				ОтборИнформация = ОтборКонтрагент;
				ОтборДоговорКонтрагента = Неопределено;
				
			КонецЕсли;
			
		Иначе
			
			ОтборКонтрагент = ОтборИнформация;
			ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Информация");
			
			Если ЗначениеЗаполнено(ОтборИнформация)
			   И ЗначениеЗаполнено(ОтборДоговорКонтрагента)
			   И ОтборКонтрагент <> ПараметрыВыбораДоговораКонтрагента(ОтборДоговорКонтрагента).Владелец Тогда
				// Обрабатываем некорректно сохраненные настройки.
				ОтборДоговорКонтрагентаИспользование = Ложь;
				ОтборДоговорКонтрагента = Неопределено;
			КонецЕсли;
			ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "ДоговорКонтрагента");
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТипИнформацииПриИзменении(Элемент)
	
	УстановитьОграничениеТипаОтбораИнформации(ЭтотОбъект);
	Если ТипИнформации = "ПодотчетноеЛицо" Тогда
		ОтборИнформация = ПредопределенноеЗначение("Справочник.ФизическиеЛица.ПустаяСсылка");
	Иначе
		ОтборИнформация = ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка");
	КонецЕсли;
	ОтборИнформацияИспользование = Ложь;
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Информация");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборИнформацияИспользованиеПриИзменении(Элемент)
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Информация");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборИнформацияПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ОтборИнформация) Тогда
		ОтборИнформацияИспользование = Истина;
	КонецЕсли;
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Информация");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияИспользованиеПриИзменении(Элемент)
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
	
	Если КлючНазначенияИспользования = "ДокументыПоКонтрагенту" Тогда
		УстановитьСвязиПараметровВыбораДоговораКонтрагента();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ОтборОрганизация) Тогда
		ОтборОрганизацияИспользование = Истина;
		Если КлючНазначенияИспользования = "ДокументыПоКонтрагенту" Тогда
			УстановитьСвязиПараметровВыбораДоговораКонтрагента();
		КонецЕсли;
	КонецЕсли;
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборДоговорКонтрагентаИспользованиеПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список, "ОтборДоговорКонтрагента", ОтборДоговорКонтрагента, ОтборДоговорКонтрагентаИспользование);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборДоговорКонтрагентаПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ОтборДоговорКонтрагента) Тогда
		ОтборДоговорКонтрагентаИспользование = Истина;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список, "ОтборДоговорКонтрагента", ОтборДоговорКонтрагента, ОтборДоговорКонтрагентаИспользование);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Не Копирование Тогда
		
		Отказ = Истина;
		
		ТекущиеДанные = Элементы.Список.ТекущиеДанные;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("КлючНазначения", КлючНазначенияИспользования);
		
		Если ТекущиеДанные <> Неопределено Тогда
			ПараметрыФормы.Вставить("НачальноеЗначение", ТипЗнч(ТекущиеДанные.Ссылка));
		КонецЕсли;
		
		ОткрытьФорму("ЖурналДокументов.ЖурналОпераций.Форма.ВыборТипаДокумента", ПараметрыФормы, ЭтотОбъект, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриИзменении(Элемент)
	
	Если БухгалтерскийУчетКлиентСерверПереопределяемый.ЭтоРегламентнаяОперация(Элемент.ТекущаяСтрока)
		И Элемент.ТекущиеДанные <> Неопределено
		И Элемент.ТекущиеДанные.СостояниеДокумента = 1 Тогда
		Оповестить("ОбновитьЗакрытиеМесяца");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПереключитьАктивностьПроводок(Команда)

	ТекДанные = Элементы.Список.ТекущиеДанные;

	Если ТекДанные = Неопределено Тогда
		ПоказатьПредупреждение(Неопределено, НСтр("ru='Не выбран документ';uk='Не вибраний документ'"));
		Возврат;
	КонецЕсли;

	ПереключитьАктивностьПроводокСервер(ТекДанные.Ссылка);

КонецПроцедуры

&НаКлиенте
Процедура СоздатьНаОсновании(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("КлючНазначенияИспользования", КлючНазначенияИспользования);
	
	Если ТекущиеДанные <> Неопределено Тогда
		ОтборДокумент = ТекущиеДанные.Ссылка;
		ПараметрыФормы.Вставить("Основание", ТекущиеДанные.Ссылка);
	КонецЕсли;
	
	ОткрытьФорму("ЖурналДокументов.ЖурналОпераций.Форма.ВыборТипаДокумента", ПараметрыФормы, ЭтотОбъект, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура ПереключитьАктивностьПроводокСервер(ТекДокумент)

	БухгалтерскийУчет.ПереключитьАктивностьПроводокБУ(ТекДокумент);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОграничениеТипаОтбораИнформации(Форма)

	Если Форма.ТипИнформации = "ПодотчетноеЛицо" Тогда
		Форма.Элементы.ОтборИнформация.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица");
	Иначе
		Форма.Элементы.ОтборИнформация.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.Контрагенты");
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьСвязиПараметровВыбораДоговораКонтрагента()

	МассивСвязей = Новый Массив;
	
	Если ОтборИнформацияИспользование Тогда
		МассивСвязей.Добавить(Новый СвязьПараметраВыбора("Отбор.Владелец", "ОтборИнформация", РежимИзмененияСвязанногоЗначения.Очищать));
	КонецЕсли;
	
	Если ОтборОрганизацияИспользование Тогда
		МассивСвязей.Добавить(Новый СвязьПараметраВыбора("Отбор.Организация", "ОтборОрганизация", РежимИзмененияСвязанногоЗначения.Очищать));
	КонецЕсли;
	
	Элементы.ОтборДоговорКонтрагента.СвязиПараметровВыбора = Новый ФиксированныйМассив(МассивСвязей);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПараметрыВыбораДоговораКонтрагента(Знач ДоговорКонтрагента)
	
	Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДоговорКонтрагента, "Владелец, Организация");
	
КонецФункции

&НаСервере
Процедура НастройкиДинамическогоСписка()
	
	Отчеты.РеестрДокументов.НастройкиДинамическогоСписка(ЭтотОбъект);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ БСП

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	Если Команда.Имя = "ПодменюПечатьОбычное_Реестр" Тогда
		НастройкиДинамическогоСписка();
	КонецЕсли;
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти