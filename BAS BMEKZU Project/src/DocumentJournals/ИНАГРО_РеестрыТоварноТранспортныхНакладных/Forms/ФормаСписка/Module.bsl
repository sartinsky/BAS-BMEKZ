#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.ГруппаКоманды;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды 	
		
	ОбщегоНазначенияБПВызовСервера.УстановитьОтборПоОсновнойОрганизации(ЭтотОбъект);
	
	ДокументСсылка = Тип("ДокументСсылка.ИНАГРО_РеестрТТНВвоз");

	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Тип", ДокументСсылка, ВидСравненияКомпоновкиДанных.Равно);
	
	УправлениеФормой(ЭтаФорма);
	
	ПолучитьИтогиНаСервере();

	АдресХранилищаНастройкиДинСпискаДляРеестра = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзменениеОсновнойОрганизации" Тогда
		ОбщегоНазначенияБПКлиент.ИзменитьОтборПоОсновнойОрганизации(Список, ,Параметр);
	ИначеЕсли ИмяСобытия = "СозданРеестрТТНВвоз"
		  ИЛИ ИмяСобытия = "СозданРеестрТТНВнутр"
		  ИЛИ ИмяСобытия = "СозданРеестрТТНВывоз" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки)
	
	ОбщегоНазначенияБП.ВосстановитьОтборСписка(Список, Настройки, "Организация");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИзменитьВидОперации(Команда)
		
	СтрокаТаблицы = Элементы.Список.ТекущиеДанные;
	Если СтрокаТаблицы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Ключ", СтрокаТаблицы.Ссылка);
	ПараметрыФормы.Вставить("ИзменитьВидОперации", Истина);
	
	ОткрытьФорму("Документ.ИНАГРО_РеестрТТНВывоз.Форма.ФормаДокумента", ПараметрыФормы, ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);

КонецПроцедуры

&НаКлиенте
Процедура Создать(Команда)
	
	Если ДокументСсылка = Тип("ДокументСсылка.ИНАГРО_РеестрТТНВвоз") Тогда
		ИмяДокумента = "ИНАГРО_РеестрТТНВвоз";
	ИначеЕсли ДокументСсылка = Тип("ДокументСсылка.ИНАГРО_РеестрТТНВнутр") Тогда
		ИмяДокумента = "ИНАГРО_РеестрТТНВнутр";
	ИначеЕсли ДокументСсылка = Тип("ДокументСсылка.ИНАГРО_РеестрТТНВывоз") Тогда
		ИмяДокумента = "ИНАГРО_РеестрТТНВывоз";
	КонецЕсли;
	
	СтруктураПараметров = ПолучитьСтруктуруПараметровФормы();
	ОткрытьФорму("Документ." + ИмяДокумента + ".Форма.ФормаДокумента", СтруктураПараметров, ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура РеестрТТНВвоз(Команда)
	
	ДокументСсылка = Тип("ДокументСсылка.ИНАГРО_РеестрТТНВвоз");
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Тип", ДокументСсылка, ВидСравненияКомпоновкиДанных.Равно);
	
	УправлениеФормой(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура РеестрТТНВнутр(Команда)
	
	ДокументСсылка = Тип("ДокументСсылка.ИНАГРО_РеестрТТНВнутр");
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Тип", ДокументСсылка, ВидСравненияКомпоновкиДанных.Равно);
	
	УправлениеФормой(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура РеестрТТНВывоз(Команда)
	
	ДокументСсылка = Тип("ДокументСсылка.ИНАГРО_РеестрТТНВывоз");	
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Тип", ДокументСсылка, ВидСравненияКомпоновкиДанных.Равно);
	
	УправлениеФормой(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура СформироватьРеестрыТТН(Команда)
	
	ПараметрыФормы = Новый Структура;
		
	Если ДокументСсылка = Тип("ДокументСсылка.ИНАГРО_РеестрТТНВвоз") Тогда
		ПараметрыФормы.Вставить("Реестр", "РеестрТТНВвоз");	
	ИначеЕсли ДокументСсылка = Тип("ДокументСсылка.ИНАГРО_РеестрТТНВнутр") Тогда
		ПараметрыФормы.Вставить("Реестр", "РеестрТТНВнутр");
	ИначеЕсли ДокументСсылка = Тип("ДокументСсылка.ИНАГРО_РеестрТТНВывоз") Тогда
		ПараметрыФормы.Вставить("Реестр", "РеестрТТНВывоз");	
	КонецЕсли;
		
	ОткрытьФорму("Обработка.ИНАГРО_ФормированиеРеестровТТН.Форма.Форма", ПараметрыФормы, ЭтаФорма, Новый УникальныйИдентификатор(), , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьИтоги(Команда)
	
	ПолучитьИтогиНаСервере();

КонецПроцедуры

&НаСервере
Процедура ПолучитьИтогиНаСервере()
	
	СхемаКомпоновкиДанных        = Элементы.Список.ПолучитьИсполняемуюСхемуКомпоновкиДанных();
    Настройки                    = Элементы.Список.ПолучитьИсполняемыеНастройкиКомпоновкиДанных();
	
	ЭлементОтбора                = Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Проведен");
	ЭлементОтбора.ПравоеЗначение = Истина;
	ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.Использование  = Истина;

	КомпоновщикМакета            = Новый КомпоновщикМакетаКомпоновкиДанных;
    МакетКомпоновкиДанных        = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, , , Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	ПроцессорКомпоновкиДанных    = Новый ПроцессорКомпоновкиДанных;
    ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных);
	
	ПроцессорВывода              = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
    Результат                    = ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
	ОбщийФизическийВес           = Результат.Итог("СсылкаОбщийФизическийВес");
	ФизическийВес                = Результат.Итог("СсылкаФизическийВес");
	ЗачетныйВес                  = Результат.Итог("СсылкаЗачетныйВес");
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНаОсновании(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("КлючНазначенияИспользования", КлючНазначенияИспользования);
	
	Если ТекущиеДанные <> Неопределено Тогда
		ОтборДокумент = ТекущиеДанные.Ссылка;
		ПараметрыФормы.Вставить("Основание", ТекущиеДанные.Ссылка);
	КонецЕсли;
	
	ОткрытьФорму("ЖурналДокументов.ИНАГРО_РеестрыТоварноТранспортныхНакладных.Форма.ВыборТипаДокумента", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ВыбранноеЗначение) <> Тип("Строка") Или ПустаяСтрока(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборДокумент) Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыОткрытияДокумента = Неопределено;
		ПараметрыФормы.Вставить("Основание", ОтборДокумент);
		
		Если ПараметрыОткрытияДокумента <> Неопределено Тогда
			ВыбранноеЗначение = ПараметрыОткрытияДокумента.ИмяДокумента;
			ПараметрыФормы    = ПараметрыОткрытияДокумента.ПараметрыФормы;
		КонецЕсли;
		
		ОтборДокумент = Неопределено;
		ОткрытьФорму("Документ." + ВыбранноеЗначение + ".ФормаОбъекта", ПараметрыФормы);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.ГруппаДокументы.Заголовок = УстановитьЗаголовок(Форма);
	
	РеестрТТНВвоз  = Форма.ДокументСсылка = Тип("ДокументСсылка.ИНАГРО_РеестрТТНВвоз");
	РеестрТТНВнутр = Форма.ДокументСсылка = Тип("ДокументСсылка.ИНАГРО_РеестрТТНВнутр");
	РеестрТТНВывоз = Форма.ДокументСсылка = Тип("ДокументСсылка.ИНАГРО_РеестрТТНВывоз");

	Элементы.РеестрТТНВвоз.Доступность  = НЕ РеестрТТНВвоз;
	Элементы.РеестрТТНВнутр.Доступность = НЕ РеестрТТНВнутр;
	Элементы.РеестрТТНВывоз.Доступность = НЕ РеестрТТНВывоз;
	
	Элементы.ГруппаВладелецДоговорКонтрагента.Видимость              = РеестрТТНВвоз ИЛИ РеестрТТНВывоз;
	Элементы.ГруппаПоставщикДоговорПоставщика.Видимость              = РеестрТТНВвоз;
	Элементы.ГруппаНоваяНоменклатураУрожай.Видимость                 = РеестрТТНВнутр;
	Элементы.ГруппаСтепеньВлажностиЗагрязненияЗараженности.Видимость = РеестрТТНВвоз;
	
	Элементы.СкладскаяКвитанция.Видимость       = РеестрТТНВвоз;
	Элементы.СсылкаСилос.Видимость              = РеестрТТНВвоз;
	Элементы.СсылкаНовоеМестоХранения.Видимость = РеестрТТНВнутр;
	Элементы.СсылкаОбщийФизическийВес.Видимость = РеестрТТНВвоз;
	Элементы.СсылкаФизическийВес.Видимость      = РеестрТТНВнутр ИЛИ РеестрТТНВывоз;
	Элементы.СсылкаЗачетныйВес.Видимость        = РеестрТТНВвоз;
	Элементы.СсылкаЛабораторныйАнализ.Видимость = РеестрТТНВвоз ИЛИ РеестрТТНВывоз;
	Элементы.СсылкаНомерПробы.Видимость         = РеестрТТНВвоз;
	
	Элементы.СоздатьНаОсновании.Доступность = РеестрТТНВвоз;
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция УстановитьЗаголовок(Форма)
	
	Если Форма.ДокументСсылка      = Тип("ДокументСсылка.ИНАГРО_РеестрТТНВвоз") Тогда
		ТекстЗаголовка = НСтр("ru='Реестры товарно-транспортных накладных (ввоз)';uk='Реєстри товарно-транспортних накладних (ввезення)'");
	ИначеЕсли Форма.ДокументСсылка = Тип("ДокументСсылка.ИНАГРО_РеестрТТНВнутр") Тогда
		ТекстЗаголовка = НСтр("ru='Реестры товарно-транспортных накладных (внутренних)';uk='Реєстри товарно-транспортних накладних (внутрішніх)'");
	ИначеЕсли Форма.ДокументСсылка = Тип("ДокументСсылка.ИНАГРО_РеестрТТНВывоз") Тогда
		ТекстЗаголовка = НСтр("ru='Реестры товарно-транспортных накладных (вывоз)';uk='Реєстри товарно-транспортних накладних (вивезення)'");
	КонецЕсли;
	
	Возврат ТекстЗаголовка;

КонецФункции

&НаКлиенте
Функция ПолучитьСтруктуруПараметровФормы()

	СтруктураПараметров = Новый Структура;
	
	ЗначенияЗаполнения = ОбщегоНазначенияБПВызовСервера.ЗначенияЗаполненияДинамическогоСписка(Список.КомпоновщикНастроек);
		
	СтруктураПараметров.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	Возврат СтруктураПараметров;
	
КонецФункции

&НаСервере
Процедура НастройкиДинамическогоСписка()
	
	Отчеты.РеестрДокументов.НастройкиДинамическогоСписка(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункцииБСП

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	Если Команда.Имя = "ПодменюПечатьОбычное_Реестр" Тогда
		НастройкиДинамическогоСписка();
	КонецЕсли;
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти