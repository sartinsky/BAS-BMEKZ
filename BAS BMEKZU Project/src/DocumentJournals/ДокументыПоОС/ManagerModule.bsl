#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЧтениеОбъектаРазрешено(Ссылка)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область ПроцедурыИФункцииПечати

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Реестр";
	КомандаПечати.Представление = НСтр("ru='Реестр документов';uk='Реєстр документів'");
	КомандаПечати.ЗаголовокФормы= НСтр("ru='Реестр документов ""Документы по ОС""';uk='Реєстр документів ""Документи по ОЗ""'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм    = "ФормаСписка";
	КомандаПечати.Порядок       = 100;
	
КонецПроцедуры

Функция ПолучитьДополнительныеРеквизитыДляРеестра() Экспорт
	Результат = Новый Структура;
	
	ПолеЗапросаИнфо =
		"	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(Таб.Ссылка) = ТИП(Документ.ИнвентаризацияОС)
		|			ТОГДА ВЫРАЗИТЬ(ВЫРАЗИТЬ(Таб.Ссылка КАК Документ.ИнвентаризацияОС).ПричинаПроведенияИнвентаризации КАК СТРОКА(1000))
		|		КОГДА ТИПЗНАЧЕНИЯ(Таб.Ссылка) = ТИП(Документ.ПередачаОС)
		|			ТОГДА ВЫРАЗИТЬ(Таб.Ссылка КАК Документ.ПередачаОС).Контрагент
		|		КОГДА ТИПЗНАЧЕНИЯ(Таб.Ссылка) = ТИП(Документ.ВводВЭксплуатациюОС)
		|			ТОГДА ВЫРАЗИТЬ(Таб.Ссылка КАК Документ.ВводВЭксплуатациюОС).ВидОперации
		|		КОГДА ТИПЗНАЧЕНИЯ(Таб.Ссылка) = ТИП(Документ.МодернизацияОС) ИЛИ ТИПЗНАЧЕНИЯ(Таб.Ссылка) = ТИП(Документ.ПередачаОборудованияВМонтаж)
		|			ТОГДА Таб.ОбъектСтроительства
		|		ИНАЧЕ Таб.СобытиеОС
		|	КОНЕЦ";
	
	ПолеЗапросаСумма =
	"	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(Таб.Ссылка) = ТИП(Документ.ПередачаОС)
	|			ТОГДА Таб.Ссылка.СуммаДокумента
	|		ИНАЧЕ 0
	|	КОНЕЦ";
	
	ПолеЗапросаВалюта =
	"	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(Таб.Ссылка) = ТИП(Документ.ПередачаОС)
	|			ТОГДА Таб.Ссылка.ВалютаДокумента
	|	КОНЕЦ";
	
	Результат.Вставить("Информация",      ПолеЗапросаИнфо);
	Результат.Вставить("СуммаДокумента",  ПолеЗапросаСумма);
	Результат.Вставить("ВалютаДокумента", ПолеЗапросаВалюта);
	
	Возврат Результат;
КонецФункции

#КонецОбласти

#КонецЕсли