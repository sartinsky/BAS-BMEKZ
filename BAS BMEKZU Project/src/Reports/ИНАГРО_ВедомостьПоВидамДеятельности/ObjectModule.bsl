#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	Настройки = КомпоновщикНастроек.ПолучитьНастройки(); 
	
	ТабОтборов = Настройки.Отбор.Элементы;
	НастройкиВидаОтчета = Настройки.ПараметрыДанных.Элементы.Найти("ВидОтчета");
	
	ПериодОтчета = Настройки.ПараметрыДанных.Элементы.Найти("Период");
	Если ПериодОтчета.Использование Тогда
		ДатаНачалаОтчета = Настройки.ПараметрыДанных.Элементы.Найти("Период").Значение.ДатаНачала;
		ДатаОкончанияОтчета = Настройки.ПараметрыДанных.Элементы.Найти("Период").Значение.ДатаОкончания;
	Иначе
		ДатаНачалаОтчета = '00010101';
		ДатаОкончанияОтчета = '39991231235959';
	КонецЕсли; 	
	
	Если НастройкиВидаОтчета <> Неопределено Тогда
		Если НастройкиВидаОтчета.Значение = 1 Тогда // Это автотранспорт
			Настройки = СхемаКомпоновкиДанных.ВариантыНастроек.Автотранспорт.Настройки;
			ЗаголовокОтчета = Настройки.ПараметрыВывода.Элементы.Найти("Заголовок");
			Если ЗаголовокОтчета <> Неопределено Тогда
				ЗаголовокОтчета.Значение = НСтр("ru='Анализ путевых листов по видам деятельности автотранспорт';uk='Аналіз подорожніх листів по видам діяльності автотранспорт'");	
				ЗаголовокОтчета.Использование = Истина;
			КонецЕсли; 
		ИначеЕсли НастройкиВидаОтчета.Значение = 2 Тогда // Это Сельхозтехника
			Настройки = СхемаКомпоновкиДанных.ВариантыНастроек.Сельхозтехника.Настройки;
			ЗаголовокОтчета = Настройки.ПараметрыВывода.Элементы.Найти("Заголовок");
			Если ЗаголовокОтчета <> Неопределено Тогда
				ЗаголовокОтчета.Значение = НСтр("ru='Анализ путевых листов по видам деятельности сельхозтехника';uk='Аналіз подорожніх листів по видам діяльності сільгосптехніка'");	
				ЗаголовокОтчета.Использование = Истина;
			КонецЕсли;
		Иначе
			Возврат;
		КонецЕсли;
		Настройки.ПараметрыДанных.Элементы.Найти("Период").Значение.ДатаНачала = ДатаНачалаОтчета;
		Настройки.ПараметрыДанных.Элементы.Найти("Период").Значение.ДатаОкончания = ДатаОкончанияОтчета;
		Настройки.ПараметрыДанных.Элементы.Найти("Период").Использование = Истина;
	КонецЕсли;
	
	Настройки.Отбор.Элементы.Очистить();
	Если ТабОтборов.Количество() > 0 Тогда
		Для Каждого Отб Из ТабОтборов Цикл
			ЭлементОтбора = Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ЗаполнитьЗначенияСвойств(ЭлементОтбора,Отб);
		КонецЦикла; 	
	КонецЕсли; 
	
	МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки);
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных, ,ДанныеРасшифровки, Истина);
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных, Истина);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли