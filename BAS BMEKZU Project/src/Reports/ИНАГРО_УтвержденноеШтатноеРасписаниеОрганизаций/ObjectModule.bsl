#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДокументРезультат.Очистить();
	
	ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ДокументРезультат.ПолеСлева			 = 0;
	ДокументРезультат.ПолеСправа		 = 0;
	ДокументРезультат.Защита             = Ложь;
	ДокументРезультат.ТолькоПросмотр     = Истина;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	Настройки = КомпоновщикНастроек.ПолучитьНастройки();   	
	
	НайденныйПериод = Настройки.ПараметрыДанных.Элементы.Найти("Период");
	Если Не НайденныйПериод = Неопределено Тогда
		Если НайденныйПериод.Использование Тогда
			ДатаАктуальности = КонецДня(НайденныйПериод.Значение);
		Иначе
			НайденныйПериод.Значение = ТекущаяДата();
			ДатаАктуальности = ТекущаяДата();
		КонецЕсли; 
		
		ПериодНачМесяца = Настройки.ПараметрыДанных.Элементы.Найти("НачалоМесяца0");
		Если Не ПериодНачМесяца = Неопределено Тогда
			ПериодНачМесяца.Значение = НачалоМесяца(ДатаАктуальности);	
		КонецЕсли;
		
		ПериодКонМесяца = Настройки.ПараметрыДанных.Элементы.Найти("ОкончаниеМесяца0");
		Если Не ПериодКонМесяца = Неопределено Тогда
			ПериодКонМесяца.Значение = КонецМесяца(ДатаАктуальности);	
		КонецЕсли;
		НайденныйПериод.Использование =Истина;
		ПериодНачМесяца.Использование =Истина;
		ПериодКонМесяца.Использование =Истина;			
	КонецЕсли;
	
	МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных,
	Настройки, , ,
	Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	
	ТаблицаРезультатаЗапросса = Новый ТаблицаЗначений;  
	
	ПроцессорВывода.УстановитьОбъект(ТаблицаРезультатаЗапросса);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
	МассивОрганизаций = ТаблицаРезультатаЗапросса.ВыгрузитьКолонку("Организация");	
	ОбщегоНазначенияБПВызовСервера.УдалитьПовторяющиесяЭлементыМассива(МассивОрганизаций);
	
	Макет = ЭтотОбъект.ПолучитьМакет("ПФ_MXL_ШтатноеРасписание");	
	КодЯзыкаПечать = Локализация.ПолучитьЯзыкФормированияПечатныхФорм();
	Макет.КодЯзыкаМакета = КодЯзыкаПечать;
	
	ПерваяСтраница = Истина;
	Для Каждого Организ Из МассивОрганизаций Цикл
		
		Если Не ПерваяСтраница Тогда
			ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
		Иначе
			ПерваяСтраница = Ложь;
		КонецЕсли;
		
		ТабОрганизация = ТаблицаРезультатаЗапросса.Скопировать(Новый Структура ("Организация",Организ));
		
		МассивНадбавок = ТабОрганизация.ВыгрузитьКолонку("ВидНадбавки");
		ОбщегоНазначенияБПВызовСервера.УдалитьПовторяющиесяЭлементыМассива(МассивНадбавок);
		
		Индекс = МассивНадбавок.Найти(ПланыВидовРасчета.ИНАГРО_Начисления.ПустаяСсылка());
		Если Индекс <> Неопределено Тогда
			МассивНадбавок.Удалить(Индекс); 	
		КонецЕсли;
		
		КолДополнНадбавок = 3 - МассивНадбавок.Количество();
		Для КН = 1 По КолДополнНадбавок Цикл
			МассивНадбавок.Добавить(ПланыВидовРасчета.ИНАГРО_Начисления.ПустаяСсылка());		
		КонецЦикла; 
		КоличествоНадбавок = МассивНадбавок.Количество();
		
		МассивВидовРуководителей = Новый Массив();
		МассивВидовРуководителей.Добавить(Перечисления.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер);
		МассивВидовРуководителей.Добавить(Перечисления.ОтветственныеЛицаОрганизаций.РуководительКадровойСлужбы);
		
		Запрос = Новый Запрос;			
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ОтветственныеЛицаОрганизацийСрезПоследних.Должность.Представление КАК ДолжностьРуководителя,
		|	ЕСТЬNULL(ФИОФизЛицСрезПоследних.Фамилия + "" "" + ФИОФизЛицСрезПоследних.Имя + "" "" + ФИОФизЛицСрезПоследних.Отчество, ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо.Наименование) КАК ФИОРуководителя,
		|	ОтветственныеЛицаОрганизацийСрезПоследних.ОтветственноеЛицо
		|ИЗ
		|	РегистрСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(
		|			&ДатаАктуальности,
		|			ОтветственноеЛицо В (&ВидЛица)
		|				И СтруктурнаяЕдиница = &Организация) КАК ОтветственныеЛицаОрганизацийСрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизическихЛиц.СрезПоследних(&ДатаАктуальности, ) КАК ФИОФизЛицСрезПоследних
		|		ПО ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо = ФИОФизЛицСрезПоследних.ФизическоеЛицо";
		
		Запрос.УстановитьПараметр("ВидЛица", МассивВидовРуководителей);
		Запрос.УстановитьПараметр("ДатаАктуальности", ДатаАктуальности);
		Запрос.УстановитьПараметр("Организация", Организ);
		
		ЗапросДляПодвала = Запрос.Выполнить();
		
		ОбластьШапкаДоНадбавок = Макет.ПолучитьОбласть("Шапка|ДоНадбавок");
		ОбластьШапкаДоНадбавок.Параметры.НазваниеОрганизации = Организ.НаименованиеПолное;
		ОбластьШапкаДоНадбавок.Параметры.ДатаАктуальности = НачалоМесяца(ДатаАктуальности);
		ДокументРезультат.Вывести(ОбластьШапкаДоНадбавок);
		
		ОбластьШапкаНадбавка = Макет.ПолучитьОбласть("Шапка|Надбавка");
		Для Счетчик=0 По КоличествоНадбавок - 1 Цикл
			ДокументРезультат.Присоединить(ОбластьШапкаНадбавка);	
		КонецЦикла;	
		
		ВсегоСтавокОрганизации = ТабОрганизация.Итог("КоличествоСтавок");
		ОбластьШапкаПослеНадбавок = Макет.ПолучитьОбласть("Шапка|ПослеНадбавок");
		ОбластьШапкаПослеНадбавок.Параметры.ШтатВКоличестве = НСтр("ru='штат в количестве "+ВсегоСтавокОрганизации+" единиц';uk='штат в кількості "+ВсегоСтавокОрганизации+" одиниць'",КодЯзыкаПечать);
		ДокументРезультат.Присоединить(ОбластьШапкаПослеНадбавок);
		
		НомерВерхнейСтрокиШапки = ДокументРезультат.ВысотаТаблицы+1;
		ОбластьДоНадбавок = Макет.ПолучитьОбласть("ШапкаЛиста|ДоНадбавок");
		ДокументРезультат.Вывести(ОбластьДоНадбавок);
		
		ОбластьНадбавка = Макет.ПолучитьОбласть("ШапкаЛиста|Надбавка");
		Для Счетчик=0 По КоличествоНадбавок-1 Цикл
			ОбластьНадбавка.Параметры.ИмяНадбавки = МассивНадбавок[Счетчик];
			ОбластьНадбавка.Параметры.НомерКолонки = 6+Счетчик;
			ДокументРезультат.Присоединить(ОбластьНадбавка);	
		КонецЦикла;	
		
		ОбластьШапкиНадбавок = ДокументРезультат.Область(НомерВерхнейСтрокиШапки,ОбластьДоНадбавок.ШиринаТаблицы+1,НомерВерхнейСтрокиШапки,ОбластьДоНадбавок.ШиринаТаблицы+КоличествоНадбавок*3);
		ОбластьШапкиНадбавок.Объединить();
		ОбластьШапкиНадбавок.Текст = "Надбавки, грн";
		
		ОбластьПослеНадбавок = Макет.ПолучитьОбласть("ШапкаЛиста|ПослеНадбавок");
		ОбластьПослеНадбавок.Параметры.НомерКолонкиФонд = 6+КоличествоНадбавок;
		ОбластьПослеНадбавок.Параметры.НомерКолонкиПримечание = 6+КоличествоНадбавок+1;
		ДокументРезультат.Присоединить(ОбластьПослеНадбавок);
		
		СтрокаДоНадбавок = Макет.ПолучитьОбласть("Строка|ДоНадбавок");
		СтрокаНадбавка = Макет.ПолучитьОбласть("Строка|Надбавка");		
		СтрокаПослеНадбавок = Макет.ПолучитьОбласть("Строка|ПослеНадбавок");
		
		КолонкиСуммирования = "КоличествоСтавок, КоличествоСтрок, МаксОсновнойТариф, МинОсновнойТариф, МаксСтавка, МинСтавка, МинСуммаНадбавки, МаксСуммаНадбавки";
		Ном = 0;	
		Для каждого ЭлементМас Из МассивНадбавок Цикл		
			Если ЭлементМас <> "" Тогда
				КолонкаНаим = "Надбавка"+Строка(Ном);
				
				ТабОрганизация.Колонки.Добавить(КолонкаНаим);
				
				КолонкиСуммирования = КолонкиСуммирования + "," + КолонкаНаим;
				Ном = Ном + 1; 
			КонецЕсли;
		КонецЦикла;
		ТабОрганизация.Колонки.Добавить("КоличествоСтрок");
		
		Для каждого Стр Из ТабОрганизация Цикл
			Если ЗначениеЗаполнено(Стр.ВидНадбавки) Тогда				
				Ном = 0;
				Для каждого ЭлементМас Из МассивНадбавок Цикл				
					Если Стр.ВидНадбавки = ЭлементМас Тогда
						Стр["Надбавка"+Строка(Ном)] = Стр.РазмерНадбавки;
					КонецЕсли;
					Ном = Ном +1;		
				КонецЦикла; 	
			КонецЕсли;
			Стр.КоличествоСтрок = 1;
		КонецЦикла;
		
		ТабОрганизация.Свернуть("ПодразделениеОрганизации,Должность",КолонкиСуммирования);		
		
		
		СуммаСтавок = 0;
		СуммаМинСтавки = 0;
		СуммаМаксСтавки = 0;
		СуммаМинСтавкиСНадбавками = 0;
		СуммаМаксСтавкиСНадбавками = 0;
		
		// Заполнение ТабЧасти
		ВСЕГОКоличествоНадбавок = 0;
		ВСЕГОСуммаСтавок = 0;
		ВСЕГОСуммаМинСтавки = 0;
		ВСЕГОСуммаМаксСтавки = 0;
		ВСЕГОСуммаМинСтавкиСНадбавками = 0;
		ВСЕГОСуммаМаксСтавкиСНадбавками = 0;
		
		Для каждого Строка Из ТабОрганизация Цикл
			
			ВыводимыеОбласти = Новый Массив();
			ВыводимыеОбласти.Добавить(СтрокаДоНадбавок);
			ВыводимыеОбласти.Добавить(Макет.ПолучитьОбласть("ПодвалЛиста"));
			
			Если Не ДокументРезультат.ПроверитьВывод(ВыводимыеОбласти) Тогда
				
				ВСЕГОСуммаСтавок = ВСЕГОСуммаСтавок + СуммаСтавок;
				ВСЕГОСуммаМинСтавки = ВСЕГОСуммаМинСтавки + СуммаМинСтавки;
				ВСЕГОСуммаМаксСтавки = ВСЕГОСуммаМаксСтавки + СуммаМаксСтавки;
				ВСЕГОСуммаМинСтавкиСНадбавками = ВСЕГОСуммаМинСтавкиСНадбавками + СуммаМинСтавкиСНадбавками;
				ВСЕГОСуммаМаксСтавкиСНадбавками = ВСЕГОСуммаМаксСтавкиСНадбавками + СуммаМаксСтавкиСНадбавками;
				
				ВывестиПодвалЛиста(ДокументРезультат, Макет, КоличествоНадбавок, СуммаСтавок,СуммаМинСтавки,СуммаМаксСтавки,СуммаМинСтавкиСНадбавками,СуммаМаксСтавкиСНадбавками);
				
				ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();

				СуммаСтавок = 0;
				СуммаМинСтавки = 0;
				СуммаМаксСтавки = 0;
				СуммаМинСтавкиСНадбавками = 0;
				СуммаМаксСтавкиСНадбавками = 0;
				
			КонецЕсли; 
			
			СтрокаДоНадбавок.Параметры.ПодразделениеОрганизацииПредставление = Строка.ПодразделениеОрганизации;
			СтрокаДоНадбавок.Параметры.ПодразделениеОрганизацииКод = Строка.ПодразделениеОрганизации.Код;
			СтрокаДоНадбавок.Параметры.ДолжностьПредставление = Строка.Должность;
			
			КоличествоСтавок = ?(Строка.КоличествоСтрок = 0,0,Строка.КоличествоСтавок / Строка.КоличествоСтрок);
			
			СтрокаДоНадбавок.Параметры.КоличествоСтавок = КоличествоСтавок;
			
			МинСтавка = ?(Строка.КоличествоСтрок = 0,0,Строка.МинСтавка / Строка.КоличествоСтрок);
			МаксСтавка = ?(Строка.КоличествоСтрок = 0,0,Строка.МаксСтавка / Строка.КоличествоСтрок);
			
			СтрокаДоНадбавок.Параметры.ОкладТарифнаяСтавка	= ПолучитьСтрокуИзДвухЧисел(МинСтавка,МаксСтавка);
			ДокументРезультат.Вывести(СтрокаДоНадбавок);
			
			СуммаСтавок = СуммаСтавок + КоличествоСтавок;
			СуммаМинСтавки = СуммаМинСтавки + МинСтавка;
			СуммаМаксСтавки = СуммаМаксСтавки + МаксСтавка;
			
			РазмерНадбавок = 0;
			Для Счетчик=0 По КоличествоНадбавок-1 Цикл
				Если Не МассивНадбавок[Счетчик] = "" Тогда				
					СтрокаНадбавка.Параметры.РазмерНадбавки = Строка["Надбавка"+Строка(Счетчик)];
					РазмерНадбавок = РазмерНадбавок + СтрокаНадбавка.Параметры.РазмерНадбавки;
				Иначе
					СтрокаНадбавка.Параметры.РазмерНадбавки = ""; 
				КонецЕсли;	
				ДокументРезультат.Присоединить(СтрокаНадбавка);	
			КонецЦикла;
			
			МинСтавкаСНадбавкой = КоличествоСтавок * (МинСтавка + РазмерНадбавок);
			МаксСтавкаСНадбавкой = КоличествоСтавок * (МаксСтавка + РазмерНадбавок);
			
			СтрокаПослеНадбавок.Параметры.МесячныйФонд = ПолучитьСтрокуИзДвухЧисел(МинСтавкаСНадбавкой, МаксСтавкаСНадбавкой);
			ДокументРезультат.Присоединить(СтрокаПослеНадбавок);
			
			СуммаМинСтавкиСНадбавками = СуммаМинСтавкиСНадбавками + МинСтавкаСНадбавкой;
			СуммаМаксСтавкиСНадбавками = СуммаМаксСтавкиСНадбавками + МаксСтавкаСНадбавкой;
			
		КонецЦикла; 
		
		// подвал листа
		ВывестиПодвалЛиста(ДокументРезультат, Макет, КоличествоНадбавок, СуммаСтавок,СуммаМинСтавки,СуммаМаксСтавки,СуммаМинСтавкиСНадбавками,СуммаМаксСтавкиСНадбавками);
		
		// подвал отчета
		НомерВерхнейСтрокиПодвала = ДокументРезультат.ВысотаТаблицы+1;
		
		ПодвалДоНадбавок = Макет.ПолучитьОбласть("Подвал|ДоНадбавок");
		ПодвалНадбавка = Макет.ПолучитьОбласть("Подвал|Надбавка");
		ПодвалПослеНадбавок = Макет.ПолучитьОбласть("Подвал|ПослеНадбавок");
		
		ПодвалДоНадбавок.Параметры.КоличествоСтавок = ВСЕГОСуммаСтавок +  СуммаСтавок;
		ПодвалДоНадбавок.Параметры.ОкладТарифнаяСтавка = ПолучитьСтрокуИзДвухЧисел(ВСЕГОСуммаМинСтавки + СуммаМинСтавки,ВСЕГОСуммаМаксСтавки + СуммаМаксСтавки);
		
		ВыборкаДляПодвала = ЗапросДляПодвала.Выбрать();
		Если ВыборкаДляПодвала.НайтиСледующий(Перечисления.ОтветственныеЛицаОрганизаций.РуководительКадровойСлужбы, "ОтветственноеЛицо") Тогда
			ПодвалДоНадбавок.Параметры.ДолжностьРуководителя = ВыборкаДляПодвала.ДолжностьРуководителя;
		КонецЕсли;
		ДокументРезультат.Вывести(ПодвалДоНадбавок);
		
		Для Счетчик=0 По КоличествоНадбавок-1 Цикл                 
			ДокументРезультат.Присоединить(ПодвалНадбавка);	
		КонецЦикла;
		
		ШиринаДляПодписи = КоличествоНадбавок*3-1;
		НомерЛевойКолонкиДляПодписи = ПодвалДоНадбавок.ШиринаТаблицы+2;
		НомерПравойКолонкиДляПодписи = НомерЛевойКолонкиДляПодписи + ШиринаДляПодписи-1;
		ЛинияДляПодписи = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная,1);
		
		// Объединим ячейки для 2-ух подписей руководителей
		Для Счетчик=1 По 2 Цикл
			НомерСтрокиДляПодписи = НомерВерхнейСтрокиПодвала+Счетчик*2;
			ОбластьВерхнейСтрокиПодписи = ДокументРезультат.Область(НомерСтрокиДляПодписи,НомерЛевойКолонкиДляПодписи, НомерСтрокиДляПодписи,НомерПравойКолонкиДляПодписи);
			ОбластьВерхнейСтрокиПодписи.Объединить();
			ОбластьВерхнейСтрокиПодписи.ГраницаСнизу = ЛинияДляПодписи;
			ОбластьНижнейСтрокиПодписи = ДокументРезультат.Область(НомерСтрокиДляПодписи+1,НомерЛевойКолонкиДляПодписи, НомерСтрокиДляПодписи+1,НомерПравойКолонкиДляПодписи);
			ОбластьНижнейСтрокиПодписи.Объединить();
			ОбластьНижнейСтрокиПодписи.Текст = НСтр("ru='личная подпись';uk='особистий підпис'",КодЯзыкаПечать);
		КонецЦикла;
		
		ПодвалПослеНадбавок.Параметры.МесячныйФонд	= ПолучитьСтрокуИзДвухЧисел(ВСЕГОСуммаМинСтавкиСНадбавками + СуммаМинСтавкиСНадбавками, ВСЕГОСуммаМаксСтавкиСНадбавками + СуммаМаксСтавкиСНадбавками);
		
		ВыборкаДляПодвала = ЗапросДляПодвала.Выбрать();
		Если ВыборкаДляПодвала.НайтиСледующий(Перечисления.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер,"ОтветственноеЛицо") Тогда
			ПодвалПослеНадбавок.Параметры.ФИОРуководителяГБ = ВыборкаДляПодвала.ФИОРуководителя;
		КонецЕсли;
		
		ВыборкаДляПодвала = ЗапросДляПодвала.Выбрать();
		Если ВыборкаДляПодвала.НайтиСледующий(Перечисления.ОтветственныеЛицаОрганизаций.РуководительКадровойСлужбы,"ОтветственноеЛицо") Тогда
			ПодвалПослеНадбавок.Параметры.ФИОРуководителяКС = ВыборкаДляПодвала.ФИОРуководителя;
		КонецЕсли;
		
		ДокументРезультат.Присоединить(ПодвалПослеНадбавок);
	КонецЦикла;	
		
КонецПроцедуры

#КонецОбласти    

#Область СлужебныеПроцедурыИФункции

Процедура ВывестиПодвалЛиста(ДокументРезультат, Макет, КоличествоНадбавок, СуммаСтавок,СуммаМинСтавки, СуммаМаксСтавки, СуммаМинСтавкиСНадбавками, СуммаМаксСтавкиСНадбавками)

	ПодвалЛистаДоНадбавок = Макет.ПолучитьОбласть("ПодвалЛиста|ДоНадбавок");
	ПодвалЛистаНадбавка = Макет.ПолучитьОбласть("ПодвалЛиста|Надбавка");
	ПодвалЛистаПослеНадбавок = Макет.ПолучитьОбласть("ПодвалЛиста|ПослеНадбавок");
	
	ПодвалЛистаДоНадбавок.Параметры.КоличествоСтавок = СуммаСтавок;
	ПодвалЛистаДоНадбавок.Параметры.ОкладТарифнаяСтавка = ПолучитьСтрокуИзДвухЧисел(СуммаМинСтавки,СуммаМаксСтавки);	
	ДокументРезультат.Вывести(ПодвалЛистаДоНадбавок);
	
	Для Счетчик=0 По КоличествоНадбавок-1 Цикл                 
		ДокументРезультат.Присоединить(ПодвалЛистаНадбавка);	
	КонецЦикла;
	
	ПодвалЛистаПослеНадбавок.Параметры.МесячныйФонд	= ПолучитьСтрокуИзДвухЧисел(СуммаМинСтавкиСНадбавками, СуммаМаксСтавкиСНадбавками);
	ДокументРезультат.Присоединить(ПодвалЛистаПослеНадбавок);
	
КонецПроцедуры

Функция ПолучитьСтрокуИзДвухЧисел(Число1,Число2)
		
	Если Число1=Число2 Тогда
		Возврат ОбщегоНазначенияБПВызовСервера.ФорматСумм(Число1);
	Иначе
		Возврат ""+ОбщегоНазначенияБПВызовСервера.ФорматСумм(Число1) + " - " + ОбщегоНазначенияБПВызовСервера.ФорматСумм(Число2);
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецЕсли