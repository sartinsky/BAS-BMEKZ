
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Отчет.Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ВидОтчета = "" Тогда
		ВидОтчета = "ФормаЗХС28";	
	КонецЕсли; 
	
	ПерезаполнитьСписокВыбораТипаОтчета();
	
	СменитьМакетСКДНаСервере();	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийРеквизитов

&НаКлиенте
Процедура ВидОтчетаПриИзменении(Элемент)
	
   	ПерезаполнитьСписокВыбораТипаОтчета();
	
	СменитьМакетСКДНаСервере();
		
КонецПроцедуры

&НаКлиенте
Процедура ТипОтчетаПриИзменении(Элемент)
	
	СменитьМакетСКДНаСервере();
		
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПерезаполнитьСписокВыбораТипаОтчета()
	
	Элементы.ТипОтчета.СписокВыбора.Очистить();
	
	Если ВидОтчета = "ФормаЗХС28" Тогда
		Элементы.ТипОтчета.СписокВыбора.Добавить("ТипВвоз", 		НСтр("ru = 'Ввоз'; uk = 'Надходження'"));
		Элементы.ТипОтчета.СписокВыбора.Добавить("ТипВывоз", 		НСтр("ru = 'Вывоз'; uk = 'Вивіз'"));
		Элементы.ТипОтчета.СписокВыбора.Добавить("ТипВнутр",		НСтр("ru = 'Внутр. перемещение'; uk = 'Внутр. переміщення'"));
		Элементы.ТипОтчета.СписокВыбора.Добавить("ТипВзвешивание",  НСтр("ru = 'Взвешивание'; uk = 'Зважування'"));
		Элементы.ТипОтчета.СписокВыбора.Добавить("ТипВвозВывоз", 	НСтр("ru = 'Ввоз и вывоз'; uk = 'Надходження та вивіз'"));
		
	ИначеЕсли ВидОтчета = "ФормаЖД" Тогда
		Элементы.ТипОтчета.СписокВыбора.Добавить("ТипЖДВвоз", 		НСтр("ru = 'Ж/Д ввоз'; uk = 'З/Д надходження'"));
		Элементы.ТипОтчета.СписокВыбора.Добавить("ТипЖДВывоз", 		НСтр("ru = 'Ж/Д вывоз'; uk = 'З/Д вивіз'"));
		
	Иначе
		Элементы.ТипОтчета.СписокВыбора.Добавить("ТипВвоз", 		НСтр("ru = 'Ввоз'; uk = 'Надходження'"));
		Элементы.ТипОтчета.СписокВыбора.Добавить("ТипВывоз", 		НСтр("ru = 'Вывоз'; uk = 'Вивіз'"));
		Элементы.ТипОтчета.СписокВыбора.Добавить("ТипВнутр",		НСтр("ru = 'Внутр. перемещение'; uk = 'Внутр. переміщення'"));
		Элементы.ТипОтчета.СписокВыбора.Добавить("ТипВзвешивание",  НСтр("ru = 'Взвешивание'; uk = 'Зважування'"));
		Элементы.ТипОтчета.СписокВыбора.Добавить("ТипВвозВывоз", 	НСтр("ru = 'Ввоз и вывоз'; uk = 'Надходження та вивіз'"));
		Элементы.ТипОтчета.СписокВыбора.Добавить("ТипЖДВвоз", 		НСтр("ru = 'Ж/Д ввоз'; uk = 'З/Д надходження'"));
		Элементы.ТипОтчета.СписокВыбора.Добавить("ТипЖДВывоз", 		НСтр("ru = 'Ж/Д вывоз'; uk = 'З/Д вивіз'"));
	КонецЕсли; 
	
	Если ВидОтчетаИзНастроек = "" Тогда
		Отчет.ТипОтчета = Элементы.ТипОтчета.СписокВыбора[0].Значение;
	Иначе
		ВидОтчетаИзНастроек = "";
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура СменитьМакетСКДНаСервере()
	
	НеобходимаяСКД = Неопределено;
	
	ОбъектОтчет = РеквизитФормыВЗначение("Отчет");
	
	Если Отчет.ТипОтчета = "ТипВвоз" Тогда
		НеобходимаяСКД = ОбъектОтчет.ПолучитьМакет("СКД_Ввоз");	
		
	ИначеЕсли Отчет.ТипОтчета = "ТипВывоз" Тогда
		НеобходимаяСКД = ОбъектОтчет.ПолучитьМакет("СКД_Вывоз");
			
	ИначеЕсли Отчет.ТипОтчета = "ТипВнутр" Тогда
		НеобходимаяСКД = ОбъектОтчет.ПолучитьМакет("СКД_ВнутреннеПеремещение");
		
	ИначеЕсли Отчет.ТипОтчета = "ТипВзвешивание" Тогда
		НеобходимаяСКД = ОбъектОтчет.ПолучитьМакет("СКД_Взвешивание");
		
	ИначеЕсли Отчет.ТипОтчета = "ТипВвозВывоз" Тогда
		НеобходимаяСКД = ОбъектОтчет.ПолучитьМакет("СКД_ВвозВывоз");
		
	ИначеЕсли Отчет.ТипОтчета = "ТипЖДВвоз" Тогда
		НеобходимаяСКД = ОбъектОтчет.ПолучитьМакет("СКД_ВвозЖД");
		
	ИначеЕсли Отчет.ТипОтчета = "ТипЖДВывоз" Тогда
		НеобходимаяСКД = ОбъектОтчет.ПолучитьМакет("СКД_ВывозЖД");
	КонецЕсли; 
	
	ОбъектОтчет.СхемаКомпоновкиДанных = НеобходимаяСКД;
	ОбъектОтчет.КомпоновщикНастроек.ЗагрузитьНастройки(НеобходимаяСКД.НастройкиПоУмолчанию);
	ЗначениеВРеквизитФормы(ОбъектОтчет, "Отчет");
	
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииПользовательскихНастроекНаСервере(Настройки)
	
	Настройки.ДополнительныеСвойства.Вставить("Организация" , 	Отчет.Организация);
	Настройки.ДополнительныеСвойства.Вставить("Период" 		,	Отчет.Период);
	Настройки.ДополнительныеСвойства.Вставить("ТипОтчета"	,	Отчет.ТипОтчета);
	Настройки.ДополнительныеСвойства.Вставить("ВидОтчета"	,	ВидОтчета);

КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойПользовательскихНастроекНаСервере(Настройки)
	
	ПараметрыОтчета = Настройки.ДополнительныеСвойства;
	
	Если ПараметрыОтчета.Свойство("Организация") Тогда
		Отчет.Организация = ПараметрыОтчета.Организация;
	КонецЕсли; 
	
	Если ПараметрыОтчета.Свойство("Период") Тогда
		Отчет.Период = ПараметрыОтчета.Период;
	КонецЕсли; 
	
	Если ПараметрыОтчета.Свойство("ТипОтчета") Тогда
		Отчет.ТипОтчета = ПараметрыОтчета.ТипОтчета;
	КонецЕсли; 
	
	Если ПараметрыОтчета.Свойство("ВидОтчета") Тогда
		ВидОтчета = ПараметрыОтчета.ВидОтчета;
		ВидОтчетаИзНастроек = ПараметрыОтчета.ВидОтчета;
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти