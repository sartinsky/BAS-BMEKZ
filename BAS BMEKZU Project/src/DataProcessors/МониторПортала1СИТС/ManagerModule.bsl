
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Процедура ПолучитьДанныеМонитора(ПараметрыЗадания, АдресРезультата) Экспорт
	
	МониторПортала1СИТС.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru='Начало получения данных Монитора Портала ИТС';uk='Початок отримання даних Монітора Порталу ІТС'"));
	
	РезультатОперации = МониторПортала1СИТС.ДанныеМонитора();
	
	Если ПустаяСтрока(РезультатОперации.ИмяОшибки) Тогда
		ДополнительныеДанные = Неопределено;
		МониторПортала1СИТСПереопределяемый.ПриПолученииДополнительныхДанныхМонитора(
			ДополнительныеДанные,
			ПараметрыЗадания.ПараметрыПолученияДополнительныхДанных);
		РезультатОперации.Вставить("ДополнительныеДанные", ДополнительныеДанные);
	ИначеЕсли РезультатОперации.ИмяОшибки <> "НеЗаполненыДанныеАутентификации" Тогда
		МониторПортала1СИТС.ЗаписатьОшибкуВЖурналРегистрации(
			НСтр("ru='Не удалось получить данные Монитора Портала ИТС.';uk='Не вдалося отримати дані Монітора Порталу ІТС.'")
				+ Символы.ПС
				+ РезультатОперации.ИнформацияОбОшибке);
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(РезультатОперации, АдресРезультата);
	
	МониторПортала1СИТС.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru='Завершено получение данных Монитора Портала ИТС';uk='Завершено отримання даних Монітора Порталу ІТС'"));
	
КонецПроцедуры

Процедура ПолучитьДеталиДанныхМонитора(Параметры, АдресРезультата) Экспорт
	
	МониторПортала1СИТС.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru='Начало получения деталей данных Монитора Портала ИТС';uk='Початок отримання деталей даних Монітора Порталу ІТС'"));
	
	РезультатОперации = МониторПортала1СИТС.ДеталиДанныхМонитора(
		Параметры.Логин,
		Параметры.Договоры1СИТС,
		Параметры.АктивацияСервисовСопровождения,
		Параметры.ДоговорыНаСервисы);
	ПоместитьВоВременноеХранилище(РезультатОперации, АдресРезультата);
	
	Если Не ПустаяСтрока(РезультатОперации.ИмяОшибки) Тогда
		МониторПортала1СИТС.ЗаписатьОшибкуВЖурналРегистрации(
			НСтр("ru='Не удалось получить детали данных Монитора Портала ИТС.';uk='Не вдалося отримати деталі даних Монітора Порталу ІТС.'")
				+ Символы.ПС
				+ РезультатОперации.ИнформацияОбОшибке);
	КонецЕсли;
	
	МониторПортала1СИТС.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru='Завершено получение деталей данных Монитора Портала ИТС';uk='Завершено отримання деталей даних Монітора Порталу ІТС'"));
	
КонецПроцедуры

Процедура ПолучитьДеталиИнформацияОДоступномОбновлении(Параметры, АдресРезультата) Экспорт
	
	МониторПортала1СИТС.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru='Начало получения деталей информации о доступном обновлении';uk='Початок отримання деталей інформації про доступне оновлення'"));
	
	МодульПолучениеОбновленийПрограммы = ОбщегоНазначения.ОбщийМодуль("ПолучениеОбновленийПрограммы");
	ИнформацияОбОбновлении = МодульПолучениеОбновленийПрограммы.ИнформацияОДоступномОбновлении();
	ПоместитьВоВременноеХранилище(ИнформацияОбОбновлении, АдресРезультата);
	
	Если Не ПустаяСтрока(ИнформацияОбОбновлении.ИмяОшибки) Тогда
		МониторПортала1СИТС.ЗаписатьОшибкуВЖурналРегистрации(
			НСтр("ru='Не удалось получить детали информации о доступном обновлении.';uk='Не вдалося отримати деталі інформації про доступне оновлення.'")
				+ Символы.ПС
				+ ИнформацияОбОбновлении.ИнформацияОбОшибке);
	КонецЕсли;
	
	МониторПортала1СИТС.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru='Завершено получение деталей информации о доступном обновлении';uk='Завершено отримання деталей інформації про доступне оновлення'"));
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли