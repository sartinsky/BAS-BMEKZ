
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОповещениеПослеПеререгистрации = Параметры.ОповещениеПослеПеререгистрации;
	
	РеквизитыОрганизации = РеквизитыОрганизации();
	КодПоЕДРПОУОрганизацииДоАктивации = РеквизитыОрганизации.КодПоЕДРПОУ;
	
	Если ЗначениеЗаполнено(Параметры.ОшибкаПроверкиАктивации) Тогда
		ПодготовитьФормуНаСервере(Параметры.ОшибкаПроверкиАктивации);
	Иначе
		ОбновитьДанныеРегистрации();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьПовторнуюАктивацию(Команда)
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("Подключаемый_ВыполненоОбновлениеЛицензии", ЭтотОбъект);
	
	ПараметрыФормы = Новый Структура();
	ОткрытьФорму("Обработка.НачалоРаботы.Форма.АктивацияПрограммы", ПараметрыФормы, , , , , 
		ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Подключаемый_ВыполненоОбновлениеЛицензии(Результат, ДополнительныеПараметры) Экспорт
	
	ОбновитьДанныеРегистрации();
	
	Если ЗначениеЗаполнено(ОповещениеПослеПеререгистрации)
		И ЭтотОбъект.ВладелецФормы <> Неопределено Тогда
		ДополнительныеПараметры = Новый Структура;
		Оповещение = Новый ОписаниеОповещения(ОповещениеПослеПеререгистрации, ЭтотОбъект.ВладелецФормы, ДополнительныеПараметры);
		ВыполнитьОбработкуОповещения(Оповещение, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеРегистрации()
	
	РезультатПроверки = НачалоРаботы.ПроверитьЛегальностьИспользования();
	ПодготовитьФормуНаСервере(РезультатПроверки.Ошибка);
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере(ОшибкаПроверкиАктивации)
	
	РеквизитыОрганизации = РеквизитыОрганизации();
	КодПоЕДРПОУОрганизацииПослеАктивации = РеквизитыОрганизации.КодПоЕДРПОУ;
	
	// Получаем сведения о лицензии
	СведенияОРегистрации = НачалоРаботы.СведенияОРегистрации();
	
	ОписаниеРегистрации = НачалоРаботы.РегистрационныеДанныеФорматированнойСтрокой(СведенияОРегистрации);
	
	Подстроки = Новый Массив;
	
	ОсновнойШрифт = Элементы.ТекстСообщения.Шрифт;
	ПолужирныйШрифт = Новый Шрифт(ОсновнойШрифт, , , Истина);
	
	Если ЗначениеЗаполнено(ОшибкаПроверкиАктивации) Тогда
		Если ОшибкаПроверкиАктивации = "НесколькоОрганизаций" Тогда
			Подстроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='В информационной базе ведется учет нескольких организаций.';uk='В інформаційній базі ведеться облік декількох організацій.'"), ОсновнойШрифт));
			Подстроки.Добавить(Символы.ПС);
			Подстроки.Добавить(Символы.ПС);
			Подстроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='Программа предназначена для ведения учета только одной организации.';uk='Програма призначена для обліку тільки однієї організації.'"), ОсновнойШрифт));
			
			Элементы.ВыполнитьПовторнуюАктивацию.Доступность = Ложь;
		ИначеЕсли ОшибкаПроверкиАктивации = "НетРегистрационныхДанных" Тогда
			Подстроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='Программа не активирована.';uk='Програму не активовано.'"), ОсновнойШрифт));
			Подстроки.Добавить(Символы.ПС);
			Подстроки.Добавить(Символы.ПС);
			Подстроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='Выполните активацию программы.';uk='Виконайте активацію програми.'"), ОсновнойШрифт));
		ИначеЕсли ОшибкаПроверкиАктивации = "НевернаяПодпись" Тогда
			Подстроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='В информационной базе указаны некорректные регистрационные данные.';uk='В інформаційній базі вказано некоректні реєстраційні дані.'"), ОсновнойШрифт));
			Подстроки.Добавить(Символы.ПС);
			Подстроки.Добавить(Символы.ПС);
			Подстроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='Выполните активацию программы.';uk='Виконайте активацію програми.'"), ОсновнойШрифт));
		ИначеЕсли ОшибкаПроверкиАктивации = "РегистрацияДругойОрганизации" Тогда
			Подстроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='Программа активирована для: ';uk='Програму активовано для:'"), ОсновнойШрифт));
			Подстроки.Добавить(Символы.ПС);
			Подстроки.Добавить(ОписаниеРегистрации);
			Подстроки.Добавить(Символы.ПС);
			
			РеквизитыОрганизации = РеквизитыОрганизации();
			
			Подстроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='В информационной базе ведется учет';uk='В інформаційній базі ведеться облік'"), ОсновнойШрифт));
			Подстроки.Добавить(" ");
			Подстроки.Добавить(Новый ФорматированнаяСтрока(РеквизитыОрганизации.Наименование, ПолужирныйШрифт));
			Подстроки.Добавить(" ");
			Подстроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='(Код ЕДРПОУ';uk='(Код ЄДРПОУ'"), ОсновнойШрифт));
			Подстроки.Добавить(" ");
			Подстроки.Добавить(Новый ФорматированнаяСтрока(РеквизитыОрганизации.КодПоЕДРПОУ, ПолужирныйШрифт));
			Подстроки.Добавить(" ");
			Подстроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru=').';uk=').'"), ОсновнойШрифт));
			Подстроки.Добавить(Символы.ПС);
			Подстроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='Необходимо выполнить активацию программы для этой организации.';uk='Необхідно активувати програму для цієї організації.'"), ОсновнойШрифт));
			
		Иначе
			Подстроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='Неизвестная ошибка активации:';uk='Невідома помилка активації:'"), ОсновнойШрифт));
			Подстроки.Добавить(" ");
			Подстроки.Добавить(Новый ФорматированнаяСтрока(ОшибкаПроверкиАктивации , ПолужирныйШрифт));
			Подстроки.Добавить(Символы.ПС);
			Подстроки.Добавить(Символы.ПС);
			Подстроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='Выполните активацию программы.';uk='Виконайте активацію програми.'"), ОсновнойШрифт));
		КонецЕсли;
		
		ЭтотОбъект.Заголовок = НСтр("ru='Ошибка активации программы';uk='Помилка активації програми'");
		Элементы.ГруппаСообщение.ЦветФона = ЦветаСтиля.ЦветФонаПредупреждения;
	Иначе
		Подстроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='Программа активирована для: ';uk='Програму активовано для:'"), ОсновнойШрифт));
		Подстроки.Добавить(Символы.ПС);
		Подстроки.Добавить(ОписаниеРегистрации);
		Подстроки.Добавить(Символы.ПС);
		Подстроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='Повторная активация не требуется.';uk='Повторна активація не потрібна.'"), ОсновнойШрифт));
		
		Элементы.ГруппаСообщение.ЦветФона = ЦветаСтиля.ДобавленныйРеквизитФон;
		ЭтотОбъект.Заголовок = НСтр("ru='Сведения об активации программы';uk='Відомості про активацію програми'");
	КонецЕсли;
	
	Элементы.ТекстСообщения.Заголовок = Новый ФорматированнаяСтрока(Подстроки);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РеквизитыОрганизации()
	
	РеквизитыОрганизации = Новый Структура("КодПоЕДРПОУ, Наименование", "", "");
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Организации.КодПоЕДРПОУ КАК КодПоЕДРПОУ,
	|	Организации.Наименование КАК Наименование
	|ИЗ
	|	Справочник.Организации КАК Организации";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(РеквизитыОрганизации, Выборка);
	КонецЕсли;
	
	Возврат РеквизитыОрганизации;
	
КонецФункции

#КонецОбласти

