#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ЗаполнитьШапку(Документ, НалоговаяНакладная = Неопределено, ДатаДокумента, Настройки)

	Документ.Дата 		 = КонецДня(ДатаДокумента)-2;
	Документ.Организация = Настройки.Организация;
	
	Если ТипЗнч(Документ) = Тип("ДокументОбъект.НалоговаяНакладная") Тогда
		Документ.ОбособленноеПодразделение = Настройки.ОбособленноеПодразделение;
	КонецЕсли;
	
	Документ.ВалютаДокумента = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	Документ.СуммаВключаетНДС 	= Истина;
	Документ.АвторасчетНДС 		= Ложь;
	
	Документ.Ответственный 				  = Пользователи.ТекущийПользователь();
	Документ.КтоВыписалНалоговуюНакладную = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("КтоВыписалНалоговуюНакладную");	
	
	Документ.СчетНДС 		= ПланыСчетов.Хозрасчетный.РасчетыПоНДС;
	Документ.СчетУчетаНДС 	= ПланыСчетов.Хозрасчетный.НалоговыеОбязательстваРозница;
	
	Документы.НалоговаяНакладная.УстановитьТипПричиныНевыдачиПокупателюПоУмолчанию(Документ);
	
	Если НЕ НалоговаяНакладная = Неопределено Тогда
	    // Свяжем Налоговую накладную и Приложение 2
		Документ.НалоговаяНакладная = НалоговаяНакладная.Ссылка;
	КонецЕсли;
	
	Документ.ФормаРасчетов = Настройки.ФормаРасчетов;
	
КонецПроцедуры

Процедура ОбработатьДокумент(Документ)

	Попытка
		Документ.Записать(РежимЗаписиДокумента.Проведение);
	Исключение
		Документ.Записать();
	КонецПопытки;

КонецПроцедуры

Функция РаспределитьНоменклатурныйСоставДеньВДень(Настройки) Экспорт
	
	//Объект "Запрос", который будет хранить параметры запроса и менеджер временных таблиц
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ПолучитьВременныеТаблицыИДанныеДляПроверки(Настройки.ДатаНачала, Настройки.ДатаОкончания, Настройки.Организация, Запрос);
	
	//ПараметрыПоУмолчанию - это структура, которая содержит номенклатуру заполнения по умолчанию для налоговых накладных,
	//ее базовую единицу измерения и код УКТВЭД. Необходимо на случай, когда не введенны все документы "Отчет о розничных продажах",
	//в которых указывается номенклатурный состав
	ПараметрыПоУмолчанию = ПолучитьПараметрыПоУмолчанию();
	
	ТаблицаСозданныхНалоговыхНакладных = Новый ТаблицаЗначений;
	ТаблицаСозданныхНалоговыхНакладных.Колонки.Добавить("Дата");
	ТаблицаСозданныхНалоговыхНакладных.Колонки.Добавить("Документ");
	ТаблицаСозданныхНалоговыхНакладных.Колонки.Добавить("ЭтоНалоговаяНакладная");
	ТаблицаСозданныхНалоговыхНакладных.Колонки.Добавить("ОблагаемаяДеятельность");
	
	//Необходимые данные для заполнения статьи декларации в строке ТЧ документов НН и П2, последних три параметры одинаковые для всех документов,
	//только вид операции может изменяться его будем определять непосредственно при создании конкретного налогового документа 
	ДанныеОбъекта = Новый Структура("ВидОперации, ДоговорКонтрагента, ВалютаВзаиморасчетов, НеЯвляетсяРезидентом, Дата, ТипПричиныНевыдачиПокупателю");
	ДанныеОбъекта.ДоговорКонтрагента   = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
	ДанныеОбъекта.ВалютаВзаиморасчетов = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	ДанныеОбъекта.НеЯвляетсяРезидентом = Ложь;
	
	//РАБОТАЕМ С НАЛОГОВЫМИ НАКЛАДНЫМИ
	МассивРезультатовЗапроса = ПолучитьДанныеДляДокументовНалоговаяНакладная(Запрос);		
	ОбработатьСозданияНалоговыхДокументов(МассивРезультатовЗапроса, Истина, ПараметрыПоУмолчанию, ТаблицаСозданныхНалоговыхНакладных, Настройки, ДанныеОбъекта);
	
	//РАБОТАЕМ С ПРИЛОЖЕНИЕМ 2 К НАЛОГОВОЙ НАКЛАДНОЙ
	МассивРезультатовЗапроса = ПолучитьДанныеДляДокументовП2(Запрос);	
	ОбработатьСозданияНалоговыхДокументов(МассивРезультатовЗапроса, Ложь, ПараметрыПоУмолчанию, ТаблицаСозданныхНалоговыхНакладных, Настройки, ДанныеОбъекта);
	
	ТаблицаСозданныхНалоговыхНакладных.Сортировать("Дата");
	
	Возврат ТаблицаСозданныхНалоговыхНакладных.ВыгрузитьКолонку("Документ");
	
КонецФункции

функция ПолучитьВременныеТаблицыИДанныеДляПроверки(ДатаНачала, ДатаОкончания, Организация, Запрос, Проверка = Ложь) Экспорт
	
	ТекстДанныеРегистра = 
	"ВЫБРАТЬ
	|	КОНЕЦПЕРИОДА(ОжидаемыйИПодтвержденныйНДСПродаж.Период, ДЕНЬ) КАК Дата,
	|	ОжидаемыйИПодтвержденныйНДСПродаж.СтавкаНДС КАК СтавкаНДС,
	|	ОжидаемыйИПодтвержденныйНДСПродаж.СобытиеНДС,
	|	ОжидаемыйИПодтвержденныйНДСПродаж.БазаНДС КАК БазаНДС,
	|	ОжидаемыйИПодтвержденныйНДСПродаж.СуммаНДС КАК СуммаНДС,
	|	ОжидаемыйИПодтвержденныйНДСПродаж.ВидДвижения,
	|	ОжидаемыйИПодтвержденныйНДСПродаж.Регистратор КАК Документ,
	|	ОжидаемыйИПодтвержденныйНДСПродаж.Регистратор.ВключенаВЕдиныйРеестрНалоговыхНакладных КАК ВключенаВЕдиныйРеестрНалоговыхНакладных
	|ПОМЕСТИТЬ ДанныеРегистра
	|ИЗ
	|	РегистрНакопления.ОжидаемыйИПодтвержденныйНДСПродаж КАК ОжидаемыйИПодтвержденныйНДСПродаж
	|ГДЕ
	|	ОжидаемыйИПодтвержденныйНДСПродаж.Период >= &ДатаНачала
	|	И ОжидаемыйИПодтвержденныйНДСПродаж.Период <= &ДатаОкончания
	|	И ОжидаемыйИПодтвержденныйНДСПродаж.Организация = &Организация
	|	И НЕ ОжидаемыйИПодтвержденныйНДСПродаж.Регистратор.ВидОперации В (&МассивВидовОперацийРозницаКонтрагенту, &МассивВидовОперацийРозницаКонтрагентуВозврат)
	|	И ОжидаемыйИПодтвержденныйНДСПродаж.СобытиеНДС В (&РеализацияРозница, &ВозвратРозница)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	ТекстНомСоставИСоставКонтрагентуВРозницу = 
	"ВЫБРАТЬ
	|	КОНЕЦПЕРИОДА(ОтчетОРозничныхПродажахТовары.Ссылка.Дата, ДЕНЬ) КАК Дата,
	|	ОтчетОРозничныхПродажахТовары.СтавкаНДС КАК СтавкаНДС,
	|	ВЫБОР
	|		КОГДА ОтчетОРозничныхПродажахТовары.Количество < 0
	|			ТОГДА &ВозвратРозница
	|		ИНАЧЕ &РеализацияРозница
	|	КОНЕЦ КАК СобытиеНДС,
	|	ОтчетОРозничныхПродажахТовары.Номенклатура,
	|	ОтчетОРозничныхПродажахТовары.Номенклатура.Услуга КАК Услуга,
	|	СУММА(ВЫБОР
	|			КОГДА ОтчетОРозничныхПродажахТовары.Количество < 0
	|				ТОГДА -ОтчетОРозничныхПродажахТовары.Количество
	|			ИНАЧЕ ОтчетОРозничныхПродажахТовары.Количество
	|		КОНЕЦ) КАК Количество,
	|	СУММА(ВЫБОР
	|			КОГДА ОтчетОРозничныхПродажахТовары.СуммаНДС < 0
	|				ТОГДА -ОтчетОРозничныхПродажахТовары.СуммаНДС
	|			ИНАЧЕ ОтчетОРозничныхПродажахТовары.СуммаНДС
	|		КОНЕЦ) КАК СуммаНДС,
	|	ОтчетОРозничныхПродажахТовары.ЕдиницаИзмерения,
	|	ОтчетОРозничныхПродажахТовары.Коэффициент,
	|	СУММА(ВЫБОР
	|			КОГДА ВЫБОР
	|					КОГДА ОтчетОРозничныхПродажахТовары.Ссылка.СуммаВключаетНДС
	|						ТОГДА ОтчетОРозничныхПродажахТовары.Сумма - ОтчетОРозничныхПродажахТовары.СуммаАкцизногоНалога
	|					ИНАЧЕ ОтчетОРозничныхПродажахТовары.Сумма + ОтчетОРозничныхПродажахТовары.СуммаНДС
	|				КОНЕЦ < 0
	|				ТОГДА -ВЫБОР
	|						КОГДА ОтчетОРозничныхПродажахТовары.Ссылка.СуммаВключаетНДС
	|							ТОГДА ОтчетОРозничныхПродажахТовары.Сумма - ОтчетОРозничныхПродажахТовары.СуммаАкцизногоНалога
	|						ИНАЧЕ ОтчетОРозничныхПродажахТовары.Сумма + ОтчетОРозничныхПродажахТовары.СуммаНДС
	|					КОНЕЦ
	|			ИНАЧЕ ВЫБОР
	|					КОГДА ОтчетОРозничныхПродажахТовары.Ссылка.СуммаВключаетНДС
	|						ТОГДА ОтчетОРозничныхПродажахТовары.Сумма - ОтчетОРозничныхПродажахТовары.СуммаАкцизногоНалога
	|					ИНАЧЕ ОтчетОРозничныхПродажахТовары.Сумма + ОтчетОРозничныхПродажахТовары.СуммаНДС
	|				КОНЕЦ
	|		КОНЕЦ) КАК СуммаСНДС,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ОтчетОРозничныхПродажахТовары.Ссылка.СуммаВключаетНДС
	|				ТОГДА ОтчетОРозничныхПродажахТовары.Сумма - ОтчетОРозничныхПродажахТовары.СуммаАкцизногоНалога
	|			ИНАЧЕ ОтчетОРозничныхПродажахТовары.Сумма + ОтчетОРозничныхПродажахТовары.СуммаНДС
	|		КОНЕЦ / ОтчетОРозничныхПродажахТовары.Количество КАК ЧИСЛО(15, 2)) КАК РасчетнаяЦенаСНДС,
	|	ВЫБОР
	|		КОГДА ОтчетОРозничныхПродажахТовары.Номенклатура.НоменклатураГТД = НЕОПРЕДЕЛЕНО
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КлассификаторУКТВЭД.ПустаяСсылка)
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ТИПЗНАЧЕНИЯ(ОтчетОРозничныхПродажахТовары.Номенклатура.НоменклатураГТД) = ТИП(Справочник.НоменклатураГТД)
	|					ТОГДА ЕСТЬNULL(ОтчетОРозничныхПродажахТовары.Номенклатура.НоменклатураГТД.КодУКТВЭД, ЗНАЧЕНИЕ(Справочник.КлассификаторУКТВЭД.ПустаяСсылка))
	|				ИНАЧЕ ЕСТЬNULL(ОтчетОРозничныхПродажахТовары.Номенклатура.НоменклатураГТД, ЗНАЧЕНИЕ(Справочник.КлассификаторУКТВЭД.ПустаяСсылка))
	|			КОНЕЦ
	|	КОНЕЦ КАК КодУКТВЭД,
	|	ВЫБОР
	|		КОГДА ОтчетОРозничныхПродажахТовары.Количество < 0
	|			ТОГДА ВЫБОР
	|					КОГДА ОтчетОРозничныхПродажахТовары.ДокументПродажи = ЗНАЧЕНИЕ(Документ.ОтчетОРозничныхПродажах.ПустаяСсылка)
	|						ТОГДА ОтчетОРозничныхПродажахТовары.Ссылка
	|					ИНАЧЕ ОтчетОРозничныхПродажахТовары.ДокументПродажи
	|				КОНЕЦ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ОтчетОРозничныхПродажах.ПустаяСсылка)
	|	КОНЕЦ КАК ДокументПродажи,
	|	ВЫБОР
	|		КОГДА ОтчетОРозничныхПродажахТовары.Количество < 0
	|			ТОГДА ВЫБОР
	|					КОГДА ОтчетОРозничныхПродажахТовары.ДокументПродажи = ЗНАЧЕНИЕ(Документ.ОтчетОРозничныхПродажах.ПустаяСсылка)
	|						ТОГДА ОтчетОРозничныхПродажахТовары.Ссылка.Дата
	|					ИНАЧЕ ОтчетОРозничныхПродажахТовары.ДокументПродажи.Дата
	|				КОНЕЦ
	|		ИНАЧЕ ОтчетОРозничныхПродажахТовары.Ссылка.Дата
	|	КОНЕЦ КАК ДатаДокументаПродажи
	|ПОМЕСТИТЬ НоменклатурныйСостав
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах.Товары КАК ОтчетОРозничныхПродажахТовары
	|ГДЕ
	|	ОтчетОРозничныхПродажахТовары.Ссылка.Дата >= &ДатаНачала
	|	И ОтчетОРозничныхПродажахТовары.Ссылка.Дата <= &ДатаОкончания
	|	И ОтчетОРозничныхПродажахТовары.Ссылка.Организация = &Организация
	|	И ОтчетОРозничныхПродажахТовары.Ссылка.Проведен
	|
	|СГРУППИРОВАТЬ ПО
	|	КОНЕЦПЕРИОДА(ОтчетОРозничныхПродажахТовары.Ссылка.Дата, ДЕНЬ),
	|	ОтчетОРозничныхПродажахТовары.СтавкаНДС,
	|	ВЫБОР
	|		КОГДА ОтчетОРозничныхПродажахТовары.Количество < 0
	|			ТОГДА &ВозвратРозница
	|		ИНАЧЕ &РеализацияРозница
	|	КОНЕЦ,
	|	ОтчетОРозничныхПродажахТовары.Номенклатура,
	|	ОтчетОРозничныхПродажахТовары.Номенклатура.Услуга,
	|	ОтчетОРозничныхПродажахТовары.ЕдиницаИзмерения,
	|	ОтчетОРозничныхПродажахТовары.Коэффициент,
	|	ВЫБОР
	|		КОГДА ОтчетОРозничныхПродажахТовары.Номенклатура.НоменклатураГТД = НЕОПРЕДЕЛЕНО
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КлассификаторУКТВЭД.ПустаяСсылка)
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ТИПЗНАЧЕНИЯ(ОтчетОРозничныхПродажахТовары.Номенклатура.НоменклатураГТД) = ТИП(Справочник.НоменклатураГТД)
	|					ТОГДА ЕСТЬNULL(ОтчетОРозничныхПродажахТовары.Номенклатура.НоменклатураГТД.КодУКТВЭД, ЗНАЧЕНИЕ(Справочник.КлассификаторУКТВЭД.ПустаяСсылка))
	|				ИНАЧЕ ЕСТЬNULL(ОтчетОРозничныхПродажахТовары.Номенклатура.НоменклатураГТД, ЗНАЧЕНИЕ(Справочник.КлассификаторУКТВЭД.ПустаяСсылка))
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ОтчетОРозничныхПродажахТовары.Количество < 0
	|			ТОГДА ВЫБОР
	|					КОГДА ОтчетОРозничныхПродажахТовары.ДокументПродажи = ЗНАЧЕНИЕ(Документ.ОтчетОРозничныхПродажах.ПустаяСсылка)
	|						ТОГДА ОтчетОРозничныхПродажахТовары.Ссылка
	|					ИНАЧЕ ОтчетОРозничныхПродажахТовары.ДокументПродажи
	|				КОНЕЦ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ОтчетОРозничныхПродажах.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ОтчетОРозничныхПродажахТовары.Количество < 0
	|			ТОГДА ВЫБОР
	|					КОГДА ОтчетОРозничныхПродажахТовары.ДокументПродажи = ЗНАЧЕНИЕ(Документ.ОтчетОРозничныхПродажах.ПустаяСсылка)
	|						ТОГДА ОтчетОРозничныхПродажахТовары.Ссылка.Дата
	|					ИНАЧЕ ОтчетОРозничныхПродажахТовары.ДокументПродажи.Дата
	|				КОНЕЦ
	|		ИНАЧЕ ОтчетОРозничныхПродажахТовары.Ссылка.Дата
	|	КОНЕЦ,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ОтчетОРозничныхПродажахТовары.Ссылка.СуммаВключаетНДС
	|				ТОГДА ОтчетОРозничныхПродажахТовары.Сумма - ОтчетОРозничныхПродажахТовары.СуммаАкцизногоНалога
	|			ИНАЧЕ ОтчетОРозничныхПродажахТовары.Сумма + ОтчетОРозничныхПродажахТовары.СуммаНДС
	|		КОНЕЦ / ОтчетОРозничныхПродажахТовары.Количество КАК ЧИСЛО(15, 2))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОНЕЦПЕРИОДА(НалоговаяНакладнаяТовары.Ссылка.Дата, ДЕНЬ) КАК Дата,
	|	НалоговаяНакладнаяТовары.Номенклатура,
	|	НалоговаяНакладнаяТовары.ЕдиницаИзмерения,
	|	НалоговаяНакладнаяТовары.Коэффициент,
	|	СУММА(НалоговаяНакладнаяТовары.Количество) КАК Количество,
	|	НалоговаяНакладнаяТовары.СтавкаНДС,
	|	СУММА(НалоговаяНакладнаяТовары.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ВЫБОР
	|			КОГДА НалоговаяНакладнаяТовары.Ссылка.СуммаВключаетНДС
	|				ТОГДА НалоговаяНакладнаяТовары.Сумма
	|			ИНАЧЕ НалоговаяНакладнаяТовары.Сумма + НалоговаяНакладнаяТовары.СуммаНДС
	|		КОНЕЦ) КАК СуммаСНДС,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА НалоговаяНакладнаяТовары.Ссылка.СуммаВключаетНДС
	|				ТОГДА НалоговаяНакладнаяТовары.Сумма
	|			ИНАЧЕ НалоговаяНакладнаяТовары.Сумма + НалоговаяНакладнаяТовары.СуммаНДС
	|		КОНЕЦ / НалоговаяНакладнаяТовары.Количество КАК ЧИСЛО(15, 2)) КАК РасчетнаяЦенаСНДС,
	|	НалоговаяНакладнаяТовары.Ссылка.ВидОперации КАК ВидОперации,
	|	НалоговаяНакладнаяТовары.Номенклатура.Услуга КАК Услуга
	|ПОМЕСТИТЬ НалоговыеДокументыВРозницуКонтрагенту
	|ИЗ
	|	Документ.НалоговаяНакладная.Товары КАК НалоговаяНакладнаяТовары
	|ГДЕ
	|	НалоговаяНакладнаяТовары.Ссылка.Дата >= &ДатаНачала
	|	И НалоговаяНакладнаяТовары.Ссылка.Дата <= &ДатаОкончания
	|	И НалоговаяНакладнаяТовары.Ссылка.Организация = &Организация
	|	И НалоговаяНакладнаяТовары.Ссылка.Проведен
	|	И НалоговаяНакладнаяТовары.Ссылка.ВидОперации В(&МассивВидовОперацийРозницаКонтрагенту)
	|
	|СГРУППИРОВАТЬ ПО
	|	НалоговаяНакладнаяТовары.Ссылка.ВидОперации,
	|	НалоговаяНакладнаяТовары.Номенклатура,
	|	НалоговаяНакладнаяТовары.ЕдиницаИзмерения,
	|	НалоговаяНакладнаяТовары.СтавкаНДС,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА НалоговаяНакладнаяТовары.Ссылка.СуммаВключаетНДС
	|				ТОГДА НалоговаяНакладнаяТовары.Сумма
	|			ИНАЧЕ НалоговаяНакладнаяТовары.Сумма + НалоговаяНакладнаяТовары.СуммаНДС
	|		КОНЕЦ / НалоговаяНакладнаяТовары.Количество КАК ЧИСЛО(15, 2)),
	|	НалоговаяНакладнаяТовары.Коэффициент,
	|	НалоговаяНакладнаяТовары.Номенклатура.Услуга,
	|	КОНЕЦПЕРИОДА(НалоговаяНакладнаяТовары.Ссылка.Дата, ДЕНЬ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КОНЕЦПЕРИОДА(НалоговаяНакладнаяУслуги.Ссылка.Дата, ДЕНЬ),
	|	НалоговаяНакладнаяУслуги.Номенклатура,
	|	НалоговаяНакладнаяУслуги.Номенклатура.БазоваяЕдиницаИзмерения,
	|	1,
	|	СУММА(НалоговаяНакладнаяУслуги.Количество),
	|	НалоговаяНакладнаяУслуги.СтавкаНДС,
	|	СУММА(НалоговаяНакладнаяУслуги.СуммаНДС),
	|	СУММА(ВЫБОР
	|			КОГДА НалоговаяНакладнаяУслуги.Ссылка.СуммаВключаетНДС
	|				ТОГДА НалоговаяНакладнаяУслуги.Сумма
	|			ИНАЧЕ НалоговаяНакладнаяУслуги.Сумма + НалоговаяНакладнаяУслуги.СуммаНДС
	|		КОНЕЦ),
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА НалоговаяНакладнаяУслуги.Ссылка.СуммаВключаетНДС
	|				ТОГДА НалоговаяНакладнаяУслуги.Сумма
	|			ИНАЧЕ НалоговаяНакладнаяУслуги.Сумма + НалоговаяНакладнаяУслуги.СуммаНДС
	|		КОНЕЦ / НалоговаяНакладнаяУслуги.Количество КАК ЧИСЛО(15, 2)),
	|	НалоговаяНакладнаяУслуги.Ссылка.ВидОперации,
	|	НалоговаяНакладнаяУслуги.Номенклатура.Услуга
	|ИЗ
	|	Документ.НалоговаяНакладная.Услуги КАК НалоговаяНакладнаяУслуги
	|ГДЕ
	|	НалоговаяНакладнаяУслуги.Ссылка.Дата >= &ДатаНачала
	|	И НалоговаяНакладнаяУслуги.Ссылка.Дата <= &ДатаОкончания
	|	И НалоговаяНакладнаяУслуги.Ссылка.Организация = &Организация
	|	И НалоговаяНакладнаяУслуги.Ссылка.Проведен
	|	И НалоговаяНакладнаяУслуги.Ссылка.ВидОперации В(&МассивВидовОперацийРозницаКонтрагенту)
	|
	|СГРУППИРОВАТЬ ПО
	|	НалоговаяНакладнаяУслуги.Ссылка.ВидОперации,
	|	НалоговаяНакладнаяУслуги.Номенклатура,
	|	НалоговаяНакладнаяУслуги.СтавкаНДС,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА НалоговаяНакладнаяУслуги.Ссылка.СуммаВключаетНДС
	|				ТОГДА НалоговаяНакладнаяУслуги.Сумма
	|			ИНАЧЕ НалоговаяНакладнаяУслуги.Сумма + НалоговаяНакладнаяУслуги.СуммаНДС
	|		КОНЕЦ / НалоговаяНакладнаяУслуги.Количество КАК ЧИСЛО(15, 2)),
	|	НалоговаяНакладнаяУслуги.Номенклатура.Услуга,
	|	КОНЕЦПЕРИОДА(НалоговаяНакладнаяУслуги.Ссылка.Дата, ДЕНЬ),
	|	НалоговаяНакладнаяУслуги.Номенклатура.БазоваяЕдиницаИзмерения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КОНЕЦПЕРИОДА(Приложение2КНалоговойНакладнойТовары.Ссылка.Дата, ДЕНЬ),
	|	Приложение2КНалоговойНакладнойТовары.Номенклатура,
	|	Приложение2КНалоговойНакладнойТовары.ЕдиницаИзмерения,
	|	Приложение2КНалоговойНакладнойТовары.Коэффициент,
	|	СУММА(-Приложение2КНалоговойНакладнойТовары.ИзменениеКоличества),
	|	Приложение2КНалоговойНакладнойТовары.СтавкаНДС,
	|	СУММА(ВЫБОР
	|			КОГДА Приложение2КНалоговойНакладнойТовары.ИзменениеСуммыНДС < 0
	|				ТОГДА -Приложение2КНалоговойНакладнойТовары.ИзменениеСуммыНДС
	|			ИНАЧЕ Приложение2КНалоговойНакладнойТовары.ИзменениеСуммыНДС
	|		КОНЕЦ),
	|	СУММА(ВЫБОР
	|			КОГДА ВЫБОР
	|					КОГДА Приложение2КНалоговойНакладнойТовары.Ссылка.СуммаВключаетНДС
	|						ТОГДА Приложение2КНалоговойНакладнойТовары.ИзменениеСуммы
	|					ИНАЧЕ Приложение2КНалоговойНакладнойТовары.ИзменениеСуммы + Приложение2КНалоговойНакладнойТовары.ИзменениеСуммыНДС
	|				КОНЕЦ < 0
	|				ТОГДА -ВЫБОР
	|						КОГДА Приложение2КНалоговойНакладнойТовары.Ссылка.СуммаВключаетНДС
	|							ТОГДА Приложение2КНалоговойНакладнойТовары.ИзменениеСуммы
	|						ИНАЧЕ Приложение2КНалоговойНакладнойТовары.ИзменениеСуммы + Приложение2КНалоговойНакладнойТовары.ИзменениеСуммыНДС
	|					КОНЕЦ
	|			ИНАЧЕ ВЫБОР
	|					КОГДА Приложение2КНалоговойНакладнойТовары.Ссылка.СуммаВключаетНДС
	|						ТОГДА Приложение2КНалоговойНакладнойТовары.ИзменениеСуммы
	|					ИНАЧЕ Приложение2КНалоговойНакладнойТовары.ИзменениеСуммы + Приложение2КНалоговойНакладнойТовары.ИзменениеСуммыНДС
	|				КОНЕЦ
	|		КОНЕЦ),
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА Приложение2КНалоговойНакладнойТовары.Ссылка.СуммаВключаетНДС
	|				ТОГДА Приложение2КНалоговойНакладнойТовары.ИзменениеСуммы
	|			ИНАЧЕ Приложение2КНалоговойНакладнойТовары.ИзменениеСуммы + Приложение2КНалоговойНакладнойТовары.ИзменениеСуммыНДС
	|		КОНЕЦ / Приложение2КНалоговойНакладнойТовары.ИзменениеКоличества КАК ЧИСЛО(15, 2)),
	|	Приложение2КНалоговойНакладнойТовары.Ссылка.ВидОперации,
	|	Приложение2КНалоговойНакладнойТовары.Номенклатура.Услуга
	|ИЗ
	|	Документ.Приложение2КНалоговойНакладной.Товары КАК Приложение2КНалоговойНакладнойТовары
	|ГДЕ
	|	Приложение2КНалоговойНакладнойТовары.Ссылка.Дата >= &ДатаНачала
	|	И Приложение2КНалоговойНакладнойТовары.Ссылка.Дата <= &ДатаОкончания
	|	И Приложение2КНалоговойНакладнойТовары.Ссылка.Организация = &Организация
	|	И Приложение2КНалоговойНакладнойТовары.Ссылка.Проведен
	|	И Приложение2КНалоговойНакладнойТовары.Ссылка.ВидОперации В(&МассивВидовОперацийРозницаКонтрагентуВозврат)
	|	И Приложение2КНалоговойНакладнойТовары.ИзменениеКоличества <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	Приложение2КНалоговойНакладнойТовары.Ссылка.ВидОперации,
	|	Приложение2КНалоговойНакладнойТовары.Номенклатура,
	|	Приложение2КНалоговойНакладнойТовары.ЕдиницаИзмерения,
	|	Приложение2КНалоговойНакладнойТовары.СтавкаНДС,
	|	Приложение2КНалоговойНакладнойТовары.Коэффициент,
	|	Приложение2КНалоговойНакладнойТовары.Номенклатура.Услуга,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА Приложение2КНалоговойНакладнойТовары.Ссылка.СуммаВключаетНДС
	|				ТОГДА Приложение2КНалоговойНакладнойТовары.ИзменениеСуммы
	|			ИНАЧЕ Приложение2КНалоговойНакладнойТовары.ИзменениеСуммы + Приложение2КНалоговойНакладнойТовары.ИзменениеСуммыНДС
	|		КОНЕЦ / Приложение2КНалоговойНакладнойТовары.ИзменениеКоличества КАК ЧИСЛО(15, 2)),
	|	КОНЕЦПЕРИОДА(Приложение2КНалоговойНакладнойТовары.Ссылка.Дата, ДЕНЬ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КОНЕЦПЕРИОДА(Приложение2КНалоговойНакладнойУслуги.Ссылка.Дата, ДЕНЬ),
	|	Приложение2КНалоговойНакладнойУслуги.Номенклатура,
	|	Приложение2КНалоговойНакладнойУслуги.Номенклатура.БазоваяЕдиницаИзмерения,
	|	1,
	|	СУММА(-Приложение2КНалоговойНакладнойУслуги.ИзменениеКоличества),
	|	Приложение2КНалоговойНакладнойУслуги.СтавкаНДС,
	|	СУММА(ВЫБОР
	|			КОГДА Приложение2КНалоговойНакладнойУслуги.ИзменениеСуммыНДС < 0
	|				ТОГДА -Приложение2КНалоговойНакладнойУслуги.ИзменениеСуммыНДС
	|			ИНАЧЕ Приложение2КНалоговойНакладнойУслуги.ИзменениеСуммыНДС
	|		КОНЕЦ),
	|	СУММА(ВЫБОР
	|			КОГДА ВЫБОР
	|					КОГДА Приложение2КНалоговойНакладнойУслуги.Ссылка.СуммаВключаетНДС
	|						ТОГДА Приложение2КНалоговойНакладнойУслуги.ИзменениеСуммы
	|					ИНАЧЕ Приложение2КНалоговойНакладнойУслуги.ИзменениеСуммы + Приложение2КНалоговойНакладнойУслуги.ИзменениеСуммыНДС
	|				КОНЕЦ < 0
	|				ТОГДА -ВЫБОР
	|						КОГДА Приложение2КНалоговойНакладнойУслуги.Ссылка.СуммаВключаетНДС
	|							ТОГДА Приложение2КНалоговойНакладнойУслуги.ИзменениеСуммы
	|						ИНАЧЕ Приложение2КНалоговойНакладнойУслуги.ИзменениеСуммы + Приложение2КНалоговойНакладнойУслуги.ИзменениеСуммыНДС
	|					КОНЕЦ
	|			ИНАЧЕ ВЫБОР
	|					КОГДА Приложение2КНалоговойНакладнойУслуги.Ссылка.СуммаВключаетНДС
	|						ТОГДА Приложение2КНалоговойНакладнойУслуги.ИзменениеСуммы
	|					ИНАЧЕ Приложение2КНалоговойНакладнойУслуги.ИзменениеСуммы + Приложение2КНалоговойНакладнойУслуги.ИзменениеСуммыНДС
	|				КОНЕЦ
	|		КОНЕЦ),
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА Приложение2КНалоговойНакладнойУслуги.Ссылка.СуммаВключаетНДС
	|				ТОГДА Приложение2КНалоговойНакладнойУслуги.ИзменениеСуммы
	|			ИНАЧЕ Приложение2КНалоговойНакладнойУслуги.ИзменениеСуммы + Приложение2КНалоговойНакладнойУслуги.ИзменениеСуммыНДС
	|		КОНЕЦ / Приложение2КНалоговойНакладнойУслуги.ИзменениеКоличества КАК ЧИСЛО(15, 2)),
	|	Приложение2КНалоговойНакладнойУслуги.Ссылка.ВидОперации,
	|	Приложение2КНалоговойНакладнойУслуги.Номенклатура.Услуга
	|ИЗ
	|	Документ.Приложение2КНалоговойНакладной.Услуги КАК Приложение2КНалоговойНакладнойУслуги
	|ГДЕ
	|	Приложение2КНалоговойНакладнойУслуги.Ссылка.Дата >= &ДатаНачала
	|	И Приложение2КНалоговойНакладнойУслуги.Ссылка.Дата <= &ДатаОкончания
	|	И Приложение2КНалоговойНакладнойУслуги.Ссылка.Организация = &Организация
	|	И Приложение2КНалоговойНакладнойУслуги.Ссылка.Проведен
	|	И Приложение2КНалоговойНакладнойУслуги.Ссылка.ВидОперации В(&МассивВидовОперацийРозницаКонтрагентуВозврат)
	|	И Приложение2КНалоговойНакладнойУслуги.ИзменениеКоличества <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	Приложение2КНалоговойНакладнойУслуги.Ссылка.ВидОперации,
	|	Приложение2КНалоговойНакладнойУслуги.Номенклатура,
	|	Приложение2КНалоговойНакладнойУслуги.СтавкаНДС,
	|	Приложение2КНалоговойНакладнойУслуги.Номенклатура.Услуга,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА Приложение2КНалоговойНакладнойУслуги.Ссылка.СуммаВключаетНДС
	|				ТОГДА Приложение2КНалоговойНакладнойУслуги.ИзменениеСуммы
	|			ИНАЧЕ Приложение2КНалоговойНакладнойУслуги.ИзменениеСуммы + Приложение2КНалоговойНакладнойУслуги.ИзменениеСуммыНДС
	|		КОНЕЦ / Приложение2КНалоговойНакладнойУслуги.ИзменениеКоличества КАК ЧИСЛО(15, 2)),
	|	КОНЕЦПЕРИОДА(Приложение2КНалоговойНакладнойУслуги.Ссылка.Дата, ДЕНЬ),
	|	Приложение2КНалоговойНакладнойУслуги.Номенклатура.БазоваяЕдиницаИзмерения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВидОперации";
	
	ТекстПроверкиИтоговыхННЗаПериод = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Документ
	|ИЗ
	|	ДанныеРегистра КАК ДанныеРегистра
	|ГДЕ
	|	ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И ДанныеРегистра.ВключенаВЕдиныйРеестрНалоговыхНакладных = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Документ
	|ИЗ
	|	ДанныеРегистра КАК ДанныеРегистра
	|ГДЕ
	|	ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)";
	
	Запрос.УстановитьПараметр("ДатаНачала", 		ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", 		КонецДня(ДатаОкончания));
	Запрос.УстановитьПараметр("Организация", 		Организация);
	Запрос.УстановитьПараметр("РеализацияРозница", 	Перечисления.СобытияОжидаемыйИПодтвержденныйНДСПродаж.РеализацияРозница);
	Запрос.УстановитьПараметр("ВозвратРозница", 	Перечисления.СобытияОжидаемыйИПодтвержденныйНДСПродаж.ВозвратРозница);
	
	МассивОблагаемыхСтавокНДС = Новый Массив(5);
	МассивОблагаемыхСтавокНДС[0] = Перечисления.СтавкиНДС.НДС0;
	МассивОблагаемыхСтавокНДС[1] = Перечисления.СтавкиНДС.НДС7;
	МассивОблагаемыхСтавокНДС[2] = Перечисления.СтавкиНДС.НДС14;
	МассивОблагаемыхСтавокНДС[3] = Перечисления.СтавкиНДС.НДС20;
	МассивОблагаемыхСтавокНДС[4] = Перечисления.СтавкиНДС.СпецСт8;
	
	МассивНеОблагаемыхСтавокНДС = Новый Массив(1);
	МассивНеОблагаемыхСтавокНДС[0] = Перечисления.СтавкиНДС.БезНДС;
	
	МассивВидовОперацийРозницаКонтрагенту = Новый Массив(2);
	МассивВидовОперацийРозницаКонтрагенту[0] = Перечисления.ВидыОперацийНалоговаяНакладная.РозницаКонрагентуОблагаемыеОперации;
	МассивВидовОперацийРозницаКонтрагенту[1] = Перечисления.ВидыОперацийНалоговаяНакладная.РозницаКонрагентуОсвобожденныеОперации;
	
	МассивВидовОперацийРозницаКонтрагентуВозврат = Новый Массив(2);
	МассивВидовОперацийРозницаКонтрагентуВозврат[0] = Перечисления.ВидыОперацийПриложение2КНалоговойНакладной.РозницаКонрагентуОблагаемыеОперацииВозврат;
	МассивВидовОперацийРозницаКонтрагентуВозврат[1] = Перечисления.ВидыОперацийПриложение2КНалоговойНакладной.РозницаКонрагентуОсвобожденныеОперацииВозврат;
	
	Запрос.УстановитьПараметр("МассивОблагаемыхСтавокНДС", 						МассивОблагаемыхСтавокНДС);
	Запрос.УстановитьПараметр("МассивНеОблагаемыхСтавокНДС",					МассивНеОблагаемыхСтавокНДС);
	Запрос.УстановитьПараметр("МассивВидовОперацийРозницаКонтрагенту", 			МассивВидовОперацийРозницаКонтрагенту);
	Запрос.УстановитьПараметр("МассивВидовОперацийРозницаКонтрагентуВозврат",	МассивВидовОперацийРозницаКонтрагентуВозврат);
	
	Если Проверка Тогда
		Запрос.Текст = ТекстДанныеРегистра + ТекстПроверкиИтоговыхННЗаПериод;
		РезультатЗапроса = Запрос.ВыполнитьПакет();
	Иначе
		Запрос.Текст = ТекстДанныеРегистра + ТекстНомСоставИСоставКонтрагентуВРозницу;
		РезультатЗапроса = Запрос.Выполнить();
	КонецЕсли;
	
	Возврат РезультатЗапроса;
	
КонецФункции

Функция ПолучитьПараметрыПоУмолчанию() Экспорт
	 
	НоменклатураЗаполнения = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("НоменклатураДляЗаполненияНалоговыхНакладных");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Номенклатура.БазоваяЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА Номенклатура.НоменклатураГТД = НЕОПРЕДЕЛЕНО
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КлассификаторУКТВЭД.ПустаяСсылка)
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ТИПЗНАЧЕНИЯ(Номенклатура.НоменклатураГТД) = ТИП(Справочник.НоменклатураГТД)
	|					ТОГДА ЕСТЬNULL(Номенклатура.НоменклатураГТД.КодУКТВЭД, ЗНАЧЕНИЕ(Справочник.КлассификаторУКТВЭД.ПустаяСсылка))
	|				ИНАЧЕ ЕСТЬNULL(Номенклатура.НоменклатураГТД, ЗНАЧЕНИЕ(Справочник.КлассификаторУКТВЭД.ПустаяСсылка))
	|			КОНЕЦ
	|	КОНЕЦ КАК КодУКТВЭД
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка = &Номенклатура";
	Запрос.УстановитьПараметр("Номенклатура", НоменклатураЗаполнения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	ПараметрыПоУмолчанию = Новый Структура("Номенклатура, ЕдиницаИзмерения, КодУКТВЭД",
		НоменклатураЗаполнения, Выборка.ЕдиницаИзмерения, Выборка.КодУКТВЭД);
	
	Возврат ПараметрыПоУмолчанию; 
	
КонецФункции

Функция ПолучитьДанныеДляДокументовНалоговаяНакладная(Запрос)
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Дата КАК Дата
	|ИЗ
	|	ДанныеРегистра КАК ДанныеРегистра
	|ГДЕ
	|	ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И ДанныеРегистра.СобытиеНДС = &РеализацияРозница
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРегистра.Дата
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРегистра.Дата КАК Дата,
	|	ДанныеРегистра.СтавкаНДС,
	|	ДанныеРегистра.СобытиеНДС,
	|	СУММА(ДанныеРегистра.БазаНДС) КАК БазаНДС,
	|	СУММА(ДанныеРегистра.СуммаНДС) КАК СуммаНДС
	|ИЗ
	|	ДанныеРегистра КАК ДанныеРегистра
	|ГДЕ
	|	ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И ДанныеРегистра.СтавкаНДС В(&МассивОблагаемыхСтавокНДС)
	|	И ДанныеРегистра.СобытиеНДС = &РеализацияРозница
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРегистра.СтавкаНДС,
	|	ДанныеРегистра.СобытиеНДС,
	|	ДанныеРегистра.Дата
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРегистра.Дата КАК Дата,
	|	ДанныеРегистра.СтавкаНДС,
	|	ДанныеРегистра.СобытиеНДС,
	|	СУММА(ДанныеРегистра.БазаНДС) КАК БазаНДС,
	|	СУММА(ДанныеРегистра.СуммаНДС) КАК СуммаНДС
	|ИЗ
	|	ДанныеРегистра КАК ДанныеРегистра
	|ГДЕ
	|	ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И ДанныеРегистра.СтавкаНДС В(&МассивНеОблагаемыхСтавокНДС)
	|	И ДанныеРегистра.СобытиеНДС = &РеализацияРозница
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРегистра.СтавкаНДС,
	|	ДанныеРегистра.СобытиеНДС,
	|	ДанныеРегистра.Дата
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРегистра.Дата КАК Дата,
	|	ДанныеРегистра.Документ
	|ИЗ
	|	ДанныеРегистра КАК ДанныеРегистра
	|ГДЕ
	|	ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И ДанныеРегистра.СтавкаНДС В(&МассивОблагаемыхСтавокНДС)
	|	И ДанныеРегистра.СобытиеНДС = &РеализацияРозница
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРегистра.Дата,
	|	ДанныеРегистра.Документ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРегистра.Дата КАК Дата,
	|	ДанныеРегистра.Документ
	|ИЗ
	|	ДанныеРегистра КАК ДанныеРегистра
	|ГДЕ
	|	ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И ДанныеРегистра.СтавкаНДС В(&МассивНеОблагаемыхСтавокНДС)
	|	И ДанныеРегистра.СобытиеНДС = &РеализацияРозница
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРегистра.Дата,
	|	ДанныеРегистра.Документ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатурныйСостав.Дата КАК Дата,
	|	НоменклатурныйСостав.СтавкаНДС,
	|	НоменклатурныйСостав.СобытиеНДС,
	|	НоменклатурныйСостав.Номенклатура,
	|	НоменклатурныйСостав.Услуга,
	|	НоменклатурныйСостав.Количество,
	|	НоменклатурныйСостав.СуммаНДС,
	|	НоменклатурныйСостав.ЕдиницаИзмерения,
	|	НоменклатурныйСостав.Коэффициент,
	|	НоменклатурныйСостав.СуммаСНДС,
	|	НоменклатурныйСостав.РасчетнаяЦенаСНДС,
	|	НоменклатурныйСостав.КодУКТВЭД
	|ИЗ
	|	НоменклатурныйСостав КАК НоменклатурныйСостав
	|ГДЕ
	|	НоменклатурныйСостав.СтавкаНДС В(&МассивОблагаемыхСтавокНДС)
	|	И НоменклатурныйСостав.СобытиеНДС = &РеализацияРозница
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатурныйСостав.Дата КАК Дата,
	|	НоменклатурныйСостав.СтавкаНДС,
	|	НоменклатурныйСостав.СобытиеНДС,
	|	НоменклатурныйСостав.Номенклатура,
	|	НоменклатурныйСостав.Услуга,
	|	НоменклатурныйСостав.Количество,
	|	НоменклатурныйСостав.СуммаНДС,
	|	НоменклатурныйСостав.ЕдиницаИзмерения,
	|	НоменклатурныйСостав.Коэффициент,
	|	НоменклатурныйСостав.СуммаСНДС,
	|	НоменклатурныйСостав.РасчетнаяЦенаСНДС,
	|	НоменклатурныйСостав.КодУКТВЭД
	|ИЗ
	|	НоменклатурныйСостав КАК НоменклатурныйСостав
	|ГДЕ
	|	НоменклатурныйСостав.СтавкаНДС В(&МассивНеОблагаемыхСтавокНДС)
	|	И НоменклатурныйСостав.СобытиеНДС = &РеализацияРозница
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НалоговыеДокументыВРозницуКонтрагенту.Дата,
	|	НалоговыеДокументыВРозницуКонтрагенту.Номенклатура,
	|	НалоговыеДокументыВРозницуКонтрагенту.ЕдиницаИзмерения,
	|	НалоговыеДокументыВРозницуКонтрагенту.Коэффициент,
	|	НалоговыеДокументыВРозницуКонтрагенту.Количество,
	|	НалоговыеДокументыВРозницуКонтрагенту.СтавкаНДС,
	|	НалоговыеДокументыВРозницуКонтрагенту.СуммаНДС,
	|	НалоговыеДокументыВРозницуКонтрагенту.СуммаСНДС,
	|	НалоговыеДокументыВРозницуКонтрагенту.РасчетнаяЦенаСНДС,
	|	НалоговыеДокументыВРозницуКонтрагенту.Услуга
	|ИЗ
	|	НалоговыеДокументыВРозницуКонтрагенту КАК НалоговыеДокументыВРозницуКонтрагенту
	|ГДЕ
	|	НалоговыеДокументыВРозницуКонтрагенту.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговаяНакладная.РозницаКонрагентуОблагаемыеОперации)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НалоговыеДокументыВРозницуКонтрагенту.Дата,
	|	НалоговыеДокументыВРозницуКонтрагенту.Номенклатура,
	|	НалоговыеДокументыВРозницуКонтрагенту.ЕдиницаИзмерения,
	|	НалоговыеДокументыВРозницуКонтрагенту.Коэффициент,
	|	НалоговыеДокументыВРозницуКонтрагенту.Количество,
	|	НалоговыеДокументыВРозницуКонтрагенту.СтавкаНДС,
	|	НалоговыеДокументыВРозницуКонтрагенту.СуммаНДС,
	|	НалоговыеДокументыВРозницуКонтрагенту.СуммаСНДС,
	|	НалоговыеДокументыВРозницуКонтрагенту.РасчетнаяЦенаСНДС,
	|	НалоговыеДокументыВРозницуКонтрагенту.Услуга
	|ИЗ
	|	НалоговыеДокументыВРозницуКонтрагенту КАК НалоговыеДокументыВРозницуКонтрагенту
	|ГДЕ
	|	НалоговыеДокументыВРозницуКонтрагенту.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговаяНакладная.РозницаКонрагентуОсвобожденныеОперации)";
	
	Возврат Запрос.ВыполнитьПакет();
	
КонецФункции

Функция ПолучитьДанныеДляДокументовП2(Запрос)
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Дата КАК Дата
	|ИЗ
	|	ДанныеРегистра КАК ДанныеРегистра
	|ГДЕ
	|	ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И ДанныеРегистра.СобытиеНДС = &ВозвратРозница
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРегистра.Дата
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРегистра.Дата КАК Дата,
	|	ДанныеРегистра.СтавкаНДС,
	|	ДанныеРегистра.СобытиеНДС,
	|	СУММА(ДанныеРегистра.БазаНДС) КАК БазаНДС,
	|	СУММА(ДанныеРегистра.СуммаНДС) КАК СуммаНДС
	|ИЗ
	|	ДанныеРегистра КАК ДанныеРегистра
	|ГДЕ
	|	ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И ДанныеРегистра.СтавкаНДС В(&МассивОблагаемыхСтавокНДС)
	|	И ДанныеРегистра.СобытиеНДС = &ВозвратРозница
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРегистра.СтавкаНДС,
	|	ДанныеРегистра.СобытиеНДС,
	|	ДанныеРегистра.Дата
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРегистра.Дата КАК Дата,
	|	ДанныеРегистра.СтавкаНДС,
	|	ДанныеРегистра.СобытиеНДС,
	|	СУММА(ДанныеРегистра.БазаНДС) КАК БазаНДС,
	|	СУММА(ДанныеРегистра.СуммаНДС) КАК СуммаНДС
	|ИЗ
	|	ДанныеРегистра КАК ДанныеРегистра
	|ГДЕ
	|	ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И ДанныеРегистра.СтавкаНДС В(&МассивНеОблагаемыхСтавокНДС)
	|	И ДанныеРегистра.СобытиеНДС = &ВозвратРозница
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРегистра.СтавкаНДС,
	|	ДанныеРегистра.СобытиеНДС,
	|	ДанныеРегистра.Дата
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРегистра.Дата КАК Дата,
	|	ДанныеРегистра.Документ
	|ИЗ
	|	ДанныеРегистра КАК ДанныеРегистра
	|ГДЕ
	|	ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И ДанныеРегистра.СтавкаНДС В(&МассивОблагаемыхСтавокНДС)
	|	И ДанныеРегистра.СобытиеНДС = &ВозвратРозница
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРегистра.Дата,
	|	ДанныеРегистра.Документ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРегистра.Дата КАК Дата,
	|	ДанныеРегистра.Документ
	|ИЗ
	|	ДанныеРегистра КАК ДанныеРегистра
	|ГДЕ
	|	ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И ДанныеРегистра.СтавкаНДС В(&МассивНеОблагаемыхСтавокНДС)
	|	И ДанныеРегистра.СобытиеНДС = &ВозвратРозница
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРегистра.Дата,
	|	ДанныеРегистра.Документ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатурныйСостав.Дата КАК Дата,
	|	НоменклатурныйСостав.СтавкаНДС,
	|	НоменклатурныйСостав.СобытиеНДС,
	|	НоменклатурныйСостав.Номенклатура,
	|	НоменклатурныйСостав.Услуга,
	|	НоменклатурныйСостав.Количество,
	|	НоменклатурныйСостав.СуммаНДС,
	|	НоменклатурныйСостав.ЕдиницаИзмерения,
	|	НоменклатурныйСостав.Коэффициент,
	|	НоменклатурныйСостав.СуммаСНДС,
	|	НоменклатурныйСостав.РасчетнаяЦенаСНДС,
	|	НоменклатурныйСостав.КодУКТВЭД,
	|	НоменклатурныйСостав.ДокументПродажи КАК ДокументПродажи,
	|	НоменклатурныйСостав.ДатаДокументаПродажи
	|ИЗ
	|	НоменклатурныйСостав КАК НоменклатурныйСостав
	|ГДЕ
	|	НоменклатурныйСостав.СтавкаНДС В(&МассивОблагаемыхСтавокНДС)
	|	И НоменклатурныйСостав.СобытиеНДС = &ВозвратРозница
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	ДокументПродажи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатурныйСостав.Дата КАК Дата,
	|	НоменклатурныйСостав.СтавкаНДС,
	|	НоменклатурныйСостав.СобытиеНДС,
	|	НоменклатурныйСостав.Номенклатура,
	|	НоменклатурныйСостав.Услуга,
	|	НоменклатурныйСостав.Количество,
	|	НоменклатурныйСостав.СуммаНДС,
	|	НоменклатурныйСостав.ЕдиницаИзмерения,
	|	НоменклатурныйСостав.Коэффициент,
	|	НоменклатурныйСостав.СуммаСНДС,
	|	НоменклатурныйСостав.РасчетнаяЦенаСНДС,
	|	НоменклатурныйСостав.КодУКТВЭД,
	|	НоменклатурныйСостав.ДокументПродажи КАК ДокументПродажи,
	|	НоменклатурныйСостав.ДатаДокументаПродажи
	|ИЗ
	|	НоменклатурныйСостав КАК НоменклатурныйСостав
	|ГДЕ
	|	НоменклатурныйСостав.СтавкаНДС В(&МассивНеОблагаемыхСтавокНДС)
	|	И НоменклатурныйСостав.СобытиеНДС = &ВозвратРозница
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	ДокументПродажи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НалоговыеДокументыВРозницуКонтрагенту.Дата,
	|	НалоговыеДокументыВРозницуКонтрагенту.Номенклатура,
	|	НалоговыеДокументыВРозницуКонтрагенту.ЕдиницаИзмерения,
	|	НалоговыеДокументыВРозницуКонтрагенту.Коэффициент,
	|	НалоговыеДокументыВРозницуКонтрагенту.Количество,
	|	НалоговыеДокументыВРозницуКонтрагенту.СтавкаНДС,
	|	НалоговыеДокументыВРозницуКонтрагенту.СуммаНДС,
	|	НалоговыеДокументыВРозницуКонтрагенту.СуммаСНДС,
	|	НалоговыеДокументыВРозницуКонтрагенту.РасчетнаяЦенаСНДС,
	|	НалоговыеДокументыВРозницуКонтрагенту.Услуга
	|ИЗ
	|	НалоговыеДокументыВРозницуКонтрагенту КАК НалоговыеДокументыВРозницуКонтрагенту
	|ГДЕ
	|	НалоговыеДокументыВРозницуКонтрагенту.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриложение2КНалоговойНакладной.РозницаКонрагентуОблагаемыеОперацииВозврат)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НалоговыеДокументыВРозницуКонтрагенту.Дата,
	|	НалоговыеДокументыВРозницуКонтрагенту.Номенклатура,
	|	НалоговыеДокументыВРозницуКонтрагенту.ЕдиницаИзмерения,
	|	НалоговыеДокументыВРозницуКонтрагенту.Коэффициент,
	|	НалоговыеДокументыВРозницуКонтрагенту.Количество,
	|	НалоговыеДокументыВРозницуКонтрагенту.СтавкаНДС,
	|	НалоговыеДокументыВРозницуКонтрагенту.СуммаНДС,
	|	НалоговыеДокументыВРозницуКонтрагенту.СуммаСНДС,
	|	НалоговыеДокументыВРозницуКонтрагенту.РасчетнаяЦенаСНДС,
	|	НалоговыеДокументыВРозницуКонтрагенту.Услуга
	|ИЗ
	|	НалоговыеДокументыВРозницуКонтрагенту КАК НалоговыеДокументыВРозницуКонтрагенту
	|ГДЕ
	|	НалоговыеДокументыВРозницуКонтрагенту.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриложение2КНалоговойНакладной.РозницаКонрагентуОсвобожденныеОперацииВозврат)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатурныйСостав.Дата КАК Дата,
	|	НоменклатурныйСостав.СтавкаНДС,
	|	НоменклатурныйСостав.Номенклатура,
	|	НоменклатурныйСостав.Количество,
	|	НоменклатурныйСостав.СуммаНДС,
	|	НоменклатурныйСостав.СуммаСНДС,
	|	НоменклатурныйСостав.ДокументПродажи,
	|	НалоговаяНакладнаяТовары.Ссылка КАК НалоговаяОснование,
	|	НалоговаяНакладнаяТовары.Количество КАК КоличествоНН,
	|	НалоговаяНакладнаяТовары.СуммаНДС КАК СуммаНДСНН,
	|	ВЫБОР
	|		КОГДА НалоговаяНакладнаяТовары.Ссылка.СуммаВключаетНДС
	|			ТОГДА НалоговаяНакладнаяТовары.Сумма
	|		ИНАЧЕ НалоговаяНакладнаяТовары.Сумма + НалоговаяНакладнаяТовары.СуммаНДС
	|	КОНЕЦ КАК СуммаСНДСНН,
	|	НалоговаяНакладнаяТовары.Ссылка.ВидОперации КАК ВидОперации,
	|	НоменклатурныйСостав.ЕдиницаИзмерения,
	|	НоменклатурныйСостав.РасчетнаяЦенаСНДС
	|ПОМЕСТИТЬ НалоговыеОснованияДляП2
	|ИЗ
	|	НоменклатурныйСостав КАК НоменклатурныйСостав
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НалоговаяНакладная.Товары КАК НалоговаяНакладнаяТовары
	|		ПО (НАЧАЛОПЕРИОДА(НоменклатурныйСостав.ДатаДокументаПродажи, ДЕНЬ) = НАЧАЛОПЕРИОДА(НалоговаяНакладнаяТовары.Ссылка.Дата, ДЕНЬ))
	|			И (ВЫБОР
	|				КОГДА НоменклатурныйСостав.СтавкаНДС В (ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0), ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС7), ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС14), ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС20))
	|					ТОГДА НалоговаяНакладнаяТовары.Ссылка.ВидОперации В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговаяНакладная.РозницаКонрагентуОблагаемыеОперации), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговаяНакладная.ИтоговаяРозницаОблагаемыеОперации))
	|				ИНАЧЕ НалоговаяНакладнаяТовары.Ссылка.ВидОперации В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговаяНакладная.РозницаКонрагентуОсвобожденныеОперации), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговаяНакладная.ИтоговаяРозницаОсвобожденныеОперации))
	|			КОНЕЦ)
	|			И НоменклатурныйСостав.Номенклатура = НалоговаяНакладнаяТовары.Номенклатура
	|			И НоменклатурныйСостав.СтавкаНДС = НалоговаяНакладнаяТовары.СтавкаНДС
	|			И НоменклатурныйСостав.ЕдиницаИзмерения = НалоговаяНакладнаяТовары.ЕдиницаИзмерения
	|			И (НалоговаяНакладнаяТовары.Ссылка.Проведен)
	|			И (НоменклатурныйСостав.РасчетнаяЦенаСНДС МЕЖДУ (ВЫРАЗИТЬ(ВЫБОР
	|					КОГДА НалоговаяНакладнаяТовары.Ссылка.СуммаВключаетНДС
	|						ТОГДА НалоговаяНакладнаяТовары.Сумма
	|					ИНАЧЕ НалоговаяНакладнаяТовары.Сумма + НалоговаяНакладнаяТовары.СуммаНДС
	|				КОНЕЦ / НалоговаяНакладнаяТовары.Количество КАК ЧИСЛО(15, 2))) - 0.02 И (ВЫРАЗИТЬ(ВЫБОР
	|					КОГДА НалоговаяНакладнаяТовары.Ссылка.СуммаВключаетНДС
	|						ТОГДА НалоговаяНакладнаяТовары.Сумма
	|					ИНАЧЕ НалоговаяНакладнаяТовары.Сумма + НалоговаяНакладнаяТовары.СуммаНДС
	|				КОНЕЦ / НалоговаяНакладнаяТовары.Количество КАК ЧИСЛО(15, 2))) + 0.02)
	|ГДЕ
	|	НоменклатурныйСостав.СобытиеНДС = &ВозвратРозница
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НоменклатурныйСостав.Дата,
	|	НоменклатурныйСостав.СтавкаНДС,
	|	НоменклатурныйСостав.Номенклатура,
	|	НоменклатурныйСостав.Количество,
	|	НоменклатурныйСостав.СуммаНДС,
	|	НоменклатурныйСостав.СуммаСНДС,
	|	НоменклатурныйСостав.ДокументПродажи,
	|	НалоговаяНакладнаяУслуги.Ссылка,
	|	НалоговаяНакладнаяУслуги.Количество,
	|	НалоговаяНакладнаяУслуги.СуммаНДС,
	|	ВЫБОР
	|		КОГДА НалоговаяНакладнаяУслуги.Ссылка.СуммаВключаетНДС
	|			ТОГДА НалоговаяНакладнаяУслуги.Сумма
	|		ИНАЧЕ НалоговаяНакладнаяУслуги.Сумма + НалоговаяНакладнаяУслуги.СуммаНДС
	|	КОНЕЦ,
	|	НалоговаяНакладнаяУслуги.Ссылка.ВидОперации,
	|	НоменклатурныйСостав.ЕдиницаИзмерения,
	|	НоменклатурныйСостав.РасчетнаяЦенаСНДС
	|ИЗ
	|	НоменклатурныйСостав КАК НоменклатурныйСостав
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НалоговаяНакладная.Услуги КАК НалоговаяНакладнаяУслуги
	|		ПО (НАЧАЛОПЕРИОДА(НоменклатурныйСостав.ДатаДокументаПродажи, ДЕНЬ) = НАЧАЛОПЕРИОДА(НалоговаяНакладнаяУслуги.Ссылка.Дата, ДЕНЬ))
	|			И (ВЫБОР
	|				КОГДА НоменклатурныйСостав.СтавкаНДС В (ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0), ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС7), ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС14), ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС20))
	|					ТОГДА НалоговаяНакладнаяУслуги.Ссылка.ВидОперации В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговаяНакладная.РозницаКонрагентуОблагаемыеОперации), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговаяНакладная.ИтоговаяРозницаОблагаемыеОперации))
	|				ИНАЧЕ НалоговаяНакладнаяУслуги.Ссылка.ВидОперации В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговаяНакладная.РозницаКонрагентуОсвобожденныеОперации), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговаяНакладная.ИтоговаяРозницаОсвобожденныеОперации))
	|			КОНЕЦ)
	|			И НоменклатурныйСостав.Номенклатура = НалоговаяНакладнаяУслуги.Номенклатура
	|			И НоменклатурныйСостав.СтавкаНДС = НалоговаяНакладнаяУслуги.СтавкаНДС
	|			И (НалоговаяНакладнаяУслуги.Ссылка.Проведен)
	|			И (НоменклатурныйСостав.РасчетнаяЦенаСНДС МЕЖДУ (ВЫРАЗИТЬ(ВЫБОР
	|					КОГДА НалоговаяНакладнаяУслуги.Ссылка.СуммаВключаетНДС
	|						ТОГДА НалоговаяНакладнаяУслуги.Сумма
	|					ИНАЧЕ НалоговаяНакладнаяУслуги.Сумма + НалоговаяНакладнаяУслуги.СуммаНДС
	|				КОНЕЦ / НалоговаяНакладнаяУслуги.Количество КАК ЧИСЛО(15, 2))) - 0.02 И (ВЫРАЗИТЬ(ВЫБОР
	|					КОГДА НалоговаяНакладнаяУслуги.Ссылка.СуммаВключаетНДС
	|						ТОГДА НалоговаяНакладнаяУслуги.Сумма
	|					ИНАЧЕ НалоговаяНакладнаяУслуги.Сумма + НалоговаяНакладнаяУслуги.СуммаНДС
	|				КОНЕЦ / НалоговаяНакладнаяУслуги.Количество КАК ЧИСЛО(15, 2))) + 0.02)
	|ГДЕ
	|	НоменклатурныйСостав.СобытиеНДС = &ВозвратРозница
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НалоговыеОснованияДляП2.Дата,
	|	НалоговыеОснованияДляП2.СтавкаНДС,
	|	НалоговыеОснованияДляП2.Номенклатура,
	|	НалоговыеОснованияДляП2.Количество,
	|	НалоговыеОснованияДляП2.СуммаНДС,
	|	НалоговыеОснованияДляП2.СуммаСНДС,
	|	НалоговыеОснованияДляП2.ДокументПродажи,
	|	НалоговыеОснованияДляП2.НалоговаяОснование,
	|	СУММА(НалоговыеОснованияДляП2.КоличествоНН) КАК КоличествоНН,
	|	СУММА(НалоговыеОснованияДляП2.СуммаНДСНН) КАК СуммаНДСНН,
	|	СУММА(НалоговыеОснованияДляП2.СуммаСНДСНН) КАК СуммаСНДСНН,
	|	ВЫБОР
	|		КОГДА НалоговыеОснованияДляП2.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговаяНакладная.ИтоговаяРозницаОблагаемыеОперации)
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК Приоритет,
	|	НалоговыеОснованияДляП2.ЕдиницаИзмерения,
	|	НалоговыеОснованияДляП2.РасчетнаяЦенаСНДС
	|ИЗ
	|	НалоговыеОснованияДляП2 КАК НалоговыеОснованияДляП2
	|ГДЕ
	|	НалоговыеОснованияДляП2.СтавкаНДС В(&МассивОблагаемыхСтавокНДС)
	|
	|СГРУППИРОВАТЬ ПО
	|	НалоговыеОснованияДляП2.Дата,
	|	НалоговыеОснованияДляП2.СуммаНДС,
	|	НалоговыеОснованияДляП2.ДокументПродажи,
	|	НалоговыеОснованияДляП2.СтавкаНДС,
	|	НалоговыеОснованияДляП2.НалоговаяОснование,
	|	НалоговыеОснованияДляП2.СуммаСНДС,
	|	НалоговыеОснованияДляП2.Номенклатура,
	|	НалоговыеОснованияДляП2.Количество,
	|	ВЫБОР
	|		КОГДА НалоговыеОснованияДляП2.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговаяНакладная.ИтоговаяРозницаОблагаемыеОперации)
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ,
	|	НалоговыеОснованияДляП2.ЕдиницаИзмерения,
	|	НалоговыеОснованияДляП2.РасчетнаяЦенаСНДС
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НалоговыеОснованияДляП2.Дата,
	|	НалоговыеОснованияДляП2.СтавкаНДС,
	|	НалоговыеОснованияДляП2.Номенклатура,
	|	НалоговыеОснованияДляП2.Количество,
	|	НалоговыеОснованияДляП2.СуммаНДС,
	|	НалоговыеОснованияДляП2.СуммаСНДС,
	|	НалоговыеОснованияДляП2.ДокументПродажи,
	|	НалоговыеОснованияДляП2.НалоговаяОснование,
	|	СУММА(НалоговыеОснованияДляП2.КоличествоНН) КАК КоличествоНН,
	|	СУММА(НалоговыеОснованияДляП2.СуммаНДСНН) КАК СуммаНДСНН,
	|	СУММА(НалоговыеОснованияДляП2.СуммаСНДСНН) КАК СуммаСНДСНН,
	|	ВЫБОР
	|		КОГДА НалоговыеОснованияДляП2.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговаяНакладная.ИтоговаяРозницаОсвобожденныеОперации)
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК Приоритет,
	|	НалоговыеОснованияДляП2.ЕдиницаИзмерения,
	|	НалоговыеОснованияДляП2.РасчетнаяЦенаСНДС
	|ИЗ
	|	НалоговыеОснованияДляП2 КАК НалоговыеОснованияДляП2
	|ГДЕ
	|	НалоговыеОснованияДляП2.СтавкаНДС В(&МассивНеОблагаемыхСтавокНДС)
	|
	|СГРУППИРОВАТЬ ПО
	|	НалоговыеОснованияДляП2.Дата,
	|	НалоговыеОснованияДляП2.СтавкаНДС,
	|	НалоговыеОснованияДляП2.Номенклатура,
	|	НалоговыеОснованияДляП2.Количество,
	|	НалоговыеОснованияДляП2.СуммаНДС,
	|	НалоговыеОснованияДляП2.СуммаСНДС,
	|	НалоговыеОснованияДляП2.ДокументПродажи,
	|	НалоговыеОснованияДляП2.НалоговаяОснование,
	|	ВЫБОР
	|		КОГДА НалоговыеОснованияДляП2.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговаяНакладная.ИтоговаяРозницаОсвобожденныеОперации)
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ,
	|	НалоговыеОснованияДляП2.ЕдиницаИзмерения,
	|	НалоговыеОснованияДляП2.РасчетнаяЦенаСНДС
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет";
	
	Возврат Запрос.ВыполнитьПакет();
	
КонецФункции

Процедура ОбработатьСозданияНалоговыхДокументов(МассивРезультатовЗапроса, ЭтоНалоговаяНакладная, ПараметрыПоУмолчанию, ТаблицаСозданныхНалоговыхНакладных, Настройки, ДанныеОбъекта)
	
	ДатыНалоговыхДокументов = МассивРезультатовЗапроса[0].Выгрузить().ВыгрузитьКолонку("Дата");
	
	//Эта процедура общая для документов НН и для П2
	//Главный цикл проходит дважды: первый раз для облагаемых операций, второй - для необлагаемых операций
	//Индекс - это данные регистра
	//Индекс + 2 - это уже созданные итоговые налоговые документы за этот период, которые мы будем переформировывать (возможно обработка запускается повторно) 
	//Индекс + 4 - номенклатурный состав для заполнения налоговых документов, который взят из документов "Отчет о розничных продажах"
	//Индекс + 6 - номенклатурный состав, который уже выписан в налоговых документах контрагенту в рознице, его необходимо вычесть из номенклатурного состава
	//Индекс + 9 - есть только для П2, это таблица возможных оснований для П2, то-есть по каким налоговым накладным выписывается П2
	Индекс = 0;	
	Пока Индекс < 2 Цикл
		
		Индекс = Индекс + 1;
		
		//Данные регистра
		Если МассивРезультатовЗапроса[Индекс].Пустой() Тогда
			//Если у нас нет в регистре данных за период, то дальше нет смысла продолжать эту итерацию
			Продолжить;
		КонецЕсли;
		
		ТаблицаДанныеРегистра = ПолучитьТаблицуЗначений(МассивРезультатовЗапроса[Индекс]);
		//Суммы дополнения номенклатурой по умолчанию в разрезе ставок будем хранить в таблице ТаблицаДанныеРегистраЗаДень
		ОписаниеТиповЧисло = Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(15, 2));
		ТаблицаДанныеРегистра.Колонки.Добавить("СуммаНДСДополнения", ОписаниеТиповЧисло);
		ТаблицаДанныеРегистра.Колонки.Добавить("БазаСНДСДополнения", ОписаниеТиповЧисло);
		ТаблицаДанныеРегистра.Колонки.Добавить("НекорректныеДанные", Новый ОписаниеТипов("Булево"));
		
		//Уже сформированные налоговые документы, которые будем переформировывать
		ТаблицаНалоговыеДокументы = ПолучитьТаблицуЗначений(МассивРезультатовЗапроса[Индекс + 2]);	
		
		//Номенклатурный состав для заполнения
		ТаблицаНоменклатурныйСостав = ПолучитьТаблицуЗначений(МассивРезультатовЗапроса[Индекс + 4], "Дата");
		//Колонка нам нужна для того, чтобы при вычитании из ном. состава, товаров выписанных в налоговых контрагенту в рознице, не вычесть одну и туже строку дважды
		ТаблицаНоменклатурныйСостав.Колонки.Добавить("Обработан", Новый ОписаниеТипов("Булево"));
		
		//Номенклатурный состав из налоговых документов, которые выписаны контрагенту в рознице, его необходимо исключить из номенклатурного состава для
		//итоговых накладных в рознице
		ТаблицаНоменклатурныйСоставКонтрВРозницу = ПолучитьТаблицуЗначений(МассивРезультатовЗапроса[Индекс + 6], "Дата");
		
		//Эта таблица содержит данные с возможными документамы основания для П2
		Если Не ЭтоНалоговаяНакладная Тогда
			ТаблицаНалоговыеОснованияДляП2 = ПолучитьТаблицуЗначений(МассивРезультатовЗапроса[Индекс + 9], "Дата");	
		КонецЕсли; 
		
		//Проходим по дням, за которые есть данные в регистре и пытаемся сформировать налоговые документы 
		Для каждого ДатаДокумента Из ДатыНалоговыхДокументов Цикл
			
			СтруктураОтбора = Новый Структура("Дата", ДатаДокумента);
			
			ТаблицаДанныеРегистраЗаДень = ТаблицаДанныеРегистра.Скопировать(СтруктураОтбора);
			Если ТаблицаДанныеРегистраЗаДень.Количество() = 0 Тогда
				Продолжить;	
			КонецЕсли; 
			
			ТаблицаНалоговыеДокументыЗаДень = ТаблицаНалоговыеДокументы.Скопировать(СтруктураОтбора);
			ТаблицаНоменклатурныйСоставЗаДень = ТаблицаНоменклатурныйСостав.Скопировать(СтруктураОтбора);	
			ТаблицаНоменклатурныйСоставКонтрВРозницуЗаДень = ТаблицаНоменклатурныйСоставКонтрВРозницу.Скопировать(СтруктураОтбора);
			
			Если Не ЭтоНалоговаяНакладная Тогда
				ТаблицаНалоговыеОснованияДляП2ЗаДень = ТаблицаНалоговыеОснованияДляП2.Скопировать(СтруктураОтбора);	
			КонецЕсли;
			
			//СуммаНДС и СуммаСНДС заполнения номенклатурой по умолчанию в разрезе ставок
			ПосчитатьСуммуДополненияВРазрезеСтавок(ТаблицаДанныеРегистраЗаДень, ТаблицаНоменклатурныйСоставЗаДень);
			
			//Проверим корректны ли данные, есть ли смысл продолжать далее итерацию
			ПродолжаемИтерацию = Истина;
			Для каждого СтрокаДанныхРегистра Из ТаблицаДанныеРегистраЗаДень Цикл
				Если СтрокаДанныхРегистра.НекорректныеДанные Тогда
						
					Сообщить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='За %1 сумма по номенклатурному составу %2 по ставке НДС %3 в документе ""Отчет о розничных продажах"" больше принятой выручки на %4 (с НДС). %5 за этот день сформированы не будут!';uk='За %1 сума за номенклатурним складом %2 за ставкою ПДВ %3 в документі ""Звіт про роздрібні продажі"" більше ніж прийнята виручка на %4 (з ПДВ). %5 за цей день сформовані не будуть!'"), 
					Формат(ДатаДокумента, "ДФ=dd.MM.yyyy"), ?(ЭтоНалоговаяНакладная, НСтр("ru='продаж';uk='продажу'"), НСтр("ru='возвратов';uk='повернень'")), СтрокаДанныхРегистра.СтавкаНДС, -СтрокаДанныхРегистра.БазаСНДСДополнения,
					?(ЭтоНалоговаяНакладная, НСтр("ru='Налоговые накладные';uk='Податкові накладні'"), НСтр("ru='Корректировки к налоговым накладным (П2)';uk='Коригування до податкових накладних (Д2)'"))));	
												
					ПродолжаемИтерацию = Ложь;		
				КонецЕсли; 		
			КонецЦикла;
			Если Не ПродолжаемИтерацию Тогда
				Продолжить;	
			КонецЕсли; 
			
			//Попробуем найти и вычесть из номенклатурного состава номенклатуру, которая была выписана контрагенту в розницу, чтобы она не попала в ТЧ итоговой
			СуммаПоНомСоставуДоВычета 	 = ТаблицаНоменклатурныйСоставЗаДень.Итог("СуммаСНДС");
			СуммаПоНомСоставуКонтрагента = ТаблицаНоменклатурныйСоставКонтрВРозницуЗаДень.Итог("СуммаСНДС");
			
			ВычестьИзНоменклатурногоСоставаНоменклатуруКонтрВРозницу(ТаблицаНоменклатурныйСоставКонтрВРозницуЗаДень, ТаблицаНоменклатурныйСоставЗаДень);
			СуммаПоНомСоставуПослеВычета = ТаблицаНоменклатурныйСоставЗаДень.Итог("СуммаСНДС");
			
			//Проверим получилось ли у нас вычесть из номенклатурного склада весь ном. состав контраггентам в розницу 
			НевычтеннаяСумма = СуммаПоНомСоставуПослеВычета + СуммаПоНомСоставуКонтрагента - СуммаПоНомСоставуДоВычета;
			Если НевычтеннаяСумма > 0 Тогда
				Сообщить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='За %1 не удалось вычесть из номенклатурного состава для формирования %2 товары, которые выписаны в розницу плательщику НДС на сумму %3 (с НДС)';uk='За %1 не вдалося відняти з номенклатурного складу для формування %2 товари, які виписані в роздріб платнику ПДВ на суму %3 (з ПДВ)'"), 
					Формат(ДатаДокумента, "ДФ=dd.MM.yyyy"), ?(ЭтоНалоговаяНакладная, НСтр("ru='налоговых накладных';uk='податкових накладних'"), НСтр("ru='корректировок к НН (П2)';uk='коригування до ПН (Д2)'")),
					НевычтеннаяСумма));		
			КонецЕсли; 
			
			Если ЭтоНалоговаяНакладная Тогда
				
				//Если количество документов больше 1-го, то оставим только один, а остальные удалим
				КоличествоННДляПереформирования = ТаблицаНалоговыеДокументыЗаДень.Количество();
				Если КоличествоННДляПереформирования > 1 Тогда
					Для НомерСтроки = 1 По КоличествоННДляПереформирования - 1 Цикл
						Попытка
							НалоговыйДокумент = ТаблицаНалоговыеДокументыЗаДень[НомерСтроки].Документ.ПолучитьОбъект();
							НалоговыйДокумент.Записать(РежимЗаписиДокумента.ОтменаПроведения);
							НалоговыйДокумент.Удалить();
						Исключение
							Сообщить(ОписаниеОшибки());
							Возврат;
						КонецПопытки; 
					КонецЦикла; 
				КонецЕсли;  
				
				Если КоличествоННДляПереформирования > 0 Тогда
					НалоговыйДокумент = ТаблицаНалоговыеДокументыЗаДень[0].Документ.ПолучитьОбъект();
				Иначе
					НалоговыйДокумент = Документы.НалоговаяНакладная.СоздатьДокумент(); 
				КонецЕсли;
				
				//Возьмем первую строку данных регистра, чтобы определить вид операции документа
				СтрокаОсновныхПараметров = ТаблицаДанныеРегистраЗаДень[0];
				НалоговыйДокумент.ВидОперации = ОпределитьВидОперацииДокумента(СтрокаОсновныхПараметров, ЭтоНалоговаяНакладная);
				
				//Заполним шапку документа
				ЗаполнитьШапку(НалоговыйДокумент, , ДатаДокумента, Настройки);  
				
				НалоговыйДокумент.Товары.Очистить();
				НалоговыйДокумент.Услуги.Очистить(); 
				
				ЗаполнитьЗначенияСвойств(ДанныеОбъекта, НалоговыйДокумент, "ВидОперации, Дата, ТипПричиныНевыдачиПокупателю");
				
				//Формируем табличную часть документа
				Для каждого СтрокаНомСостава Из ТаблицаНоменклатурныйСоставЗаДень Цикл
					ДобавитьИЗаполнитьСтрокуТЧ(НалоговыйДокумент, СтрокаНомСостава, ЭтоНалоговаяНакладная,,,,,ДанныеОбъекта);	
				КонецЦикла; 
								
				Для каждого СтрокаДанныхРегистра Из ТаблицаДанныеРегистраЗаДень Цикл
					Если СтрокаДанныхРегистра.БазаСНДСДополнения > 0 Или СтрокаДанныхРегистра.СуммаНДСДополнения > 0 Тогда
						ДобавитьИЗаполнитьСтрокуТЧНаНедостающуюСумму(НалоговыйДокумент, ЭтоНалоговаяНакладная, ПараметрыПоУмолчанию,
							СтрокаДанныхРегистра.СуммаНДСДополнения, СтрокаДанныхРегистра.БазаСНДСДополнения, СтрокаДанныхРегистра["СтавкаНДС"], ДанныеОбъекта);			
					КонецЕсли; 		
				КонецЦикла;
				
				Если НеобходимоЗаписатьДокумент(НалоговыйДокумент) Тогда
					СвернутьТЧДокумента(НалоговыйДокумент, ЭтоНалоговаяНакладная);
					ОбработатьДокумент(НалоговыйДокумент);
					
					СтрокаСозданойНалоговойНакладной = ТаблицаСозданныхНалоговыхНакладных.Добавить();
					СтрокаСозданойНалоговойНакладной.Дата = ДатаДокумента;
					СтрокаСозданойНалоговойНакладной.Документ = НалоговыйДокумент.Ссылка;
					СтрокаСозданойНалоговойНакладной.ЭтоНалоговаяНакладная = ЭтоНалоговаяНакладная;
					СтрокаСозданойНалоговойНакладной.ОблагаемаяДеятельность = ?(СтрокаОсновныхПараметров["СтавкаНДС"] = Перечисления.СтавкиНДС.БезНДС, Ложь, Истина);
				КонецЕсли; 
				
			Иначе
				
				СоответствиеП2ИНалоговой = Новый Соответствие;
				КоличествоННДляПереформирования = ТаблицаНалоговыеДокументыЗаДень.Количество();
				КоличествоСозданныхДокументовП2 = 0;
				
				//Возьмем первую строку данных регистра, чтобы определить вид операции документа
				СтрокаОсновныхПараметров = ТаблицаДанныеРегистраЗаДень[0];
				
				Для каждого СтрокаНомСостава Из ТаблицаНоменклатурныйСоставЗаДень Цикл 
					
					//Сначала найдем все строки из таблицы-оснований, которые могут быть основанием для текущей строки
					СтруктураОтбораОснований = Новый Структура("Номенклатура, СтавкаНДС, ДокументПродажи, ЕдиницаИзмерения, РасчетнаяЦенаСНДС",
						СтрокаНомСостава.Номенклатура, СтрокаНомСостава.СтавкаНДС, СтрокаНомСостава.ДокументПродажи, 
						СтрокаНомСостава.ЕдиницаИзмерения, СтрокаНомСостава.РасчетнаяЦенаСНДС);	
						
						
					МассивСтрокОснований = ТаблицаНалоговыеОснованияДляП2ЗаДень.НайтиСтроки(СтруктураОтбораОснований); 
					
					КоличествоКРаспределению = СтрокаНомСостава.Количество;
					Для каждого СтрокаОснование Из МассивСтрокОснований Цикл
						
						Количество = Мин(КоличествоКРаспределению, СтрокаОснование.КоличествоНН); 
						
						НалоговыйДокумент = СоответствиеП2ИНалоговой.Получить(СтрокаОснование.НалоговаяОснование);
						Если НалоговыйДокумент = Неопределено Тогда
							//Это нужно для того, чтобы повторно использовать уже существующие документы, а не тратить ресурсы на удаление и создание новых
							НалоговыйДокумент = ПолучитьДокументП2ДляЗаполнения(КоличествоННДляПереформирования, КоличествоСозданныхДокументовП2, ТаблицаНалоговыеДокументыЗаДень, 
								СтрокаОсновныхПараметров, ЭтоНалоговаяНакладная, СтрокаОснование.НалоговаяОснование, ДатаДокумента, Настройки);
							СоответствиеП2ИНалоговой.Вставить(СтрокаОснование.НалоговаяОснование, НалоговыйДокумент);
						КонецЕсли; 
						
						ЗаполнитьЗначенияСвойств(ДанныеОбъекта, НалоговыйДокумент, "ВидОперации, Дата, ТипПричиныНевыдачиПокупателю");
						
						ДобавитьИЗаполнитьСтрокуТЧ(НалоговыйДокумент, СтрокаНомСостава, ЭтоНалоговаяНакладная, СтрокаОснование.КоличествоНН, 
							СтрокаОснование.СуммаНДСНН, СтрокаОснование.СуммаСНДСНН, Количество, ДанныеОбъекта);
						
						КоличествоКРаспределению = КоличествоКРаспределению - Количество;
						Если КоличествоКРаспределению = 0 Тогда
							Прервать;	
						КонецЕсли; 	
					КонецЦикла;    
					
					Если КоличествоКРаспределению <> 0 Тогда	
						Сообщить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='За %1 для товара: %2 в количестве %3 не найден документ ""Налоговая накладная"" по которому выполняется возврат. Этот товар не будет добавлен в документ ""Приложение 2 к налоговой накладной""!';uk='За %1 для товару: %2 в кількості %3 не знайдений документ ""Податкова накладна"" за яким здійснюється повернення. Цей товар не буде доданий у документ ""Додаток 2 до податкової накладної""!'"), 
						Формат(ДатаДокумента, "ДФ=dd.MM.yyyy"), Строка(СтрокаНомСостава.Номенклатура), КоличествоКРаспределению));
					КонецЕсли; 
					
				КонецЦикла;
				
				//Эту номенклатуру необходимо добавить в П2, которая была создана за этот день и с налоговой-основанием также за этот день
				Для каждого СтрокаДанныхРегистра Из ТаблицаДанныеРегистраЗаДень Цикл
					  
					Если СтрокаДанныхРегистра.БазаСНДСДополнения > 0 Или СтрокаДанныхРегистра.СуммаНДСДополнения > 0 Тогда
						
						СтруктураОтбораОснований = Новый Структура("Дата, ЭтоНалоговаяНакладная, ОблагаемаяДеятельность",
							ДатаДокумента, Истина, ?(СтрокаОсновныхПараметров["СтавкаНДС"] = Перечисления.СтавкиНДС.БезНДС, Ложь, Истина));
						
						МассивНалоговыхОснований = ТаблицаСозданныхНалоговыхНакладных.НайтиСтроки(СтруктураОтбораОснований);
						
						Если МассивНалоговыхОснований.Количество() = 0 Тогда
							Сообщить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='За %1 не найдена налоговая накладная по %2 облагаемому виду деятельности. Строка с номенклатурой автоматического заполнения НН на сумму %3 (с НДС) в документ ""Приложение 2 к налоговой накладной"" за %1 добавлена не будет!';uk='За %1 не знайдено податкову накладну за %2 оподатковуваним видом діяльності. Рядок з номенклатурою автоматичного заповнення ПН на суму %3 (з ПДВ) в документ ""Додаток 2 до податкової накладної"" за %1 додано не буде!'"), 
							Формат(ДатаДокумента, "ДФ=dd.MM.yyyy"), ?(СтрокаДанныхРегистра["СтавкаНДС"] = Перечисления.СтавкиНДС.БезНДС, "не", ""), СтрокаДанныхРегистра.БазаСНДСДополнения));
									
							Продолжить;	
						КонецЕсли;
						
						НалоговаяОснование = МассивНалоговыхОснований[0].Документ;
						//Сначала попробуем найти П2 среди созданных
						НалоговыйДокумент = СоответствиеП2ИНалоговой.Получить(НалоговаяОснование);
						Если НалоговыйДокумент = Неопределено Тогда
							НалоговыйДокумент = ПолучитьДокументП2ДляЗаполнения(КоличествоННДляПереформирования, КоличествоСозданныхДокументовП2, ТаблицаНалоговыеДокументыЗаДень, 
								СтрокаОсновныхПараметров, ЭтоНалоговаяНакладная, НалоговаяОснование, ДатаДокумента, Настройки);
							СоответствиеП2ИНалоговой.Вставить(НалоговаяОснование, НалоговыйДокумент);
						КонецЕсли;
						
						ДанныеОбъекта.ВидОперации = НалоговыйДокумент.ВидОперации;
						
						ДобавитьИЗаполнитьСтрокуТЧНаНедостающуюСумму(НалоговыйДокумент, ЭтоНалоговаяНакладная, ПараметрыПоУмолчанию,
							СтрокаДанныхРегистра.СуммаНДСДополнения, СтрокаДанныхРегистра.БазаСНДСДополнения, СтрокаДанныхРегистра["СтавкаНДС"], ДанныеОбъекта);			
					КонецЕсли;	
				КонецЦикла;
				
				Для каждого Элемент Из СоответствиеП2ИНалоговой Цикл
					НалоговыйДокумент = Элемент.Значение;
					Если НеобходимоЗаписатьДокумент(НалоговыйДокумент) Тогда
						СвернутьТЧДокумента(НалоговыйДокумент, ЭтоНалоговаяНакладная);
						ОбработатьДокумент(НалоговыйДокумент);
						
						СтрокаСозданойНалоговойНакладной = ТаблицаСозданныхНалоговыхНакладных.Добавить();
						СтрокаСозданойНалоговойНакладной.Дата = ДатаДокумента;
						СтрокаСозданойНалоговойНакладной.Документ = НалоговыйДокумент.Ссылка;
						СтрокаСозданойНалоговойНакладной.ЭтоНалоговаяНакладная = ЭтоНалоговаяНакладная;
					КонецЕсли; 		
				КонецЦикла;
				
				//Удалим документы, которые мы не использовали, если такие остались
				Если КоличествоННДляПереформирования > КоличествоСозданныхДокументовП2 Тогда
					Для НомерСтроки = КоличествоСозданныхДокументовП2 По КоличествоННДляПереформирования - 1 Цикл
						Попытка
							НалоговыйДокумент = ТаблицаНалоговыеДокументыЗаДень[НомерСтроки].Документ.ПолучитьОбъект();
							НалоговыйДокумент.Записать(РежимЗаписиДокумента.ОтменаПроведения);
							НалоговыйДокумент.Удалить();
						Исключение
							Сообщить(ОписаниеОшибки());
							Возврат;
						КонецПопытки; 
					КонецЦикла; 
				КонецЕсли;
				
			КонецЕсли; 
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьТаблицуЗначений(РезультатЗапроса, ИндексируемыеПоля = "")
	
	Таблица = РезультатЗапроса.Выгрузить();
	Если ТипЗнч(ИндексируемыеПоля) = Тип("Строка") И Не ПустаяСтрока(ИндексируемыеПоля) Тогда
		Таблица.Индексы.Добавить(ИндексируемыеПоля);		
	КонецЕсли; 
	
	Возврат Таблица;
	
КонецФункции

Процедура ВычестьИзНоменклатурногоСоставаНоменклатуруКонтрВРозницу(ТаблицаНомСоставКонтрВРозницу, ТаблицаНомСостав)
	
	МассивСтрокНомСоставаКУдалению = Новый Массив;
	Для каждого СтрокаНомСоставаКонтрВРозницу Из ТаблицаНомСоставКонтрВРозницу Цикл
		
		СтруктураОтбора = Новый Структура("Номенклатура, ЕдиницаИзмерения, СтавкаНДС, Обработан", 
			СтрокаНомСоставаКонтрВРозницу.Номенклатура, СтрокаНомСоставаКонтрВРозницу.ЕдиницаИзмерения, СтрокаНомСоставаКонтрВРозницу.СтавкаНДС, Ложь);
		МассивСтрокНомСостава = ТаблицаНомСостав.НайтиСтроки(СтруктураОтбора);
		
		КоличествоВычесть = СтрокаНомСоставаКонтрВРозницу.Количество;
		Для каждого СтрокаНомСостава Из МассивСтрокНомСостава Цикл
			
			//Зададим возможную разницу цен с дельтой +/-0.02
			РазницаЦен = СтрокаНомСоставаКонтрВРозницу.РасчетнаяЦенаСНДС - СтрокаНомСостава.РасчетнаяЦенаСНДС;
			//Найдем значение по модулю
			РазницаЦен = ?(РазницаЦен < 0, -РазницаЦен, РазницаЦен);
			Если РазницаЦен > 0.02 Тогда
				Продолжить;
			КонецЕсли; 
			
			КоличествоВычестьВИтерации = Мин(СтрокаНомСостава.Количество, КоличествоВычесть);
			
			Если КоличествоВычестьВИтерации = СтрокаНомСостава.Количество Тогда
				МассивСтрокНомСоставаКУдалению.Добавить(СтрокаНомСостава);
				СтрокаНомСостава.Обработан = Истина;
				
				СтрокаНомСоставаКонтрВРозницу.СуммаНДС 	= СтрокаНомСоставаКонтрВРозницу.СуммаНДС  - СтрокаНомСостава.СуммаНДС;
				СтрокаНомСоставаКонтрВРозницу.СуммаСНДС = СтрокаНомСоставаКонтрВРозницу.СуммаСНДС - СтрокаНомСостава.СуммаСНДС;
			Иначе
				СтрокаНомСостава.Количество = СтрокаНомСостава.Количество - КоличествоВычестьВИтерации;
				СтрокаНомСостава.СуммаНДС 	= СтрокаНомСостава.СуммаНДС   - СтрокаНомСоставаКонтрВРозницу.СуммаНДС;
				СтрокаНомСостава.СуммаСНДС 	= СтрокаНомСостава.СуммаСНДС  - СтрокаНомСоставаКонтрВРозницу.СуммаСНДС;
			КонецЕсли; 
		
			КоличествоВычесть = КоличествоВычесть - КоличествоВычестьВИтерации;	
			Если КоличествоВычесть = 0 Тогда
				Прервать;	
			КонецЕсли; 	
		КонецЦикла;  
	КонецЦикла;
	
	//Удалим строки, которые полностью перекрылись номенклатурой выписанной контрагенту в розницу
	Для каждого СтрокаНомСостава Из МассивСтрокНомСоставаКУдалению Цикл
		ТаблицаНомСостав.Удалить(СтрокаНомСостава);	
	КонецЦикла;	
	
КонецПроцедуры

Процедура ПосчитатьСуммуДополненияВРазрезеСтавок(ТаблицаДанныеРегистра, ТаблицаНоменклатурныйСостав)
		
	КопияНомСклада = ТаблицаНоменклатурныйСостав.Скопировать();
	КопияНомСклада.Свернуть("СтавкаНДС", "СуммаСНДС, СуммаНДС");
	
	Для каждого СтрокаДанныхРегистра Из ТаблицаДанныеРегистра Цикл
		СтрокаТЧНомСклада = КопияНомСклада.Найти(СтрокаДанныхРегистра.СтавкаНДС, "СтавкаНДС");
		Если СтрокаТЧНомСклада <> Неопределено Тогда
			Если СтрокаДанныхРегистра.БазаНДС + СтрокаДанныхРегистра.СуммаНДС < СтрокаТЧНомСклада.СуммаСНДС Тогда
				СтрокаДанныхРегистра.НекорректныеДанные = Истина;
			КонецЕсли;
			//БазаНДСДополнения будем считать сразу с НДС, чтобы потом еще раз не пересчитывать
			СтрокаДанныхРегистра.БазаСНДСДополнения = СтрокаДанныхРегистра.БазаНДС + СтрокаДанныхРегистра.СуммаНДС - СтрокаТЧНомСклада.СуммаСНДС;
			СтрокаДанныхРегистра.СуммаНДСДополнения = СтрокаДанныхРегистра.СуммаНДС - СтрокаТЧНомСклада.СуммаНДС; 
		Иначе
			//Возможно номенклатурный состав еще вовсе не введен
			СтрокаДанныхРегистра.БазаСНДСДополнения = СтрокаДанныхРегистра.БазаНДС + СтрокаДанныхРегистра.СуммаНДС;
			СтрокаДанныхРегистра.СуммаНДСДополнения = СтрокаДанныхРегистра.СуммаНДС;
		КонецЕсли; 
	КонецЦикла; 
	
КонецПроцедуры
 
Функция НеобходимоЗаписатьДокумент(НалоговыйДокумент)
	
	НеобходимоЗаписатьДокумент = Истина;
	
	Если НалоговыйДокумент.Товары.Количество() = 0 И НалоговыйДокумент.Услуги.Количество() = 0 Тогда
		Если Не НалоговыйДокумент.ЭтоНовый() Тогда
			Попытка
				НалоговыйДокумент.Удалить();
			Исключение
			    //ОписаниеОшибки()
			КонецПопытки;	
		КонецЕсли; 	
		
		НеобходимоЗаписатьДокумент = Ложь;
	КонецЕсли;
	
	Возврат НеобходимоЗаписатьДокумент;
	
КонецФункции

Функция ОпределитьВидОперацииДокумента(СтрокаОпределения, ЭтоНалоговаяНакладная)
	
	//Определим вид операции документа
	Если ЭтоНалоговаяНакладная Тогда
 		Если СтрокаОпределения.СтавкаНДС = Перечисления.СтавкиНДС.БезНДС Тогда
			ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.ИтоговаяРозницаОсвобожденныеОперации;
		Иначе
			ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.ИтоговаяРозницаОблагаемыеОперации;
		КонецЕсли;
	Иначе
		Если СтрокаОпределения.СтавкаНДС = Перечисления.СтавкиНДС.БезНДС Тогда
			ВидОперации = Перечисления.ВидыОперацийПриложение2КНалоговойНакладной.ИтоговаяРозницаОсвобожденныеОперацииВозврат;
		Иначе
			ВидОперации = Перечисления.ВидыОперацийПриложение2КНалоговойНакладной.ИтоговаяРозницаОблагаемыеОперацииВозврат;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ВидОперации;
	
КонецФункции

Процедура ДобавитьИЗаполнитьСтрокуТЧ(НалоговыйДокумент, СтрокаНомСостава, ЭтоНалоговаяНакладная, КоличествоВНН = 0, СуммаНДСВНН = 0, СуммаВНН = 0, ИзменениеКоличества = 1, ДанныеОбъекта)
	
	СтрокаТЧДокумента = НалоговыйДокумент.Товары.Добавить();
	
	//Заполним общие свойства для документов НН и П2, а также для ТЧ "Товары" и "Услуги"
	ЗаполнитьЗначенияСвойств(СтрокаТЧДокумента, СтрокаНомСостава); 
	
	Если Не ЭтоНалоговаяНакладная Тогда
		СтрокаТЧДокумента.Цена  	 = СуммаВНН / КоличествоВНН;
		СтрокаТЧДокумента.Количество = КоличествоВНН;
		СтрокаТЧДокумента.СуммаНДС   = СуммаНДСВНН;
		СтрокаТЧДокумента.Сумма      = СуммаВНН;
		СтрокаТЧДокумента.СуммаБезСкидки = СуммаВНН;
		СтрокаТЧДокумента.ИзменениеКоличества 	= -ИзменениеКоличества;
		СтрокаТЧДокумента.ИзменениеСуммы 		= -СтрокаНомСостава.СуммаСНДС / СтрокаНомСостава.Количество * ИзменениеКоличества;
		СтрокаТЧДокумента.ИзменениеСуммыНДС 	= -СтрокаНомСостава.СуммаНДС  / СтрокаНомСостава.Количество * ИзменениеКоличества;
		
		//Еще одна дополнительная проверка и при необходимости кооректировка суммы и суммы НДС, 
		//если сумма возврата по П2 будет больше выписанных обязательств в НН, то шлюз налоговой такую П2 не примет
		//Да, в таком случае будут зависать копейки, но пользователь сам должен решить, что с ними делать 
		Если -СтрокаТЧДокумента.ИзменениеСуммы > СтрокаТЧДокумента.Сумма Тогда
			СтрокаТЧДокумента.ИзменениеСуммы = - СтрокаТЧДокумента.Сумма; 		
		КонецЕсли;
		Если -СтрокаТЧДокумента.ИзменениеСуммыНДС > СтрокаТЧДокумента.СуммаНДС Тогда
			СтрокаТЧДокумента.ИзменениеСуммыНДС = - СтрокаТЧДокумента.СуммаНДС; 		
		КонецЕсли;
	Иначе
		СтрокаТЧДокумента.Цена  = СтрокаНомСостава.РасчетнаяЦенаСНДС;
		СтрокаТЧДокумента.Сумма = СтрокаНомСостава.СуммаСНДС;
		СтрокаТЧДокумента.СуммаБезСкидки = СтрокаНомСостава.СуммаСНДС;
	КонецЕсли; 
	
	Если ЭтоНалоговаяНакладная Тогда
		Документы.НалоговаяНакладная.ЗаполнитьСтатьюДекларацииНДСНалоговыеОбязательства(ДанныеОбъекта, СтрокаТЧДокумента, "Товары", Неопределено);
		Документы.НалоговаяНакладная.ОбновитьЗначениеЛьготыНДС(НалоговыйДокумент, СтрокаТЧДокумента);
	Иначе
		Документы.Приложение2КНалоговойНакладной.ЗаполнитьСтатьюДекларацииНДСНалоговыеОбязательства(ДанныеОбъекта, СтрокаТЧДокумента, "Товары");
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьИЗаполнитьСтрокуТЧНаНедостающуюСумму(НалоговыйДокумент, ЭтоНалоговаяНакладная, ПараметрыПоУмолчанию, СуммаНДС, СуммаСНДС, СтавкаНДС, ДанныеОбъекта)
	
	СтрокаТЧДокумента = НалоговыйДокумент.Товары.Добавить();
	СтрокаТЧДокумента.Номенклатура = ПараметрыПоУмолчанию.Номенклатура;
	СтрокаТЧДокумента.ЕдиницаИзмерения = ПараметрыПоУмолчанию.ЕдиницаИзмерения;
	СтрокаТЧДокумента.СтавкаНДС = СтавкаНДС;
	СтрокаТЧДокумента.КодУКТВЭД = ПараметрыПоУмолчанию.КодУКТВЭД;
	СтрокаТЧДокумента.Количество = 1;
	
	Если ЭтоНалоговаяНакладная Тогда
		
		СтрокаТЧДокумента.Коэффициент = 1;
		СтрокаТЧДокумента.СуммаНДС = СуммаНДС;
		СтрокаТЧДокумента.Сумма = СуммаСНДС;
		СтрокаТЧДокумента.Цена = СтрокаТЧДокумента.Сумма;
		СтрокаТЧДокумента.СуммаБезСкидки = СуммаСНДС;
	Иначе	
		СтрокаТЧДокумента.ИзменениеСуммыНДС = -СуммаНДС;
		СтрокаТЧДокумента.ИзменениеСуммы = -СуммаСНДС;
	КонецЕсли;
	
	Если ЭтоНалоговаяНакладная Тогда
		Документы.НалоговаяНакладная.ЗаполнитьСтатьюДекларацииНДСНалоговыеОбязательства(ДанныеОбъекта, СтрокаТЧДокумента, "Товары", Неопределено);
		Документы.НалоговаяНакладная.ОбновитьЗначениеЛьготыНДС(НалоговыйДокумент, СтрокаТЧДокумента);
	Иначе
		Документы.Приложение2КНалоговойНакладной.ЗаполнитьСтатьюДекларацииНДСНалоговыеОбязательства(ДанныеОбъекта, СтрокаТЧДокумента, "Товары");
	КонецЕсли;; 
	
КонецПроцедуры

Процедура СвернутьТЧДокумента(НалоговыйДокумент, ЭтоНалоговаяНакладная)
	
	Если ЭтоНалоговаяНакладная Тогда
		НалоговыйДокумент.Товары.Свернуть("Номенклатура, ЕдиницаИзмерения, Коэффициент, Цена, СтавкаНДС, СчетУчетаНДС, ДатаОтгрузкиОплаты, СтатьяДекларацииНДСНалоговыеОбязательства,
			|ЦенаОбычная, КодУКТВЭД, НомерГТД", "Количество, СуммаНДС, Сумма, СуммаПревышения, СуммаНДСПревышения, СуммаБезСкидки, СуммаСкидки");
	Иначе
		НалоговыйДокумент.Товары.Свернуть("Номенклатура, ЕдиницаИзмерения, Коэффициент, Цена, СтавкаНДС, ИзменениеЦены, Причина, ДатаКорректировки, СтатьяДекларацииНДСНалоговыеОбязательства,
			|НомерСтрокиНН, КодУКТВЭД, НомерГТД, Количество, СуммаНДС, Сумма, СуммаБезСкидки, СуммаСкидки", "ИзменениеКоличества, ИзменениеСуммы, ИзменениеСуммыНДС");
	КонецЕсли; 
	
КонецПроцедуры
 
Функция ПолучитьДокументП2ДляЗаполнения(КоличествоННДляПереформирования, КоличествоСозданныхДокументовП2, ТаблицаНалоговыеДокументыЗаДень, СтрокаОсновныхПараметров,
	ЭтоНалоговаяНакладная, НалоговаяОснование, ДатаДокумента, Настройки)
	
	Если КоличествоННДляПереформирования > КоличествоСозданныхДокументовП2 Тогда
		НалоговыйДокумент = ТаблицаНалоговыеДокументыЗаДень[КоличествоСозданныхДокументовП2].Документ.ПолучитьОбъект();
		НалоговыйДокумент.Товары.Очистить();
		НалоговыйДокумент.Услуги.Очистить();
	Иначе
		НалоговыйДокумент = Документы.Приложение2КНалоговойНакладной.СоздатьДокумент();
	КонецЕсли;
	
	НалоговыйДокумент.ВидОперации = ОпределитьВидОперацииДокумента(СтрокаОсновныхПараметров, ЭтоНалоговаяНакладная);
	ЗаполнитьШапку(НалоговыйДокумент, НалоговаяОснование, ДатаДокумента, Настройки);
	
	КоличествоСозданныхДокументовП2 = КоличествоСозданныхДокументовП2 + 1;
	
	Возврат НалоговыйДокумент;
	
КонецФункции

#КонецЕсли
