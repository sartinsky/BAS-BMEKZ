////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("КлючНастроек",	КлючНастроек);
	
	Обработка = РеквизитФормыВЗначение("Объект");
	Попытка
		Макет = Обработка.ПолучитьМакет(КлючНастроек);
	Исключение
		Отказ = Истина;
		Возврат;
	КонецПопытки;
	
	ТекстПредупреждения = Макет.ПолучитьТекст();
	Заголовок = ?(Найти(КлючНастроек,"БыстроеОсвоение") > 0, НСтр("ru='Быстрое освоение';uk='Швидке освоєння'"), НСтр("ru='Внимание!';uk='Увага!'"));
	
	Если НЕ ПустаяСтрока(КлючНастроек) Тогда
		
		БольшеНеПоказывать = ХранилищеОбщихНастроек.Загрузить("Обработка.ПредупреждениеОбИзменениях", КлючНастроек);
		
		Если БольшеНеПоказывать <> Неопределено И БольшеНеПоказывать Тогда
			Отказ	= Истина;
		КонецЕсли;
		
	Иначе
		Элементы.КнопкаБольшеНеПоказывать.Видимость	= Ложь;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ТектсПредупрежденияПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Ссылка = ДанныеСобытия.Href;
	Если НЕ ПустаяСтрока(Ссылка) Тогда
		// Сначала проверяем особые случаи
		Если Найти(Ссылка, "ПерсональныеНастройки") Тогда
			// Отметим, чтобы предупреждение об изменениях не показывалось второй раз
			ПараметрыФормы = Новый Структура();
			ПараметрыФормы.Вставить("ПоказыватьПредупреждениеОбИзменениях", Ложь);
			
			ОткрытьФорму("ОбщаяФорма.ПерсональныеНастройки", ПараметрыФормы);
		ИначеЕсли Найти(Ссылка, "ПараметрыУчета") Тогда
			
			МассивПодстрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Ссылка, "#");
			ИмяЗакладки = ?(МассивПодстрок.Количество() < 2, "", МассивПодстрок[МассивПодстрок.ВГраница()]);
			
			ФормаНастройки = ПолучитьФорму("ОбщаяФорма.ПараметрыУчета");
			Если НЕ ПустаяСтрока(ИмяЗакладки) Тогда
				ФормаНастройки.Элементы.ГруппаСтраницы.ТекущаяСтраница = ФормаНастройки.Элементы[ИмяЗакладки];
				ФормаНастройки.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
			КонецЕсли;
			ФормаНастройки.Открыть();
			
		Иначе
			ПерейтиПоНавигационнойСсылке(Ссылка);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервереБезКонтекста
Процедура СохранитьЗначениеБольшеНеПоказыватьНаСервере(КлючНастроек, БольшеНеПоказывать)
	
	ХранилищеОбщихНастроек.Сохранить("Обработка.ПредупреждениеОбИзменениях", КлючНастроек, БольшеНеПоказывать);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// КОМАНДЫ ФОРМЫ

&НаКлиенте
Процедура НажатиеБольшеНеПоказывать(Команда)
	
	Если НЕ ПустаяСтрока(КлючНастроек) Тогда
		
		СохранитьЗначениеБольшеНеПоказыватьНаСервере(КлючНастроек, Истина);
		
	КонецЕсли;
	Закрыть();
	
КонецПроцедуры


&НаКлиенте
Процедура НажатиеПоказатьПозже(Команда)
	
	Закрыть();
	
КонецПроцедуры

