
&НаКлиенте
Процедура ВыполнитьJSON(Команда)
	
	ВыполнитьJSONСервер();
	
КонецПроцедуры // ВыполнитьJSON()

&НаСервере
Процедура ВыполнитьJSONСервер()
	
	Модуль = РеквизитФормыВЗначение("Объект");
	
	Попытка
		Объект.Исходящий = Модуль.ЗаписатьJSON82(Модуль.ПрочитатьJSON82(Объект.Входящий, Объект.ВходящийРежим), Объект.ИсходящийРежим);
		Объект.НекорректныеДанные = Ложь;
	Исключение
		Объект.Исходящий = ИнформацияОбОшибке().Описание;
		Объект.НекорректныеДанные = Истина;
	КонецПопытки;
	
КонецПроцедуры // ВыполнитьJSONСервер()


&НаКлиенте
Процедура ВходящийРежимОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

КонецПроцедуры // ВходящийРежимОчистка()

&НаКлиенте
Процедура ИсходящийРежимОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

КонецПроцедуры // ИсходящийРежимОчистка()


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Объект.Входящий = ПолучитьТекст();
	Объект.ВходящийРежим = Неопределено;
	Объект.ИсходящийРежим = Истина;
	
	Элементы.ВходящийРежим.СписокВыбора.Добавить(Неопределено, "Автоопределение");
	Элементы.ВходящийРежим.СписокВыбора.Добавить(Истина, "Стандарт");
	Элементы.ВходящийРежим.СписокВыбора.Добавить(Ложь, "Альтернативный");
	
	Элементы.ИсходящийРежим.СписокВыбора.Добавить(Истина, "Стандарт");
	Элементы.ИсходящийРежим.СписокВыбора.Добавить(Ложь, "Альтернативный");
	
КонецПроцедуры // ПриСозданииНаСервере()


&НаСервере
Функция ПолучитьТекст()
	
	Модуль = РеквизитФормыВЗначение("Объект");
	
	Данные = Модуль.ПрочитатьJSON82(Модуль.ПолучитьМакет("Макет").ПолучитьТекст(), Истина);
	
	МассивЧисел = Данные["COMSafeArray"];
	
	
	// COMSafeArray.
	COMSafeArray = Новый COMSafeArray(МассивЧисел, "VT_I4");
	
	// СписокЗначений.
	СписокЗначений = Новый СписокЗначений;
	Для Каждого Число Из МассивЧисел Цикл
		СписокЗначений.Добавить(Число, ЧислоПрописью(Число, "Л=en_US; НП=Ложь;", ",,,, 0"), Число % 2);
	КонецЦикла;
	
	// КлючИЗначение.
	Структура = Новый Структура("Key", "Value");
	Для Каждого КлючИЗначение Из Структура Цикл
		Прервать;
	КонецЦикла;
	
	// ТаблицаЗначений.
	ТаблицаЗначений = Новый ТаблицаЗначений;
	ТаблицаЗначений.Колонки.Добавить("Выражение");
	ТаблицаЗначений.Колонки.Добавить("Результат");
	Для Каждого Число Из МассивЧисел Цикл
		СтрокаТаблицы = ТаблицаЗначений.Добавить();
		СтрокаТаблицы.Выражение = Строка(Число) + " × " + Строка(Число);
		СтрокаТаблицы.Результат = Число * Число;
	КонецЦикла;
						  
	// ДеревоЗначений.
	ДеревоЗначений = Новый ДеревоЗначений;
	ДеревоЗначений.Колонки.Добавить("Номер");
	СтрокаДерева = ДеревоЗначений.Строки.Добавить();
	СтрокаДерева.Номер = "Номер 1";
	СтрокаДерева = СтрокаДерева.Строки.Добавить();
	СтрокаДерева.Номер = "Номер 1.1";
	СтрокаДерева = ДеревоЗначений.Строки.Добавить();
	СтрокаДерева.Номер = "Номер 2";
	СтрокаДерева = СтрокаДерева.Строки.Добавить();
	СтрокаДерева.Номер = "Номер 2.1";
	
	// РезультатЗапроса.
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	1 КАК Номер,
	                      |	""Первая строка результата запроса"" КАК Значение
	                      |
	                      |ОБЪЕДИНИТЬ ВСЕ
	                      |
	                      |ВЫБРАТЬ
	                      |	2,
	                      |	""Вторая строка результата запроса""");
	РезультатЗапроса = Запрос.Выполнить();
	
	// РезультатЗапроса.
	ВыборкаИзРезультатаЗапроса = РезультатЗапроса.Выбрать();
	ВыборкаИзРезультатаЗапроса.Следующий();
	
	// Запрос.
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	""Запрос..."" КАК КтоЗдесь";
	
	// ПостроительЗапроса.
	ПостроительЗапроса = Новый ПостроительЗапроса;
	ПостроительЗапроса.Текст = 
		"ВЫБРАТЬ
		|	""Построитель запроса..."" КАК КтоЗдесь";
	
	// ПостроительЗапроса.
	ПостроительОтчета = Новый ПостроительОтчета;
	ПостроительОтчета.Текст = 
		"ВЫБРАТЬ
		|	""Построитель отчета..."" КАК КтоЗдесь";
							   
	// ДвоичныеДанные.
	#Если НаСервере Тогда
	ДвоичныеДанные = Base64Значение("Qk1iAAAAAAAAAD4AAAAoAAAACQAAAAkAAAABAAEAAAAAACQAAADEDgAAxA4AAAAAAAAAAAAAAAAAAP///wD+gAAA/AAAAMCAAAC5gAAAfYAAAH2AAAB9gAAAu4AAAMeAAAA=");
	#Иначе
	ДвоичныеДанные = БиблиотекаКартинок.Лупа.ПолучитьДвоичныеДанные();
	#КонецЕсли
	
	// Картинка.
	#Если НаСервере Тогда
	Картинка = Новый Картинка(Base64Значение("Qk1iAAAAAAAAAD4AAAAoAAAACQAAAAkAAAABAAEAAAAAAAAAAAAAAAAAAAAAAAIAAAACAAAAAAAAAP///wD/gOQAnIDkAMmAAADjgP//44AzAMmAZgCcgAAA/4AAAP+A5AA="));
	#Иначе
	Картинка = БиблиотекаКартинок.Очистить;
	#КонецЕсли
	
	// ХранилищеЗначения.
	ХранилищеЗначения = Новый ХранилищеЗначения("Сохраненное в хранилище значение.");
	
	
	Данные.Вставить("СписокЗначений", СписокЗначений);
	Данные.Вставить("КлючИЗначение", КлючИЗначение);
	Данные.Вставить("ТаблицаЗначений", ТаблицаЗначений);
	Данные.Вставить("ДеревоЗначений", ДеревоЗначений);
	Данные.Вставить("Запрос", Запрос);
	Данные.Вставить("РезультатЗапроса", РезультатЗапроса);
	Данные.Вставить("ВыборкаИзРезультатаЗапроса", ВыборкаИзРезультатаЗапроса);
	Данные.Вставить("ПостроительЗапроса", ПостроительЗапроса);
	Данные.Вставить("ПостроительОтчета", ПостроительОтчета);
	Данные.Вставить("COMSafeArray", COMSafeArray);
	Данные.Вставить("ДвоичныеДанные", ДвоичныеДанные);
	Данные.Вставить("Картинка", Картинка);
	Данные.Вставить("ХранилищеЗначения", ХранилищеЗначения);
	
	Возврат Модуль.ЗаписатьJSON82(Данные, Истина);
	
КонецФункции // ПолучитьТекст()
