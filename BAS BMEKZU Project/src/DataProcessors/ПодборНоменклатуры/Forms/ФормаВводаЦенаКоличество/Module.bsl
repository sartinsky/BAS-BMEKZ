#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СписокСвойств = "Номенклатура, Количество, ЕдиницаИзмерения, Цена, Валюта,
		|ЕстьКоличество, ЕстьЦена";
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, СписокСвойств);
	
	Если НЕ ЕстьКоличество И НЕ ЕстьЦена Тогда
		Отказ = Истина;
	КонецЕсли;
	
	СвойстваНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Номенклатура, "Наименование, БазоваяЕдиницаИзмерения");
	
	Заголовок = СвойстваНоменклатуры.Наименование;
	Если НЕ ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
		ЕдиницаИзмерения = СвойстваНоменклатуры.БазоваяЕдиницаИзмерения;
	КонецЕсли;
	Коэффициент = ПолучитьКоэффициентЕдиницыИзмеренияСервер(Номенклатура,ЕдиницаИзмерения);
	
	Элементы.Количество.Видимость       = ЕстьКоличество;
	Элементы.ЕдиницаИзмерения.Видимость = ЕстьКоличество;
	
	Элементы.Цена.Видимость         = ЕстьЦена;
	Элементы.ВалютаЦены.Видимость   = ЕстьЦена;
	Элементы.Сумма.Видимость        = ЕстьЦена;
	Элементы.ВалютаСуммы.Видимость  = ЕстьЦена;
	
	Сумма = Цена * Количество;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если НЕ ЕстьКоличество Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Количество");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовШапки

&НаКлиенте
Процедура КоличествоПриИзменении(Элемент)
	
	Сумма = Цена * Количество;
	
КонецПроцедуры

&НаКлиенте
Процедура ЦенаПриИзменении(Элемент)
	
	Сумма = Цена * Количество;
	
КонецПроцедуры

&НаКлиенте
Процедура ЕдиницаИзмеренияПриИзменении(Элемент)
	
	СтарыйКоэффициент = Коэффициент;
	Коэффициент = ПолучитьКоэффициентЕдиницыИзмеренияСервер(Номенклатура, ЕдиницаИзмерения);
	Если СтарыйКоэффициент <> 0 Тогда
		Цена = Цена * Коэффициент / СтарыйКоэффициент;
		ЦенаПриИзменении(Элемент);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКоманд

&НаКлиенте
Процедура ОК(Команда)
	
	Отказ = НЕ ПроверитьЗаполнение();
	
	Если НЕ Отказ Тогда
		ПараметрЗакрытия = Новый Структура("Номенклатура, Количество, Цена, Валюта, ЕдиницаИзмерения, Коэффициент");
		ЗаполнитьЗначенияСвойств(ПараметрЗакрытия, ЭтотОбъект);
		Закрыть(ПараметрЗакрытия);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьКоэффициентЕдиницыИзмеренияСервер(Номенклатура,ЕдиницаИзмерения)

	Возврат Справочники.Номенклатура.ПолучитьКоэффициентЕдиницыИзмерения(Номенклатура,ЕдиницаИзмерения);

КонецФункции // ПолучитьКоэффициентЕдиницыИзмерения()


#КонецОбласти 