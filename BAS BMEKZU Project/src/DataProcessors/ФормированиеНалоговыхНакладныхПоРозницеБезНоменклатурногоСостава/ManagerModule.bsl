#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ЗаполнитьШапку(Настройки, Документ, НалоговаяНакладная = Неопределено)

	Документ.Дата 		 = КонецДня(Настройки.Дата)-2;
	Документ.Организация = Настройки.Организация;
	
	Если ТипЗнч(Документ) = Тип("ДокументОбъект.НалоговаяНакладная") Тогда
		Документ.ОбособленноеПодразделение = Настройки.ОбособленноеПодразделение;
	КонецЕсли;
	
	Документ.ВалютаДокумента = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	Документ.СуммаВключаетНДС 	= Истина;
	Документ.АвторасчетНДС 		= Ложь;
	
	Документ.Ответственный 				  = Пользователи.ТекущийПользователь();
	Документ.КтоВыписалНалоговуюНакладную = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("КтоВыписалНалоговуюНакладную");	
	
	Документ.СчетНДС 		= ПланыСчетов.Хозрасчетный.РасчетыПоНДС;
	Документ.СчетУчетаНДС 	= ПланыСчетов.Хозрасчетный.НалоговыеОбязательстваРозница;
	
	Документы.НалоговаяНакладная.УстановитьТипПричиныНевыдачиПокупателюПоУмолчанию(Документ);
	
	Если НЕ НалоговаяНакладная = Неопределено Тогда
	    // Свяжем Налоговую накладную и Приложение 2
		Документ.НалоговаяНакладная = НалоговаяНакладная.Ссылка;
	
	КонецЕсли;
	
	Документ.ФормаРасчетов = Настройки.ФормаРасчетов;
	
КонецПроцедуры

Процедура ОбработатьДокумент(Настройки, Документ, СписокСозданныхДокументов)

	
	Если Настройки.ПроводитьДокументы Тогда
		Попытка
			Документ.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			Документ.Записать();
		КонецПопытки;
		
		
	Иначе
		Документ.Записать();
	КонецЕсли;

	СписокСозданныхДокументов.Добавить(Документ.Ссылка);
	
КонецПроцедуры

Функция СформироватьНалоговыеДокументыПоРознице(Настройки) Экспорт
	
	Перем ДокументОблагаемые, 		 ДокументОсвобожденные;
	Перем ДокументОблагаемыеВозврат, ДокументОсвобожденныеВозврат;
	
	СписокСозданныхДокументов = Новый Массив();
	
	МоментПолученияДанных = Новый Граница(КонецДня(Настройки.Дата), ВидГраницы.Включая);
	
	// Облагаемые НДС операции
	ДокументОблагаемые = Документы.НалоговаяНакладная.СоздатьДокумент();
	
	ДокументОблагаемые.ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.ИтоговаяРозницаОблагаемыеОперации;
	ЗаполнитьШапку(Настройки, ДокументОблагаемые);
	
	ДокументОблагаемые.ЗаполнитьПоОстаткам(ДокументОблагаемые.Товары, МоментПолученияДанных);
	Если ДокументОблагаемые.Товары.Количество() = 0 Тогда
		ДокументОблагаемые = Неопределено;
	Иначе
		ОбработатьДокумент(Настройки, ДокументОблагаемые, СписокСозданныхДокументов);
	КонецЕсли;
	
	// Освобожденные от НДС операции
	ДокументОсвобожденные = Документы.НалоговаяНакладная.СоздатьДокумент();
	
	ДокументОсвобожденные.ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.ИтоговаяРозницаОсвобожденныеОперации;
	ЗаполнитьШапку(Настройки, ДокументОсвобожденные);
	
	ДокументОсвобожденные.ЗаполнитьПоОстаткам(ДокументОсвобожденные.Товары, МоментПолученияДанных);
	Если ДокументОсвобожденные.Товары.Количество() = 0 Тогда
		ДокументОсвобожденные = Неопределено;
	Иначе
		ОбработатьДокумент(Настройки, ДокументОсвобожденные, СписокСозданныхДокументов);
	КонецЕсли;
	
	
	// Облагаемые НДС операции (возврат)
	ДокументОблагаемыеВозврат = Документы.Приложение2КНалоговойНакладной.СоздатьДокумент();
	
	ДокументОблагаемыеВозврат.ВидОперации = Перечисления.ВидыОперацийПриложение2КНалоговойНакладной.ИтоговаяРозницаОблагаемыеОперацииВозврат;
	ЗаполнитьШапку(Настройки, ДокументОблагаемыеВозврат, ДокументОблагаемые);
	
	ДокументОблагаемыеВозврат.ЗаполнитьПоОстаткам(ДокументОблагаемыеВозврат.Товары, МоментПолученияДанных);
	Если ДокументОблагаемыеВозврат.Товары.Количество() = 0 Тогда
		ДокументОблагаемыеВозврат = Неопределено;
	Иначе
		ОбработатьДокумент(Настройки, ДокументОблагаемыеВозврат, СписокСозданныхДокументов);
	КонецЕсли;
	
	
	// Освобожденные от НДС операции возврат
	ДокументОсвобожденныеВозврат = Документы.Приложение2КНалоговойНакладной.СоздатьДокумент();
	
	ДокументОсвобожденныеВозврат.ВидОперации = Перечисления.ВидыОперацийПриложение2КНалоговойНакладной.ИтоговаяРозницаОсвобожденныеОперацииВозврат;
	ЗаполнитьШапку(Настройки, ДокументОсвобожденныеВозврат, ДокументОсвобожденные);
	
	ДокументОсвобожденныеВозврат.ЗаполнитьПоОстаткам(ДокументОсвобожденныеВозврат.Товары, МоментПолученияДанных);
	Если ДокументОсвобожденныеВозврат.Товары.Количество() = 0 Тогда
		ДокументОсвобожденныеВозврат = Неопределено;
	Иначе
		ОбработатьДокумент(Настройки, ДокументОсвобожденныеВозврат, СписокСозданныхДокументов);
	КонецЕсли;
	
	Возврат СписокСозданныхДокументов;
	
КонецФункции

#КонецЕсли
