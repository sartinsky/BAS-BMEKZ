&НаКлиенте
Перем ПараметрыОбработчикаОжидания;

&НаКлиенте
Перем ФормаДлительнойОперации;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Объект.Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация"); 
	
	Если НЕ ЗначениеЗаполнено(Объект.ДатаНачала) ИЛИ НЕ ЗначениеЗаполнено(Объект.ДатаОкончания) Тогда
		УстановитьПериодПерепроведения();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	УстановитьПериодПерепроведения();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьПериод(Команда)
	
	ПараметрыВыбора = Новый Структура("НачалоПериода, КонецПериода", Объект.ДатаНачала, Объект.ДатаОкончания);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериода", ПараметрыВыбора, Элементы.ВыбратьПериод, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ДатаНачала	 = РезультатВыбора.НачалоПериода;
	Объект.ДатаОкончания = РезультатВыбора.КонецПериода;
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаВыполнить(Команда)
	
	ИБФайловая = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиента().ИнформационнаяБазаФайловая;
	Результат = ЗапуститьПерепроведениеДокументов(ИБФайловая);
	
	Если Результат <> Неопределено Тогда
		// Результат получен и уже обработан на стороне сервера
		ПоказатьРезультатПерепроведения(Результат);
	ИначеЕсли ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		// Результата еще нет, но есть шансы дождаться
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 1, Истина);
		ФормаДлительнойОперации = ДлительныеОперацииКлиент.ОткрытьФормуДлительнойОперации(ЭтаФорма, ИдентификаторЗадания);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьПериодПерепроведения()

	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		МассивОрганизаций = Новый Массив;
		МассивОрганизаций.Добавить(Объект.Организация);
	Иначе
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Организации.Ссылка
		|ИЗ
		|	Справочник.Организации КАК Организации";
		МассивОрганизаций = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	КонецЕсли; 
	
	Объект.ДатаОкончания = ОбщегоНазначения.ТекущаяДатаПользователя();
	Объект.ДатаНачала = Объект.ДатаОкончания;
	ТекстСостояния = "";
	
	Для каждого Организация  Из МассивОрганизаций Цикл
		ТекстСостояния = ТекстСостояния + ?(ЗначениеЗаполнено(ТекстСостояния),Символы.ПС,"");
		МоментНарушенияПоследовательности = РаботаСПоследовательностями.МоментНачалаПерепроведенияСУчетомИсключенныхМесяцев(Организация, Объект.ДатаОкончания);
		Если МоментНарушенияПоследовательности <> Неопределено Тогда
			Объект.ДатаНачала = Мин(Объект.ДатаНачала,МоментНарушенияПоследовательности.Дата);
			ТекстСостояния = ТекстСостояния + 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Необходимо выполнить перепроведение документов с %1 по организации ""%2""';uk='Необхідно виконати перепроведення документів з %1 за організацією ""%2""'"),
			Формат(МоментНарушенияПоследовательности.Дата,"ДФ='дд ММММ гггг'"),Организация);
		КонецЕсли; 
	КонецЦикла; 
	
	Если Объект.ДатаНачала = Объект.ДатаОкончания Тогда
		Если ЗначениеЗаполнено(Объект.Организация) Тогда
			ТекстСостояния = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Перепроведение документов по организации %1 не требуется';uk='Перепроведення документів по організації %1 не потрібне'"), Объект.Организация);	
		Иначе
			ТекстСостояния = НСтр("ru='Перепроведение документов не требуется';uk='Перепроведення документів не потрібне'");		
		КонецЕсли; 
		Элементы.ДекорацияТекстСостояния.Заголовок = ТекстСостояния;
		Элементы.ДекорацияТекстСостояния.ЦветТекста = ЦветаСтиля.РезультатУспехЦвет;
	Иначе
		Элементы.ДекорацияТекстСостояния.Заголовок = ТекстСостояния;
		Элементы.ДекорацияТекстСостояния.ЦветТекста = ЦветаСтиля.РезультатПроблемаЦвет;
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Функция ЗапуститьПерепроведениеДокументов(ИБФайловая)
	
	Если НЕ ИБФайловая Тогда
		// Возможно, что фоновое задание было запущено раньше, 
		// пользователь дал команду его отменить, однако задание не отменено.
		// В таком случае не следует запускать задание повторно - следует дождаться его выполнения.
		// Мы можем отследить ситуацию только, если все это происходит в одной форме.
		// Потому что подсистема ДлительныеОперации не умеет устанавливать ключ фонового задания.
		Если ЗакрытиеМесяца.ЗаданиеЕщеВыполняется(ИдентификаторЗадания) Тогда
			// Надо ждать
			Возврат Неопределено;
		КонецЕсли;
	Конецесли;

	ИдентификаторЗадания = Неопределено;
	
	ПараметрыПерепроведения = Обработки.ИНАГРО_ГрупповоеПерепроведениеДокументов.ПараметрыПерепроведения();
	
	ПараметрыПерепроведения.Организация 	= Объект.Организация;
	ПараметрыПерепроведения.МоментНачала 	= Новый МоментВремени(Объект.ДатаНачала, Неопределено);
	ПараметрыПерепроведения.ДатаОкончания	= Объект.ДатаОкончания;
	ПараметрыПерепроведения.УникальныйИдентификаторФормы 	= УникальныйИдентификатор;
	ПараметрыПерепроведения.ОстанавливатьсяПоОшибке 		= Объект.ОстанавливатьсяПоОшибке;
	
	Результат = Обработки.ИНАГРО_ГрупповоеПерепроведениеДокументов.ЗапуститьПерепроведение(ПараметрыПерепроведения);

	Если Результат = Неопределено Тогда
		Результат = Новый Структура();
		Результат.Вставить("ПоказатьПредупреждение", НСтр("ru='Перепроведение уже выполняется другим пользователем.';uk='Перепроведення вже виконується іншим користувачем.'"));
		Возврат Результат;
	КонецЕсли;
	
	АдресХранилища       = Результат.АдресХранилища;
	ИдентификаторЗадания = Результат.ИдентификаторЗадания;
	
	Если Не Результат.ЗаданиеВыполнено Тогда
		// Надо ждать
		Возврат Неопределено;
	Иначе
		// Задание выполнено.
		// Результат выполнения возвращен через хранилище значения.
		// Загрузим его в форму и передадим данные на клиент.
		
		Возврат ОбновитьДанныеОперацийПоРезультатуДлительнойОперации();
	
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗадания()

	Попытка
		Если ЗаданиеВыполнено(ИдентификаторЗадания) Тогда			
			ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
			ПоказатьРезультатПерепроведения(ОбновитьДанныеОперацийПоРезультатуДлительнойОперации());
		Иначе
			ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
			ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания",
				ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
		КонецЕсли;
	Исключение
		ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
		ВызватьИсключение;
	КонецПопытки;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаданиеВыполнено(ИдентификаторЗадания)
	
	Возврат ДлительныеОперации.ЗаданиеВыполнено(ИдентификаторЗадания);
	
КонецФункции

&НаКлиенте
Процедура ПоказатьРезультатПерепроведения(Результат)

	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если Результат.Свойство("ПоказатьПредупреждение") Тогда
		ПоказатьПредупреждение(, Результат.ПоказатьПредупреждение);
	КонецЕсли;

	Если Результат.Свойство("ПроведеноДокументов")
		И Результат.Свойство("НеУдалосьПровести")
		И Результат.Свойство("АдресХранилищаСОшибками") Тогда

		Если Результат.НеУдалосьПровести = 0 Тогда
			Если Объект.ВыводитьИнформационныеСообщения И ЗначениеЗаполнено(Результат.АдресХранилищаСОшибками) Тогда
				ОбщегоНазначенияБПКлиент.ОткрытьФормуОшибокПерепроведения(ЭтотОбъект, Результат.АдресХранилищаСОшибками);
			Иначе	
				ТекстСообщения = НСтр("ru='Выполнено перепроведение документов:
		| - проведено документов: %1;
		| - ошибок не обнаружено';uk='Виконано перепроведення документів:
		|- проведено документів: %1;
		|- помилок не виявлено'");
				
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ТекстСообщения, Результат.ПроведеноДокументов);
				
				ПоказатьПредупреждение(, ТекстСообщения);
				
			КонецЕсли;
			
		Иначе

			// Покажем подробный отчет об ошибках.
			ОбщегоНазначенияБПКлиент.ОткрытьФормуОшибокПерепроведения(ЭтотОбъект, Результат.АдресХранилищаСОшибками);

		КонецЕсли;
	
	КонецЕсли;
	
	УстановитьПериодПерепроведения();	
	
КонецПроцедуры

&НаСервере
Функция ОбновитьДанныеОперацийПоРезультатуДлительнойОперации()
	
	// Результат закрытия подготовлен в 
	// Обработки.ИНАГРО_ГрупповоеПерепроведениеДокументов.ПерепроведениеДокументов()
	Результат = ПолучитьИзВременногоХранилища(АдресХранилища);
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// Окончание группового перепроведения, разблокируем захваченную организацию.
	// Она блокируется при старте группового перепроведения в привязке к идентификатору формы,
	// из которой вызывано перепроведение.
	Обработки.ИНАГРО_ГрупповоеПерепроведениеДокументов.ПослеОкончанияПерепроведения(УникальныйИдентификатор);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти