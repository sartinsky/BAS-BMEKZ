#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Весовщик      = Пользователи.ТекущийПользователь();
	ДатаНачала    = НачалоДня(ТекущаяДата());
	ДатаОкончания = КонецДня(ТекущаяДата());
	
	ПоказатьДокументыПоВсемВесовщикам = ХранилищеОбщихНастроек.Загрузить("ИНАГРО_ПомощникВесовщика_ПоказатьДокументыПоВсемВесовщикам", "Элеватор");
	ЗаполнениеИзПриказов              = ХранилищеОбщихНастроек.Загрузить("ИНАГРО_ПомощникВесовщика_ЗаполнениеИзПриказов",              "Элеватор");
	
	ОбновитьНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы И Модифицированность Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;

	ПередЗакрытиемНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗакрытиемНаСервере()
	
	ХранилищеОбщихНастроек.Сохранить("ИНАГРО_ПомощникВесовщика_ПоказатьДокументыПоВсемВесовщикам", "Элеватор", ПоказатьДокументыПоВсемВесовщикам);
	ХранилищеОбщихНастроек.Сохранить("ИНАГРО_ПомощникВесовщика_ЗаполнениеИзПриказов",              "Элеватор", ЗаполнениеИзПриказов);

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ИсточникВыбора.ИмяФормы = "Документ.ИНАГРО_ПриказНаВывоз.Форма.ФормаВыбораДляПомощникаВесовщика" Тогда		
		ОбработкаВыбораЗавершение(ВыбранноеЗначение);				         
	КонецЕсли;

КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПоказатьДокументыПоВсемВесовщикамПриИзменении(Элемент)
	
	ОбновитьНаСервере();

КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовТаблицФормы

&НаКлиенте
Процедура ТаблицаВвозЖДВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	ТекущиеДанные = Элементы.ТаблицаВвозЖД.ТекущиеДанные;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", ТекущиеДанные.ТТН);

	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ТаблицаВвозВыборЖДЗавершение", ЭтотОбъект, Параметры);
			
	ОткрытьФорму("Документ.ИНАГРО_ТТНВвозЖД.Форма.ФормаДокумента", ПараметрыФормы, , УникальныйИдентификатор, , , ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ТаблицаВвозВыборЖДЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	ЗаполнитьТаблицаВвозЖД();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаВывозЖДВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	ТекущиеДанные = Элементы.ТаблицаВывозЖД.ТекущиеДанные;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", ТекущиеДанные.ТТН);

	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ТаблицаВывозВыборЖДЗавершение", ЭтотОбъект, Параметры);
			
	ОткрытьФорму("Документ.ИНАГРО_ТТНВывозЖД.Форма.ФормаДокумента", ПараметрыФормы, , УникальныйИдентификатор, , , ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ТаблицаВывозВыборЖДЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	ЗаполнитьТаблицаВывозЖД();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы 

&НаКлиенте
Процедура ВыбратьПериод(Команда)

	ПараметрыВыбора = Новый Структура("НачалоПериода, КонецПериода", ДатаНачала, ДатаОкончания);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериода", ПараметрыВыбора, Элементы.ВыбратьПериод, , , , ОписаниеОповещения);
	
КонецПроцедуры 

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДатаНачала	  = РезультатВыбора.НачалоПериода;
	ДатаОкончания = РезультатВыбора.КонецПериода;		
		
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ОбновитьНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьНаСервере()
	
	ЗаполнитьТаблицаВвозЖД();
	ЗаполнитьТаблицаВывозЖД();

КонецПроцедуры

&НаКлиенте
Процедура НовыйВагонВвоз(Команда)
	
	ПараметрыФормы = Новый Структура;
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("НовыйВагонВвозЖДЗавершение", ЭтотОбъект, Параметры);
			
	ОткрытьФорму("Документ.ИНАГРО_ТТНВвозЖД.Форма.ФормаДокумента", ПараметрыФормы, , УникальныйИдентификатор, , , ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура НовыйВагонВвозЖДЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
		
	ЗаполнитьТаблицаВвозЖД();
	
КонецПроцедуры

&НаКлиенте
Процедура НовыйВагонВывоз(Команда)
	
	Если ЗаполнениеИзПриказов Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ДатаОкончания",      НачалоДня(ДатаОкончания));
		ПараметрыФормы.Вставить("ВидПеревозки",       ПредопределенноеЗначение("Перечисление.ИНАГРО_ВидыПеревозки.Железнодорожный"));
		ПараметрыФормы.Вставить("РежимВыбора",        Истина); 
		ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Истина);
				
		ОткрытьФорму("Документ.ИНАГРО_ПриказНаВывоз.Форма.ФормаВыбораДляПомощникаВесовщика", ПараметрыФормы, ЭтаФорма); 
		
	Иначе
		
		ПараметрыФормы = Новый Структура;
		
		ОповещениеОЗакрытии = Новый ОписаниеОповещения("НовыйВагонВывозЖДЗавершение", ЭтотОбъект, Параметры);
		
		ОткрытьФорму("Документ.ИНАГРО_ТТНВывозЖД.Форма.ФормаДокумента", ПараметрыФормы, , УникальныйИдентификатор, , , ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура НовыйВагонВывозЖДЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
		
	ЗаполнитьТаблицаВывозЖД();
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТаблицаВвозЖД()
	
	ТаблицаВвозЖД.Очистить();
	
	Выборка = Документы.ИНАГРО_ТТНВвозЖД.Выбрать(ДатаНачала, ДатаОкончания);
	
	Пока Выборка.Следующий() Цикл
		
		Если НЕ Выборка.ПометкаУдаления Тогда
			
			Если НЕ Выборка.Проведен Тогда
							
				Если Выборка.Весовщик <> Весовщик Тогда
					Если НЕ ПоказатьДокументыПоВсемВесовщикам Тогда
						Продолжить;
					КонецЕсли;
				КонецЕсли;
				
				НоваяСтрока = ТаблицаВвозЖД.Добавить();
				НоваяСтрока.Владелец       = Выборка.Владелец;
				НоваяСтрока.НомерВагона    = Выборка.НомерВагона;
				НоваяСтрока.НомерДокумента = ?(Выборка.НомерНакладной = "", Выборка.Номер, Выборка.НомерНакладной);					
				НоваяСтрока.ВремяПрибытия  = Выборка.ВремяПрибытия;
				НоваяСтрока.ТТН = Выборка.Ссылка;
				Если Выборка.ВесБрутто = 0 Тогда	
					НоваяСтрока.Статус = НСтр("ru='Нет веса!';uk='Немає ваги!'");
				ИначеЕсли Выборка.ВесТары = 0 Тогда
					НоваяСтрока.Статус = НСтр("ru='Нет тары!';uk='Немає тари!'");
				Иначе
					НоваяСтрока.Статус = НСтр("ru='Не проведен!';uk='Не проведений!'");
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицаВывозЖД()
	
	ТаблицаВывозЖД.Очистить();
	
	Выборка = Документы.ИНАГРО_ТТНВывозЖД.Выбрать(ДатаНачала, ДатаОкончания);
	
	Пока Выборка.Следующий() Цикл
		
		Если НЕ Выборка.ПометкаУдаления  Тогда
			
			Если НЕ Выборка.Проведен Тогда				
								
				Если Выборка.Весовщик <> Весовщик Тогда
					Если НЕ ПоказатьДокументыПоВсемВесовщикам Тогда
						Продолжить;
					КонецЕсли;
				КонецЕсли;				
				
				НоваяСтрока = ТаблицаВывозЖД.Добавить();				
				НоваяСтрока.Владелец       = Выборка.Владелец;
				НоваяСтрока.НомерВагона    = Выборка.НомерВагона;
				НоваяСтрока.НомерДокумента = Выборка.Номер;						
				НоваяСтрока.ВремяПрибытия  = Выборка.ВремяПрибытия;
				НоваяСтрока.ТТН            = Выборка.Ссылка;					
				Если Выборка.ВесБрутто = 0 Тогда	
					НоваяСтрока.Статус = НСтр("ru='Нет веса!';uk='Немає ваги!'");
				ИначеЕсли Выборка.ВесТары = 0 Тогда
					НоваяСтрока.Статус = НСтр("ru='Нет тары!';uk='Немає тари!'");
				Иначе
					НоваяСтрока.Статус = НСтр("ru='Не проведен!';uk='Не проведений!'");
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораЗавершение(ВыбранноеЗначение)
	
	ПараметрыФормы = Новый Структура;	
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ПолучитьЗначенияЗаполнения(ВыбранноеЗначение)); 

	ОповещениеОЗакрытии = Новый ОписаниеОповещения("НовыйВагонВывозЖДЗавершение", ЭтотОбъект, Параметры);
	
	ОткрытьФорму("Документ.ИНАГРО_ТТНВывозЖД.Форма.ФормаДокумента", ПараметрыФормы, , УникальныйИдентификатор, , , ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьЗначенияЗаполнения(ВыбранноеЗначение)
	
	ЗначенияЗаполнения = Новый Структура;

	ПриказНаВывоз      = ВыбранноеЗначение.Ссылка;
	ЛабораторныйАнализ = ВыбранноеЗначение.ЛабораторныйАнализ;

	ЗначенияЗаполнения.Вставить("Ссылка",             ПриказНаВывоз);
	ЗначенияЗаполнения.Вставить("Организация",        ПриказНаВывоз.Организация);
	ЗначенияЗаполнения.Вставить("Владелец",           ПриказНаВывоз.Владелец);
	ЗначенияЗаполнения.Вставить("ДоговорКонтрагента", ПриказНаВывоз.ДоговорКонтрагента);	
	ЗначенияЗаполнения.Вставить("Склад",              ПриказНаВывоз.Склад);
	ЗначенияЗаполнения.Вставить("МестоХранения",      ПриказНаВывоз.МестоХранения);	
	ЗначенияЗаполнения.Вставить("ВидХранения",        ПриказНаВывоз.ВидХранения);
	ЗначенияЗаполнения.Вставить("Номенклатура",       ВыбранноеЗначение.Номенклатура);
	ЗначенияЗаполнения.Вставить("Урожай",             ПриказНаВывоз.Урожай);
	Если ЛабораторныйАнализ <> Неопределено Тогда
		ЗначенияЗаполнения.Вставить("ЛабораторныйАнализ", ЛабораторныйАнализ);
		ЗначенияЗаполнения.Вставить("НомерАнализа",       ЛабораторныйАнализ.Номер);
		ЗначенияЗаполнения.Вставить("Влажность",          ЛабораторныйАнализ.Влажность);
		ЗначенияЗаполнения.Вставить("СорнаяПримесь",      ЛабораторныйАнализ.СорнаяПримесь);
		ЗначенияЗаполнения.Вставить("ЗерноваяПримесь",    ЛабораторныйАнализ.ЗерноваяПримесь);				
	КонецЕсли;
	ЗначенияЗаполнения.Вставить("Комментарий",         ПриказНаВывоз.Комментарий);
	
	Возврат ЗначенияЗаполнения;
	
КонецФункции

#КонецОбласти