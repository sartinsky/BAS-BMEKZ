#Область ОписаниеПеременных

// СтандартныеПодсистемы

// Хранилище глобальных переменных.
//
// ПараметрыПриложения - Соответствие - хранилище переменных, где:
//   * Ключ - Строка - имя переменной в формате "ИмяБиблиотеки.ИмяПеременной";
//   * Значение - Произвольный - значение переменной.
//
// Инициализация (на примере СообщенияДляЖурналаРегистрации):
//   ИмяПараметра = "СтандартныеПодсистемы.СообщенияДляЖурналаРегистрации";
//   Если ПараметрыПриложения[ИмяПараметра] = Неопределено Тогда
//     ПараметрыПриложения.Вставить(ИмяПараметра, Новый СписокЗначений);
//   КонецЕсли;
//  
// Использование (на примере СообщенияДляЖурналаРегистрации):
//   ПараметрыПриложения["СтандартныеПодсистемы.СообщенияДляЖурналаРегистрации"].Добавить(...);
//   ПараметрыПриложения["СтандартныеПодсистемы.СообщенияДляЖурналаРегистрации"] = ...;
Перем ПараметрыПриложения Экспорт;// ПараметрыПриложения - Соответствие - хранилище переменных.

// Конец СтандартныеПодсистемы

Перем глМенеджерЗвит1С   Экспорт; // Для работы Звит1С.
Перем глКомпонентаЗвит1С Экспорт; // Для работы Звит1С.

// ИНАГРО РаботаСВнешнимОборудованием 
Перем глПодключаемоеОборудование Экспорт; // Для кэширования на клиенте.
Перем глДоступныеТипыОборудования Экспорт; // Доступные типы оборудования клиента.
Перем глВесовогоОборудования Экспорт; // Текущий объект оборудования. 
// ИНАГРО Конец РаботаСВнешнимОборудованием 

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередНачаломРаботыСистемы(Отказ)
	
	// СтандартныеПодсистемы
	СтандартныеПодсистемыКлиент.ПередНачаломРаботыСистемы();
	// Конец СтандартныеПодсистемы
	
	// ИнтернетПоддержкаПользователей
	ИнтернетПоддержкаПользователейКлиент.ПередНачаломРаботыСистемы();
	// Конец ИнтернетПоддержкаПользователей
	
КонецПроцедуры

Процедура ПриНачалеРаботыСистемы()
	
	// СтандартныеПодсистемы
	СтандартныеПодсистемыКлиент.ПриНачалеРаботыСистемы();
	// Конец СтандартныеПодсистемы
	
	// ПроверитьСоответствиеЯзыкаПлатформыИПользователя
	Локализация.ПроверитьСоответствиеЯзыкаПлатформыИПользователя();
	
	РегламентированнаяОтчетностьКлиент.ПодключитьМенеджерЗвит1С(Ложь); 
	
КонецПроцедуры

Процедура ПередЗавершениемРаботыСистемы(Отказ, ТекстПредупреждения)
	
	// СтандартныеПодсистемы
	СтандартныеПодсистемыКлиент.ПередЗавершениемРаботыСистемы(Отказ, ТекстПредупреждения);
	// Конец СтандартныеПодсистемы
	
	Если Не Отказ И глМенеджерЗвит1С <> Неопределено Тогда
		глМенеджерЗвит1С.ЗавершитьЗвит1С(Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаВнешнегоСобытия(Источник, Событие, Данные)
	
	Если ВРег(Источник) = Врег("Zvit1C") Тогда
		
		Если Не РегламентированнаяОтчетностьКлиент.ПодключитьМенеджерЗвит1С() Тогда
			Возврат;
		КонецЕсли;

		глМенеджерЗвит1С.ОбработкаВнешнегоСобытияЗвит1С(Источник, Событие, Данные);	
		
		Возврат;
		
	КонецЕсли;
	
	// ИНАГРО РаботаСВнешнимОборудованием
	// Подготовить данные
	ОписаниеСобытия = Новый Структура();
	ОписаниеОшибки  = "";
	
	ОписаниеСобытия.Вставить("Источник", Источник);
	ОписаниеСобытия.Вставить("Событие",  Событие);
	ОписаниеСобытия.Вставить("Данные",   Данные);
	
	// Передать на обработку данные
	Результат = ИНАГРО_МенеджерОборудованияКлиент.ОбработатьСобытиеОтУстройства(ОписаниеСобытия, ОписаниеОшибки);
	Если Не Результат Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='При обработке внешнего события от устройства произошла ошибка.';uk='При обробці зовнішньої події від пристрою сталася помилка.'")
		                                                 + Символы.ПС + ОписаниеОшибки);
	КонецЕсли;
	// ИНАГРО Конец РаботаСВнешнимОборудованием
	
КонецПроцедуры

#КонецОбласти

