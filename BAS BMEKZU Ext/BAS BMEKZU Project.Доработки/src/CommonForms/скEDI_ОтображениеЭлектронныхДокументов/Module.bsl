//added by Al
&НаСервере
Процедура ПодготовитьИРазместитьНетиповыеЭлементыНаФорме()
	                                                                         
	//создаем команды и их кнопки
	СМС_ДФИ_КлиентСервер.СоздатьКоманду(ЭтаФорма, "ПодобратьДокумент1СИПривязатьКЭлектронномуДокументу", 
						НСтр("ru = 'Подобрать учетный документ'; uk = 'Підібрати обліковий документ'"), 
						"ПодобратьДокумент1СИПривязатьКЭлектронномуДокументу",);
	СтруктураПараметровЭлемента = Новый Структура("Отображение, Картинка", ОтображениеКнопки.Картинка, БиблиотекаКартинок.ВыбратьЗначение);	
	СМС_ДФИ_КлиентСервер.СздКнопка(ЭтаФорма,"ПодобратьДокумент1СИПривязатьКЭлектронномуДокументу",
								   Элементы.ГруппаГоризонтальнаяДокумент1С,"Выбрать",
								   "ПодобратьДокумент1СИПривязатьКЭлектронномуДокументу",1, СтруктураПараметровЭлемента,
								   ЭтаФорма.Элементы.ПровестиДокумент1С);
	
КонецПроцедуры
//added by Al

&НаСервере
Процедура СМС_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	ПодготовитьИРазместитьНетиповыеЭлементыНаФорме();
	
КонецПроцедуры

&ИзменениеИКонтроль("УстановитьДоступностьКнопокКомандФормы")
&НаКлиенте
Процедура СМС_УстановитьДоступностьКнопокКомандФормы()	
	Если ЕстьДанныеЭлектронногоДокумента Тогда
		лМассивДанныхДокументов = Новый Массив;
		
		лСтруктураДанныхДокумента = Новый Структура();
		лСтруктураДанныхДокумента.Вставить("Дата", ПолучитьРеквизитОбъектаНаСервере(ЭлектронныйДокумент, "Дата"));
		лСтруктураДанныхДокумента.Вставить("ВидЭлектронногоДокумента",        ВидЭлектронногоДокумента);
		лСтруктураДанныхДокумента.Вставить("ТипДокумента",                    ТипДокумента);
		лСтруктураДанныхДокумента.Вставить("Документ1С",                      Документ1С);
		лСтруктураДанныхДокумента.Вставить("ЭлектронныйДокумент",             ЭлектронныйДокумент);
		лСтруктураДанныхДокумента.Вставить("ВхИсх",                           ВхИсх);
		лСтруктураДанныхДокумента.Вставить("ГдеСоздан",                       ГдеСоздан);
		лСтруктураДанныхДокумента.Вставить("Состояние",                       СостояниеЭлектронногоДокумента);
		лСтруктураДанныхДокумента.Вставить("СостояниеРегистрацияВДФС",        СостояниеЭлектронногоДокументаРегистрацияВДФС);
		лСтруктураДанныхДокумента.Вставить("ЕстьФинальнаяКвитанцияДФС",       ЕстьФинальнаяКвитанцияДФС);
		лСтруктураДанныхДокумента.Вставить("ВидДокумента1С",                  скEDI_НастройкиПодКонфигурацию.ПолучитьВидДокумента1С(Документ1С));
		лСтруктураДанныхДокумента.Вставить("РегистрируетсяВЕРННПокупателем",  РегистрируетсяВЕРННПокупателем);
		Если ЗначениеЗаполнено(ЭлектронныйДокумент) И Не ЗначениеЗаполнено(Документ1С) Тогда
			лСоответствиеДокументов = ПредопределенноеЗначение("Перечисление.скEDI_СоответствиеДокументов1СиЭлектронныхДокументов.ЕстьЭлектронныйДокументНетДокумент1С");
		ИначеЕсли ЗначениеЗаполнено(Документ1С) И Не ЗначениеЗаполнено(ЭлектронныйДокумент) Тогда
			лСоответствиеДокументов = ПредопределенноеЗначение("Перечисление.скEDI_СоответствиеДокументов1СиЭлектронныхДокументов.ЕстьДокумент1СнетЭлектронногоДокумента");
		Иначе
			лСоответствиеДокументов = ПредопределенноеЗначение("Перечисление.скEDI_СоответствиеДокументов1СиЭлектронныхДокументов.ЕстьДокумент1СиЭлектронныйДокумент");
		КонецЕсли;
		лСтруктураДанныхДокумента.Вставить("СоответствиеДокументов",          лСоответствиеДокументов);
		лИнформацияОНеобходимостиПроведенияДокумента1С = скEDI_НастройкиПодКонфигурацию.ПолучитьИнформациюОНеобходимостиПроведенияДокумента1С(Документ1С);
		лСтруктураДанныхДокумента.Вставить("НеобходимоПроведениеДокумента1С", лИнформацияОНеобходимостиПроведенияДокумента1С.НеобходимоПроведение);
		лСтруктураДанныхДокумента.Вставить("Документ1СПроведен",              лИнформацияОНеобходимостиПроведенияДокумента1С.Проведен);
		
		лСтруктураДанныхДокумента.Вставить("ЭтоОсновнойВариант",              ЭтоОсновнойВариант);
		лСтруктураДанныхДокумента.Вставить("ОшибкаШифрованияПриОтправке",     ОшибкаШифрованияПриОтправке);
		
		лМассивДанныхДокументов.Добавить(лСтруктураДанныхДокумента);
		лДоступныеКоманды = скEDI_ОбщегоНазначенияКлиент.ПолучитьДоступныеКоманды(лМассивДанныхДокументов);
		
		Элементы.ФормаПодписатьЭлектронныйДокумент.Доступность                            = лДоступныеКоманды.Подписать;
		Элементы.ФормаПодписатьИОтправитьЭлектронныйДокумент.Доступность                  = лДоступныеКоманды.ПодписатьИОтправить;
		Элементы.ПодписатьИОтправитьЭлектронныйДокументТолькоСсылка.Доступность           = лДоступныеКоманды.ПодписатьИОтправитьТолькоСсылку;
		Элементы.ФормаОтправитьНаДоподписаниеЭлектронныйДокумент.Доступность              = лДоступныеКоманды.ОтправитьНаДоподписание;
		Элементы.ФормаПодписатьИОтправитьЭлектронныйДокументВДФС.Доступность              = лДоступныеКоманды.ПодписатьИОтправитьВДФС;
		Элементы.ФормаПодтвердитьИОтправитьОтветНаВходящийЭлектронныйДокумент.Доступность = лДоступныеКоманды.Подтвердить;
		Элементы.ФормаОтклонитьВходящийЭлектронныйДокумент.Доступность                    = лДоступныеКоманды.Отклонить;
		Элементы.ФормаОтозватьЭлектронныйДокумент.Доступность                             = лДоступныеКоманды.Отозвать;
		Элементы.ФормаАннулироватьЭлектронныйДокумент.Доступность                         = лДоступныеКоманды.Аннулировать;
		Элементы.СоздатьДокумент1СИПривязатьКЭлектронномуДокументу.Доступность            = лДоступныеКоманды.СоздатьДокумент1С;
		Элементы.НайтиДокумент1С.Доступность                                              = лДоступныеКоманды.НайтиДокумент1С;
		Элементы.ПровестиДокумент1С.Доступность                                           = лДоступныеКоманды.ПровестиДокумент1С;
		Элементы.СоздатьНовыйЭлектронныйДокумент.Видимость                                = лДоступныеКоманды.СоздатьНовыйЭлектронныйДокумент; 
		Элементы.ФормаСнятьПодписиСЭлектронногоДокумента.Доступность                      = лДоступныеКоманды.СнятьПодписи;
		Элементы.ФормаПроверитьЭлектронныйДокумент.Доступность                            = лДоступныеКоманды.Проверить;
		Элементы.ФормаПодписатьИОтправитьЭлектронныйДокумент_Переподписать.Доступность    = лДоступныеКоманды.ПовторноеУтверждение;
		Элементы.ФормаПодписатьИОтправитьЭлектронныйДокумент_Переподписать.Видимость      = лДоступныеКоманды.ПовторноеУтверждение;
		Если СостояниеЭлектронногоДокумента = ПредопределенноеЗначение("Перечисление.скEDI_СостоянияЭлектронныхДокументов.Отправлен")
			или СостояниеЭлектронногоДокумента = ПредопределенноеЗначение("Перечисление.скEDI_СостоянияЭлектронныхДокументов.ПолученоСообщениеОДоставке") Тогда
			
			Если ЗначениеЗаполнено(ИмяФайлаСДО) Тогда
				Элементы.ОтправкаСсылкиНаДокументОтправить.Доступность = Истина;
			Иначе
				Элементы.ОтправкаСсылкиНаДокументОтправить.Доступность = Ложь;
			КонецЕсли;
		Иначе
			Элементы.ОтправкаСсылкиНаДокументОтправить.Доступность = Ложь;
		КонецЕсли;
		Элементы.ФормаРедактироватьЭлектронныйДокумент.Доступность = лДоступныеКоманды.ОтредактироватьДокумент;
		Если ЭлектроннаяФорма_IsMultisided Тогда
			Элементы.ФормаРедактироватьЭлектронныйДокумент.Доступность = Ложь;
		КонецЕсли;
		Если ДоступноОтменитьРедактированиеЭлектронногоДокумента Тогда
			Элементы.ФормаОтменитьРедактированиеЭлектронногоДокумента.Видимость = Истина;
			Элементы.ФормаРедактироватьЭлектронныйДокумент.Видимость = Истина;
		Иначе
			Элементы.ФормаОтменитьРедактированиеЭлектронногоДокумента.Видимость = Ложь;
			Если Элементы.ФормаРедактироватьЭлектронныйДокумент.Доступность Тогда
				Элементы.ФормаРедактироватьЭлектронныйДокумент.Видимость = Истина;
			Иначе
				Элементы.ФормаРедактироватьЭлектронныйДокумент.Видимость = Ложь;
			КонецЕсли;
		КонецЕсли;
		#Вставка
		Элементы.ПодобратьДокумент1СИПривязатьКЭлектронномуДокументу.Доступность      = лДоступныеКоманды.ВыбратьУчетныйДокумент;
		#КонецВставки
	Иначе
		Элементы.ФормаПодписатьЭлектронныйДокумент.Доступность = Ложь;
		Элементы.ФормаПодписатьИОтправитьЭлектронныйДокумент.Доступность = Ложь;
		Элементы.ПодписатьИОтправитьЭлектронныйДокументТолькоСсылка.Доступность = Ложь;
		Элементы.ФормаОтправитьНаДоподписаниеЭлектронныйДокумент.Доступность = Ложь;
		Элементы.ФормаПодписатьИОтправитьЭлектронныйДокументВДФС.Доступность = Ложь;
		Элементы.ФормаПодтвердитьИОтправитьОтветНаВходящийЭлектронныйДокумент.Доступность = Ложь;
		Элементы.ФормаОтклонитьВходящийЭлектронныйДокумент.Доступность = Ложь;
		Элементы.ФормаОтозватьЭлектронныйДокумент.Доступность = Ложь;
		Элементы.ФормаАннулироватьЭлектронныйДокумент.Доступность = Ложь;
		Элементы.СоздатьДокумент1СИПривязатьКЭлектронномуДокументу.Доступность = Ложь;
		Элементы.НайтиДокумент1С.Доступность = Ложь;
		Элементы.ПровестиДокумент1С.Доступность = Ложь;
		Элементы.СоздатьНовыйЭлектронныйДокумент.Видимость = Ложь; 
		Элементы.ФормаСнятьПодписиСЭлектронногоДокумента.Доступность = Ложь;
		Элементы.ФормаПроверитьЭлектронныйДокумент.Доступность = Ложь;
		Элементы.ОтправкаСсылкиНаДокументОтправить.Доступность = Ложь;
		Элементы.ФормаРедактироватьЭлектронныйДокумент.Доступность = Ложь;
		Элементы.ФормаОтменитьРедактированиеЭлектронногоДокумента.Видимость = Ложь;
		Элементы.ФормаРедактироватьЭлектронныйДокумент.Видимость = Ложь;
		#Вставка
		Элементы.ПодобратьДокумент1СИПривязатьКЭлектронномуДокументу.Доступность = Ложь;
		#КонецВставки
	КонецЕсли;
	
	Если ОтображенияЭлектронногоДокументаВозможноПереключение Тогда
		Элементы.ВложенияОтображатьЭлектронныйДокумент.Видимость = Истина;
		Элементы.ВложенияОтображать.Видимость = Истина;
		Если ОтображенияЭлектронногоДокументаИндекс = 0 Тогда
			Элементы.ВложенияОтображатьЭлектронныйДокумент.Пометка = Истина;
		Иначе
			Элементы.ВложенияОтображатьЭлектронныйДокумент.Пометка = Ложь;
		КонецЕсли;
	Иначе
		Элементы.ВложенияОтображатьЭлектронныйДокумент.Видимость = Ложь;
		Элементы.ВложенияОтображать.Видимость = Ложь;
	КонецЕсли;
КонецПроцедуры

//added by Al
&НаКлиенте
Процедура ПодобратьДокумент1СИПривязатьКЭлектронномуДокументу(Команда)
	
	Если ЗначениеЗаполнено(ЭлектронныйДокумент) Тогда
		
		ПараметрыДляФОрмыВыбора = Новый Структура;
		ПараметрыДляФОрмыВыбора.Вставить("Контрагент", Неопределено);
		ПараметрыДляФОрмыВыбора.Вставить("ТипДок",Неопределено);
		
		ЭтоИсходящий = Ложь;
		ПараметрыПроверки = Новый Структура("ЭлектронныйДокумент", ЭлектронныйДокумент);		
		Если скEDI_ОбщегоНазначенияПолныеПрава.ОпределитьДокументНакладнаяНаСервере(ПараметрыПроверки, ПараметрыДляФОрмыВыбора) Тогда
			
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("Контрагент", ПараметрыДляФОрмыВыбора.Контрагент);
			 
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Отбор", ПараметрыОтбора);
			
			ОповещениеВыбора = Новый ОписаниеОповещения("ЗавершитьПодборДокумента1С", ЭтаФорма); 			
			ОткрытьФорму("Документ." +ПараметрыДляФОрмыВыбора.ТипДок + ".ФормаВыбора", ПараметрыФормы, ЭтаФорма,,,,ОповещениеВыбора,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
		Иначе
			
			ТекстСообщения = НСтр("ru = 'Для данного вида документа не предусмотрено сопоставление с учетным документом для подтверждения с нашей стороны'; uk = 'Для даного виду документа не передбачено співставлення з обліковми документом для підтвердження з нашого боку'");
			скEDI_ОбщегоНазначенияКлиент.ПоказатьПредупреждение_(ТекстСообщения);
			
		КонецЕсли;	
			
	КонецЕсли;
	
КонецПроцедуры

//added by Al
&НаСервере
Процедура УстановитьПодобранныйДокументНаСервере(Док1С)
	
	лЭлектронныйДокументОбъект = ЭлектронныйДокумент.ПолучитьОбъект();
	лЭлектронныйДокументОбъект.Документ1С = Док1С;
	лЭлектронныйДокументОбъект.Записать();
	УстановитьРежим_ЕстьЭлектронныйДокументИЕстьДокумент1С_НаСервере(ЭлектронныйДокумент);

КонецПроцедуры
//added by Al

//added by Al
&НаКлиенте
Процедура ЗавершитьПодборДокумента1С(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		
		Документ1С = Результат;
		УстановитьПодобранныйДокументНаСервере(Результат);
		СформироватьДанныеЭлектронногоДокументаИПоказатьИзображение();
		ВладелецФормы.Элементы.СписокДокументов.Обновить();
		
	КонецЕсли;
	
КонецПроцедуры	
//added by Al
